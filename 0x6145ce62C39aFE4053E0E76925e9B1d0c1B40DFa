
// File: @openzeppelin/contracts/access/Ownable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}


// File: @openzeppelin/contracts/interfaces/IERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (interfaces/IERC20.sol)

pragma solidity ^0.8.0;

import "../token/ERC20/IERC20.sol";


// File: @openzeppelin/contracts/security/Pausable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (security/Pausable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which allows children to implement an emergency stop
 * mechanism that can be triggered by an authorized account.
 *
 * This module is used through inheritance. It will make available the
 * modifiers `whenNotPaused` and `whenPaused`, which can be applied to
 * the functions of your contract. Note that they will not be pausable by
 * simply including this module, only once the modifiers are put in place.
 */
abstract contract Pausable is Context {
    /**
     * @dev Emitted when the pause is triggered by `account`.
     */
    event Paused(address account);

    /**
     * @dev Emitted when the pause is lifted by `account`.
     */
    event Unpaused(address account);

    bool private _paused;

    /**
     * @dev Initializes the contract in unpaused state.
     */
    constructor() {
        _paused = false;
    }

    /**
     * @dev Returns true if the contract is paused, and false otherwise.
     */
    function paused() public view virtual returns (bool) {
        return _paused;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is not paused.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    modifier whenNotPaused() {
        require(!paused(), "Pausable: paused");
        _;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is paused.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    modifier whenPaused() {
        require(paused(), "Pausable: not paused");
        _;
    }

    /**
     * @dev Triggers stopped state.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    function _pause() internal virtual whenNotPaused {
        _paused = true;
        emit Paused(_msgSender());
    }

    /**
     * @dev Returns to normal state.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    function _unpause() internal virtual whenPaused {
        _paused = false;
        emit Unpaused(_msgSender());
    }
}


// File: @openzeppelin/contracts/security/ReentrancyGuard.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (security/ReentrancyGuard.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}


// File: @openzeppelin/contracts/token/ERC20/ERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/ERC20.sol)

pragma solidity ^0.8.0;

import "./IERC20.sol";
import "./extensions/IERC20Metadata.sol";
import "../../utils/Context.sol";

/**
 * @dev Implementation of the {IERC20} interface.
 *
 * This implementation is agnostic to the way tokens are created. This means
 * that a supply mechanism has to be added in a derived contract using {_mint}.
 * For a generic mechanism see {ERC20PresetMinterPauser}.
 *
 * TIP: For a detailed writeup see our guide
 * https://forum.zeppelin.solutions/t/how-to-implement-erc20-supply-mechanisms/226[How
 * to implement supply mechanisms].
 *
 * We have followed general OpenZeppelin Contracts guidelines: functions revert
 * instead returning `false` on failure. This behavior is nonetheless
 * conventional and does not conflict with the expectations of ERC20
 * applications.
 *
 * Additionally, an {Approval} event is emitted on calls to {transferFrom}.
 * This allows applications to reconstruct the allowance for all accounts just
 * by listening to said events. Other implementations of the EIP may not emit
 * these events, as it isn't required by the specification.
 *
 * Finally, the non-standard {decreaseAllowance} and {increaseAllowance}
 * functions have been added to mitigate the well-known issues around setting
 * allowances. See {IERC20-approve}.
 */
contract ERC20 is Context, IERC20, IERC20Metadata {
    mapping(address => uint256) private _balances;

    mapping(address => mapping(address => uint256)) private _allowances;

    uint256 private _totalSupply;

    string private _name;
    string private _symbol;

    /**
     * @dev Sets the values for {name} and {symbol}.
     *
     * The default value of {decimals} is 18. To select a different value for
     * {decimals} you should overload it.
     *
     * All two of these values are immutable: they can only be set once during
     * construction.
     */
    constructor(string memory name_, string memory symbol_) {
        _name = name_;
        _symbol = symbol_;
    }

    /**
     * @dev Returns the name of the token.
     */
    function name() public view virtual override returns (string memory) {
        return _name;
    }

    /**
     * @dev Returns the symbol of the token, usually a shorter version of the
     * name.
     */
    function symbol() public view virtual override returns (string memory) {
        return _symbol;
    }

    /**
     * @dev Returns the number of decimals used to get its user representation.
     * For example, if `decimals` equals `2`, a balance of `505` tokens should
     * be displayed to a user as `5.05` (`505 / 10 ** 2`).
     *
     * Tokens usually opt for a value of 18, imitating the relationship between
     * Ether and Wei. This is the value {ERC20} uses, unless this function is
     * overridden;
     *
     * NOTE: This information is only used for _display_ purposes: it in
     * no way affects any of the arithmetic of the contract, including
     * {IERC20-balanceOf} and {IERC20-transfer}.
     */
    function decimals() public view virtual override returns (uint8) {
        return 18;
    }

    /**
     * @dev See {IERC20-totalSupply}.
     */
    function totalSupply() public view virtual override returns (uint256) {
        return _totalSupply;
    }

    /**
     * @dev See {IERC20-balanceOf}.
     */
    function balanceOf(address account) public view virtual override returns (uint256) {
        return _balances[account];
    }

    /**
     * @dev See {IERC20-transfer}.
     *
     * Requirements:
     *
     * - `recipient` cannot be the zero address.
     * - the caller must have a balance of at least `amount`.
     */
    function transfer(address recipient, uint256 amount) public virtual override returns (bool) {
        _transfer(_msgSender(), recipient, amount);
        return true;
    }

    /**
     * @dev See {IERC20-allowance}.
     */
    function allowance(address owner, address spender) public view virtual override returns (uint256) {
        return _allowances[owner][spender];
    }

    /**
     * @dev See {IERC20-approve}.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function approve(address spender, uint256 amount) public virtual override returns (bool) {
        _approve(_msgSender(), spender, amount);
        return true;
    }

    /**
     * @dev See {IERC20-transferFrom}.
     *
     * Emits an {Approval} event indicating the updated allowance. This is not
     * required by the EIP. See the note at the beginning of {ERC20}.
     *
     * Requirements:
     *
     * - `sender` and `recipient` cannot be the zero address.
     * - `sender` must have a balance of at least `amount`.
     * - the caller must have allowance for ``sender``'s tokens of at least
     * `amount`.
     */
    function transferFrom(
        address sender,
        address recipient,
        uint256 amount
    ) public virtual override returns (bool) {
        _transfer(sender, recipient, amount);

        uint256 currentAllowance = _allowances[sender][_msgSender()];
        require(currentAllowance >= amount, "ERC20: transfer amount exceeds allowance");
        unchecked {
            _approve(sender, _msgSender(), currentAllowance - amount);
        }

        return true;
    }

    /**
     * @dev Atomically increases the allowance granted to `spender` by the caller.
     *
     * This is an alternative to {approve} that can be used as a mitigation for
     * problems described in {IERC20-approve}.
     *
     * Emits an {Approval} event indicating the updated allowance.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function increaseAllowance(address spender, uint256 addedValue) public virtual returns (bool) {
        _approve(_msgSender(), spender, _allowances[_msgSender()][spender] + addedValue);
        return true;
    }

    /**
     * @dev Atomically decreases the allowance granted to `spender` by the caller.
     *
     * This is an alternative to {approve} that can be used as a mitigation for
     * problems described in {IERC20-approve}.
     *
     * Emits an {Approval} event indicating the updated allowance.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     * - `spender` must have allowance for the caller of at least
     * `subtractedValue`.
     */
    function decreaseAllowance(address spender, uint256 subtractedValue) public virtual returns (bool) {
        uint256 currentAllowance = _allowances[_msgSender()][spender];
        require(currentAllowance >= subtractedValue, "ERC20: decreased allowance below zero");
        unchecked {
            _approve(_msgSender(), spender, currentAllowance - subtractedValue);
        }

        return true;
    }

    /**
     * @dev Moves `amount` of tokens from `sender` to `recipient`.
     *
     * This internal function is equivalent to {transfer}, and can be used to
     * e.g. implement automatic token fees, slashing mechanisms, etc.
     *
     * Emits a {Transfer} event.
     *
     * Requirements:
     *
     * - `sender` cannot be the zero address.
     * - `recipient` cannot be the zero address.
     * - `sender` must have a balance of at least `amount`.
     */
    function _transfer(
        address sender,
        address recipient,
        uint256 amount
    ) internal virtual {
        require(sender != address(0), "ERC20: transfer from the zero address");
        require(recipient != address(0), "ERC20: transfer to the zero address");

        _beforeTokenTransfer(sender, recipient, amount);

        uint256 senderBalance = _balances[sender];
        require(senderBalance >= amount, "ERC20: transfer amount exceeds balance");
        unchecked {
            _balances[sender] = senderBalance - amount;
        }
        _balances[recipient] += amount;

        emit Transfer(sender, recipient, amount);

        _afterTokenTransfer(sender, recipient, amount);
    }

    /** @dev Creates `amount` tokens and assigns them to `account`, increasing
     * the total supply.
     *
     * Emits a {Transfer} event with `from` set to the zero address.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     */
    function _mint(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: mint to the zero address");

        _beforeTokenTransfer(address(0), account, amount);

        _totalSupply += amount;
        _balances[account] += amount;
        emit Transfer(address(0), account, amount);

        _afterTokenTransfer(address(0), account, amount);
    }

    /**
     * @dev Destroys `amount` tokens from `account`, reducing the
     * total supply.
     *
     * Emits a {Transfer} event with `to` set to the zero address.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     * - `account` must have at least `amount` tokens.
     */
    function _burn(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: burn from the zero address");

        _beforeTokenTransfer(account, address(0), amount);

        uint256 accountBalance = _balances[account];
        require(accountBalance >= amount, "ERC20: burn amount exceeds balance");
        unchecked {
            _balances[account] = accountBalance - amount;
        }
        _totalSupply -= amount;

        emit Transfer(account, address(0), amount);

        _afterTokenTransfer(account, address(0), amount);
    }

    /**
     * @dev Sets `amount` as the allowance of `spender` over the `owner` s tokens.
     *
     * This internal function is equivalent to `approve`, and can be used to
     * e.g. set automatic allowances for certain subsystems, etc.
     *
     * Emits an {Approval} event.
     *
     * Requirements:
     *
     * - `owner` cannot be the zero address.
     * - `spender` cannot be the zero address.
     */
    function _approve(
        address owner,
        address spender,
        uint256 amount
    ) internal virtual {
        require(owner != address(0), "ERC20: approve from the zero address");
        require(spender != address(0), "ERC20: approve to the zero address");

        _allowances[owner][spender] = amount;
        emit Approval(owner, spender, amount);
    }

    /**
     * @dev Hook that is called before any transfer of tokens. This includes
     * minting and burning.
     *
     * Calling conditions:
     *
     * - when `from` and `to` are both non-zero, `amount` of ``from``'s tokens
     * will be transferred to `to`.
     * - when `from` is zero, `amount` tokens will be minted for `to`.
     * - when `to` is zero, `amount` of ``from``'s tokens will be burned.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 amount
    ) internal virtual {}

    /**
     * @dev Hook that is called after any transfer of tokens. This includes
     * minting and burning.
     *
     * Calling conditions:
     *
     * - when `from` and `to` are both non-zero, `amount` of ``from``'s tokens
     * has been transferred to `to`.
     * - when `from` is zero, `amount` tokens have been minted for `to`.
     * - when `to` is zero, `amount` of ``from``'s tokens have been burned.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _afterTokenTransfer(
        address from,
        address to,
        uint256 amount
    ) internal virtual {}
}


// File: @openzeppelin/contracts/token/ERC20/IERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address sender,
        address recipient,
        uint256 amount
    ) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}


// File: @openzeppelin/contracts/token/ERC20/extensions/ERC20Burnable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/extensions/ERC20Burnable.sol)

pragma solidity ^0.8.0;

import "../ERC20.sol";
import "../../../utils/Context.sol";

/**
 * @dev Extension of {ERC20} that allows token holders to destroy both their own
 * tokens and those that they have an allowance for, in a way that can be
 * recognized off-chain (via event analysis).
 */
abstract contract ERC20Burnable is Context, ERC20 {
    /**
     * @dev Destroys `amount` tokens from the caller.
     *
     * See {ERC20-_burn}.
     */
    function burn(uint256 amount) public virtual {
        _burn(_msgSender(), amount);
    }

    /**
     * @dev Destroys `amount` tokens from `account`, deducting from the caller's
     * allowance.
     *
     * See {ERC20-_burn} and {ERC20-allowance}.
     *
     * Requirements:
     *
     * - the caller must have allowance for ``accounts``'s tokens of at least
     * `amount`.
     */
    function burnFrom(address account, uint256 amount) public virtual {
        uint256 currentAllowance = allowance(account, _msgSender());
        require(currentAllowance >= amount, "ERC20: burn amount exceeds allowance");
        unchecked {
            _approve(account, _msgSender(), currentAllowance - amount);
        }
        _burn(account, amount);
    }
}


// File: @openzeppelin/contracts/token/ERC20/extensions/IERC20Metadata.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/extensions/IERC20Metadata.sol)

pragma solidity ^0.8.0;

import "../IERC20.sol";

/**
 * @dev Interface for the optional metadata functions from the ERC20 standard.
 *
 * _Available since v4.1._
 */
interface IERC20Metadata is IERC20 {
    /**
     * @dev Returns the name of the token.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the symbol of the token.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the decimals places of the token.
     */
    function decimals() external view returns (uint8);
}


// File: @openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/utils/SafeERC20.sol)

pragma solidity ^0.8.0;

import "../IERC20.sol";
import "../../../utils/Address.sol";

/**
 * @title SafeERC20
 * @dev Wrappers around ERC20 operations that throw on failure (when the token
 * contract returns false). Tokens that return no value (and instead revert or
 * throw on failure) are also supported, non-reverting calls are assumed to be
 * successful.
 * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,
 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.
 */
library SafeERC20 {
    using Address for address;

    function safeTransfer(
        IERC20 token,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));
    }

    function safeTransferFrom(
        IERC20 token,
        address from,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));
    }

    /**
     * @dev Deprecated. This function has issues similar to the ones found in
     * {IERC20-approve}, and its usage is discouraged.
     *
     * Whenever possible, use {safeIncreaseAllowance} and
     * {safeDecreaseAllowance} instead.
     */
    function safeApprove(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        // safeApprove should only be called when setting an initial allowance,
        // or when resetting it to zero. To increase and decrease it, use
        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
        require(
            (value == 0) || (token.allowance(address(this), spender) == 0),
            "SafeERC20: approve from non-zero to non-zero allowance"
        );
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
    }

    function safeIncreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        uint256 newAllowance = token.allowance(address(this), spender) + value;
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
    }

    function safeDecreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            uint256 newAllowance = oldAllowance - value;
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
        }
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     */
    function _callOptionalReturn(IERC20 token, bytes memory data) private {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We use {Address.functionCall} to perform this call, which verifies that
        // the target address contains contract code and also asserts for success in the low-level call.

        bytes memory returndata = address(token).functionCall(data, "SafeERC20: low-level call failed");
        if (returndata.length > 0) {
            // Return data is optional
            require(abi.decode(returndata, (bool)), "SafeERC20: ERC20 operation did not succeed");
        }
    }
}


// File: @openzeppelin/contracts/utils/Address.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Address.sol)

pragma solidity ^0.8.0;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize, which returns 0 for contracts in
        // construction, since the code is only stored at the end of the
        // constructor execution.

        uint256 size;
        assembly {
            size := extcodesize(account)
        }
        return size > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}


// File: @openzeppelin/contracts/utils/Context.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}


// File: @openzeppelin/contracts/utils/structs/EnumerableSet.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/structs/EnumerableSet.sol)

pragma solidity ^0.8.0;

/**
 * @dev Library for managing
 * https://en.wikipedia.org/wiki/Set_(abstract_data_type)[sets] of primitive
 * types.
 *
 * Sets have the following properties:
 *
 * - Elements are added, removed, and checked for existence in constant time
 * (O(1)).
 * - Elements are enumerated in O(n). No guarantees are made on the ordering.
 *
 * ```
 * contract Example {
 *     // Add the library methods
 *     using EnumerableSet for EnumerableSet.AddressSet;
 *
 *     // Declare a set state variable
 *     EnumerableSet.AddressSet private mySet;
 * }
 * ```
 *
 * As of v3.3.0, sets of type `bytes32` (`Bytes32Set`), `address` (`AddressSet`)
 * and `uint256` (`UintSet`) are supported.
 */
library EnumerableSet {
    // To implement this library for multiple types with as little code
    // repetition as possible, we write it in terms of a generic Set type with
    // bytes32 values.
    // The Set implementation uses private functions, and user-facing
    // implementations (such as AddressSet) are just wrappers around the
    // underlying Set.
    // This means that we can only create new EnumerableSets for types that fit
    // in bytes32.

    struct Set {
        // Storage of set values
        bytes32[] _values;
        // Position of the value in the `values` array, plus 1 because index 0
        // means a value is not in the set.
        mapping(bytes32 => uint256) _indexes;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function _add(Set storage set, bytes32 value) private returns (bool) {
        if (!_contains(set, value)) {
            set._values.push(value);
            // The value is stored at length-1, but we add 1 to all indexes
            // and use 0 as a sentinel value
            set._indexes[value] = set._values.length;
            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function _remove(Set storage set, bytes32 value) private returns (bool) {
        // We read and store the value's index to prevent multiple reads from the same storage slot
        uint256 valueIndex = set._indexes[value];

        if (valueIndex != 0) {
            // Equivalent to contains(set, value)
            // To delete an element from the _values array in O(1), we swap the element to delete with the last one in
            // the array, and then remove the last element (sometimes called as 'swap and pop').
            // This modifies the order of the array, as noted in {at}.

            uint256 toDeleteIndex = valueIndex - 1;
            uint256 lastIndex = set._values.length - 1;

            if (lastIndex != toDeleteIndex) {
                bytes32 lastvalue = set._values[lastIndex];

                // Move the last value to the index where the value to delete is
                set._values[toDeleteIndex] = lastvalue;
                // Update the index for the moved value
                set._indexes[lastvalue] = valueIndex; // Replace lastvalue's index to valueIndex
            }

            // Delete the slot where the moved value was stored
            set._values.pop();

            // Delete the index for the deleted slot
            delete set._indexes[value];

            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function _contains(Set storage set, bytes32 value) private view returns (bool) {
        return set._indexes[value] != 0;
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function _length(Set storage set) private view returns (uint256) {
        return set._values.length;
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function _at(Set storage set, uint256 index) private view returns (bytes32) {
        return set._values[index];
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function _values(Set storage set) private view returns (bytes32[] memory) {
        return set._values;
    }

    // Bytes32Set

    struct Bytes32Set {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _add(set._inner, value);
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _remove(set._inner, value);
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(Bytes32Set storage set, bytes32 value) internal view returns (bool) {
        return _contains(set._inner, value);
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(Bytes32Set storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(Bytes32Set storage set, uint256 index) internal view returns (bytes32) {
        return _at(set._inner, index);
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(Bytes32Set storage set) internal view returns (bytes32[] memory) {
        return _values(set._inner);
    }

    // AddressSet

    struct AddressSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(AddressSet storage set, address value) internal returns (bool) {
        return _add(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(AddressSet storage set, address value) internal returns (bool) {
        return _remove(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(AddressSet storage set, address value) internal view returns (bool) {
        return _contains(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(AddressSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(AddressSet storage set, uint256 index) internal view returns (address) {
        return address(uint160(uint256(_at(set._inner, index))));
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(AddressSet storage set) internal view returns (address[] memory) {
        bytes32[] memory store = _values(set._inner);
        address[] memory result;

        assembly {
            result := store
        }

        return result;
    }

    // UintSet

    struct UintSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(UintSet storage set, uint256 value) internal returns (bool) {
        return _add(set._inner, bytes32(value));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(UintSet storage set, uint256 value) internal returns (bool) {
        return _remove(set._inner, bytes32(value));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(UintSet storage set, uint256 value) internal view returns (bool) {
        return _contains(set._inner, bytes32(value));
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function length(UintSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(UintSet storage set, uint256 index) internal view returns (uint256) {
        return uint256(_at(set._inner, index));
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(UintSet storage set) internal view returns (uint256[] memory) {
        bytes32[] memory store = _values(set._inner);
        uint256[] memory result;

        assembly {
            result := store
        }

        return result;
    }
}


// File: contracts/interfaces/IMultiOracle.sol
// SPDX-License-Identifier: MIT

pragma solidity =0.8.4;
pragma experimental ABIEncoderV2;

interface IMultiOracle {
    function consult(address _token) external view returns (uint256);
}


// File: contracts/interfaces/IRandomNumberGenerator.sol
// SPDX-License-Identifier: MIT

pragma solidity =0.8.4;

interface IRandomNumberGenerator {
    function computerSeed(uint256) external view returns(uint256);
    function getNumber(uint256) external view returns(uint256[] memory);
}

// File: contracts/jackpot/PandoPotV3.sol
//SPDX-License-Identifier: UNLICENSED
pragma solidity =0.8.4;

import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/interfaces/IERC20.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "@openzeppelin/contracts/security/Pausable.sol";
import "@openzeppelin/contracts/utils/structs/EnumerableSet.sol";
import "@openzeppelin/contracts/token/ERC20/extensions/ERC20Burnable.sol";


import "../interfaces/IRandomNumberGenerator.sol";
import "../interfaces/IMultiOracle.sol";


contract PandoPotV3 is Ownable, ReentrancyGuard, Pausable {
    using EnumerableSet for EnumerableSet.UintSet;
    using SafeERC20 for IERC20;

    enum PRIZE_STATUS {AVAILABLE, CLAIMED, LIQUIDATED}
    enum PRIZE_TYPE {LEADERBOARD, MEGA, MINOR, MINI}

    uint256 public maxTypes = 4;
    uint256 public totalPrizePercentage = 0;
    mapping(PRIZE_TYPE => TypePrize) public typePrize;

    struct TypePrize {
        PRIZE_TYPE types;
        uint256 sampleSpace;
        uint256 winners;
        uint256 percentage;
        string name;
    }

    // 0 : mega, 1 : minor, 2 : leaderboard
    struct PrizeInfo {
        uint256 USD;
        uint256 PSR;
        uint256 PAN;
        uint256 expire;
        uint256 nClaimed;
        uint256 totalWinner;
    }

    struct LeaderboardPrizeInfo {
        uint256 USD;
        uint256 PSR;
        uint256 PAN;
        uint256 expire;
        PRIZE_STATUS status;
    }

    struct RoundInfo {
        //type => numbers
        mapping(PRIZE_TYPE => EnumerableSet.UintSet) numbers;
        uint256 finishedAt;
        uint256 status; //0 : need Update prizeInfo
    }

    address public USD;
    address public PSR;
    address public PAN;
    address public randomNumberGenerator;
    address public multiOracle;

    uint256 public constant unlockPeriod = 2 * 365 * 1 days;
    uint256 public constant ONE_HUNDRED_PERCENT = 1000000;
    uint256 public timeBomb = 2 * 30 * 1 days;
    uint256 public prizeExpireTime = 14 * 1 days;
    uint256 public timeBombPrizePercentage = 250000;
    uint256 public roundDuration = 1 hours;

    uint256 public lastJackpot;
    uint256 public totalPSRAllocated;
    uint256 public lastUpdatePot;

    uint256 public USDForCurrentPot;
    uint256 public PSRForCurrentPot;
    uint256 public PANForCurrentPot;

    uint256 public USDForTimeBomb;
    uint256 public PSRForTimeBomb;
    uint256 public PANForTimeBomb;

    uint256 public currentRoundId;
    uint256 public currentDistributeId;

    uint256 public panBurnPercent = 0;
    uint256 public ticketPrice = 1e18;

    // round => type => number => address => quantity // ticket mapping with users
    mapping(uint256 => mapping (PRIZE_TYPE => mapping(uint256 => mapping(address => uint)))) public tickets;
    mapping(uint256 => mapping (PRIZE_TYPE => mapping(uint256 => mapping(address => bool)))) public isClaimed;

    //user
    mapping(address => bool) public isReceivedFreeTicket;
    uint256 public numFreeTicket = 1;

    // round => type => number => quantity
    mapping(uint256 => mapping(PRIZE_TYPE => mapping (uint256 => uint))) public nTickets; // number of tickets mapping with round

    // round => type => prize
    mapping(uint256 => mapping(PRIZE_TYPE => PrizeInfo)) public prizes;

    //round => address => prize
    mapping (uint256 => mapping(address => LeaderboardPrizeInfo)) public leaderboardPrize;
    mapping (uint256 => RoundInfo) roundInfo;

    mapping (address => bool) public whitelist;

    uint256 public pendingUSD;
    uint256 public pendingPAN;

    uint256[21] public discountPercentages;
//    uint256 public discountPerAmount = 1e16;

    /*----------------------------CONSTRUCTOR----------------------------*/
    constructor (address _USD, address _PSR, address _PAN, address _randomNumberGenerator, address _router, address _multiOracle, uint256 _lastUpdated) {
        USD = _USD;
        PSR = _PSR;
        PAN = _PAN;
        multiOracle = _multiOracle;
        randomNumberGenerator = _randomNumberGenerator;
        lastJackpot = _lastUpdated;
        lastUpdatePot = _lastUpdated;
        currentRoundId = 1;
        roundInfo[0].finishedAt = block.timestamp;
        roundInfo[0].status = 1;
        whitelist[_router] = true;
        _initPrize();

        for(uint i = 0; i < 21; i++) {
            if(i < 5){
                discountPercentages[i] = 0;
            }else if(i < 10){
                discountPercentages[i] = 30000;
            }else if(i <15){
                discountPercentages[i] = 70000;
            }else if(i < 20){
                discountPercentages[i] = 110000;
            }else{
                discountPercentages[i] = 150000;
            }
        }
    }

    /*----------------------------INTERNAL FUNCTIONS----------------------------*/

    function _transferToken(address _token, address _receiver, uint256 _amount) internal {
        if (_amount > 0) {
            IERC20(_token).safeTransfer(_receiver, _amount);
        }
    }

    function _generateTicket(uint256 _rand, uint256 _sample, uint256 _salt) internal view returns(uint256) {
        return uint256(keccak256(abi.encodePacked(_rand, _salt)))% _sample;
    }

    function _updateRound(uint256 _id) internal {
        RoundInfo storage _roundInfo = roundInfo[_id];
        uint256 _expire = _roundInfo.finishedAt + prizeExpireTime;
        if (_roundInfo.status == 0) {
            _roundInfo.status = 1;
            _updateLuckyNumber(_id, _roundInfo);
            (uint256 _usd, uint256 _psr, uint256 _pan) = _updatePrize(_id, _roundInfo, _expire);
            if (_psr + _usd + _pan > 0) {
                pendingUSD += _usd;
                pendingPAN += _pan;
                PSRForCurrentPot -= _psr;
            }
            emit RoundCompleted(_id, _expire,_roundInfo.numbers[PRIZE_TYPE.MEGA].values(), _roundInfo.numbers[PRIZE_TYPE.MINOR].values(), _roundInfo.numbers[PRIZE_TYPE.MINI].values());
        }
    }

    function _updateLuckyNumber(uint256 _id, RoundInfo storage _roundInfo) internal  {
        uint256[] memory _numbers = IRandomNumberGenerator(randomNumberGenerator).getNumber(_id);
        for(uint256 i = 1; i < maxTypes; i++) {
            {
                uint256 _winners = typePrize[PRIZE_TYPE(i)].winners;
                _calculateNumbers(_numbers[0], _numbers[1], _numbers[2], PRIZE_TYPE(i), _winners, _roundInfo.numbers[PRIZE_TYPE(i)]);
            }
        }
    }

    function _updatePrize(uint256 _id, RoundInfo storage _roundInfo, uint256 _expire) internal returns(uint, uint, uint) {
        uint256 _totalUSD = 0;
        uint256 _totalPSR = 0;
        uint256 _totalPAN = 0;

        uint256 _usd;
        uint256 _psr;
        uint256 _pan;
        for(uint256 i = 1; i < maxTypes; i++) {
            {
                (_usd, _psr, _pan)= _calcPrize(_id, _roundInfo.numbers[PRIZE_TYPE(i)].values(), _expire, PRIZE_TYPE(i));
                _totalUSD += _usd;
                _totalPSR += _psr;
                _totalPAN += _pan;
            }
        }
        return (_totalUSD, _totalPSR, _totalPAN);
    }

    function _calculateNumbers(uint256 _number1, uint256 _number2, uint256 _number3, PRIZE_TYPE _type, uint256 _quantity, EnumerableSet.UintSet storage _set) internal {
        uint256 _seed = (block.timestamp + block.number + uint256(_type) + _quantity ) % 256;
        uint256 _rand = uint256(keccak256(abi.encodePacked(_number1 >> _seed, _number2, _number3 >> (256 - _seed ), _type, _quantity)));

        uint256 _value;
        for(uint256 i = 0; i < _quantity; i++) {
            _value = uint256(keccak256(abi.encodePacked(_rand, i))) % typePrize[_type].sampleSpace;
            while(!_set.add(_value)){
                _value += 1;
            }
        }
    }

    function _countWinners(uint256 _roundId, uint256[] memory _numbers, PRIZE_TYPE _type) internal view returns (uint) {
        uint256 _count = 0;
        for(uint256 i = 0; i < _numbers.length; i++) {
            _count += nTickets[_roundId][_type][_numbers[i]];
        }
        return _count;
    }

    function _calcPrize(uint256 _roundId, uint256[] memory _numbers, uint256 _expire, PRIZE_TYPE _type) internal returns(uint256, uint256, uint256) {
        PrizeInfo memory _prize = PrizeInfo({
            USD : 0,
            PSR : 0,
            PAN : 0,
            expire : _expire,
            nClaimed : 0,
            totalWinner : 0
        });
        uint256 _percentage = typePrize[_type].percentage;

        uint256 _USDForWinner = USDForCurrentPot * _percentage / ONE_HUNDRED_PERCENT;
        uint256 _PSRForWinner = PSRForCurrentPot * _percentage / ONE_HUNDRED_PERCENT;
        uint256 _PANForWinner = PANForCurrentPot * _percentage / ONE_HUNDRED_PERCENT;

        _prize.totalWinner = _countWinners(_roundId, _numbers, _type);
        if (_prize.totalWinner > 0) {
            _prize.USD = _USDForWinner;
            _prize.PSR = _PSRForWinner;
            _prize.PAN = _PANForWinner;
            if(_type == PRIZE_TYPE.MEGA) {
                lastJackpot = _expire - prizeExpireTime;
            }
        }
        prizes[_roundId][_type] = _prize;

        emit PriceForWinner(_roundId, _type, _USDForWinner, _PSRForWinner, _PANForWinner);
        return (_prize.USD, _prize.PSR, _prize.PAN);
    }


    // 0: leaderboard // 1 : mega // 2: minor // 3: mini
    function _liquidate(PRIZE_TYPE _type, uint256 _roundId, address _owner) internal {
        uint256 _totalUSD = 0;
        uint256 _totalPSR = 0;
        uint256 _totalPAN = 0;

        if (_type != PRIZE_TYPE.LEADERBOARD) {
            PrizeInfo storage _prize = prizes[_roundId][_type];
            require(_prize.expire < block.timestamp , 'PandoPot: !expire');
            if(_prize.totalWinner > _prize.nClaimed) {
                _totalUSD = _prize.USD * (_prize.totalWinner - _prize.nClaimed) / _prize.totalWinner;
                _totalPSR = _prize.PSR * (_prize.totalWinner - _prize.nClaimed) / _prize.totalWinner;
                _totalPAN = _prize.PAN * (_prize.totalWinner - _prize.nClaimed) / _prize.totalWinner;
                _prize.nClaimed = _prize.totalWinner;
            }
        } else {
            LeaderboardPrizeInfo storage _prize = leaderboardPrize[_roundId][_owner];
            require(_prize.expire < block.timestamp, 'PandoPot: !expire');
            require(_prize.status == PRIZE_STATUS.AVAILABLE, 'PandoPot: !AVAILABLE');
            _prize.status = PRIZE_STATUS.LIQUIDATED;
            _totalUSD = _prize.USD;
            _totalPSR = _prize.PSR;
            _totalPAN = _prize.PAN;
        }
        pendingUSD -= _totalUSD;
        pendingPAN -= _totalPAN;
        PSRForCurrentPot += _totalPSR;
        emit Liquidated(_type, _roundId, _owner, _totalUSD, _totalPSR, _totalPAN);
    }

    function _addTicket(address _receiver, uint256 _mega, uint256 _minor, uint256 _mini, uint256 _roundId) internal{
        tickets[_roundId][PRIZE_TYPE.MEGA][_mega][_receiver]++;
        tickets[_roundId][PRIZE_TYPE.MINOR][_minor][_receiver]++;
        tickets[_roundId][PRIZE_TYPE.MINI][_mini][_receiver]++;

        nTickets[_roundId][PRIZE_TYPE.MEGA][_mega]++;
        nTickets[_roundId][PRIZE_TYPE.MINOR][_minor]++;
        nTickets[_roundId][PRIZE_TYPE.MINI][_mini]++;

    }

    function _randomTicket(address _receiver, uint256 _rand, uint256 _quantity) internal {
        uint256[] memory _tickets = new uint[](_quantity);
        uint256 _ticketNumber;
        uint256 _megaSpace = typePrize[PRIZE_TYPE.MEGA].sampleSpace; // mega sampleSpace
        uint256 _minorSpace = typePrize[PRIZE_TYPE.MINOR].sampleSpace;
        uint256 _miniSpace =  typePrize[PRIZE_TYPE.MINI].sampleSpace;
        uint256 _seed = uint256(keccak256(abi.encodePacked(_rand, block.timestamp)));
        for (uint256 i = 0; i < _quantity; i++) {
            if (_seed > _megaSpace) {
                _ticketNumber = _seed % _megaSpace;
                _seed = _seed / _megaSpace;
            } else {
                _seed = uint256(keccak256(abi.encodePacked(_rand, i, _seed)));
            }
            uint256 _minor = _ticketNumber % _minorSpace;
            uint256 _mini = _ticketNumber % _miniSpace;

           _addTicket(_receiver, _ticketNumber, _minor, _mini, currentRoundId);

            _tickets[i] = _ticketNumber;
        }
        emit NewTicket(currentRoundId, _receiver, _tickets, 1);
    }

    function _getPriceOfTickets(uint256 _quantity) internal view returns (uint256) {
        return ticketPrice * _quantity * (ONE_HUNDRED_PERCENT - discountPercentages[_quantity]) / ONE_HUNDRED_PERCENT;
    }

    function _checkAndTransferPan(uint256 _quantity) internal{
        require(_quantity > 0,"PandoPot: Quantity ticket buy must more than zero");
        uint256 _amount_PAN = getAmountPan(_quantity);
        if(_amount_PAN > 0){
            IERC20(PAN).safeTransferFrom(msg.sender, address(this), _amount_PAN);
            if(panBurnPercent > 0){
                ERC20Burnable(PAN).burn(_amount_PAN * panBurnPercent / ONE_HUNDRED_PERCENT);
            }
        }
    }

    function _addNewPrize(uint256 _sampleSpace, uint256 _numberOfWinner, uint256 _percentage, string memory _name, PRIZE_TYPE _types) internal {
        require(_percentage + totalPrizePercentage <= ONE_HUNDRED_PERCENT, "PandoPot: percentage over");
        require(_numberOfWinner > 0 , "PandoPot: _numberOfWinner zero");
        require(_sampleSpace > 0 , "PandoPot: _sampleSpace zero");
        TypePrize storage _typePrize = typePrize[_types];
        _typePrize.sampleSpace = _sampleSpace;
        _typePrize.winners = _numberOfWinner;
        _typePrize.percentage = _percentage;
        _typePrize.name = _name;
        _typePrize.types = _types;
        totalPrizePercentage += _percentage;
        emit NewPrize(_types, _sampleSpace, _numberOfWinner, _percentage, _name);
    }

    function _checkTicketInRound(PRIZE_TYPE _type, uint256 _roundId, uint256 _ticketNumber) internal view returns (bool) {
        return roundInfo[_roundId].numbers[_type].contains(_ticketNumber);

    }

    function _claim(PRIZE_TYPE _type, uint256 _roundId, uint256 _ticketNumber, address _receiver) internal {
        require(uint256(_type) <  maxTypes, 'PandoPot: Invalid type');

        uint256 _USDAmount = 0;
        uint256 _PANAmount = 0;
        uint256 _PSRAmount = 0;

        if (_type != PRIZE_TYPE.LEADERBOARD) {
            uint256 _roundInfoStatus = roundInfo[_roundId].status;
            require(_roundInfoStatus == 1, 'PandoPot: Round hasnt been finished yet');
            
            uint256 _number = _ticketNumber % typePrize[_type].sampleSpace;
            require(tickets[_roundId][_type][_number][msg.sender] > 0 && _checkTicketInRound(_type, _roundId, _number), 'PandoPot: no prize');
           
            require(!isClaimed[_roundId][_type][_ticketNumber][msg.sender], 'Pandot:  claimed');
            isClaimed[_roundId][_type][_ticketNumber][msg.sender] = true;

            PrizeInfo storage _prizeInfo = prizes[_roundId][_type];
            if (_prizeInfo.expire >= block.timestamp) {
               
                uint256 _nWiningTicket = tickets[_roundId][PRIZE_TYPE.MEGA][_ticketNumber][msg.sender];
                uint256 _totalWinner = _prizeInfo.totalWinner;
                
                _USDAmount = _prizeInfo.USD * _nWiningTicket / _totalWinner;
                _PSRAmount = _prizeInfo.PSR * _nWiningTicket / _totalWinner;
                _PANAmount = _prizeInfo.PAN * _nWiningTicket / _totalWinner;
                
                _prizeInfo.nClaimed +=_nWiningTicket;
            } else {
                _liquidate(_type, _roundId, msg.sender);
            }
        } else {
            LeaderboardPrizeInfo storage _prize = leaderboardPrize[_roundId][msg.sender];
            require(_prize.USD + _prize.PSR > 0, 'PandoPot: no prize');
            if (_prize.expire >= block.timestamp) {
                require(_prize.status == PRIZE_STATUS.AVAILABLE, 'PandoPot: prize not available');
                _prize.status = PRIZE_STATUS.CLAIMED;
                _USDAmount = _prize.USD;
                _PSRAmount = _prize.PSR;
                _PANAmount = _prize.PAN;
            } else {
                _liquidate(_type, _roundId, msg.sender);
            }
        }
        pendingUSD -= _USDAmount;
        pendingPAN -= _PANAmount;

        _transferToken(USD, _receiver, _USDAmount);
        _transferToken(PSR, _receiver, _PSRAmount);
        _transferToken(PAN, _receiver, _PANAmount);
        emit Claimed(_type, _roundId, _ticketNumber, _USDAmount, _PSRAmount, _PANAmount, _receiver);
    }


    function _initPrize() internal {
        _addNewPrize(1e6, 2, 250000, "Mega", PRIZE_TYPE.MEGA); // 6 digits
        _addNewPrize(1e4, 2, 5000, "Minor", PRIZE_TYPE.MINOR); // 4 digits
        _addNewPrize(1e3, 4, 750, "Mini", PRIZE_TYPE.MINI); // 3 digits
    }

    function _computerSeed() internal view returns (uint256) {
        uint256 seed =
        uint256(
            keccak256(
                abi.encodePacked(
                    (block.timestamp),
                    block.gaslimit,
                    blockhash(block.number - 1),
                    block.coinbase,
                    tx.origin
                )
            )
        );
        return seed;
    }
    /*----------------------------EXTERNAL FUNCTIONS----------------------------*/

    function getRoundDuration() external view returns(uint256) {
        return roundDuration;
    }

    function getAmountPan(uint256 _quantityTicket) public view returns(uint256) {
        uint256 _priceTicket = _getPriceOfTickets(_quantityTicket);
        if(_priceTicket > 0){
            uint256 _price_PAN = IMultiOracle(multiOracle).consult(address(PAN));
            uint256 _price_USD = IMultiOracle(multiOracle).consult(address(USD));
            if (_price_PAN > 0 && _price_USD > 0) {
                return _priceTicket * _price_USD / _price_PAN;
            }
        }
        return 0;
    }

    function buyTickets(address _receiver, uint256[] memory _tickets) external availableBuyTicket(_receiver) onlyEOA whenNotPaused nonReentrant{
        require(0 < _tickets.length && _tickets.length < 21,"PandoPot: Quantity ticket buy must more than zero or maximum 20 tickets");
        uint256 _roundId = currentRoundId;
        _checkAndTransferPan(_tickets.length);

        uint256 _megaSampleSpace = typePrize[PRIZE_TYPE.MEGA].sampleSpace;
        uint256 _minorSampleSpace = typePrize[PRIZE_TYPE.MINOR].sampleSpace;
        uint256 _miniSampleSpace = typePrize[PRIZE_TYPE.MINI].sampleSpace;

        uint256 _megaNumber;
        uint256 _minorNumber;
        uint256 _miniNumber;
        for (uint256 i = 0; i < _tickets.length; i++) {
            _megaNumber = _tickets[i] % _megaSampleSpace;
            _minorNumber = _tickets[i] % _minorSampleSpace;
            _miniNumber = _tickets[i] % _miniSampleSpace;

            _addTicket(_receiver, _megaNumber, _minorNumber, _miniNumber, _roundId);
        }

        if(!isReceivedFreeTicket[_receiver]){
            isReceivedFreeTicket[_receiver] = true;
            _randomTicket(_receiver, _computerSeed(), numFreeTicket);
        }

        emit NewTicket(_roundId, _receiver, _tickets, 0);
    }

    function enter(address _receiver, uint256 _rand, uint256 _quantity) external whenNotPaused nonReentrant onlyWhitelist() {
        _randomTicket(_receiver, _rand, _quantity); // mega
    }

    function enterWithoutRand(address _receiver, uint256[] memory _tickets) external whenNotPaused nonReentrant onlyWhitelist() {
        uint256 _len = _tickets.length;
        uint256 _roundId = currentRoundId;
        uint256 _megaSampleSpace = typePrize[PRIZE_TYPE.MEGA].sampleSpace;
        uint256 _minorSampleSpace = typePrize[PRIZE_TYPE.MINOR].sampleSpace;
        uint256 _miniSampleSpace = typePrize[PRIZE_TYPE.MINI].sampleSpace;

        for (uint256 i = 0; i < _len; i++) {
            uint256 _ticketNumber = _tickets[i];
            _addTicket(_receiver, _ticketNumber % _megaSampleSpace, _ticketNumber % _minorSampleSpace, _ticketNumber % _miniSampleSpace, _roundId);
        }
        emit NewTicket(_roundId, _receiver, _tickets, 0);
    }

    function claim(PRIZE_TYPE _type, uint256 _roundId, uint256 _ticketNumber, address _receiver) external whenNotPaused nonReentrant {
        _claim(_type, _roundId, _ticketNumber, _receiver);
    }

    function distribute(address[] memory _leaderboards, uint256[] memory ratios, uint256 _lastJackpot) external onlyOwner whenNotPaused {
        require(_leaderboards.length == ratios.length, 'PandoPot: leaderboards != ratios');
        require(block.timestamp - lastJackpot >= timeBomb, 'PandoPot: not enough timebomb');
        uint256 _cur = 0;
        for (uint256 i = 0; i < ratios.length; i++) {
            _cur += ratios[i];
        }
        require(_cur == ONE_HUNDRED_PERCENT, 'PandoPot: ratios incorrect');
        currentDistributeId++;
        updatePandoPot();
        require(USDForTimeBomb + PSRForTimeBomb + PANForTimeBomb > 0, 'PandoPot: no prize');
        uint256 _nRatios = ratios.length;
        uint256[] memory _usdAmounts = new uint256[](_nRatios);
        uint256[] memory _psrAmounts = new uint256[](_nRatios);
        uint256[] memory _panAmounts = new uint256[](_nRatios);

        for (uint256 i = 0; i < _leaderboards.length; i++) {
            uint256 _USDAmount = USDForTimeBomb * ratios[i] / ONE_HUNDRED_PERCENT;
            uint256 _PSRAmount = PSRForTimeBomb * ratios[i] / ONE_HUNDRED_PERCENT;
            uint256 _PANAmount = PANForTimeBomb * ratios[i] / ONE_HUNDRED_PERCENT;
            LeaderboardPrizeInfo memory _prize = LeaderboardPrizeInfo({
                USD : _USDAmount,
                PSR : _PSRAmount,
                PAN : _PANAmount,
                expire : block.timestamp + prizeExpireTime,
                status : PRIZE_STATUS.AVAILABLE
            });
            leaderboardPrize[currentDistributeId][_leaderboards[i]] = _prize;
            _usdAmounts[i] = _USDAmount;
            _psrAmounts[i] = _PSRAmount;
            _panAmounts[i] = _PANAmount;
        }
        pendingUSD += USDForTimeBomb;
        pendingPAN += PANForTimeBomb;
        USDForTimeBomb = 0;
        PSRForTimeBomb = 0;
        PANForTimeBomb = 0;
        if (_lastJackpot == 0) {
            lastJackpot = block.timestamp;
        } else {
            lastJackpot = _lastJackpot;
        }
        emit Distributed(currentDistributeId, block.timestamp + prizeExpireTime, _leaderboards, _usdAmounts, _psrAmounts, _panAmounts);
    }

    function updatePandoPot() public {
        _updateRound(currentRoundId - 1);

        PSRForCurrentPot += totalPSRAllocated * (block.timestamp - lastUpdatePot) / unlockPeriod;
        USDForCurrentPot = IERC20(USD).balanceOf(address(this)) - USDForTimeBomb - pendingUSD;
        PANForCurrentPot = IERC20(PAN).balanceOf(address(this)) - PANForTimeBomb - pendingPAN;

        if (block.timestamp - lastJackpot >= timeBomb) {
            if (PSRForTimeBomb == 0 && USDForTimeBomb == 0 && PANForTimeBomb == 0) {
                USDForTimeBomb = USDForCurrentPot * timeBombPrizePercentage / ONE_HUNDRED_PERCENT;
                PSRForTimeBomb = PSRForCurrentPot * timeBombPrizePercentage / ONE_HUNDRED_PERCENT;
                PANForTimeBomb = PANForCurrentPot * timeBombPrizePercentage / ONE_HUNDRED_PERCENT;
                PSRForCurrentPot -= PSRForTimeBomb;
            }
        }
        lastUpdatePot = block.timestamp;
    }

    function liquidate(PRIZE_TYPE _type, uint256 _roundId, address[] memory _owners) external whenNotPaused {
        require(uint256(_type) <  maxTypes, 'PandoPot: Invalid type');
        for(uint256 i = 0; i < _owners.length; i++){
            _liquidate(_type, _roundId, _owners[i]);
        }
        updatePandoPot();
    }

    function currentPot() external view returns(uint256, uint256, uint256) {
        uint256 _USD = IERC20(USD).balanceOf(address(this)) - USDForTimeBomb - pendingUSD;
        uint256 _PAN = IERC20(PAN).balanceOf(address(this)) - PANForTimeBomb - pendingPAN;
        uint256 _PSR = totalPSRAllocated * (block.timestamp - lastUpdatePot) / unlockPeriod + PSRForCurrentPot;

        if (currentRoundId > 1) {
            uint256 _preRound = currentRoundId - 1;
            if (roundInfo[_preRound].status == 0) {
                for(uint256 i = 1; i < maxTypes; i++) {
                    if(_countWinners(_preRound, roundInfo[_preRound].numbers[PRIZE_TYPE(i)].values(), PRIZE_TYPE(i)) > 0) {
                        _USD -= USDForCurrentPot * typePrize[PRIZE_TYPE(i)].percentage / ONE_HUNDRED_PERCENT;
                        _PSR -= PSRForCurrentPot * typePrize[PRIZE_TYPE(i)].percentage / ONE_HUNDRED_PERCENT;
                        _PAN -= PANForCurrentPot * typePrize[PRIZE_TYPE(i)].percentage / ONE_HUNDRED_PERCENT;
                    }
                }
            }
        }
        return (_USD, _PSR, _PAN);
    }

    function finishRound() external onlyRNG {
        require(block.timestamp > roundDuration + roundInfo[currentRoundId - 1].finishedAt, 'PandoPot: < roundDuration');
        roundInfo[currentRoundId].finishedAt = block.timestamp;
        emit RoundFinished(currentRoundId);
        currentRoundId++;
    }

    // 0: wrong
    // 1: valid
    // 2: expired
    // 3: claimed
    // 4: you is not winner
    //_type: 0: distribute 1: mega 2: minor 3: mini
    function checkTicketStatus(uint256 _roundId, PRIZE_TYPE _type, address _owner, uint256 _ticketNumber) external view returns (uint256) {
        if (roundInfo[_roundId].numbers[_type].contains(_ticketNumber)) {
            if (roundInfo[_roundId].finishedAt + prizeExpireTime < block.timestamp) {
                return 2;
            }
            if(tickets[_roundId][_type][_ticketNumber][_owner] == 0){
                return 4;
            }
            if (!isClaimed[_roundId][_type][_ticketNumber][_owner]) {
                return 1;
            }
            return 3;
        }
        return 0;
    }

    function getDiscountPercentage(uint256 _quantity) external view returns (uint256) {
        require(_quantity <= 20);
        return discountPercentages[_quantity];
    }

    function getWinningNumbers(uint _roundId) public view returns (uint256[] memory, uint256[] memory, uint256[] memory) {
        return (roundInfo[_roundId].numbers[PRIZE_TYPE.MEGA].values(), roundInfo[_roundId].numbers[PRIZE_TYPE.MINOR].values(), roundInfo[_roundId].numbers[PRIZE_TYPE.MINI].values());
    }

    function getRoundInfo(uint256 _roundId) public view returns (uint256[] memory, uint256[] memory, uint256[] memory, uint256 finishedAt, uint256 status) {
        return (roundInfo[_roundId].numbers[PRIZE_TYPE.MEGA].values(), roundInfo[_roundId].numbers[PRIZE_TYPE.MINOR].values(), roundInfo[_roundId].numbers[PRIZE_TYPE.MINI].values(), roundInfo[_roundId].finishedAt, roundInfo[_roundId].status);
    }

    /*----------------------------RESTRICTED FUNCTIONS----------------------------*/
    modifier availableBuyTicket(address _receive) {
        require( _receive != address(0), "PandoPot: Receiver must different address zero");
        _;
    }

    modifier onlyEOA() {
        // Try to make flash-loan exploit harder to do by only allowing externally owned addresses.
        require(msg.sender == tx.origin, "PandoPot: must use EOA");
        _;
    }

    modifier onlyWhitelist() {
        require(whitelist[msg.sender], 'PandoPot: caller is not in the whitelist');
        _;
    }

    modifier onlyRNG() {
        require(msg.sender == randomNumberGenerator, 'PandoPot: !RNG');
        _;
    }

    function toggleWhitelist(address _addr) external onlyOwner {
        whitelist[_addr] = !whitelist[_addr];
        emit WhitelistChanged(_addr, whitelist[_addr]);
    }

    function allocatePSR(uint256 _amount) external onlyOwner {
        totalPSRAllocated += _amount;
        IERC20(PSR).safeTransferFrom(msg.sender, address(this), _amount);
        emit PSRAllocated(_amount);
    }

    function changeTimeBomb(uint256 _second) external onlyOwner {
        uint256 oldSecond = timeBomb;
        timeBomb = _second;
        emit TimeBombChanged(oldSecond, _second);
    }

    function pause() external onlyOwner {
        _pause();
    }

    function unpause() external onlyOwner {
        _unpause();
    }

    function emergencyWithdraw() external onlyOwner whenPaused {
        IERC20 _USD = IERC20(USD);
        IERC20 _PSR = IERC20(PSR);
        IERC20 _PAN = IERC20(PAN);
        uint256 _USDAmount = _USD.balanceOf(address(this));
        uint256 _PSRAmount = _PSR.balanceOf(address(this));
        uint256 _PANAmount = _PAN.balanceOf(address(this));
        _USD.safeTransfer(owner(), _USDAmount);
        _PSR.safeTransfer(owner(), _PSRAmount);
        _PAN.safeTransfer(owner(), _PANAmount);
        emit EmergencyWithdraw(owner(), _USDAmount, _PSRAmount, _PANAmount);
    }

    function changeRewardExpireTime(uint256 _newExpireTime) external onlyOwner whenPaused {
        uint256 _oldExpireTIme = prizeExpireTime;
        prizeExpireTime = _newExpireTime;
        emit RewardExpireTimeChanged(_oldExpireTIme, _newExpireTime);
    }

    function changeRandomNumberGenerator(address _rng) external onlyOwner whenPaused {
        address _oldRNG = randomNumberGenerator;
        randomNumberGenerator = _rng;
        emit RandomNumberGeneratorChanged(_oldRNG, _rng);
    }

    function changeRoundDuration(uint256 _newDuration) external onlyOwner whenPaused {
        uint256 _oldDuration = roundDuration;
        roundDuration = _newDuration;
        emit RoundDurationChanged(_oldDuration, _newDuration);
    }

    function changeMultiOracle(address _newOracle) external onlyOwner {
        address  _oldOracle = multiOracle;
        multiOracle = _newOracle;
        emit MultiOracleChanged(_oldOracle, _newOracle);
    }


    function changePanBurnPercent(uint256 _newPercent) external onlyOwner {
        uint256 _oldPercent = panBurnPercent;
        panBurnPercent = _newPercent;
        emit PanBurnPercentChanged(_oldPercent, _newPercent);
    }

//    function changeDiscount(uint256 _percentage, uint256 _usd) external onlyOwner {
//        require(_percentage <= ONE_HUNDRED_PERCENT, "PandoPot: over 100 percentage");
//        require(_usd <=  ticketPrice, "PandoPot: over price");
//        discountPercentage = _percentage;
//        discountPerAmount = _usd;
//        emit DiscountChange(discountPercentage, discountPerAmount);
//    }

    function changePriceTicket(uint256 _value) external onlyOwner {
        uint256 _old = ticketPrice;
        ticketPrice = _value;
        emit PriceTicketChanged(_old, ticketPrice);
    }

    function changeNumberFreeTicket(uint256 _value) external onlyOwner {
        uint256 _old = numFreeTicket;
        numFreeTicket = _value;
        emit NumberFreeTicketChanged(_old, numFreeTicket);
    }

    function updatePrizeInfo(uint256 _sampleSpace, uint256 _numberOfWinner, uint256 _percentage, string memory _name, PRIZE_TYPE _types) external onlyOwner whenPaused {
        require(_percentage + totalPrizePercentage <= ONE_HUNDRED_PERCENT, "PandoPot: percentage over");
        require(_numberOfWinner > 0 , "PandoPot: _numberOfWinner zero");
        require(_sampleSpace > 0 , "PandoPot: _sampleSpace zero");
        TypePrize storage _typePrize = typePrize[_types];
        totalPrizePercentage += _percentage - _typePrize.percentage;
        _typePrize.sampleSpace = _sampleSpace;
        _typePrize.winners = _numberOfWinner;
        _typePrize.percentage = _percentage;
        _typePrize.name = _name;
        _typePrize.types = _types;
        emit PrizeUpdated(_types, _sampleSpace, _numberOfWinner, _percentage, _name);
    }

    function changeDiscountPercentage(uint256[] memory _percentages) external onlyOwner {
        require(_percentages.length == 21, "PandoPot: Overflow");
        for(uint i = 0; i < _percentages.length; i++) {
            require(_percentages[i] < ONE_HUNDRED_PERCENT, "PandoPot: over 100 percent");
            discountPercentages[i] = _percentages[i];
        }
        emit DiscountChange(_percentages);
    }
    /*----------------------------EVENTS----------------------------*/

    event PriceForWinner(uint256 _roundId, PRIZE_TYPE _type, uint256 USDForWinner, uint256 PSRForWinner, uint256 PANForWinner);
    event NewTicket(uint256 indexed roundId, address indexed user, uint256[] numbers, uint256 indexed _type);//_type: 0-BuyTicket, 1-EnterTicket
    event Claimed(PRIZE_TYPE _type, uint256 roundId, uint256 ticketNumber, uint256 USD, uint256 PSR,uint256 PAN, address receiver);
    event Liquidated(PRIZE_TYPE _type, uint256 id, address owner, uint256 USD, uint256 PSR, uint256 PAN);
    event WhitelistChanged(address indexed whitelist, bool status);
    event PSRAllocated(uint256 amount);
    event TimeBombChanged(uint256 oldValueSecond, uint256 newValueSecond);
    event EmergencyWithdraw(address owner, uint256 USD, uint256 PSR, uint256 PAN);
    event RewardExpireTimeChanged(uint256 oldExpireTime, uint256 newExpireTime);
    event RandomNumberGeneratorChanged(address indexed _oldRNG, address indexed _RNG);
    event RoundCompleted(uint256 roundId, uint256 expireTime, uint256[] mega, uint256[] minor, uint256[] mini);
    event RoundFinished(uint256 newRoundId);
    event Distributed(uint256 distributeId, uint256 expire, address[] leaderboards, uint256[] usdAmounts, uint[] psrAmounts, uint[] panAmounts);
    event RoundDurationChanged(uint256 oldDuration, uint256 newDuration);
    event MultiOracleChanged(address oldOracle, address newOracle);
    event PriceTicketChanged(uint256 oldPrice, uint256 newPrice);
    event NumberFreeTicketChanged(uint256 oldTicket, uint256 newTicket);
    event PanBurnPercentChanged(uint256 oldPercent, uint256 newPercent);
    event NewPrize(PRIZE_TYPE _type, uint256 _sampleSpace, uint256 _numberOfWinner, uint256 _percentage, string  _name);
    event DiscountChange(uint256[] percentages);
    event PrizeUpdated(PRIZE_TYPE _type, uint256 _sampleSpace, uint256 _numberOfWinner, uint256 _percentage, string  _name);
}

