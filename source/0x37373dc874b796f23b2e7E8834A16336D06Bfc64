
// File: contracts/LMAOPAY.sol
// SPDX-License-Identifier: Unlicensed

pragma solidity ^0.8.15;

import "@openzeppelin/contracts/utils/math/SignedSafeMath.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC20/extensions/IERC20Metadata.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import "./interfaces.sol";

contract LMAOPAY is IERC20Metadata, Ownable {
    using SignedSafeMath for int256;
    using SafeERC20 for IERC20;

    string private _name;
    string private _symbol;

    bool public initialDistributionFinished;
    bool public swapEnabled = true;
    bool public buyBackEnabled = true;
    bool public walletToWalletEnabled;
    bool public autoRebase = true;
    bool public feesOnNormalTransfers;
    bool private inSwap;

    address[] public _markerPairs;
    address DEAD = 0x000000000000000000000000000000000000dEaD;
    address ZERO = 0x0000000000000000000000000000000000000000;
    address public LIFWallet;
    address public treasuryWallet;
    address public BUSDWallet;
    address public BNBWallet;
    address public uniswapV2Pair;
    address public BUSDToken;

    uint256 public rewardYield = 4252083;
    uint256 public rewardYieldDenominator = 10000000000;
    uint256 public maxSellTransactionAmount = 2_500_000 * 10 ** 18;
    uint256 public rebaseFrequency = 1800; // 30-minutes
    uint256 public nextRebase = block.timestamp + 604800; // 7-days
    uint256 public allowedSellPercent = 100;
    uint256 public cooldownPeriod = 86400;  // 1-day
    uint256 public swapThreshold = 200_000 ether;

    uint256 private constant MAX_REBASE_FREQUENCY = 86400 * 7;   // 7-days
    uint256 private constant DECIMALS = 18;
    uint256 private constant MAX_UINT256 = type(uint256).max;
    uint256 private constant INITIAL_FRAGMENTS_SUPPLY = 5 * 10**9 * 10**DECIMALS;
    uint256 private constant TOTAL_GONS = MAX_UINT256 - (MAX_UINT256 % INITIAL_FRAGMENTS_SUPPLY);
    uint256 public constant MAX_SUPPLY = type(uint112).max;
    uint256 targetLiquidity = 5000;

    uint256 private _totalSupply;
    uint256 private _gonsPerFragment;
    uint256 private buyBackUpperLimit = 100 * 10**18;

    IUniswapV2Router02 public uniswapV2Router;

    struct feeRateStruct {
        uint256 liquidity;
        uint256 lif;
        uint256 treasury;
        uint256 busd;
        uint256 buyBack;
        uint256 bnb;
    }

    struct User {
        uint256 lastTradeTime;
        uint256 allowed;
    }

    feeRateStruct public buyFeeRates = feeRateStruct(
        {
            liquidity: 200,
            lif: 400,
            treasury: 400,
            busd: 200,
            buyBack: 50,
            bnb: 50
        }
    );

    feeRateStruct public sellFeeRates = feeRateStruct(
        {
            liquidity: 200,
            lif: 500,
            treasury: 500,
            busd: 200,
            buyBack: 50,
            bnb: 50
        }
    );

    uint256 public pendingBuyFees;
    uint256 public pendingSellFees;

    uint256 totalBuyFees = buyFeeRates.liquidity
                + buyFeeRates.lif
                + buyFeeRates.treasury
                + buyFeeRates.busd
                + buyFeeRates.buyBack
                + buyFeeRates.bnb;

    uint256 totalSellFees = sellFeeRates.liquidity
                + sellFeeRates.lif
                + sellFeeRates.treasury
                + sellFeeRates.busd
                + sellFeeRates.buyBack
                + sellFeeRates.bnb;

    mapping(address => bool) public isBlacklisted;
    mapping(address => bool) public isWhitelisted;
    mapping(address => bool) public isExcludedFromFee;
    mapping(address => bool) public isExcludedFromSellLimit;
    mapping(address => bool) public isExcludedFromW2W;
    mapping(address => bool) public automatedMarketMakerPairs;
    mapping(address => User) public tradeData;
    mapping(address => uint256) private _gonBalances;
    mapping(address => mapping(address => uint256)) private _allowedFragments;


    event SwapAndLiquify(uint256 tokensSwapped, uint256 BNBReceived, uint256 tokensIntoLiqudity);
    event SwapAndLiquifyBUSD(uint256 tokensSwapped, uint256 BUSDReceived, uint256 tokensIntoLiqudity);
    event LogRebase(uint256 indexed epoch, uint256 totalSupply);
    event SetAutomatedMarketMakerPair(address indexed pair, bool indexed value);
    event FeesChanged();


    modifier swapping() {
        inSwap = true;
        _;
        inSwap = false;
    }

    constructor(
        string memory name_, 
        string memory symbol_,
        address router_,
        address busdToken_,
        address LIFWallet_,
        address treasuryWallet_,
        address BNBWallet_,
        address BUSDWallet_
    ) {
        require(router_ != address(0), "Zero Address");
        require(busdToken_ != address(0), "Zero Address");
        require(LIFWallet_ != address(0), "Zero Address");
        require(treasuryWallet_ != address(0), "Zero Address");
        require(BNBWallet_ != address(0), "Zero Address");
        require(BUSDWallet_ != address(0), "Zero Address");

        _name = name_;
        _symbol = symbol_;

        BUSDToken = busdToken_;
        LIFWallet = LIFWallet_;
        treasuryWallet = treasuryWallet_;
        BNBWallet = BNBWallet_;
        BUSDWallet = BUSDWallet_;

        uniswapV2Router = IUniswapV2Router02(router_);
        uniswapV2Pair = IUniswapV2Factory(uniswapV2Router.factory()).createPair(address(this), busdToken_);

        _allowedFragments[address(this)][address(uniswapV2Router)] = type(uint256).max;
        _allowedFragments[address(this)][address(this)] = type(uint256).max;

        setAutomatedMarketMakerPair(uniswapV2Pair, true);

        _totalSupply = INITIAL_FRAGMENTS_SUPPLY;
        _gonBalances[msg.sender] = TOTAL_GONS;
        _gonsPerFragment = TOTAL_GONS / _totalSupply;

        isExcludedFromFee[address(this)] = true;
        isExcludedFromFee[msg.sender] = true;

        isExcludedFromSellLimit[address(this)] = true;
        isExcludedFromSellLimit[msg.sender] = true;

        isExcludedFromW2W[address(this)] = true;
        isExcludedFromW2W[msg.sender] = true;

        isWhitelisted[msg.sender] = true;

        IERC20(busdToken_).approve(address(uniswapV2Router), type(uint256).max);

        emit Transfer(address(0x0), msg.sender, _totalSupply);
    }

    /* --------------------------------------------- ERC-20 FUNCTIONS ----------------------------------------------- */

    function name() public view returns (string memory) {
        return _name;
    }

    function symbol() public view returns (string memory) {
        return _symbol;
    }

    function decimals() public pure returns (uint8) {
        return uint8(DECIMALS);
    }

    function totalSupply() external view override returns (uint256) {
        return _totalSupply;
    }

    function allowance(address owner_, address spender) external view override returns (uint256){
        return _allowedFragments[owner_][spender];
    }

    function balanceOf(address who) public view override returns (uint256) {
        return _gonBalances[who] / _gonsPerFragment;
    }

    function transfer(address recipient, uint256 value) external override returns (bool) {
        _transfer(_msgSender(), recipient, value);
        return true;
    }

    function transferFrom(address sender, address recipient, uint256 amount) public virtual override returns (bool) {
        _transfer(sender, recipient, amount);

        if (_allowedFragments[sender][msg.sender] != type(uint256).max) {
            require(_allowedFragments[sender][msg.sender] >= amount, "ERC20: transfer amount exceeds allowance");

            _allowedFragments[sender][msg.sender] = _allowedFragments[sender][msg.sender] - amount;
        }

        return true;
    }

    function approve(address spender, uint256 value) external override returns (bool) {
        _allowedFragments[msg.sender][spender] = value;

        emit Approval(msg.sender, spender, value);

        return true;
    }

    function decreaseAllowance(address spender, uint256 subtractedValue) external returns (bool) { 
        uint256 oldValue = _allowedFragments[msg.sender][spender];

        if (subtractedValue >= oldValue) {
            _allowedFragments[msg.sender][spender] = 0;
        } else {
            _allowedFragments[msg.sender][spender] = oldValue - subtractedValue;
        }
        
        emit Approval(msg.sender, spender, _allowedFragments[msg.sender][spender]);
        
        return true;
    }

    function increaseAllowance(address spender, uint256 addedValue) external returns (bool) {
        _allowedFragments[msg.sender][spender] = _allowedFragments[msg.sender][spender] + addedValue;
        
        emit Approval(msg.sender, spender, _allowedFragments[msg.sender][spender]);

        return true;
    }

    /* ----------------------------------------- CUSTOM FUNCTIONS --------------------------------------------------- */
    
    function percent(uint256 amount, uint256 fraction) public virtual pure returns(uint256) {
        return ((amount * fraction) / 10000);
    }

    function getCirculatingSupply() public view returns (uint256) {
        return (TOTAL_GONS - _gonBalances[DEAD] - _gonBalances[ZERO]) / _gonsPerFragment;
    }

    function getLiquidityBacking() public view returns (uint256){
        uint256 liquidityBalance = 0;

        for(uint256 i = 0; i < _markerPairs.length; i++){
            liquidityBalance += (balanceOf(_markerPairs[i]) / 10 ** 9);
        }

        return (liquidityBalance * 10000) / (getCirculatingSupply() / 10 ** 9);
    }

    function isOverLiquified(uint256 target) public view returns (bool){
        return getLiquidityBacking() > target;
    }

    function manualSync() public {
        for(uint256 i = 0; i < _markerPairs.length; i++){
            IUniswapV2Pair(_markerPairs[i]).sync();
        }
    }

    function shouldSwapBack(address sender) internal view returns (bool) {
        return
        !automatedMarketMakerPairs[sender] &&
        !inSwap &&
        swapEnabled &&
        (totalBuyFees + totalSellFees) > 0 &&
        balanceOf(address(this)) >= swapThreshold;
    }

    function _transfer(address sender, address recipient, uint256 amount) internal returns (bool) {
        require(recipient != address(0), "Not valid recipient");
        require(!isBlacklisted[sender] && !isBlacklisted[recipient], "Blacklisted");

        bool isExcludedW2W = isExcludedFromW2W[sender] || isExcludedFromW2W[recipient];

        require(initialDistributionFinished || isExcludedW2W, "Trading not started");

        if (!walletToWalletEnabled) {
            require(automatedMarketMakerPairs[sender] || automatedMarketMakerPairs[recipient] || isExcludedW2W, "Wallet to wallet is disabled");
        }

        if (automatedMarketMakerPairs[recipient] && !(isExcludedFromSellLimit[sender] || isExcludedFromSellLimit[recipient])) { 
            require(amount <= maxSellTransactionAmount, "Error amount");

            uint256 onePercent = balanceOf(sender) * allowedSellPercent / 10000; //Should use variable
            require(amount <= onePercent, "ERR: Can't sell more than allowed %");
            
            uint256 blockTime = block.timestamp;
            
            if (blockTime > tradeData[sender].lastTradeTime + cooldownPeriod) {
                tradeData[sender].lastTradeTime = blockTime;
                tradeData[sender].allowed = onePercent - amount;
            } else if((blockTime < tradeData[sender].lastTradeTime + cooldownPeriod) && 
                     ((blockTime >= tradeData[sender].lastTradeTime))) {
                require(amount <= tradeData[sender].allowed, "ERR: Can't sell more than allowed % in Cooldown Period");
                tradeData[sender].lastTradeTime = blockTime;
                tradeData[sender].allowed = tradeData[sender].allowed - amount;
            }
        }

        if (inSwap) {
            uint256 gonAmount = amount * _gonsPerFragment;

            _gonBalances[sender] = _gonBalances[sender] - gonAmount;
            _gonBalances[recipient] = _gonBalances[recipient] + gonAmount;

            emit Transfer(sender, recipient, amount);

            return true;
        }

        if (shouldSwapBack(sender)) {
            uint256 balance = IERC20(BUSDToken).balanceOf(address(this));
            
            if (buyBackEnabled && balance > uint256(100 * 10**18) && automatedMarketMakerPairs[recipient]) {
                if (balance > buyBackUpperLimit) balance = buyBackUpperLimit;
                buyBackTokens(balance);
            }

            swapBack();
        }

        _tokenTransfer(sender, recipient, amount);

        if((nextRebase <= block.timestamp) && autoRebase) {
            _rebase();

            if(!automatedMarketMakerPairs[sender] && !automatedMarketMakerPairs[recipient]){
                manualSync();
            }
        }

        return true;
    }

    function _tokenTransfer(address sender, address recipient, uint256 amount) internal {
        uint256 gonAmount = amount * _gonsPerFragment;
        uint256 gonAmountReceived;

        _gonBalances[sender] = _gonBalances[sender] - gonAmount;

        if (isExcludedFromFee[sender] || isExcludedFromFee[recipient]) {
            gonAmountReceived = gonAmount;

        } else {
            uint256 feeAmount;

            if (automatedMarketMakerPairs[sender]) {
                feeAmount = percent(gonAmount, totalBuyFees);
                pendingBuyFees += feeAmount / _gonsPerFragment;

            } else if (automatedMarketMakerPairs[recipient]) {
                feeAmount = percent(gonAmount, totalSellFees);
                pendingSellFees += feeAmount / _gonsPerFragment;

            } else if (feesOnNormalTransfers) {
                feeAmount = percent(gonAmount, totalSellFees);
                pendingBuyFees += feeAmount / _gonsPerFragment;
            }

            gonAmountReceived = gonAmount - feeAmount;

            if (feeAmount > 0) {
                _gonBalances[address(this)] = _gonBalances[address(this)] + feeAmount;

                emit Transfer(sender, address(this), feeAmount / _gonsPerFragment);
            }
        }

        _gonBalances[recipient] = _gonBalances[recipient] + gonAmountReceived;

        emit Transfer(sender, recipient, gonAmountReceived / _gonsPerFragment);
    }

    /* ---------------------------------- LIQUIDITY, BUY BACK & BURN FUNCTIONS -------------------------------------- */

    function _addLiquidity(uint256 tokenAmount, uint256 busdAmount) private {
        uniswapV2Router.addLiquidity(
            address(this),
            BUSDToken,
            tokenAmount,
            busdAmount,
            0,
            0,
            owner(),
            block.timestamp
        );
    }

    function _swapTokensForBNB(uint256 amount, address to) private {
        address[] memory path = new address[](3);
        path[0] = address(this);
        path[1] = BUSDToken;
        path[2] = uniswapV2Router.WETH();

        uniswapV2Router.swapExactTokensForETHSupportingFeeOnTransferTokens(
            amount,
            0,
            path,
            to,
            block.timestamp
        );
    }

    function _swapTokensForBUSD(uint256 amount, address to) private {
        uint256 initialBnbBalance = address(this).balance;

        _swapTokensForBNB(amount, address(this));
        _swapBNBForBUSD(address(this).balance - initialBnbBalance, to);
    }
    
    function _swapBUSDForTokens(uint256 amount, address to) private {
        address[] memory path = new address[](2);
        path[0] = BUSDToken;
        path[1] = address(this);

        uniswapV2Router.swapExactTokensForTokensSupportingFeeOnTransferTokens(
            amount,
            0,
            path,
            to,
            block.timestamp
        );
    }

    function _swapBNBForBUSD(uint256 amount, address to) private {
        address[] memory path = new address[](2);
        path[0] = uniswapV2Router.WETH();
        path[1] = BUSDToken;

        uniswapV2Router.swapExactETHForTokens{value: amount}(
            0,
            path,
            to,
            block.timestamp
        );
    }

    function buyBackTokens(uint256 amount) private swapping {
    	if (amount > 0) {
    	    _swapBUSDForTokens(amount, DEAD);
	    }
    }

    function _swapAndLiquify(uint256 contractTokenBalance) private {
        uint256 half = contractTokenBalance / 2;
        uint256 otherHalf = contractTokenBalance - half;

        uint256 initialBalance = IERC20(BUSDToken).balanceOf(address(this));

        _swapTokensForBUSD(half, address(this));

        uint256 newBalance = IERC20(BUSDToken).balanceOf(address(this)) - initialBalance;

        _addLiquidity(otherHalf, newBalance);

        emit SwapAndLiquifyBUSD(half, newBalance, otherHalf);
    }

    function swapBack() internal swapping {
        uint256 totalFeesToSwap = swapThreshold;

        uint256 liquidityFees = (totalFeesToSwap * (buyFeeRates.liquidity + sellFeeRates.liquidity)) / (totalBuyFees + totalSellFees);

        uint256 LIFFees = (totalFeesToSwap * (buyFeeRates.lif + sellFeeRates.lif)) / (totalBuyFees + totalSellFees);

        uint256 treasureFees = (totalFeesToSwap * (buyFeeRates.treasury + sellFeeRates.treasury)) / (totalBuyFees + totalSellFees);

        uint256 BUSDFees = (totalFeesToSwap * (buyFeeRates.busd + sellFeeRates.busd)) / (totalBuyFees + totalSellFees);

        uint256 buyBackFees = (totalFeesToSwap * (buyFeeRates.buyBack + sellFeeRates.buyBack)) / (totalBuyFees + totalSellFees);

        uint256 BNBFees = (totalFeesToSwap * (buyFeeRates.bnb + sellFeeRates.bnb)) / (totalBuyFees + totalSellFees);

        // Adding Liquidity Fees
        if(!isOverLiquified(targetLiquidity) && liquidityFees > 0) {
            _swapAndLiquify(liquidityFees);
        }

        // Paying LIF Fees
        _gonBalances[LIFWallet] = _gonBalances[LIFWallet] + (LIFFees * _gonsPerFragment);
        _gonBalances[address(this)] = _gonBalances[address(this)] - (LIFFees * _gonsPerFragment);

        // Paying Treasury Fees
        if (treasureFees > 0) {
            _swapTokensForBNB(treasureFees, treasuryWallet);
        }

        // Paying BUSD Fees
        if (BUSDFees > 0) {
            _swapTokensForBUSD(BUSDFees, BUSDWallet);
        }

        // Paying Buy Back Fees
        if (buyBackFees > 0) {
            _swapTokensForBUSD(buyBackFees, address(this));
        }

        // Paying BNB Fees
        if (BNBFees > 0) {
            _swapTokensForBNB(BNBFees, BNBWallet);
        }

        pendingBuyFees = (balanceOf(address(this)) * totalBuyFees) / (totalBuyFees + totalSellFees);
        pendingSellFees = (balanceOf(address(this)) * totalSellFees) / (totalBuyFees + totalSellFees);
    }

    /* --------------------------------------------- REBASE FUNCTIONS ----------------------------------------------- */

    function _rebase() private {
        if(!inSwap) {
            uint256 circulatingSupply = getCirculatingSupply();
            int256 supplyDelta = int256(circulatingSupply * rewardYield / rewardYieldDenominator);

            coreRebase(supplyDelta);
        }
    }

    function coreRebase(int256 supplyDelta) private returns (uint256) {
        uint256 epoch = block.timestamp;

        if (supplyDelta == 0) {
            emit LogRebase(epoch, _totalSupply);
            return _totalSupply;
        }

        if (supplyDelta < 0) {
            _totalSupply = _totalSupply - uint256(-supplyDelta);
        } else {
            _totalSupply = _totalSupply + uint256(supplyDelta);
        }

        if (_totalSupply > MAX_SUPPLY) {
            _totalSupply = MAX_SUPPLY;
        }

        _gonsPerFragment = TOTAL_GONS / _totalSupply;

        nextRebase = epoch + rebaseFrequency;

        emit LogRebase(epoch, _totalSupply);

        return _totalSupply;
    }

    function manualRebase() external {
        require(!inSwap, "Try again");
        require(isWhitelisted[msg.sender], "Not Whitelisted");
        require(nextRebase <= block.timestamp, "Not in time");

        uint256 circulatingSupply = getCirculatingSupply();
        int256 supplyDelta = int256(circulatingSupply * rewardYield / rewardYieldDenominator);

        coreRebase(supplyDelta);
        manualSync();
    }

    /* -------------------------------------------- OWNER ONLY FUNCTIONS -------------------------------------------- */

    function setAutomatedMarketMakerPair(address _pair, bool _value) public onlyOwner {
        require(automatedMarketMakerPairs[_pair] != _value, "Value already set");

        automatedMarketMakerPairs[_pair] = _value;

        if(_value){
            _markerPairs.push(_pair);
        } else {
            require(_markerPairs.length > 1, "Required 1 pair");
            for (uint256 i = 0; i < _markerPairs.length; i++) {
                if (_markerPairs[i] == _pair) {
                    _markerPairs[i] = _markerPairs[_markerPairs.length - 1];
                    _markerPairs.pop();
                    break;
                }
            }
        }

        emit SetAutomatedMarketMakerPair(_pair, _value);
    }

    function addToWhitelist(address[] memory account) external onlyOwner {
        for(uint256 i=0; i < account.length; i++) {
            isWhitelisted[account[i]] = true;
        }
    }

    function removeFromWhitelist(address[] memory account) external onlyOwner {
        for(uint256 i=0; i < account.length; i++) {
            isWhitelisted[account[i]] = false;
        }
    }

    function addToBlacklist(address[] memory account) external onlyOwner {
        for(uint256 i=0; i < account.length; i++) {
            isBlacklisted[account[i]] = true;
        }
    }

    function removeFromBlacklist(address[] memory account) external onlyOwner {
        for(uint256 i=0; i < account.length; i++) {
            isBlacklisted[account[i]] = false;
        }
    }

    function setWalletToWalletEnabled(bool _enabled) external onlyOwner {
        walletToWalletEnabled = _enabled;
    }

    function setInitialDistributionFinished(bool _value) external onlyOwner {
        require(initialDistributionFinished != _value, "Not changed");
        initialDistributionFinished = _value;
    }

    function includeInFee(address[] memory account) external onlyOwner {
        for(uint256 i=0; i < account.length; i++) {
            isExcludedFromFee[account[i]] = false;
        }
    }

    function excludeFromFee(address[] memory account) external onlyOwner {
        for(uint256 i=0; i < account.length; i++) {
            isExcludedFromFee[account[i]] = true;
        }
    }

    function includeInSellLimit(address[] memory account) external onlyOwner {
        for(uint256 i=0; i < account.length; i++) {
            isExcludedFromSellLimit[account[i]] = false;
        }
    }

    function excludeFromSellLimit(address[] memory account) external onlyOwner {
        for(uint256 i=0; i < account.length; i++) {
            isExcludedFromSellLimit[account[i]] = true;
        }
    }

    function includeInW2W(address[] memory account) external onlyOwner {
        for(uint256 i=0; i < account.length; i++) {
            isExcludedFromW2W[account[i]] = false;
        }
    }

    function excludeFromW2W(address[] memory account) external onlyOwner {
        for(uint256 i=0; i < account.length; i++) {
            isExcludedFromW2W[account[i]] = true;
        }
    }

    function setAllowedSellingPercent(uint256 _allowedSellPercent) external onlyOwner {
        allowedSellPercent = _allowedSellPercent;
    }

    function setCooldownPeriod(uint256 _cooldownPeriod) external onlyOwner {
        cooldownPeriod = _cooldownPeriod;
    }

    function setTargetLiquidity(uint256 target) external onlyOwner {
        targetLiquidity = target;
    }

    function setSwapBackSettings(bool _enabled) external onlyOwner {
        swapEnabled = _enabled;
    }

    function setSwapThreshold(uint256 _swapThreshold) external onlyOwner {
        swapThreshold = _swapThreshold;
    }

    function setBuyBackSettings(bool _enabled) external onlyOwner {
        buyBackEnabled = _enabled;
    }

    function setBuybackUpperLimit(uint256 buyBackLimit) external onlyOwner {
        buyBackUpperLimit = buyBackLimit;
    }

    function setFeeAddresses(
        address _LIFWallet, 
        address _treasuryWallet, 
        address _BUSDWallet,
        address _BNBWallet
    ) external onlyOwner {
        require(_LIFWallet != address(0), "Zero Address");
        require(_treasuryWallet != address(0), "Zero Address");
        require(_BUSDWallet != address(0), "Zero Address");
        require(_BNBWallet != address(0), "Zero Address");

        LIFWallet = _LIFWallet;
        treasuryWallet = _treasuryWallet;
        BUSDWallet = _BUSDWallet;
        BNBWallet = _BNBWallet;
    }

    function setFeeRates(feeRateStruct memory _buyFeeRates, feeRateStruct memory _sellFeeRates) external onlyOwner {
         uint256 _totalBuyFees = _buyFeeRates.liquidity
                + _buyFeeRates.lif
                + _buyFeeRates.treasury
                + _buyFeeRates.busd
                + _buyFeeRates.buyBack
                + _buyFeeRates.bnb;

        uint256 _totalSellFees = _sellFeeRates.liquidity
                + _sellFeeRates.lif
                + _sellFeeRates.treasury
                + _sellFeeRates.busd
                + _sellFeeRates.buyBack
                + _sellFeeRates.bnb;
        
        require(_totalBuyFees + _totalSellFees <= 3000, "Total Tax above 30%");
        
        buyFeeRates = _buyFeeRates;
        sellFeeRates = _sellFeeRates;
        totalBuyFees = _totalBuyFees;
        totalSellFees = _totalSellFees;

        emit FeesChanged();
    }

    function setAutoRebase(bool _autoRebase) external onlyOwner {
        require(autoRebase != _autoRebase, "Not changed");
        autoRebase = _autoRebase;
    }

    function setRebaseFrequency(uint256 _rebaseFrequency) external onlyOwner {
        require(_rebaseFrequency <= MAX_REBASE_FREQUENCY, "Too high");
        rebaseFrequency = _rebaseFrequency;
    }

    function setRewardYield(uint256 _rewardYield, uint256 _rewardYieldDenominator) external onlyOwner {
        rewardYield = _rewardYield;
        rewardYieldDenominator = _rewardYieldDenominator;
    }

    function setFeesOnNormalTransfers(bool _enabled) external onlyOwner {
        require(feesOnNormalTransfers != _enabled, "Not changed");
        feesOnNormalTransfers = _enabled;
    }

    function setNextRebase(uint256 _nextRebase) external onlyOwner {
        nextRebase = _nextRebase;
    }

    function setMaxSellTransaction(uint256 _maxTxn) external onlyOwner {
        maxSellTransactionAmount = _maxTxn;
    }

    function setBUSDToken(address _busdToken) external onlyOwner {
        BUSDToken = _busdToken;

        address get_pair = IUniswapV2Factory(uniswapV2Router.factory()).getPair(address(this), BUSDToken);

        if (get_pair == address(0)) {
            uniswapV2Pair = IUniswapV2Factory(uniswapV2Router.factory()).createPair(address(this), BUSDToken);
        } else {
            uniswapV2Pair = get_pair;
        }

        IERC20(BUSDToken).approve(address(uniswapV2Router), type(uint256).max);
        IERC20(BUSDToken).approve(address(uniswapV2Pair), type(uint256).max);
    }

    /**
     * @dev Update router address in case of pancakeswap migration
     */
    function setRouterAddress(address newRouter) external onlyOwner {
        require(newRouter != address(uniswapV2Router));

        IUniswapV2Router02 _newRouter = IUniswapV2Router02(newRouter);

        address get_pair = IUniswapV2Factory(_newRouter.factory()).getPair(address(this), BUSDToken);

        if (get_pair == address(0)) {
            uniswapV2Pair = IUniswapV2Factory(_newRouter.factory()).createPair(address(this), BUSDToken);
        } else {
            uniswapV2Pair = get_pair;
        }

        uniswapV2Router = _newRouter;

        _allowedFragments[address(this)][address(uniswapV2Router)] = type(uint256).max;

        IERC20(BUSDToken).approve(address(uniswapV2Router), type(uint256).max);
        IERC20(BUSDToken).approve(address(uniswapV2Pair), type(uint256).max);
    }


    function approveFromContract(address token, address spender, uint256 value) external onlyOwner {
        if (token != address(this)) {
            IERC20(token).approve(spender, value);
        } else {
            _allowedFragments[address(this)][spender] = value;
        }
    }

    /**
     * @dev 
     */
    function approveFromContractMax(address token, address spender) external onlyOwner {
        if (token != address(this)) {
            IERC20(token).approve(spender, type(uint256).max);
        } else {
            _allowedFragments[address(this)][spender] = type(uint256).max;
        }
    }

    /**
     * @dev Withdraw BNB Dust
     */
    function withdrawDust(address to, uint256 weiAmount) external onlyOwner {
        require(address(this).balance >= weiAmount, "insufficient BNB balance");
        (bool sent, ) = payable(to).call{value: weiAmount}("");
        require(sent, "Failed to withdraw");
    }

    /**
     * @dev Withdraw ERC-20 Dust
     */
    function withdrawERC20Dust(address token, address to, uint256 amount) external onlyOwner {
        require(IERC20(token).balanceOf(address(this)) >= amount, "insufficient token balance");
        
        IERC20(token).transfer(to, amount);
    }

    receive() external payable {}
}


// File: @openzeppelin/contracts/utils/math/SignedSafeMath.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/math/SignedSafeMath.sol)

pragma solidity ^0.8.0;

/**
 * @dev Wrappers over Solidity's arithmetic operations.
 *
 * NOTE: `SignedSafeMath` is no longer needed starting with Solidity 0.8. The compiler
 * now has built in overflow checking.
 */
library SignedSafeMath {
    /**
     * @dev Returns the multiplication of two signed integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `*` operator.
     *
     * Requirements:
     *
     * - Multiplication cannot overflow.
     */
    function mul(int256 a, int256 b) internal pure returns (int256) {
        return a * b;
    }

    /**
     * @dev Returns the integer division of two signed integers. Reverts on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator.
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(int256 a, int256 b) internal pure returns (int256) {
        return a / b;
    }

    /**
     * @dev Returns the subtraction of two signed integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(int256 a, int256 b) internal pure returns (int256) {
        return a - b;
    }

    /**
     * @dev Returns the addition of two signed integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `+` operator.
     *
     * Requirements:
     *
     * - Addition cannot overflow.
     */
    function add(int256 a, int256 b) internal pure returns (int256) {
        return a + b;
    }
}


// File: @openzeppelin/contracts/access/Ownable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}


// File: @openzeppelin/contracts/token/ERC20/extensions/IERC20Metadata.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/extensions/IERC20Metadata.sol)

pragma solidity ^0.8.0;

import "../IERC20.sol";

/**
 * @dev Interface for the optional metadata functions from the ERC20 standard.
 *
 * _Available since v4.1._
 */
interface IERC20Metadata is IERC20 {
    /**
     * @dev Returns the name of the token.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the symbol of the token.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the decimals places of the token.
     */
    function decimals() external view returns (uint8);
}


// File: @openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (token/ERC20/utils/SafeERC20.sol)

pragma solidity ^0.8.0;

import "../IERC20.sol";
import "../extensions/draft-IERC20Permit.sol";
import "../../../utils/Address.sol";

/**
 * @title SafeERC20
 * @dev Wrappers around ERC20 operations that throw on failure (when the token
 * contract returns false). Tokens that return no value (and instead revert or
 * throw on failure) are also supported, non-reverting calls are assumed to be
 * successful.
 * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,
 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.
 */
library SafeERC20 {
    using Address for address;

    function safeTransfer(
        IERC20 token,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));
    }

    function safeTransferFrom(
        IERC20 token,
        address from,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));
    }

    /**
     * @dev Deprecated. This function has issues similar to the ones found in
     * {IERC20-approve}, and its usage is discouraged.
     *
     * Whenever possible, use {safeIncreaseAllowance} and
     * {safeDecreaseAllowance} instead.
     */
    function safeApprove(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        // safeApprove should only be called when setting an initial allowance,
        // or when resetting it to zero. To increase and decrease it, use
        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
        require(
            (value == 0) || (token.allowance(address(this), spender) == 0),
            "SafeERC20: approve from non-zero to non-zero allowance"
        );
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
    }

    function safeIncreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        uint256 newAllowance = token.allowance(address(this), spender) + value;
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
    }

    function safeDecreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            uint256 newAllowance = oldAllowance - value;
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
        }
    }

    function safePermit(
        IERC20Permit token,
        address owner,
        address spender,
        uint256 value,
        uint256 deadline,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) internal {
        uint256 nonceBefore = token.nonces(owner);
        token.permit(owner, spender, value, deadline, v, r, s);
        uint256 nonceAfter = token.nonces(owner);
        require(nonceAfter == nonceBefore + 1, "SafeERC20: permit did not succeed");
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     */
    function _callOptionalReturn(IERC20 token, bytes memory data) private {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We use {Address.functionCall} to perform this call, which verifies that
        // the target address contains contract code and also asserts for success in the low-level call.

        bytes memory returndata = address(token).functionCall(data, "SafeERC20: low-level call failed");
        if (returndata.length > 0) {
            // Return data is optional
            require(abi.decode(returndata, (bool)), "SafeERC20: ERC20 operation did not succeed");
        }
    }
}


// File: contracts/interfaces.sol
pragma solidity ^0.8.4;

// SPDX-License-Identifier: MIT

interface IUniswapV2Factory {
    event PairCreated(address indexed token0, address indexed token1, address pair, uint);

    function feeTo() external view returns (address);
    function feeToSetter() external view returns (address);

    function getPair(address tokenA, address tokenB) external view returns (address pair);
    function allPairs(uint) external view returns (address pair);
    function allPairsLength() external view returns (uint);

    function createPair(address tokenA, address tokenB) external returns (address pair);

    function setFeeTo(address) external;
    function setFeeToSetter(address) external;
}


interface IUniswapV2Pair {
    event Approval(address indexed owner, address indexed spender, uint value);
    event Transfer(address indexed from, address indexed to, uint value);

    function name() external pure returns (string memory);
    function symbol() external pure returns (string memory);
    function decimals() external pure returns (uint8);
    function totalSupply() external view returns (uint);
    function balanceOf(address owner) external view returns (uint);
    function allowance(address owner, address spender) external view returns (uint);

    function approve(address spender, uint value) external returns (bool);
    function transfer(address to, uint value) external returns (bool);
    function transferFrom(address from, address to, uint value) external returns (bool);

    function DOMAIN_SEPARATOR() external view returns (bytes32);
    function PERMIT_TYPEHASH() external pure returns (bytes32);
    function nonces(address owner) external view returns (uint);

    function permit(address owner, address spender, uint value, uint deadline, uint8 v, bytes32 r, bytes32 s) external;

    event Mint(address indexed sender, uint amount0, uint amount1);
    event Burn(address indexed sender, uint amount0, uint amount1, address indexed to);
    event Swap(
        address indexed sender,
        uint amount0In,
        uint amount1In,
        uint amount0Out,
        uint amount1Out,
        address indexed to
    );
    event Sync(uint112 reserve0, uint112 reserve1);

    function MINIMUM_LIQUIDITY() external pure returns (uint);
    function factory() external view returns (address);
    function token0() external view returns (address);
    function token1() external view returns (address);
    function getReserves() external view returns (uint112 reserve0, uint112 reserve1, uint32 blockTimestampLast);
    function price0CumulativeLast() external view returns (uint);
    function price1CumulativeLast() external view returns (uint);
    function kLast() external view returns (uint);

    function mint(address to) external returns (uint liquidity);
    function burn(address to) external returns (uint amount0, uint amount1);
    function swap(uint amount0Out, uint amount1Out, address to, bytes calldata data) external;
    function skim(address to) external;
    function sync() external;

    function initialize(address, address) external;
}


interface IUniswapV2Router01 {
    function factory() external pure returns (address);
    function WETH() external pure returns (address);

    function addLiquidity(
        address tokenA,
        address tokenB,
        uint amountADesired,
        uint amountBDesired,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline
    ) external returns (uint amountA, uint amountB, uint liquidity);
    function addLiquidityETH(
        address token,
        uint amountTokenDesired,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline
    ) external payable returns (uint amountToken, uint amountETH, uint liquidity);
    function removeLiquidity(
        address tokenA,
        address tokenB,
        uint liquidity,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline
    ) external returns (uint amountA, uint amountB);
    function removeLiquidityETH(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline
    ) external returns (uint amountToken, uint amountETH);
    function removeLiquidityWithPermit(
        address tokenA,
        address tokenB,
        uint liquidity,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline,
        bool approveMax, uint8 v, bytes32 r, bytes32 s
    ) external returns (uint amountA, uint amountB);
    function removeLiquidityETHWithPermit(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline,
        bool approveMax, uint8 v, bytes32 r, bytes32 s
    ) external returns (uint amountToken, uint amountETH);
    function swapExactTokensForTokens(
        uint amountIn,
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external returns (uint[] memory amounts);
    function swapTokensForExactTokens(
        uint amountOut,
        uint amountInMax,
        address[] calldata path,
        address to,
        uint deadline
    ) external returns (uint[] memory amounts);
    function swapExactETHForTokens(uint amountOutMin, address[] calldata path, address to, uint deadline)
        external
        payable
        returns (uint[] memory amounts);
    function swapTokensForExactETH(uint amountOut, uint amountInMax, address[] calldata path, address to, uint deadline)
        external
        returns (uint[] memory amounts);
    function swapExactTokensForETH(uint amountIn, uint amountOutMin, address[] calldata path, address to, uint deadline)
        external
        returns (uint[] memory amounts);
    function swapETHForExactTokens(uint amountOut, address[] calldata path, address to, uint deadline)
        external
        payable
        returns (uint[] memory amounts);

    function quote(uint amountA, uint reserveA, uint reserveB) external pure returns (uint amountB);
    function getAmountOut(uint amountIn, uint reserveIn, uint reserveOut) external pure returns (uint amountOut);
    function getAmountIn(uint amountOut, uint reserveIn, uint reserveOut) external pure returns (uint amountIn);
    function getAmountsOut(uint amountIn, address[] calldata path) external view returns (uint[] memory amounts);
    function getAmountsIn(uint amountOut, address[] calldata path) external view returns (uint[] memory amounts);
}


interface IUniswapV2Router02 is IUniswapV2Router01 {
    function removeLiquidityETHSupportingFeeOnTransferTokens(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline
    ) external returns (uint amountETH);
    function removeLiquidityETHWithPermitSupportingFeeOnTransferTokens(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline,
        bool approveMax, uint8 v, bytes32 r, bytes32 s
    ) external returns (uint amountETH);

    function swapExactTokensForTokensSupportingFeeOnTransferTokens(
        uint amountIn,
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external;
    function swapExactETHForTokensSupportingFeeOnTransferTokens(
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external payable;
    function swapExactTokensForETHSupportingFeeOnTransferTokens(
        uint amountIn,
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external;
}

// File: @openzeppelin/contracts/utils/Context.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}


// File: @openzeppelin/contracts/token/ERC20/IERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);
}


// File: @openzeppelin/contracts/token/ERC20/extensions/draft-IERC20Permit.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/extensions/draft-IERC20Permit.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 Permit extension allowing approvals to be made via signatures, as defined in
 * https://eips.ethereum.org/EIPS/eip-2612[EIP-2612].
 *
 * Adds the {permit} method, which can be used to change an account's ERC20 allowance (see {IERC20-allowance}) by
 * presenting a message signed by the account. By not relying on {IERC20-approve}, the token holder account doesn't
 * need to send a transaction, and thus is not required to hold Ether at all.
 */
interface IERC20Permit {
    /**
     * @dev Sets `value` as the allowance of `spender` over ``owner``'s tokens,
     * given ``owner``'s signed approval.
     *
     * IMPORTANT: The same issues {IERC20-approve} has related to transaction
     * ordering also apply here.
     *
     * Emits an {Approval} event.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     * - `deadline` must be a timestamp in the future.
     * - `v`, `r` and `s` must be a valid `secp256k1` signature from `owner`
     * over the EIP712-formatted function arguments.
     * - the signature must use ``owner``'s current nonce (see {nonces}).
     *
     * For more information on the signature format, see the
     * https://eips.ethereum.org/EIPS/eip-2612#specification[relevant EIP
     * section].
     */
    function permit(
        address owner,
        address spender,
        uint256 value,
        uint256 deadline,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external;

    /**
     * @dev Returns the current nonce for `owner`. This value must be
     * included whenever a signature is generated for {permit}.
     *
     * Every successful call to {permit} increases ``owner``'s nonce by one. This
     * prevents a signature from being used multiple times.
     */
    function nonces(address owner) external view returns (uint256);

    /**
     * @dev Returns the domain separator used in the encoding of the signature for {permit}, as defined by {EIP712}.
     */
    // solhint-disable-next-line func-name-mixedcase
    function DOMAIN_SEPARATOR() external view returns (bytes32);
}


// File: @openzeppelin/contracts/utils/Address.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly
                /// @solidity memory-safe-assembly
                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

