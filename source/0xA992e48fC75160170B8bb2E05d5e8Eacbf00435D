// Sources flattened with hardhat v2.9.1 https://hardhat.org

// File @openzeppelin/contracts/security/ReentrancyGuard.sol@v4.5.0

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (security/ReentrancyGuard.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}


// File @openzeppelin/contracts/utils/Context.sol@v4.5.0

// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}


// File @openzeppelin/contracts/access/Ownable.sol@v4.5.0

// OpenZeppelin Contracts v4.4.1 (access/Ownable.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}


// File @openzeppelin/contracts/security/Pausable.sol@v4.5.0

// OpenZeppelin Contracts v4.4.1 (security/Pausable.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module which allows children to implement an emergency stop
 * mechanism that can be triggered by an authorized account.
 *
 * This module is used through inheritance. It will make available the
 * modifiers `whenNotPaused` and `whenPaused`, which can be applied to
 * the functions of your contract. Note that they will not be pausable by
 * simply including this module, only once the modifiers are put in place.
 */
abstract contract Pausable is Context {
    /**
     * @dev Emitted when the pause is triggered by `account`.
     */
    event Paused(address account);

    /**
     * @dev Emitted when the pause is lifted by `account`.
     */
    event Unpaused(address account);

    bool private _paused;

    /**
     * @dev Initializes the contract in unpaused state.
     */
    constructor() {
        _paused = false;
    }

    /**
     * @dev Returns true if the contract is paused, and false otherwise.
     */
    function paused() public view virtual returns (bool) {
        return _paused;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is not paused.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    modifier whenNotPaused() {
        require(!paused(), "Pausable: paused");
        _;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is paused.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    modifier whenPaused() {
        require(paused(), "Pausable: not paused");
        _;
    }

    /**
     * @dev Triggers stopped state.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    function _pause() internal virtual whenNotPaused {
        _paused = true;
        emit Paused(_msgSender());
    }

    /**
     * @dev Returns to normal state.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    function _unpause() internal virtual whenPaused {
        _paused = false;
        emit Unpaused(_msgSender());
    }
}


// File @openzeppelin/contracts/token/ERC20/IERC20.sol@v4.5.0

// OpenZeppelin Contracts (last updated v4.5.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}


// File @openzeppelin/contracts/utils/introspection/IERC165.sol@v4.5.0

// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}


// File @openzeppelin/contracts/token/ERC1155/IERC1155.sol@v4.5.0

// OpenZeppelin Contracts v4.4.1 (token/ERC1155/IERC1155.sol)

pragma solidity ^0.8.0;

/**
 * @dev Required interface of an ERC1155 compliant contract, as defined in the
 * https://eips.ethereum.org/EIPS/eip-1155[EIP].
 *
 * _Available since v3.1._
 */
interface IERC1155 is IERC165 {
    /**
     * @dev Emitted when `value` tokens of token type `id` are transferred from `from` to `to` by `operator`.
     */
    event TransferSingle(address indexed operator, address indexed from, address indexed to, uint256 id, uint256 value);

    /**
     * @dev Equivalent to multiple {TransferSingle} events, where `operator`, `from` and `to` are the same for all
     * transfers.
     */
    event TransferBatch(
        address indexed operator,
        address indexed from,
        address indexed to,
        uint256[] ids,
        uint256[] values
    );

    /**
     * @dev Emitted when `account` grants or revokes permission to `operator` to transfer their tokens, according to
     * `approved`.
     */
    event ApprovalForAll(address indexed account, address indexed operator, bool approved);

    /**
     * @dev Emitted when the URI for token type `id` changes to `value`, if it is a non-programmatic URI.
     *
     * If an {URI} event was emitted for `id`, the standard
     * https://eips.ethereum.org/EIPS/eip-1155#metadata-extensions[guarantees] that `value` will equal the value
     * returned by {IERC1155MetadataURI-uri}.
     */
    event URI(string value, uint256 indexed id);

    /**
     * @dev Returns the amount of tokens of token type `id` owned by `account`.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     */
    function balanceOf(address account, uint256 id) external view returns (uint256);

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {balanceOf}.
     *
     * Requirements:
     *
     * - `accounts` and `ids` must have the same length.
     */
    function balanceOfBatch(address[] calldata accounts, uint256[] calldata ids)
        external
        view
        returns (uint256[] memory);

    /**
     * @dev Grants or revokes permission to `operator` to transfer the caller's tokens, according to `approved`,
     *
     * Emits an {ApprovalForAll} event.
     *
     * Requirements:
     *
     * - `operator` cannot be the caller.
     */
    function setApprovalForAll(address operator, bool approved) external;

    /**
     * @dev Returns true if `operator` is approved to transfer ``account``'s tokens.
     *
     * See {setApprovalForAll}.
     */
    function isApprovedForAll(address account, address operator) external view returns (bool);

    /**
     * @dev Transfers `amount` tokens of token type `id` from `from` to `to`.
     *
     * Emits a {TransferSingle} event.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - If the caller is not `from`, it must be have been approved to spend ``from``'s tokens via {setApprovalForAll}.
     * - `from` must have a balance of tokens of type `id` of at least `amount`.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155Received} and return the
     * acceptance magic value.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 id,
        uint256 amount,
        bytes calldata data
    ) external;

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {safeTransferFrom}.
     *
     * Emits a {TransferBatch} event.
     *
     * Requirements:
     *
     * - `ids` and `amounts` must have the same length.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155BatchReceived} and return the
     * acceptance magic value.
     */
    function safeBatchTransferFrom(
        address from,
        address to,
        uint256[] calldata ids,
        uint256[] calldata amounts,
        bytes calldata data
    ) external;
}


// File @openzeppelin/contracts/utils/Strings.sol@v4.5.0

// OpenZeppelin Contracts v4.4.1 (utils/Strings.sol)

pragma solidity ^0.8.0;

/**
 * @dev String operations.
 */
library Strings {
    bytes16 private constant _HEX_SYMBOLS = "0123456789abcdef";

    /**
     * @dev Converts a `uint256` to its ASCII `string` decimal representation.
     */
    function toString(uint256 value) internal pure returns (string memory) {
        // Inspired by OraclizeAPI's implementation - MIT licence
        // https://github.com/oraclize/ethereum-api/blob/b42146b063c7d6ee1358846c198246239e9360e8/oraclizeAPI_0.4.25.sol

        if (value == 0) {
            return "0";
        }
        uint256 temp = value;
        uint256 digits;
        while (temp != 0) {
            digits++;
            temp /= 10;
        }
        bytes memory buffer = new bytes(digits);
        while (value != 0) {
            digits -= 1;
            buffer[digits] = bytes1(uint8(48 + uint256(value % 10)));
            value /= 10;
        }
        return string(buffer);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.
     */
    function toHexString(uint256 value) internal pure returns (string memory) {
        if (value == 0) {
            return "0x00";
        }
        uint256 temp = value;
        uint256 length = 0;
        while (temp != 0) {
            length++;
            temp >>= 8;
        }
        return toHexString(value, length);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.
     */
    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {
        bytes memory buffer = new bytes(2 * length + 2);
        buffer[0] = "0";
        buffer[1] = "x";
        for (uint256 i = 2 * length + 1; i > 1; --i) {
            buffer[i] = _HEX_SYMBOLS[value & 0xf];
            value >>= 4;
        }
        require(value == 0, "Strings: hex length insufficient");
        return string(buffer);
    }
}


// File @openzeppelin/contracts/utils/cryptography/ECDSA.sol@v4.5.0

// OpenZeppelin Contracts (last updated v4.5.0) (utils/cryptography/ECDSA.sol)

pragma solidity ^0.8.0;

/**
 * @dev Elliptic Curve Digital Signature Algorithm (ECDSA) operations.
 *
 * These functions can be used to verify that a message was signed by the holder
 * of the private keys of a given address.
 */
library ECDSA {
    enum RecoverError {
        NoError,
        InvalidSignature,
        InvalidSignatureLength,
        InvalidSignatureS,
        InvalidSignatureV
    }

    function _throwError(RecoverError error) private pure {
        if (error == RecoverError.NoError) {
            return; // no error: do nothing
        } else if (error == RecoverError.InvalidSignature) {
            revert("ECDSA: invalid signature");
        } else if (error == RecoverError.InvalidSignatureLength) {
            revert("ECDSA: invalid signature length");
        } else if (error == RecoverError.InvalidSignatureS) {
            revert("ECDSA: invalid signature 's' value");
        } else if (error == RecoverError.InvalidSignatureV) {
            revert("ECDSA: invalid signature 'v' value");
        }
    }

    /**
     * @dev Returns the address that signed a hashed message (`hash`) with
     * `signature` or error string. This address can then be used for verification purposes.
     *
     * The `ecrecover` EVM opcode allows for malleable (non-unique) signatures:
     * this function rejects them by requiring the `s` value to be in the lower
     * half order, and the `v` value to be either 27 or 28.
     *
     * IMPORTANT: `hash` _must_ be the result of a hash operation for the
     * verification to be secure: it is possible to craft signatures that
     * recover to arbitrary addresses for non-hashed data. A safe way to ensure
     * this is by receiving a hash of the original message (which may otherwise
     * be too long), and then calling {toEthSignedMessageHash} on it.
     *
     * Documentation for signature generation:
     * - with https://web3js.readthedocs.io/en/v1.3.4/web3-eth-accounts.html#sign[Web3.js]
     * - with https://docs.ethers.io/v5/api/signer/#Signer-signMessage[ethers]
     *
     * _Available since v4.3._
     */
    function tryRecover(bytes32 hash, bytes memory signature) internal pure returns (address, RecoverError) {
        // Check the signature length
        // - case 65: r,s,v signature (standard)
        // - case 64: r,vs signature (cf https://eips.ethereum.org/EIPS/eip-2098) _Available since v4.1._
        if (signature.length == 65) {
            bytes32 r;
            bytes32 s;
            uint8 v;
            // ecrecover takes the signature parameters, and the only way to get them
            // currently is to use assembly.
            assembly {
                r := mload(add(signature, 0x20))
                s := mload(add(signature, 0x40))
                v := byte(0, mload(add(signature, 0x60)))
            }
            return tryRecover(hash, v, r, s);
        } else if (signature.length == 64) {
            bytes32 r;
            bytes32 vs;
            // ecrecover takes the signature parameters, and the only way to get them
            // currently is to use assembly.
            assembly {
                r := mload(add(signature, 0x20))
                vs := mload(add(signature, 0x40))
            }
            return tryRecover(hash, r, vs);
        } else {
            return (address(0), RecoverError.InvalidSignatureLength);
        }
    }

    /**
     * @dev Returns the address that signed a hashed message (`hash`) with
     * `signature`. This address can then be used for verification purposes.
     *
     * The `ecrecover` EVM opcode allows for malleable (non-unique) signatures:
     * this function rejects them by requiring the `s` value to be in the lower
     * half order, and the `v` value to be either 27 or 28.
     *
     * IMPORTANT: `hash` _must_ be the result of a hash operation for the
     * verification to be secure: it is possible to craft signatures that
     * recover to arbitrary addresses for non-hashed data. A safe way to ensure
     * this is by receiving a hash of the original message (which may otherwise
     * be too long), and then calling {toEthSignedMessageHash} on it.
     */
    function recover(bytes32 hash, bytes memory signature) internal pure returns (address) {
        (address recovered, RecoverError error) = tryRecover(hash, signature);
        _throwError(error);
        return recovered;
    }

    /**
     * @dev Overload of {ECDSA-tryRecover} that receives the `r` and `vs` short-signature fields separately.
     *
     * See https://eips.ethereum.org/EIPS/eip-2098[EIP-2098 short signatures]
     *
     * _Available since v4.3._
     */
    function tryRecover(
        bytes32 hash,
        bytes32 r,
        bytes32 vs
    ) internal pure returns (address, RecoverError) {
        bytes32 s = vs & bytes32(0x7fffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffff);
        uint8 v = uint8((uint256(vs) >> 255) + 27);
        return tryRecover(hash, v, r, s);
    }

    /**
     * @dev Overload of {ECDSA-recover} that receives the `r and `vs` short-signature fields separately.
     *
     * _Available since v4.2._
     */
    function recover(
        bytes32 hash,
        bytes32 r,
        bytes32 vs
    ) internal pure returns (address) {
        (address recovered, RecoverError error) = tryRecover(hash, r, vs);
        _throwError(error);
        return recovered;
    }

    /**
     * @dev Overload of {ECDSA-tryRecover} that receives the `v`,
     * `r` and `s` signature fields separately.
     *
     * _Available since v4.3._
     */
    function tryRecover(
        bytes32 hash,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) internal pure returns (address, RecoverError) {
        // EIP-2 still allows signature malleability for ecrecover(). Remove this possibility and make the signature
        // unique. Appendix F in the Ethereum Yellow paper (https://ethereum.github.io/yellowpaper/paper.pdf), defines
        // the valid range for s in (301): 0 < s < secp256k1n ÷ 2 + 1, and for v in (302): v ∈ {27, 28}. Most
        // signatures from current libraries generate a unique signature with an s-value in the lower half order.
        //
        // If your library generates malleable signatures, such as s-values in the upper range, calculate a new s-value
        // with 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFEBAAEDCE6AF48A03BBFD25E8CD0364141 - s1 and flip v from 27 to 28 or
        // vice versa. If your library also generates signatures with 0/1 for v instead 27/28, add 27 to v to accept
        // these malleable signatures as well.
        if (uint256(s) > 0x7FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF5D576E7357A4501DDFE92F46681B20A0) {
            return (address(0), RecoverError.InvalidSignatureS);
        }
        if (v != 27 && v != 28) {
            return (address(0), RecoverError.InvalidSignatureV);
        }

        // If the signature is valid (and not malleable), return the signer address
        address signer = ecrecover(hash, v, r, s);
        if (signer == address(0)) {
            return (address(0), RecoverError.InvalidSignature);
        }

        return (signer, RecoverError.NoError);
    }

    /**
     * @dev Overload of {ECDSA-recover} that receives the `v`,
     * `r` and `s` signature fields separately.
     */
    function recover(
        bytes32 hash,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) internal pure returns (address) {
        (address recovered, RecoverError error) = tryRecover(hash, v, r, s);
        _throwError(error);
        return recovered;
    }

    /**
     * @dev Returns an Ethereum Signed Message, created from a `hash`. This
     * produces hash corresponding to the one signed with the
     * https://eth.wiki/json-rpc/API#eth_sign[`eth_sign`]
     * JSON-RPC method as part of EIP-191.
     *
     * See {recover}.
     */
    function toEthSignedMessageHash(bytes32 hash) internal pure returns (bytes32) {
        // 32 is the length in bytes of hash,
        // enforced by the type signature above
        return keccak256(abi.encodePacked("\x19Ethereum Signed Message:\n32", hash));
    }

    /**
     * @dev Returns an Ethereum Signed Message, created from `s`. This
     * produces hash corresponding to the one signed with the
     * https://eth.wiki/json-rpc/API#eth_sign[`eth_sign`]
     * JSON-RPC method as part of EIP-191.
     *
     * See {recover}.
     */
    function toEthSignedMessageHash(bytes memory s) internal pure returns (bytes32) {
        return keccak256(abi.encodePacked("\x19Ethereum Signed Message:\n", Strings.toString(s.length), s));
    }

    /**
     * @dev Returns an Ethereum Signed Typed Data, created from a
     * `domainSeparator` and a `structHash`. This produces hash corresponding
     * to the one signed with the
     * https://eips.ethereum.org/EIPS/eip-712[`eth_signTypedData`]
     * JSON-RPC method as part of EIP-712.
     *
     * See {recover}.
     */
    function toTypedDataHash(bytes32 domainSeparator, bytes32 structHash) internal pure returns (bytes32) {
        return keccak256(abi.encodePacked("\x19\x01", domainSeparator, structHash));
    }
}


// File contracts/NFTMarket/ROCNFTMarketV2.sol

pragma solidity 0.8.9;
// ROCNFTMarket contract
contract ROCNFTMarketV2 is ReentrancyGuard, Ownable, Pausable {
    using ECDSA for bytes32;

    event SaleMakeDeal(
        address indexed _buyer,
        address indexed _seller,
        uint256 indexed _id,
        uint256 _orderSignatureId,
        address _tokenAddress,
        uint256 _price,
        address payable _beneficiaryAddress,
        uint256 _beneficiaryAmount,
        uint256 _systemFeeAmount
    );
    event AddedBlacklist(address[] _accounts);
    event RemovedBlacklist(address[] _accounts);
    event OrderSignerChanged(
        address _orderSigner
    );
    event FeeRateChanged(uint256 _feeRate);
    event SystemFeeRateChanged(uint256 _systemFeeRate);
    event BeneficiaryFeeRateChanged(
        uint8 _feeRate
    );
    event PaymentTokenAllowanceChanged(
        address _tokenAddress,
        bool _allowance
    );

    struct PriceInfo {
        address paymentTokenAddress;
        uint256 price;
    }

    struct SaleInfo {
        address payable seller;
        uint256 tokenId;
        uint256 count;
        PriceInfo[] priceInfos;
        uint64 expTs;
        bytes signature;
    }

    struct OrderData {
        uint256 signatureId;
        uint64 expTs;
        address payable beneficiaryAddress;
        bytes signature;
    }

    IERC1155 public rocNFT;
    address payable private marketProfitReceiver1;
    address payable private marketProfitReceiver2;
    address payable private marketProfitReceiverSystem;
    address public orderSigner;
    // 交易手续费百分率，当成功交易时，手续费打入marketProfitReceiver
    uint8 public feeRate;
    uint8 public systemFeeRate;
    uint8 public beneficiaryFeeRate;
    mapping(uint256 => bool) public isSignatureUsed;
    mapping(address => bool) public paymentTokenAllowanceMapping;
    mapping(address => bool) public isBlacklisted;

    constructor(address payable _marketProfitReceiver1, address payable _marketProfitReceiver2, address payable _marketProfitReceiverSystem, IERC1155 _rocNFT) {
        rocNFT = _rocNFT;
        marketProfitReceiver1 = _marketProfitReceiver1;
        marketProfitReceiver2 = _marketProfitReceiver2;
        marketProfitReceiverSystem = _marketProfitReceiverSystem;
        setFeeRate(5);
        setSystemFeeRate(1);
        setBeneficiaryFeeRate(10);
    }

    function pause() public onlyOwner {
        _pause();
    }

    function unpause() public onlyOwner {
        _unpause();
    }

    
    function setOrderSigner(address _orderSigner)
        public
        onlyOwner
    {
        orderSigner = _orderSigner;
        emit OrderSignerChanged(orderSigner);
    }

    function setFeeRate(uint8 _feeRate) public onlyOwner {
        require(_feeRate <= 100, "ROCNFTMarket: feeRate must be less than 100");
        feeRate = _feeRate;
        emit FeeRateChanged(feeRate);
    }


    function setSystemFeeRate(uint8 _systemFeeRate) public onlyOwner {
        require(_systemFeeRate <= 100, "ROCNFTMarket: feeRate must be less than 100");
        systemFeeRate = _systemFeeRate;
        emit SystemFeeRateChanged(systemFeeRate);
    }

    function setBeneficiaryFeeRate(uint8 _beneficiaryFeeRate) public onlyOwner {
        require(_beneficiaryFeeRate <= 100, "ROCNFTMarket: feeRate must be less than 100");
        beneficiaryFeeRate = _beneficiaryFeeRate;
        emit BeneficiaryFeeRateChanged(beneficiaryFeeRate);
    }

    function addBlacklist(address[] memory _accounts) external onlyOwner {
        for (uint256 i = 0; i < _accounts.length; i++) {
            isBlacklisted[_accounts[i]] = true;
        }
        emit AddedBlacklist(_accounts);
    }

    function removeBlacklist(address[] memory _accounts) external onlyOwner {
        for (uint256 i = 0; i < _accounts.length; i++) {
            isBlacklisted[_accounts[i]] = false;
        }
        emit RemovedBlacklist(_accounts);
    }

    function inBlacklist(address _account) public view returns (bool) {
        return isBlacklisted[_account];
    }

    function addPaymentTokenAllowance(address[] memory _tokenAddresses)
        public
        onlyOwner
    {
        for (uint256 i = 0; i < _tokenAddresses.length; i++) {
            if (!paymentTokenAllowanceMapping[_tokenAddresses[i]]) {
                paymentTokenAllowanceMapping[_tokenAddresses[i]] = true;
                emit PaymentTokenAllowanceChanged(
                    _tokenAddresses[i],
                    true
                );
            }
        }
    }

    function removePaymentTokenAllowance(address[] memory _tokenAddresses)
        public
        onlyOwner
    {
        for (uint256 i = 0; i < _tokenAddresses.length; i++) {
            if (paymentTokenAllowanceMapping[_tokenAddresses[i]]) {
                paymentTokenAllowanceMapping[_tokenAddresses[i]] = false;
                emit PaymentTokenAllowanceChanged(
                    _tokenAddresses[i],
                    false
                );
            }
        }
    }

    function isPaymentTokenAllowance(address _tokenAddress) public view returns (bool) {
        return paymentTokenAllowanceMapping[_tokenAddress];
    }

    bytes32 private constant PRICE_INFO_TYPEHASH = keccak256("PriceInfo(address paymentTokenAddress,uint256 price)");
    bytes32 private constant SALE_INFO_TYPE = keccak256("SaleInfo(address seller,uint256 tokenId,uint256 count,PriceInfo[] priceInfos,uint64 expTs)PriceInfo(address paymentTokenAddress,uint256 price)");
    bytes32 private constant salt = 0x0123456789abcdef000000000000000000000000000000000000000000000000;
    bytes32 private constant EIP712_DOMAIN_TYPEHASH = keccak256("EIP712Domain(string name,string version,uint256 chainId,address verifyingContract,bytes32 salt)");

    function verifySaleInfoSignature(SaleInfo calldata _saleInfo) internal view returns (bool) {
        uint chainId;
        assembly {
            chainId := chainid()
        }
        bytes32 domainSeparator = keccak256(abi.encode(
            EIP712_DOMAIN_TYPEHASH,
            keccak256("Rise of Cats NFT Market"),
            keccak256("2"),
            chainId,
            address(this),
            salt
        ));

        bytes32[] memory priceHashAry = new bytes32[](_saleInfo.priceInfos.length);
        for (uint256 i = 0; i < _saleInfo.priceInfos.length; i++) {
            priceHashAry[i] = keccak256(abi.encode(
                PRICE_INFO_TYPEHASH,
                _saleInfo.priceInfos[i].paymentTokenAddress,
                _saleInfo.priceInfos[i].price
            ));
        }
        bytes32 hashSaleInfo = keccak256(abi.encodePacked(
            "\x19\x01",
            domainSeparator,
            keccak256(abi.encode(
                SALE_INFO_TYPE,
                _saleInfo.seller,
                _saleInfo.tokenId,
                _saleInfo.count,
                keccak256(abi.encodePacked(priceHashAry)),
                _saleInfo.expTs
            ))
        ));

        return hashSaleInfo.recover(_saleInfo.signature) == _saleInfo.seller;
    }

    function verifyOrderDataSignature(OrderData calldata _orderData, address _sender, address _ordlerSigner) internal pure returns (bool) {
        return keccak256(abi.encodePacked(
                _orderData.signatureId,
                _orderData.expTs,
                _sender,
                _orderData.beneficiaryAddress))
                .recover(_orderData.signature) == _ordlerSigner;
    }


    function buy(SaleInfo calldata _saleInfo, OrderData calldata _orderData, uint8 _tokenIndex) external payable nonReentrant whenNotPaused {
        require(
            !inBlacklist(msg.sender),
            "ROCNFTMarket: sender is in blacklist"
        );
        require(
            _saleInfo.seller != msg.sender,
            "ROCNFTMarket: caller is the seller"
        );
        require(
            !isSignatureUsed[_orderData.signatureId],
            "ROCNFTMarket: signatureId already used"
        );
        require(
            _saleInfo.priceInfos.length > _tokenIndex,
            "ROCNFTMarket: payment token address not match"
        );
        require(
            isPaymentTokenAllowance(_saleInfo.priceInfos[_tokenIndex].paymentTokenAddress),
            "ROCNFTMarket: paymentTokenAddress not allowed"
        );
        require(
            _saleInfo.count > 0,
            "ROCNFTMarket: count must be greater than 0"
        );
        require(
            block.timestamp <= _saleInfo.expTs,
            "ROCNFTMarket: sale signature expired"
        );
        require(
            block.timestamp <= _orderData.expTs,
            "ROCNFTMarket: order signature expired"
        );
        require(
            _saleInfo.priceInfos[_tokenIndex].price > 0,
            "ROCNFTMarket: price must be greater than 0"
        );
        require(
            rocNFT.balanceOf(_saleInfo.seller, _saleInfo.tokenId) >= _saleInfo.count,
            "ROCNFTMarket: seller does not have enough token"
        );
        require(
            rocNFT.isApprovedForAll(_saleInfo.seller, address(this)),
            "ROCNFTMarket: seller not approved for this contract"
        );
        
        require(
            verifyOrderDataSignature(_orderData, msg.sender, orderSigner),
            "ROCNFTMarket: order signature invalid"
        );
        require(
            verifySaleInfoSignature(_saleInfo),
            "ROCNFTMarket: sale signature invalid"
        );

        PriceInfo calldata priceInfo = _saleInfo.priceInfos[_tokenIndex];
        uint256 payForSystemFee = priceInfo.price * systemFeeRate / 100;
        uint256 fee = priceInfo.price * feeRate / 100;
        uint256 payForSeller = priceInfo.price - fee - payForSystemFee;
        uint256 beneficiaryAmount = _orderData.beneficiaryAddress == address(0)
                ? 0
                : fee * beneficiaryFeeRate / 100;
        fee -= beneficiaryAmount;
        uint256 halfFee = fee / 2;
        if (priceInfo.paymentTokenAddress == address(0)) {
            require(msg.value == priceInfo.price, "ROCNFTMarket: msg.value is not equal to saleInfo.price");
            if (payForSeller > 0) {
                _saleInfo.seller.transfer(payForSeller);
            }
            if (beneficiaryAmount > 0) {
                _orderData.beneficiaryAddress.transfer(beneficiaryAmount);
            }
            if (fee > 0) {
                marketProfitReceiver1.transfer(halfFee);
                marketProfitReceiver2.transfer(fee - halfFee);
            }
            if (payForSystemFee > 0) {
                marketProfitReceiverSystem.transfer(payForSystemFee);
            }
        } else {
            IERC20 paymentToken = IERC20(priceInfo.paymentTokenAddress);
            if (payForSeller > 0) {
                uint256 realPayAmount = paymentToken.balanceOf(address(this));
                paymentToken.transferFrom(msg.sender, address(this), payForSeller);
                realPayAmount = paymentToken.balanceOf(address(this)) - realPayAmount;
                paymentToken.transfer(_saleInfo.seller, realPayAmount);
            }
            if (beneficiaryAmount > 0) {
                paymentToken.transferFrom(msg.sender, _orderData.beneficiaryAddress, beneficiaryAmount);
            }
            if (fee > 0) {
                paymentToken.transferFrom(msg.sender, marketProfitReceiver1, halfFee);
                paymentToken.transferFrom(msg.sender, marketProfitReceiver2, fee - halfFee);
            }
            if (payForSystemFee > 0) {
                paymentToken.transferFrom(msg.sender, marketProfitReceiverSystem, payForSystemFee);
            }
        }
        rocNFT.safeTransferFrom(_saleInfo.seller, msg.sender, _saleInfo.tokenId, _saleInfo.count, "ROCNFTMarket Make Deal");
        emit SaleMakeDeal(
            msg.sender,
            _saleInfo.seller,
            _saleInfo.tokenId,
            _orderData.signatureId,
            priceInfo.paymentTokenAddress,
            priceInfo.price,
            _orderData.beneficiaryAddress,
            beneficiaryAmount,
            payForSystemFee
        );
    }

    //Use this in case BNB are sent to the contract by mistake
    function rescueBNB() external onlyOwner {
        uint256 balance = address(this).balance;
        require(balance > 0, "ROCNFTMarket: balance is 0");
        uint256 halfBalance = balance / 2;
        marketProfitReceiver1.transfer(halfBalance);
        marketProfitReceiver2.transfer(balance - halfBalance);
    }

    // Function to allow admin to claim *other* ERC20 tokens sent to this contract (by mistake)
    // Owner cannot transfer out catecoin from this smart contract
    function rescueAnyERC20Tokens(address _tokenAddress) external onlyOwner {
        IERC20 token = IERC20(_tokenAddress);
        uint256 balance = token.balanceOf(address(this));
        require(balance > 0, "ROCNFTMarket: balance is 0");
        uint256 halfBalance = balance / 2;
        token.transfer(marketProfitReceiver1, halfBalance);
        token.transfer(marketProfitReceiver2, balance - halfBalance);
    }

    receive() external payable{}
}