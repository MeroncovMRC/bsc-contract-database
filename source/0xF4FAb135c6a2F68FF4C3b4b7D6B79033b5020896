
// File: @openzeppelin/contracts/access/Ownable.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _setOwner(_msgSender());
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _setOwner(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _setOwner(newOwner);
    }

    function _setOwner(address newOwner) private {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}


// File: @openzeppelin/contracts/token/ERC20/IERC20.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address sender,
        address recipient,
        uint256 amount
    ) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}


// File: @openzeppelin/contracts/utils/Address.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize, which returns 0 for contracts in
        // construction, since the code is only stored at the end of the
        // constructor execution.

        uint256 size;
        assembly {
            size := extcodesize(account)
        }
        return size > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}


// File: @openzeppelin/contracts/utils/Context.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}


// File: /contracts/HKT/HKT_BOX.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/utils/Address.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";

interface HKT721 {
    function tokenOfOwnerByIndex(address owner, uint256 index) external view returns (uint256 tokenId);

    function tokenByIndex(uint256 index) external view returns (uint256);

    function balanceOf(address owner) external view returns (uint256 balance);

    function cardIdMap(uint tokenId) external view returns (uint256 cardId);

    function safeTransferFrom(address from, address to, uint256 tokenId) external;

    function mint(address player_, uint cardId_, bool uriInTokenId_) external returns (uint256);
}

interface Mining {
    function checkUserValue(address addr_) external view returns (uint);

    function stage() external view returns (address);

    function U() external view returns (address);

    function NFT() external view returns (address);

    function HKT() external view returns (address);

    function fund() external view returns (address);

    function userInfo(address addr_) external view returns (uint total, uint claimed, bool frist, uint value);
}

contract RandomGenerator {
    uint private randNonce = 0;

    function random(uint256 seed) internal returns (uint256) {
        randNonce += 1;
        return uint256(keccak256(abi.encodePacked(
                blockhash(block.number - 1),
                blockhash(block.number - 2),
                blockhash(block.number - 3),
                blockhash(block.number - 4),
                blockhash(block.number - 5),
                blockhash(block.number - 6),
                blockhash(block.number - 7),
                blockhash(block.number - 8),
                block.timestamp,
                msg.sender,
                randNonce,
                seed
            )));
    }

    function randomCeil(uint256 q) internal returns (uint256) {
        return (random(gasleft()) % q) + 1;
    }
}

contract Box is RandomGenerator, Ownable {
    uint public catFood;
    uint public Acard;
    uint public Scard;
    uint public SSScard;
    uint public price = 20 ether;
    address public U;
    address public HKT;
    address public NFT;
    address public stage;
    address public pair;
    address public fund;
    uint public limit = 2;
    uint public referPrice = 20 ether;
    address public burnAddress = 0x1111111111111111111111111111111111111111;
    Mining public mine;

    event BuyBox(address indexed sender_, uint indexed amount_);
    event BondInvitor(address indexed sender_, address indexed invitor_);
    event OpenBox(address indexed sender_, uint indexed rew_);
    constructor(){
        catFood = 7000;
        Acard = 2000;
        Scard = 800;
        SSScard = 200;
        admin[msg.sender] = true;
    }
    struct UserInfo {
        uint box;
        uint opened;
        bool isWhite;
        address invitor;
        uint lastRew;
        uint rewBox;
        uint refer;
        uint buyAmount;
        bool claimed;
        bool isInvitor;

    }

    mapping(address => bool)public admin;
    mapping(address => UserInfo)public userInfo;

    function setLimit(uint com_) public onlyOwner {
        limit = com_;
    }

    function setAddress(address pair_, address mining_) external onlyOwner {

        pair = pair_;
        mine = Mining(mining_);
        U = mine.U();
        HKT = mine.HKT();
        NFT = mine.NFT();
        fund = mine.fund();
        stage = mine.stage();
    }

    function setReferPrice(uint amount_) external onlyOwner {
        referPrice = amount_;
    }

    function setAdmin(address addr_, bool com_) public onlyOwner {
        admin[addr_] = com_;
    }

    function setBoxPirce(uint amount_) external onlyOwner {
        price = amount_;
    }

    function setwhite(address addr_, bool com_) external onlyOwner {
        userInfo[addr_].isWhite = com_;
    }

    function coutingCost(uint amount_) public view returns (uint out_){
        out_ = amount_ * price * 1e18 / getPrice();
    }

    function buyBox(uint amount_, address invitor_) public {
        require(userInfo[msg.sender].buyAmount + amount_ <= limit, 'over limit');
        require(mine.checkUserValue(msg.sender) >= 100 * 1e18, 'not enough value');
        if (userInfo[msg.sender].invitor == address(0)) {
            require(userInfo[invitor_].isInvitor || invitor_ == address(this), 'wrong invitor');
            require(invitor_ != msg.sender, 'wrong invitor');
            userInfo[msg.sender].invitor = invitor_;
            userInfo[invitor_].refer += 1;
            emit BondInvitor(msg.sender, invitor_);
        }
        userInfo[userInfo[msg.sender].invitor].box += 1;
        userInfo[userInfo[msg.sender].invitor].rewBox += 1;
        userInfo[msg.sender].box += amount_;
        userInfo[msg.sender].buyAmount += amount_;

        IERC20(HKT).transferFrom(msg.sender, burnAddress, coutingCost(amount_));
        emit BuyBox(msg.sender, amount_);
    }

    function buyRefer() public {
        IERC20(HKT).transferFrom(msg.sender, fund, referPrice * 1e18 / getPrice());
        userInfo[msg.sender].isInvitor = true;
    }

    function getReferPrice() public view returns (uint){
        return referPrice * 1e18 / getPrice();
    }


    function getPrice() public view returns (uint){
        uint a = IERC20(U).balanceOf(pair);
        uint b = IERC20(HKT).balanceOf(pair);
        uint _price = a * 1e18 / b;
        return _price;
    }

    function claimBox() public {
        require(!userInfo[msg.sender].claimed, 'claimed');
        (uint pow,,,) = mine.userInfo(msg.sender);
        require(pow >= 2000 * 1e18, 'not enough value');
        userInfo[msg.sender].box += 1;
        userInfo[msg.sender].claimed = true;

    }


    function openBox() public {
        require(userInfo[msg.sender].box > 0, 'no box');
        require(IERC20(HKT).balanceOf(_msgSender()) >= 1e16, 'not enough HKT');
        if (userInfo[msg.sender].isWhite) {
            uint temp = catFood + Acard + Scard + SSScard;
            uint out = randomCeil(temp);
            require(temp > 0, 'no cards');
            if (out > catFood + Acard + Scard) {
                userInfo[msg.sender].lastRew = 20003;
                SSScard --;
            } else if (out > catFood + Acard) {
                userInfo[msg.sender].lastRew = 20002;
                Scard --;
            } else if (out > catFood) {
                userInfo[msg.sender].lastRew = 20001;
                Acard --;
            } else {
                userInfo[msg.sender].lastRew = 1000;
                catFood --;
            }
        } else {
            uint temp = catFood + Acard + Scard;
            uint out = randomCeil(temp);
            require(temp > 0, 'no cards');
            if (out > catFood + Acard) {
                userInfo[msg.sender].lastRew = 20002;
                Scard --;
            } else if (out > catFood) {
                userInfo[msg.sender].lastRew = 20001;
                Acard --;
            } else {
                userInfo[msg.sender].lastRew = 1000;
                catFood --;
            }
        }
        // HKT721(NFT).mint(msg.sender, userInfo[msg.sender].lastRew, false);
        userInfo[msg.sender].box --;
        userInfo[msg.sender].opened ++;
        emit OpenBox(msg.sender, userInfo[msg.sender].lastRew);
    }

    function checkUserInvitor(address addr_) public view returns (address){
        return userInfo[addr_].invitor;
    }

    function bondUserInvitor(address addr_, address invitor_) public {
        require(admin[msg.sender], 'not admin');
        userInfo[addr_].invitor = invitor_;
    }
}
