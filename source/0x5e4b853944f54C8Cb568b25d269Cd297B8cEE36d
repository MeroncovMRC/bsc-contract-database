
// File: DotDotVoting.sol
pragma solidity 0.8.12;

import "Ownable.sol";
import "IERC20.sol";
import "IEpsProxy.sol";
import "ITokenLocker.sol";
import "IIncentiveVoting.sol";


contract DotDotVoting is Ownable {

    struct Vote {
        address token;
        uint256 votes;
    }
    struct TokenApprovalVote {
        uint256 week;
        uint256 ratio;
    }

    // user -> week -> votes used
    mapping(address => uint256[65535]) public userVotes;

    // user -> token -> week -> votes for pool
    mapping(address => mapping(address => uint256[65535])) public userTokenVotes;

    // token -> week -> votes received
    mapping(address => uint256[65535]) public tokenVotes;

    // week -> number of EPS votes per DDD vote
    uint256[65535] public epsVoteRatio;

    // vote ID -> user -> yes votes
    mapping(uint256 => mapping(address => uint256)) public userTokenApprovalVotes;
    // user -> timestamp of last created token approval vote
    mapping(address => uint256) public lastVote;

    mapping (uint256 => TokenApprovalVote) tokenApprovalVotes;

    uint256 constant WEEK = 86400 * 7;

    // new weeks within this contract begin on Thursday 00:00:00 UTC
    uint256 public startTime;

    IIncentiveVoting public immutable epsVoter;
    ITokenLocker public immutable epsLocker;

    ITokenLocker public dddLocker;
    address public fixedVoteLpToken;
    IEllipsisProxy public proxy;

    mapping(address => bool) public isApproved;

    event VotedForIncentives(
        address indexed voter,
        address[] tokens,
        uint256[] votes,
        uint256 userVotesUsed,
        uint256 totalUserVotes
    );
    event CreatedTokenApprovalVote(
        address indexed user,
        uint256 voteIndex,
        address token
    );
    event VotedForTokenApproval(
        address indexed voter,
        uint256 voteIndex,
        uint256 yesVotes
    );

    constructor(
        IIncentiveVoting _epsVoter,
        ITokenLocker _epsLocker
    ) {
        epsVoter = _epsVoter;
        epsLocker = _epsLocker;

        startTime = _epsVoter.startTime();
    }

    function setAddresses(
        ITokenLocker _dddLocker,
        address _fixedVoteLpToken,
        IEllipsisProxy _proxy
    ) external onlyOwner {
        dddLocker = _dddLocker;
        fixedVoteLpToken = _fixedVoteLpToken;
        proxy = _proxy;

        renounceOwnership();
    }

    function votingOpen() public view returns (bool) {
        uint256 weekStart = block.timestamp / WEEK * WEEK;
        return block.timestamp - 86400 * 4 >= weekStart;
    }

    function getWeek() public view returns (uint256) {
        if (startTime >= block.timestamp) return 0;
        return (block.timestamp - startTime) / WEEK;
    }


    function weeklyVotes(address _user, address _token, uint256 _week) external view returns (uint256, uint256) {
        return (userTokenVotes[_user][_token][_week], tokenVotes[_token][_week]);
    }

    /**
        @notice Get data on the current votes made in the active week
        @return _totalVotes Total number of votes this week for all pools
        @return _voteData Dynamic array of (token address, votes for token)
     */
    function getCurrentVotes() external view returns (uint256 _totalVotes, Vote[] memory _voteData) {
        _voteData = new Vote[](epsVoter.approvedTokensLength());
        uint256 week = getWeek();
        uint256 totalVotes;
        for (uint i = 0; i < _voteData.length; i++) {
            address token = epsVoter.approvedTokens(i);
            uint256 votes = tokenVotes[token][week];
            totalVotes += votes;
            _voteData[i] = Vote({token: token, votes: votes});
        }
        return (totalVotes, _voteData);
    }

    /**
        @notice Get data on current votes `_user` has made in the active week
        @return _totalVotes Total number of votes from `_user` this week for all pools
        @return _voteData Dynamic array of (token address, votes for token)
     */
    function getUserCurrentVotes(address _user)
        external
        view
        returns (uint256 _totalVotes, Vote[] memory _voteData)
    {
        _voteData = new Vote[](epsVoter.approvedTokensLength());
        uint256 week = getWeek();
        for (uint i = 0; i < _voteData.length; i++) {
            address token = epsVoter.approvedTokens(i);
            _voteData[i] = Vote({token: token, votes: userTokenVotes[_user][token][week]});
        }
        return (userVotes[_user][week], _voteData);
    }

    /**
        @notice Get the amount of unused votes for for the current week being voted on
        @param _user Address to query
        @return uint Amount of unused votes
     */
    function availableVotes(address _user) external view returns (uint256) {
        if (!votingOpen()) return 0;
        uint256 week = getWeek();
        uint256 usedVotes = userVotes[_user][week];
        uint256 totalVotes = dddLocker.weeklyWeightOf(_user, week) / 1e18;
        return totalVotes - usedVotes;
    }

    /**
        @notice Allocate votes toward LP tokens to receive emissions in the following week
        @dev Voting works identically to
        @param _tokens List of addresses of LP tokens to vote for
        @param _votes Votes to allocate to `_tokens`. Values are additive, they do
                        not include previous votes. For example, if you have already
                        allocated 100 votes and wish to allocate a total of 300,
                        the vote amount should be given as 200.
     */
    function vote(address[] calldata _tokens, uint256[] memory _votes) external {
        require(votingOpen(), "Voting period has not opened for this week");
        require(_tokens.length == _votes.length, "Input length mismatch");

        uint256 week = getWeek();
        uint256 ratio = epsVoteRatio[week];
        if (ratio == 0) {
            uint256 epsVotes = epsVoter.availableVotes(address(proxy));

            if (epsVoter.isApproved(fixedVoteLpToken)) {
                // use 5% of the votes for EPX/dEPX pool
                address[] memory fixedVoteToken = new address[](1);
                fixedVoteToken[0] = fixedVoteLpToken;
                uint256[] memory fixedVote = new uint256[](1);
                fixedVote[0] = epsVotes / 20;
                proxy.vote(fixedVoteToken, fixedVote);
                epsVotes -= fixedVote[0];
            }
            uint256 dddVotes = dddLocker.weeklyTotalWeight(week) / 1e18;
            ratio = epsVotes / dddVotes;
            epsVoteRatio[week] = ratio;
        }

        // update accounting for this week's votes
        uint256 usedVotes = userVotes[msg.sender][week];
        for (uint i = 0; i < _tokens.length; i++) {
            address token = _tokens[i];
            uint256 amount = _votes[i];
            tokenVotes[token][week] += amount;
            userTokenVotes[msg.sender][token][week] += amount;
            usedVotes += amount;
            // multiply by ratio after updating internal accounting but prior to submitting
            _votes[i] = amount * ratio;
        }

        // make sure user has not exceeded available votes
        uint256 totalVotes = dddLocker.weeklyWeightOf(msg.sender, week) / 1e18;
        require(usedVotes <= totalVotes, "Available votes exceeded");
        userVotes[msg.sender][week] = usedVotes;

        // submit votes
        proxy.vote(_tokens, _votes);

        emit VotedForIncentives(
            msg.sender,
            _tokens,
            _votes,
            usedVotes,
            totalVotes
        );
    }

    function minWeightForNewTokenApprovalVote() public view returns (uint256) {
        uint256 lockerWeek = epsLocker.getWeek();
        if (lockerWeek == 0) return 0;
        uint256 epsVotes = epsLocker.weeklyWeightOf(address(proxy), epsLocker.getWeek() - 1);
        uint256 dddVotes = dddLocker.weeklyTotalWeight(getWeek() - 1) / 1e18;
        uint256 ratio = epsVotes / dddVotes;
        return epsVoter.NEW_TOKEN_APPROVAL_VOTE_MIN_WEIGHT() / ratio;
    }

    function createTokenApprovalVote(address _token) external returns (uint256 _voteIndex) {
        require(epsVoter.isApproved(fixedVoteLpToken), "Cannot make vote until dEPX/EPX pool approved");
        require(lastVote[msg.sender] + 86400 * 30 < block.timestamp, "One new vote per 30 days");
        uint256 weight = dddLocker.weeklyWeightOf(msg.sender, getWeek() - 1);
        require(weight >= minWeightForNewTokenApprovalVote(), "User has insufficient DotDot lock weight");
        _voteIndex = proxy.createTokenApprovalVote(_token);
        lastVote[msg.sender] = block.timestamp;
        emit CreatedTokenApprovalVote(msg.sender, _voteIndex, _token);
    }

    function availableTokenApprovalVotes(address _user, uint256 _voteIndex) external view returns (uint256) {
        uint256 ratio = tokenApprovalVotes[_voteIndex].ratio;
        uint256 week = tokenApprovalVotes[_voteIndex].week;
        if (ratio == 0) {
            uint256 epsVotes = epsVoter.availableTokenApprovalVotes(address(proxy), _voteIndex);
            if (epsVotes == 0) return 0;
            week = getWeek() - 1;
            uint256 dddVotes = dddLocker.weeklyTotalWeight(week) / 1e18;
            ratio = epsVotes / dddVotes;
        }
        uint256 totalVotes = dddLocker.weeklyWeightOf(_user, week) / 1e18;
        uint256 usedVotes = userTokenApprovalVotes[_voteIndex][_user];
        return totalVotes - usedVotes;
    }

    /**
        @notice Vote in favor of approving a new token for protocol emissions
        @param _voteIndex Array index referencing the vote
     */
    function voteForTokenApproval(uint256 _voteIndex, uint256 _yesVotes) external {
        TokenApprovalVote storage vote = tokenApprovalVotes[_voteIndex];
        if (vote.ratio == 0) {
            uint256 epsVotes = epsVoter.availableTokenApprovalVotes(address(proxy), _voteIndex);
            require(epsVotes > 0, "Vote has closed or does not exist");
            vote.week = getWeek() - 1;
            uint256 dddVotes = dddLocker.weeklyTotalWeight(vote.week) / 1e18;
            vote.ratio = epsVotes / dddVotes;
        }

        uint256 totalVotes = dddLocker.weeklyWeightOf(msg.sender, vote.week) / 1e18;
        uint256 usedVotes = userTokenApprovalVotes[_voteIndex][msg.sender];
        if (_yesVotes == type(uint256).max) {
            _yesVotes = totalVotes - usedVotes;
        }
        usedVotes += _yesVotes;
        require(usedVotes <= totalVotes, "Exceeds available votes");

        userTokenApprovalVotes[_voteIndex][msg.sender] = usedVotes;
        proxy.voteForTokenApproval(_voteIndex, _yesVotes * vote.ratio);
        emit VotedForTokenApproval(msg.sender, _voteIndex, _yesVotes);
    }

    /**
        @notice Create a token approval vote for `fixedVoteLpToken` and vote
                with all available weight
        @dev This function is unguarded, but will revert within EPS if the the
             token is already approved or the last vote was made less than 1
             week ago.
     */
    function createFixedVoteApprovalVote() external {
        uint256 voteId = proxy.createTokenApprovalVote(fixedVoteLpToken);
        proxy.voteForTokenApproval(voteId, type(uint256).max);
        emit CreatedTokenApprovalVote(msg.sender, voteId, fixedVoteLpToken);
    }

}


// File: Ownable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (access/Ownable.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable {
    address public owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(msg.sender);
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner == msg.sender, "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = owner;
        owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}


// File: IERC20.sol
// SPDX-License-Identifier: MIT

pragma solidity 0.8.12;

/**
 * Based on the OpenZeppelin IER20 interface:
 * https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC20/IERC20.sol
 *
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);

    function name() external view returns (string memory);
    function symbol() external view returns (string memory);
    function decimals() external view returns (uint8);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}


// File: IEpsProxy.sol
pragma solidity 0.8.12;

interface IEllipsisProxy {
    function lock(uint256 _amount) external returns (bool);
    function extendLock(uint256 _amount, uint256 _weeks) external returns (bool);
    function deposit(address _token, uint256 _amount) external returns (uint256);
    function withdraw(address _receiver, address _token, uint256 _amount) external returns (uint256);
    function claimEmissions(address _token) external returns (uint256);
    function claimFees(address[] calldata _tokens) external returns (bool);
    function vote(address[] calldata _tokens, uint256[] calldata _votes) external returns (bool);
    function createTokenApprovalVote(address _token) external returns (uint256 _voteIndex);
    function voteForTokenApproval(uint256 _voteIndex, uint256 _yesVotes) external returns (bool);
    function getReward(address _lpToken, address[] calldata _rewards) external returns (bool);
}

// File: ITokenLocker.sol
pragma solidity 0.8.12;


interface ITokenLocker {

    function MAX_LOCK_WEEKS() external view returns (uint256);
    function getWeek() external view returns (uint256);
    function userWeight(address _user) external view returns (uint256);
    function totalWeight() external view returns (uint256);
    function weeklyWeight(address _user, uint256 _week) external view returns (uint256, uint256);
    function startTime() external view returns (uint256);
    function weeklyTotalWeight(uint256 week) external view returns (uint256);
    function weeklyWeightOf(address user, uint256 week) external view returns (uint256);

    /**
        @notice Get data on a user's active token locks
        @param _user Address to query data for
        @return lockData dynamic array of [weeks until expiration, balance of lock]
     */
    function getActiveUserLocks(address _user) external view returns (uint256[2][] memory lockData);

    /**
        @notice Allow or block third-party calls to deposit, withdraw
                or claim rewards on behalf of the caller
     */
    function setBlockThirdPartyActions(bool _block) external;

    /**
        @notice Deposit tokens into the contract to create a new lock.
        @param _user Address to create a new lock for (does not have to be the caller)
        @param _amount Amount of tokens to lock. This balance transfered from the caller.
        @param _weeks The number of weeks for the lock.
     */
    function lock(
        address _user,
        uint256 _amount,
        uint256 _weeks
    ) external returns (bool);

    /**
        @notice Extend the length of an existing lock.
        @param _amount Amount of tokens to extend the lock for.
        @param _weeks The number of weeks for the lock that is being extended.
        @param _newWeeks The number of weeks to extend the lock until.
     */
    function extendLock(
        uint256 _amount,
        uint256 _weeks,
        uint256 _newWeeks
    ) external returns (bool);

}


// File: IIncentiveVoting.sol
pragma solidity 0.8.12;

interface IIncentiveVoting {

    function NEW_TOKEN_APPROVAL_VOTE_MIN_WEIGHT() external view returns (uint256);
    function startTime() external view returns (uint256);
    function availableVotes(address _user) external view returns (uint256);
    function availableTokenApprovalVotes(address _user, uint256 _voteIndex) external view returns (uint256);
    function isApproved(address _token) external view returns (bool);
    function approvedTokensLength() external view returns (uint256);
    function approvedTokens(uint256) external view returns (address);

    /**
        @notice Allocate votes toward LP tokens to receive emissions in the following week
        @param _tokens List of addresses of LP tokens to vote for
        @param _votes Votes to allocate to `_tokens`
     */
    function vote(address[] calldata _tokens, uint256[] calldata _votes) external;

    /**
        @notice Create a new vote to enable protocol emissions on a given token
        @param _token Token address to create a vote for
        @return _voteIndex uint Index value used to reference the vote
     */
    function createTokenApprovalVote(address _token) external returns (uint256 _voteIndex);

    /**
        @notice Vote in favor of approving a new token for protocol emissions
        @param _voteIndex Array index referencing the vote
     */
    function voteForTokenApproval(uint256 _voteIndex, uint256 _yesVotes) external;

}

