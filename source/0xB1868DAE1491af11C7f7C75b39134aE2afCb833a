
// File: contracts/ve_dist.sol
// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity 0.8.11;

/*

@title Curve Fee Distribution modified for ve(3,3) emissions
@author Curve Finance, andrecronje
@license MIT

*/

interface erc20 {
    function totalSupply() external view returns (uint);

    function transfer(address recipient, uint amount) external returns (bool);

    function decimals() external view returns (uint8);

    function symbol() external view returns (string memory);

    function balanceOf(address) external view returns (uint);

    function transferFrom(
        address sender,
        address recipient,
        uint amount
    ) external returns (bool);

    function approve(address spender, uint value) external returns (bool);
}

library Math {
    function min(uint a, uint b) internal pure returns (uint) {
        return a < b ? a : b;
    }

    function max(uint a, uint b) internal pure returns (uint) {
        return a >= b ? a : b;
    }
}

interface VotingEscrow {
    struct Point {
        int128 bias;
        int128 slope; // # -dweight / dt
        uint ts;
        uint blk; // block
    }

    function user_point_epoch(uint tokenId) external view returns (uint);

    function epoch() external view returns (uint);

    function user_point_history(uint tokenId, uint loc) external view returns (Point memory);

    function point_history(uint loc) external view returns (Point memory);

    function checkpoint() external;

    function deposit_for(uint tokenId, uint value) external;

    function token() external view returns (address);
}

contract ve_dist {
    event CheckpointToken(uint time, uint tokens);

    event Claimed(uint tokenId, uint amount, uint claim_epoch, uint max_epoch);

    uint constant EPOCH = 1 days;
    int128 internal constant WAD = 1e9; //Must be consistent with ve's WAD

    uint public start_time;
    uint public time_cursor;
    mapping(uint => uint) public time_cursor_of;
    mapping(uint => uint) public user_epoch_of;

    uint public last_token_time;
    uint[1000000000000000] public tokens_per_day;

    address public voting_escrow;
    address public token;
    uint public token_last_balance;

    uint[1000000000000000] public ve_supply;

    address public depositor;

    constructor(address _voting_escrow) {
        uint _t = (block.timestamp / EPOCH) * EPOCH;
        start_time = _t;
        last_token_time = _t;
        time_cursor = _t;
        address _token = VotingEscrow(_voting_escrow).token();
        token = _token;
        voting_escrow = _voting_escrow;
        depositor = msg.sender;
        require(erc20(_token).approve(_voting_escrow, type(uint).max));
    }

    function timestamp() external view returns (uint) {
        return (block.timestamp / EPOCH) * EPOCH;
    }

    function _checkpoint_token() internal {
        uint token_balance = erc20(token).balanceOf(address(this));
        uint to_distribute = token_balance - token_last_balance;
        token_last_balance = token_balance;

        uint t = last_token_time;
        uint since_last = block.timestamp - t;
        last_token_time = block.timestamp;
        uint this_day = (t / EPOCH) * EPOCH;
        uint next_day = 0;

        for (uint i = 0; i < 20; i++) {
            next_day = this_day + EPOCH;
            if (block.timestamp < next_day) {
                // slither-disable-next-line incorrect-equality
                if (since_last == 0 && block.timestamp == t) {
                    tokens_per_day[this_day] += to_distribute;
                } else {
                    tokens_per_day[this_day] += (to_distribute * (block.timestamp - t)) / since_last;
                }
                break;
            } else {
                // slither-disable-next-line incorrect-equality
                if (since_last == 0 && next_day == t) {
                    tokens_per_day[this_day] += to_distribute;
                } else {
                    tokens_per_day[this_day] += (to_distribute * (next_day - t)) / since_last;
                }
            }
            t = next_day;
            this_day = next_day;
        }
        emit CheckpointToken(block.timestamp, to_distribute);
    }

    function checkpoint_token() external {
        assert(msg.sender == depositor);
        _checkpoint_token();
    }

    function _find_timestamp_epoch(address ve, uint _timestamp) internal view returns (uint) {
        uint _min = 0;
        uint _max = VotingEscrow(ve).epoch();
        for (uint i = 0; i < 128; i++) {
            if (_min >= _max) break;
            uint _mid = (_min + _max + 2) / 2;
            VotingEscrow.Point memory pt = VotingEscrow(ve).point_history(_mid);
            if (pt.ts <= _timestamp) {
                _min = _mid;
            } else {
                _max = _mid - 1;
            }
        }
        return _min;
    }

    function _find_timestamp_user_epoch(
        address ve,
        uint tokenId,
        uint _timestamp,
        uint max_user_epoch
    ) internal view returns (uint) {
        uint _min = 0;
        uint _max = max_user_epoch;
        for (uint i = 0; i < 128; i++) {
            if (_min >= _max) break;
            uint _mid = (_min + _max + 2) / 2;
            VotingEscrow.Point memory pt = VotingEscrow(ve).user_point_history(tokenId, _mid);
            if (pt.ts <= _timestamp) {
                _min = _mid;
            } else {
                _max = _mid - 1;
            }
        }
        return _min;
    }

    function ve_for_at(uint _tokenId, uint _timestamp) external view returns (uint) {
        address ve = voting_escrow;
        uint max_user_epoch = VotingEscrow(ve).user_point_epoch(_tokenId);
        uint epoch = _find_timestamp_user_epoch(ve, _tokenId, _timestamp, max_user_epoch);
        VotingEscrow.Point memory pt = VotingEscrow(ve).user_point_history(_tokenId, epoch);
        return Math.max(uint(int(pt.bias - (pt.slope * (int128(int(_timestamp - pt.ts)))) / WAD)), 0);
    }

    function _checkpoint_total_supply() internal {
        address ve = voting_escrow;
        uint t = time_cursor;
        uint rounded_timestamp = (block.timestamp / EPOCH) * EPOCH;
        // slither-disable-next-line reentrancy-no-eth
        VotingEscrow(ve).checkpoint();

        for (uint i = 0; i < 20; i++) {
            if (t > rounded_timestamp) {
                break;
            } else {
                uint epoch = _find_timestamp_epoch(ve, t);
                VotingEscrow.Point memory pt = VotingEscrow(ve).point_history(epoch);
                int128 dt = 0;
                if (t > pt.ts) {
                    dt = int128(int(t - pt.ts));
                }
                ve_supply[t] = Math.max(uint(int(pt.bias - (pt.slope * dt) / WAD)), 0);
            }
            t += EPOCH;
        }
        time_cursor = t;
    }

    function checkpoint_total_supply() external {
        _checkpoint_total_supply();
    }

    function _claim(
        uint _tokenId,
        address ve,
        uint _last_token_time
    ) internal returns (uint) {
        uint user_epoch = 0;
        uint to_distribute = 0;

        uint max_user_epoch = VotingEscrow(ve).user_point_epoch(_tokenId);
        uint _start_time = start_time;

        if (max_user_epoch == 0) return 0;

        uint day_cursor = time_cursor_of[_tokenId];
        // slither-disable-next-line incorrect-equality
        if (day_cursor == 0) {
            user_epoch = _find_timestamp_user_epoch(ve, _tokenId, _start_time, max_user_epoch);
        } else {
            user_epoch = user_epoch_of[_tokenId];
        }

        if (user_epoch == 0) user_epoch = 1;

        VotingEscrow.Point memory user_point = VotingEscrow(ve).user_point_history(_tokenId, user_epoch);
        // slither-disable-next-line incorrect-equality
        if (day_cursor == 0) day_cursor = ((user_point.ts + EPOCH - 1) / EPOCH) * EPOCH;
        if (day_cursor >= last_token_time) return 0;
        if (day_cursor < _start_time) day_cursor = _start_time;

        // slither-disable-next-line uninitialized-local
        VotingEscrow.Point memory old_user_point;

        for (uint i = 0; i < 50; i++) {
            if (day_cursor >= _last_token_time) break;

            if (day_cursor >= user_point.ts && user_epoch <= max_user_epoch) {
                user_epoch += 1;
                old_user_point = user_point;
                if (user_epoch > max_user_epoch) {
                    user_point = VotingEscrow.Point(0, 0, 0, 0);
                } else {
                    user_point = VotingEscrow(ve).user_point_history(_tokenId, user_epoch);
                }
            } else {
                int128 dt = int128(int(day_cursor - old_user_point.ts));
                uint balance_of = Math.max(uint(int(old_user_point.bias - (dt * old_user_point.slope) / WAD)), 0);
                // slither-disable-next-line incorrect-equality
                if (balance_of == 0 && user_epoch > max_user_epoch) break;
                if (balance_of > 0) {
                    to_distribute += (balance_of * tokens_per_day[day_cursor]) / ve_supply[day_cursor];
                }
                day_cursor += EPOCH;
            }
        }

        user_epoch = Math.min(max_user_epoch, user_epoch - 1);
        user_epoch_of[_tokenId] = user_epoch;
        time_cursor_of[_tokenId] = day_cursor;

        emit Claimed(_tokenId, to_distribute, user_epoch, max_user_epoch);

        return to_distribute;
    }

    function _claimable(
        uint _tokenId,
        address ve,
        uint _last_token_time
    ) internal view returns (uint) {
        uint user_epoch = 0;
        uint to_distribute = 0;

        uint max_user_epoch = VotingEscrow(ve).user_point_epoch(_tokenId);
        uint _start_time = start_time;

        if (max_user_epoch == 0) return 0;

        uint day_cursor = time_cursor_of[_tokenId];
        // slither-disable-next-line incorrect-equality
        if (day_cursor == 0) {
            user_epoch = _find_timestamp_user_epoch(ve, _tokenId, _start_time, max_user_epoch);
        } else {
            user_epoch = user_epoch_of[_tokenId];
        }

        if (user_epoch == 0) user_epoch = 1;

        VotingEscrow.Point memory user_point = VotingEscrow(ve).user_point_history(_tokenId, user_epoch);
        // slither-disable-next-line incorrect-equality
        if (day_cursor == 0) day_cursor = ((user_point.ts + EPOCH - 1) / EPOCH) * EPOCH;
        if (day_cursor >= last_token_time) return 0;
        if (day_cursor < _start_time) day_cursor = _start_time;

        // slither-disable-next-line uninitialized-local
        VotingEscrow.Point memory old_user_point;

        for (uint i = 0; i < 50; i++) {
            if (day_cursor >= _last_token_time) break;

            if (day_cursor >= user_point.ts && user_epoch <= max_user_epoch) {
                user_epoch += 1;
                old_user_point = user_point;
                if (user_epoch > max_user_epoch) {
                    user_point = VotingEscrow.Point(0, 0, 0, 0);
                } else {
                    user_point = VotingEscrow(ve).user_point_history(_tokenId, user_epoch);
                }
            } else {
                int128 dt = int128(int(day_cursor - old_user_point.ts));
                uint balance_of = Math.max(uint(int(old_user_point.bias - (dt * old_user_point.slope) / WAD)), 0);
                // slither-disable-next-line incorrect-equality
                if (balance_of == 0 && user_epoch > max_user_epoch) break;
                if (balance_of > 0) {
                    to_distribute += (balance_of * tokens_per_day[day_cursor]) / ve_supply[day_cursor];
                }
                day_cursor += EPOCH;
            }
        }

        return to_distribute;
    }

    function claimable(uint _tokenId) external view returns (uint) {
        uint _last_token_time = (last_token_time / EPOCH) * EPOCH;
        return _claimable(_tokenId, voting_escrow, _last_token_time);
    }

    function claim(uint _tokenId) external returns (uint) {
        if (block.timestamp >= time_cursor) _checkpoint_total_supply();
        uint _last_token_time = last_token_time;
        _last_token_time = (_last_token_time / EPOCH) * EPOCH;
        uint amount = _claim(_tokenId, voting_escrow, _last_token_time);
        if (amount != 0) {
            VotingEscrow(voting_escrow).deposit_for(_tokenId, amount);
            token_last_balance -= amount;
        }
        return amount;
    }

    function claim_many(uint[] memory _tokenIds) external returns (bool) {
        if (block.timestamp >= time_cursor) _checkpoint_total_supply();
        uint _last_token_time = last_token_time;
        _last_token_time = (_last_token_time / EPOCH) * EPOCH;
        address _voting_escrow = voting_escrow;
        uint total = 0;

        for (uint i = 0; i < _tokenIds.length; i++) {
            uint _tokenId = _tokenIds[i];
            if (_tokenId == 0) break;
            uint amount = _claim(_tokenId, _voting_escrow, _last_token_time);
            if (amount != 0) {
                VotingEscrow(_voting_escrow).deposit_for(_tokenId, amount);
                total += amount;
            }
        }
        if (total != 0) {
            token_last_balance -= total;
        }

        return true;
    }

    // Once off event on contract initialize
    function setDepositor(address _depositor) external {
        require(msg.sender == depositor);
        depositor = _depositor;
    }
}

