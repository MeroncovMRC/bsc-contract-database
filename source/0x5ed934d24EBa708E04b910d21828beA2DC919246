// SPDX-License-Identifier: GPL-3.0-or-later

pragma solidity ^0.7.4;

interface ERC20 {
    function balanceOf(address _owner) external view returns (uint256);
    function allowance(address _owner, address _spender) external view returns (uint256);
    function transfer(address _to, uint256 _value) external returns (bool);
    function transferFrom(address _from, address _to, uint256 _value) external returns (bool);
    function approve(address _spender, uint256 _value) external returns (bool);
    event Transfer(address indexed from, address indexed to, uint256 value);
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

library SafeMath {
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        if (a == 0) {
            return 0;
        }
        uint256 c = a * b;
        assert(c / a == b);
        return c;
    }

    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        uint256 c = a / b;
        return c;
    }

    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        assert(b <= a);
        return a - b;
    }

    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        uint256 c = a + b;
        assert(c >= a);
        return c;
    }
}


contract VerumProtocol is ERC20 {
    using SafeMath for uint256;
    address public governor;
    address public pendingGov;
    string public name = "Verum Protocol";
    string public symbol = "VRM";
    uint8 public constant decimals = 18;
    uint256 private constant decimalFactor = 10 ** uint256(decimals);
    uint256 public constant startingSupply = 300000 * decimalFactor;
    uint256 public burntTokens = 0;
    uint256 public burnRate = 0; // Burn rate is zero at start until sale and DEX listing
    bool public minted = false;
    mapping (address => uint256) balances;
    mapping (address => mapping (address => uint256)) internal allowed;


    
    modifier onlyGov() {
        require(governor == msg.sender, "Caller is not the Governor");
        _;
    }
    

    constructor() {
        governor = msg.sender;
    }
    
    function totalSupply() public view returns (uint256) {
        uint256 currentTokens = startingSupply.sub(burntTokens);
        return currentTokens;
    }
    
    function _setPendingGov(address _pendingGov) external onlyGov {
        pendingGov = _pendingGov;
    }
    
    function _acceptGov() external {
        require(msg.sender == pendingGov, "Call must be from pending Governor ");
        governor = pendingGov;
        pendingGov = address(0);
    }

    function enableBurn() public onlyGov {
        burnRate = 3; // Default is 3 percent
    }
    
    function changeBurnRate(uint256 _rate) public onlyGov {
        // Burn rate can be changed using Governance
        require(_rate <= 15, "Burn rate can't be higher than 15%");
        require(_rate >= 3, "Burn rate can't be lower than 3%");
        burnRate = _rate;
    }
    
    function mint(address _owner) public onlyGov returns (bool) {
        require(minted != true, "Tokens already minted");
        balances[_owner] = startingSupply;
        emit Transfer(address(0), _owner, startingSupply);
        minted = true; // One time event 
        return true;
    }
    
    
    function balanceOf(address _owner) public view override returns (uint256 balance) {
        return balances[_owner];
    }

    function allowance(address _owner, address _spender) public view override returns (uint256) {
        return allowed[_owner][_spender];
    }
    
    function _burn(address account, uint256 amount) internal {
        require(account != address(0));
        balances[account] = balances[account].sub(amount);
        burntTokens = burntTokens.add(amount);
        emit Transfer(account, address(0), amount);
    }

    function transfer(address _to, uint256 _value) public override returns (bool) {
        require(_to != address(0));
        require(_value <= balances[msg.sender]);
        uint256 tokensToBurn = _value.mul(burnRate).div(100);
        uint256 tokensToSend = _value.sub(tokensToBurn);
        balances[msg.sender] = balances[msg.sender].sub(tokensToSend);
        _burn(msg.sender, tokensToBurn);
        balances[_to] = balances[_to].add(tokensToSend);
        
        emit Transfer(msg.sender, _to, tokensToSend);
        return true;
    }

    function transferFrom(address _from, address _to, uint256 _value) public  override returns (bool) {
        require(_to != address(0));
        require(_value <= balances[_from]);
        require(_value <= allowed[_from][msg.sender]);
        uint256 tokensToBurn = _value.mul(burnRate).div(100);
        uint256 tokensToSend = _value.sub(tokensToBurn);
        balances[_from] = balances[_from].sub(tokensToSend);
        _burn(_from, tokensToBurn);
        balances[_to] = balances[_to].add(tokensToSend);
        allowed[_from][msg.sender] = allowed[_from][msg.sender].sub(_value);
        emit Transfer(_from, _to, tokensToSend);
        return true;
    }

    function approve(address _spender, uint256 _value) public override returns (bool) {
        allowed[msg.sender][_spender] = _value;
        emit Approval(msg.sender, _spender, _value);
        return true;
    }

    function increaseApproval(address _spender, uint _addedValue) public returns (bool) {
        allowed[msg.sender][_spender] = allowed[msg.sender][_spender].add(_addedValue);
        emit Approval(msg.sender, _spender, allowed[msg.sender][_spender]);
        return true;
    }

    function decreaseApproval(address _spender, uint _subtractedValue) public returns (bool) {
        uint oldValue = allowed[msg.sender][_spender];
        if (_subtractedValue > oldValue) {
            allowed[msg.sender][_spender] = 0;
        } else {
            allowed[msg.sender][_spender] = oldValue.sub(_subtractedValue);
        }
        emit Approval(msg.sender, _spender, allowed[msg.sender][_spender]);
        return true;
    }
}