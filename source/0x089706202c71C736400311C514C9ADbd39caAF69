
// File: contracts/Presale.sol
// SPDX-License-Identifier: UNLICENSED

pragma solidity ^0.8.9;

import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";

import "./interfaces/IEACAggregatorProxy.sol";
import "./interfaces/IPancakeRouter02.sol";
import "./interfaces/IPresale.sol";

contract Presale is IPresale, Ownable, ReentrancyGuard {
    using SafeERC20 for IERC20;

    IPancakeRouter02 public immutable PancakeRouter;
    IEACAggregatorProxy public immutable ChainlinkPriceFeed;
    IERC20 public NFTYToken;
    IERC20 public BUSDToken;
    uint8 public BNB_ROUNDING_DEGREE = 9;

    bool public isConversionEnabled;
    bool public isPresaleFinished;

    mapping(address => uint256) public claimable;

    uint256[5] public tokensSold;
    uint256[5] public pricesForStages;
    uint256[5] public stageLimits;

    address public admin;

    uint8 public currentStage;

    constructor(
        uint256[5] memory _pricesForStages,
        uint256[5] memory _stageLimits,
        address _chainlinkPriceFeedAddress,
        address _NFTYTokenAddress,
        address _BUSDTokenAddress,
        address _pancakeRouterAddress,
        address _admin
    ) {
        pricesForStages = _pricesForStages;
        stageLimits = _stageLimits;
        ChainlinkPriceFeed = IEACAggregatorProxy(_chainlinkPriceFeedAddress);
        NFTYToken = IERC20(_NFTYTokenAddress);
        BUSDToken = IERC20(_BUSDTokenAddress);
        PancakeRouter = IPancakeRouter02(_pancakeRouterAddress);
        currentStage = 0;
        admin = _admin;
    }

    modifier onlyEOA() {
        require(msg.sender.code.length == 0, "Only EOA can call this function");
        _;
    }

    modifier whenNotFinished() {
        require(!isPresaleFinished, "Presale was finished");
        _;
    }

    modifier whenFinished() {
        require(isPresaleFinished, "Presale is not finished yet");
        _;
    }

    modifier onlyAdmin() {
        require(msg.sender == admin, "Presale: caller is not the admin");
        _;
    }

    receive() external payable whenNotFinished {
        buyTokenWithBNB();
    }

    function setAdmin(address _newAdmin) external onlyAdmin {
        admin = _newAdmin;
    }

    function setBNBRoundingDegree(uint8 _degree) external onlyAdmin {
        BNB_ROUNDING_DEGREE = _degree;
    }

    function setPriceInUSD(uint256[5] memory _pricesForStages) external onlyAdmin {
        pricesForStages = _pricesForStages;
    }

    function setStageLimits(uint256[5] memory _stageLimits) external onlyAdmin {
        stageLimits = _stageLimits;
    }

    function changeConversionStatus(bool _isConversionEnabled) external onlyAdmin {
        isConversionEnabled = _isConversionEnabled;
    }

    function changePresaleStatus(bool _isPresaleFinished) external onlyAdmin {
        isPresaleFinished = _isPresaleFinished;
    }

    function getPriceFromChainlinkPriceFeed() public view returns (uint256) {
        return uint256(ChainlinkPriceFeed.latestAnswer());
    }

    function getPriceFeedDecimals() public view returns (uint256) {
        return uint256(ChainlinkPriceFeed.decimals());
    }

    function getTotalTokensSold() public view returns (uint256) {
        return tokensSold[0] + tokensSold[1] + tokensSold[2] + tokensSold[3] + tokensSold[4];
    }

    function getPriceInBNB() public view returns (uint256) {
        uint256 priceFeedDecimals = getPriceFeedDecimals();
        return (pricesForStages[currentStage] * 10**priceFeedDecimals) / getPriceFromChainlinkPriceFeed();
    }

    function getPriceInBUSD() public view returns (uint256) {
        return pricesForStages[currentStage];
    }

    function claimRemainingFunds(address remainingTokenAddress) external onlyOwner {
        uint256 remainingBalance = IERC20(remainingTokenAddress).balanceOf(address(this));
        IERC20(remainingTokenAddress).safeTransfer(msg.sender, remainingBalance);
    }

    function assignTokens(uint256 amountOut) internal {
        uint256 totalSold = getTotalTokensSold();
        uint256 presaleTokensSupply = NFTYToken.balanceOf(address(this));
        require(totalSold + amountOut <= presaleTokensSupply, "Not enough tokens left for sale");
        claimable[msg.sender] += amountOut;
        tokensSold[currentStage] += amountOut;
        if (tokensSold[currentStage] > stageLimits[currentStage]) currentStage++;
    }

    function buyTokenWithBNB() public payable onlyEOA whenNotFinished {
        require(msg.value > 0, "Amount must be greater than 0");
        uint256 BNBpriceOfOneNFTYToken = getPriceInBNB();

        if (isConversionEnabled) {
            uint256 exchangedAmount = swapBNBToBUSD(msg.value);
            require(exchangedAmount > 0, "Conversion Error");
        } else {
            (bool success, ) = owner().call{ value: msg.value }("");
            require(success, "Failed to send BNB");
        }

        uint256 amountOut = (msg.value * 1e18) / BNBpriceOfOneNFTYToken;
        amountOut = round(amountOut);
        assignTokens(amountOut);

        emit TokenPurchased(msg.sender, "BNB", msg.value, amountOut);
    }

    function buyTokenWithBUSD(uint256 BUSDAmount) public onlyEOA whenNotFinished {
        require(BUSDAmount > 0, "Amount must be greater than 0");
        BUSDToken.safeTransferFrom(msg.sender, owner(), BUSDAmount);
        uint256 BUSDpriceOfOneNFTYToken = getPriceInBUSD();
        uint256 amountOut = (BUSDAmount * 1e18) / BUSDpriceOfOneNFTYToken;
        assignTokens(amountOut);

        emit TokenPurchased(msg.sender, "BUSD", BUSDAmount, amountOut);
    }

    function claimTokens() public nonReentrant whenFinished {
        require(claimable[msg.sender] > 0, "Nothing to claim");
        uint256 amount = claimable[msg.sender];
        claimable[msg.sender] = 0;
        NFTYToken.safeTransfer(msg.sender, amount);

        emit TokenClaimed(msg.sender, amount);
    }

    /**
     * @dev internal function to swap BNB to BUSD.
     * @dev is called in {buyTokenWithBNB} only if the isConversionEnabled variable is true.
     * @param _amount <uint256> - the amount of BNB to swap.
     * @return <uint256> - the amount of BUSD received.
     */
    function swapBNBToBUSD(uint256 _amount) internal returns (uint256) {
        address[] memory path = new address[](2);
        path[0] = PancakeRouter.WETH();
        path[1] = address(BUSDToken);
        uint256[] memory exchangedAmounts = PancakeRouter.swapExactETHForTokens{ value: _amount }(
            0,
            path,
            owner(),
            block.timestamp
        );

        return exchangedAmounts[1];
    }

    /**
     * @notice internal function to round an _amountOut to a specific degree.
     * @dev calculates the modulo (remainder of division) of the _amountOut by 10^degree.
     * Divides this modulo by 10^(degree-1).
     * Using math rounding rule checks if the result is greater or equal to 5.
     * If it is, adds required remainder for rounding up and returns rounded _amountOut.
     * If it is not, substracts modulo from _amountOut and returns it.
     * @param _amountOut <uint256> - the amount to round.
     * @return <uint256> - the rounded amount.
     */
    function round(uint256 _amountOut) internal view returns (uint256) {
        uint256 roundBy = 10**BNB_ROUNDING_DEGREE;
        uint256 modulo = _amountOut % roundBy;
        return modulo / (roundBy / 10) >= 5 ? _amountOut + roundBy - modulo : _amountOut - modulo;
    }
}


// File: @openzeppelin/contracts/access/Ownable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}


// File: @openzeppelin/contracts/token/ERC20/IERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);
}


// File: @openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (token/ERC20/utils/SafeERC20.sol)

pragma solidity ^0.8.0;

import "../IERC20.sol";
import "../extensions/draft-IERC20Permit.sol";
import "../../../utils/Address.sol";

/**
 * @title SafeERC20
 * @dev Wrappers around ERC20 operations that throw on failure (when the token
 * contract returns false). Tokens that return no value (and instead revert or
 * throw on failure) are also supported, non-reverting calls are assumed to be
 * successful.
 * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,
 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.
 */
library SafeERC20 {
    using Address for address;

    function safeTransfer(
        IERC20 token,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));
    }

    function safeTransferFrom(
        IERC20 token,
        address from,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));
    }

    /**
     * @dev Deprecated. This function has issues similar to the ones found in
     * {IERC20-approve}, and its usage is discouraged.
     *
     * Whenever possible, use {safeIncreaseAllowance} and
     * {safeDecreaseAllowance} instead.
     */
    function safeApprove(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        // safeApprove should only be called when setting an initial allowance,
        // or when resetting it to zero. To increase and decrease it, use
        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
        require(
            (value == 0) || (token.allowance(address(this), spender) == 0),
            "SafeERC20: approve from non-zero to non-zero allowance"
        );
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
    }

    function safeIncreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        uint256 newAllowance = token.allowance(address(this), spender) + value;
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
    }

    function safeDecreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            uint256 newAllowance = oldAllowance - value;
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
        }
    }

    function safePermit(
        IERC20Permit token,
        address owner,
        address spender,
        uint256 value,
        uint256 deadline,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) internal {
        uint256 nonceBefore = token.nonces(owner);
        token.permit(owner, spender, value, deadline, v, r, s);
        uint256 nonceAfter = token.nonces(owner);
        require(nonceAfter == nonceBefore + 1, "SafeERC20: permit did not succeed");
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     */
    function _callOptionalReturn(IERC20 token, bytes memory data) private {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We use {Address.functionCall} to perform this call, which verifies that
        // the target address contains contract code and also asserts for success in the low-level call.

        bytes memory returndata = address(token).functionCall(data, "SafeERC20: low-level call failed");
        if (returndata.length > 0) {
            // Return data is optional
            require(abi.decode(returndata, (bool)), "SafeERC20: ERC20 operation did not succeed");
        }
    }
}


// File: @openzeppelin/contracts/security/ReentrancyGuard.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (security/ReentrancyGuard.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}


// File: contracts/interfaces/IEACAggregatorProxy.sol
// SPDX-License-Identifier: UNLICENSED

pragma solidity ^0.8.9;

interface IEACAggregatorProxy {
    function latestAnswer() external view returns (int256);

    function decimals() external view returns (uint8);
}


// File: contracts/interfaces/IPancakeRouter02.sol
pragma solidity >=0.6.2;

import "./IPancakeRouter01.sol";

interface IPancakeRouter02 is IPancakeRouter01 {
    function removeLiquidityETHSupportingFeeOnTransferTokens(
        address token,
        uint256 liquidity,
        uint256 amountTokenMin,
        uint256 amountETHMin,
        address to,
        uint256 deadline
    ) external returns (uint256 amountETH);

    function one() external view returns (uint256[] memory);

    function removeLiquidityETHWithPermitSupportingFeeOnTransferTokens(
        address token,
        uint256 liquidity,
        uint256 amountTokenMin,
        uint256 amountETHMin,
        address to,
        uint256 deadline,
        bool approveMax,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external returns (uint256 amountETH);

    function swapExactTokensForTokensSupportingFeeOnTransferTokens(
        uint256 amountIn,
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external;

    function swapExactETHForTokensSupportingFeeOnTransferTokens(
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external payable;

    function swapExactTokensForETHSupportingFeeOnTransferTokens(
        uint256 amountIn,
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external;
}


// File: contracts/interfaces/IPresale.sol
// SPDX-License-Identifier: UNLICENSED

pragma solidity ^0.8.9;

/// @title IPresale is an interface for the Presale contract.
interface IPresale {
    /**
     * @notice event for token purchase logging.
     * @param buyer <address> - who paid for the tokens.
     * @param purchasedWith <string> - what currency was used to pay for the tokens.
     * @param amountIn <uint256> - amount of tokens was used to pay for the tokens.
     * @param amountOut <uint256> - amount of tokens was received.
     */
    event TokenPurchased(address indexed buyer, string purchasedWith, uint256 amountIn, uint256 amountOut);

    /**
     * @notice event for token claim logging.
     * @param owner <address> - who claimed the tokens.
     * @param amount <uint256> - amount of tokens was received.
     */

    event TokenClaimed(address indexed owner, uint256 amount);

    /**
     * @notice sets new admin.
     * @dev only owner can call this function.
     * @dev changes the admin variable.
     */
    function setAdmin(address _newAdmin) external;

    /**
     * @notice sets rounding degree for buyTokenWithBNB function.
     * @dev only owner can call this function.
     * @dev changes the BNB_ROUNDING_DEGREE variable.
     * @param _degree <uint8> - the degree of rounding.
     */
    function setBNBRoundingDegree(uint8 _degree) external;

    /**
     * @notice sets the price of the NFTY token in BUSD.
     * @notice price should be set in the following format: 1BUSD = 10^18, 0.01BUSD = 10^16
     * @dev only owner can call this function.
     * @dev changes the _pricesForStages variable.
     * @param _pricesForStages Array.<uint256> - prices of the NFTY token in BUSD for each stage.
     */
    function setPriceInUSD(uint256[5] memory _pricesForStages) external;

    /**
     * @notice sets the limits of the NFTY token.
     * @dev only owner can call this function.
     * @dev changes the stageLimits variable.
     * @param _stageLimits Array.<uint256> - limits of the NFTY tokens for each stage.
     */
    function setStageLimits(uint256[5] memory _stageLimits) external;

    /**
     * @notice changes the status of the conversion of BNB to BUSD.
     * @dev only owner can call this function.
     * @dev changes the isConversionEnabled variable.
     * @param _isConversionEnabled <bool> - the status of the conversion of BNB to BUSD.
     */

    function changeConversionStatus(bool _isConversionEnabled) external;

    /**
     * @notice changes the status of Presale (finished or not).
     * @dev only owner can call this function.
     * @dev changes the _isPresaleFinished variable.
     * @param _isPresaleFinished <bool> - the status of the Presale
     */

    function changePresaleStatus(bool _isPresaleFinished) external;

    /**
     * @notice gets current price of 1 BNB in USD from Chainlink Price Feed.
     * @dev uses Chainlink Price Feed to get the price.
     * @return <uint256> - the price of 1 BNB in USD.
     */
    function getPriceFromChainlinkPriceFeed() external view returns (uint256);

    /**
     * @notice gets decimals of selected Chainlink Price Feed.
     * @dev uses Chainlink Price Feed to get decimals.
     * @return <uint256> - decimals.
     */
    function getPriceFeedDecimals() external view returns (uint256);

    /**
     * @notice gets total amount of tokens sold.
     * @dev sum of tokens sold in each stage.
     * @return <uint256> - total tokens sold.
     */
    function getTotalTokensSold() external view returns (uint256);

    /**
     * @notice view function to get the price of the NFTY token in BNB.
     * @dev uses the PancakeSwap WBNB/BUSD pair to get the price.
     * @return <uint256> - the price of the NFTY token in BNB.
     */
    function getPriceInBNB() external view returns (uint256);

    /**
     * @notice view function to get the price of the NFTY token in BUSD.
     * @dev uses internalPriceUSD variable to get the price.
     * @return <uint256> - the price of the NFTY token in BUSD.
     */
    function getPriceInBUSD() external view returns (uint256);

    /**
     * @notice function to withdraw token which address was provided.
     * @dev only owner can call this function.
     * @param remainingTokenAddress <address> - the address of the token to withdraw.
     */
    function claimRemainingFunds(address remainingTokenAddress) external;

    /**
     * @notice function to buy NFTY tokens with BNB.
     * @dev everyone can call this function.
     * @dev has nonReentrant modifier.
     * @dev payable function. msg.value is the amount of BNB sent.
     * @dev if the isConversionEnabled variable is true, then the function converts BNB to BUSD.
     * @dev emits the TokenPurchased event.
     */
    function buyTokenWithBNB() external payable;

    /**
     * @notice function to buy NFTY tokens with BUSD.
     * @dev everyone can call this function.
     * @param BUSDAmount <uint256> - the amount of BUSD tokens to buy NFTY tokens.
     * @dev emits the TokenPurchased event.
     */
    function buyTokenWithBUSD(uint256 BUSDAmount) external;

    /**
     * @notice function to claim NFTY tokens with.
     * @dev everyone who bought at least 1 wei NFTY can call this function.
     * @dev emits the TokenClaimed event.
     */
    function claimTokens() external;
}


// File: @openzeppelin/contracts/utils/Context.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}


// File: @openzeppelin/contracts/token/ERC20/extensions/draft-IERC20Permit.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/extensions/draft-IERC20Permit.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 Permit extension allowing approvals to be made via signatures, as defined in
 * https://eips.ethereum.org/EIPS/eip-2612[EIP-2612].
 *
 * Adds the {permit} method, which can be used to change an account's ERC20 allowance (see {IERC20-allowance}) by
 * presenting a message signed by the account. By not relying on {IERC20-approve}, the token holder account doesn't
 * need to send a transaction, and thus is not required to hold Ether at all.
 */
interface IERC20Permit {
    /**
     * @dev Sets `value` as the allowance of `spender` over ``owner``'s tokens,
     * given ``owner``'s signed approval.
     *
     * IMPORTANT: The same issues {IERC20-approve} has related to transaction
     * ordering also apply here.
     *
     * Emits an {Approval} event.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     * - `deadline` must be a timestamp in the future.
     * - `v`, `r` and `s` must be a valid `secp256k1` signature from `owner`
     * over the EIP712-formatted function arguments.
     * - the signature must use ``owner``'s current nonce (see {nonces}).
     *
     * For more information on the signature format, see the
     * https://eips.ethereum.org/EIPS/eip-2612#specification[relevant EIP
     * section].
     */
    function permit(
        address owner,
        address spender,
        uint256 value,
        uint256 deadline,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external;

    /**
     * @dev Returns the current nonce for `owner`. This value must be
     * included whenever a signature is generated for {permit}.
     *
     * Every successful call to {permit} increases ``owner``'s nonce by one. This
     * prevents a signature from being used multiple times.
     */
    function nonces(address owner) external view returns (uint256);

    /**
     * @dev Returns the domain separator used in the encoding of the signature for {permit}, as defined by {EIP712}.
     */
    // solhint-disable-next-line func-name-mixedcase
    function DOMAIN_SEPARATOR() external view returns (bytes32);
}


// File: @openzeppelin/contracts/utils/Address.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly
                /// @solidity memory-safe-assembly
                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}


// File: contracts/interfaces/IPancakeRouter01.sol
pragma solidity >=0.6.2;

interface IPancakeRouter01 {
    function factory() external pure returns (address);
    function WETH() external pure returns (address);

    function addLiquidity(
        address tokenA,
        address tokenB,
        uint amountADesired,
        uint amountBDesired,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline
    ) external returns (uint amountA, uint amountB, uint liquidity);
    function addLiquidityETH(
        address token,
        uint amountTokenDesired,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline
    ) external payable returns (uint amountToken, uint amountETH, uint liquidity);
    function removeLiquidity(
        address tokenA,
        address tokenB,
        uint liquidity,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline
    ) external returns (uint amountA, uint amountB);
    function removeLiquidityETH(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline
    ) external returns (uint amountToken, uint amountETH);
    function removeLiquidityWithPermit(
        address tokenA,
        address tokenB,
        uint liquidity,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline,
        bool approveMax, uint8 v, bytes32 r, bytes32 s
    ) external returns (uint amountA, uint amountB);
    function removeLiquidityETHWithPermit(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline,
        bool approveMax, uint8 v, bytes32 r, bytes32 s
    ) external returns (uint amountToken, uint amountETH);
    function swapExactTokensForTokens(
        uint amountIn,
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external returns (uint[] memory amounts);
    function swapTokensForExactTokens(
        uint amountOut,
        uint amountInMax,
        address[] calldata path,
        address to,
        uint deadline
    ) external returns (uint[] memory amounts);
    function swapExactETHForTokens(uint amountOutMin, address[] calldata path, address to, uint deadline)
        external
        payable
        returns (uint[] memory amounts);
    function swapTokensForExactETH(uint amountOut, uint amountInMax, address[] calldata path, address to, uint deadline)
        external
        returns (uint[] memory amounts);
    function swapExactTokensForETH(uint amountIn, uint amountOutMin, address[] calldata path, address to, uint deadline)
        external
        returns (uint[] memory amounts);
    function swapETHForExactTokens(uint amountOut, address[] calldata path, address to, uint deadline)
        external
        payable
        returns (uint[] memory amounts);

    function quote(uint amountA, uint reserveA, uint reserveB) external pure returns (uint amountB);
    function getAmountOut(uint amountIn, uint reserveIn, uint reserveOut) external pure returns (uint amountOut);
    function getAmountIn(uint amountOut, uint reserveIn, uint reserveOut) external pure returns (uint amountIn);
    function getAmountsOut(uint amountIn, address[] calldata path) external view returns (uint[] memory amounts);
    function getAmountsIn(uint amountOut, address[] calldata path) external view returns (uint[] memory amounts);
}

