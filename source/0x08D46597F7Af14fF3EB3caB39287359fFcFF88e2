
// File: @openzeppelin/contracts/access/Ownable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}


// File: @openzeppelin/contracts/token/ERC20/IERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);
}


// File: @openzeppelin/contracts/token/ERC721/IERC721Receiver.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC721/IERC721Receiver.sol)

pragma solidity ^0.8.0;

/**
 * @title ERC721 token receiver interface
 * @dev Interface for any contract that wants to support safeTransfers
 * from ERC721 asset contracts.
 */
interface IERC721Receiver {
    /**
     * @dev Whenever an {IERC721} `tokenId` token is transferred to this contract via {IERC721-safeTransferFrom}
     * by `operator` from `from`, this function is called.
     *
     * It must return its Solidity selector to confirm the token transfer.
     * If any other value is returned or the interface is not implemented by the recipient, the transfer will be reverted.
     *
     * The selector can be obtained in Solidity with `IERC721Receiver.onERC721Received.selector`.
     */
    function onERC721Received(
        address operator,
        address from,
        uint256 tokenId,
        bytes calldata data
    ) external returns (bytes4);
}


// File: @openzeppelin/contracts/token/ERC721/utils/ERC721Holder.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC721/utils/ERC721Holder.sol)

pragma solidity ^0.8.0;

import "../IERC721Receiver.sol";

/**
 * @dev Implementation of the {IERC721Receiver} interface.
 *
 * Accepts all token transfers.
 * Make sure the contract is able to use its token with {IERC721-safeTransferFrom}, {IERC721-approve} or {IERC721-setApprovalForAll}.
 */
contract ERC721Holder is IERC721Receiver {
    /**
     * @dev See {IERC721Receiver-onERC721Received}.
     *
     * Always returns `IERC721Receiver.onERC721Received.selector`.
     */
    function onERC721Received(
        address,
        address,
        uint256,
        bytes memory
    ) public virtual override returns (bytes4) {
        return this.onERC721Received.selector;
    }
}


// File: @openzeppelin/contracts/utils/Context.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}


// File: @uniswap/v2-core/contracts/interfaces/IUniswapV2Factory.sol
// SPDX-License-Identifier: MIT

pragma solidity >=0.5.0;

interface IUniswapV2Factory {
    event PairCreated(address indexed token0, address indexed token1, address pair, uint);

    function feeTo() external view returns (address);
    function feeToSetter() external view returns (address);

    function getPair(address tokenA, address tokenB) external view returns (address pair);
    function allPairs(uint) external view returns (address pair);
    function allPairsLength() external view returns (uint);

    function createPair(address tokenA, address tokenB) external returns (address pair);

    function setFeeTo(address) external;
    function setFeeToSetter(address) external;
}


// File: @uniswap/v2-periphery/contracts/interfaces/IUniswapV2Router01.sol
// SPDX-License-Identifier: MIT

pragma solidity >=0.6.2;

interface IUniswapV2Router01 {
    function factory() external pure returns (address);
    function WETH() external pure returns (address);

    function addLiquidity(
        address tokenA,
        address tokenB,
        uint amountADesired,
        uint amountBDesired,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline
    ) external returns (uint amountA, uint amountB, uint liquidity);
    function addLiquidityETH(
        address token,
        uint amountTokenDesired,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline
    ) external payable returns (uint amountToken, uint amountETH, uint liquidity);
    function removeLiquidity(
        address tokenA,
        address tokenB,
        uint liquidity,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline
    ) external returns (uint amountA, uint amountB);
    function removeLiquidityETH(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline
    ) external returns (uint amountToken, uint amountETH);
    function removeLiquidityWithPermit(
        address tokenA,
        address tokenB,
        uint liquidity,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline,
        bool approveMax, uint8 v, bytes32 r, bytes32 s
    ) external returns (uint amountA, uint amountB);
    function removeLiquidityETHWithPermit(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline,
        bool approveMax, uint8 v, bytes32 r, bytes32 s
    ) external returns (uint amountToken, uint amountETH);
    function swapExactTokensForTokens(
        uint amountIn,
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external returns (uint[] memory amounts);
    function swapTokensForExactTokens(
        uint amountOut,
        uint amountInMax,
        address[] calldata path,
        address to,
        uint deadline
    ) external returns (uint[] memory amounts);
    function swapExactETHForTokens(uint amountOutMin, address[] calldata path, address to, uint deadline)
        external
        payable
        returns (uint[] memory amounts);
    function swapTokensForExactETH(uint amountOut, uint amountInMax, address[] calldata path, address to, uint deadline)
        external
        returns (uint[] memory amounts);
    function swapExactTokensForETH(uint amountIn, uint amountOutMin, address[] calldata path, address to, uint deadline)
        external
        returns (uint[] memory amounts);
    function swapETHForExactTokens(uint amountOut, address[] calldata path, address to, uint deadline)
        external
        payable
        returns (uint[] memory amounts);

    function quote(uint amountA, uint reserveA, uint reserveB) external pure returns (uint amountB);
    function getAmountOut(uint amountIn, uint reserveIn, uint reserveOut) external pure returns (uint amountOut);
    function getAmountIn(uint amountOut, uint reserveIn, uint reserveOut) external pure returns (uint amountIn);
    function getAmountsOut(uint amountIn, address[] calldata path) external view returns (uint[] memory amounts);
    function getAmountsIn(uint amountOut, address[] calldata path) external view returns (uint[] memory amounts);
}


// File: @uniswap/v2-periphery/contracts/interfaces/IUniswapV2Router02.sol
// SPDX-License-Identifier: MIT

pragma solidity >=0.6.2;

import './IUniswapV2Router01.sol';

interface IUniswapV2Router02 is IUniswapV2Router01 {
    function removeLiquidityETHSupportingFeeOnTransferTokens(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline
    ) external returns (uint amountETH);
    function removeLiquidityETHWithPermitSupportingFeeOnTransferTokens(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline,
        bool approveMax, uint8 v, bytes32 r, bytes32 s
    ) external returns (uint amountETH);

    function swapExactTokensForTokensSupportingFeeOnTransferTokens(
        uint amountIn,
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external;
    function swapExactETHForTokensSupportingFeeOnTransferTokens(
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external payable;
    function swapExactTokensForETHSupportingFeeOnTransferTokens(
        uint amountIn,
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external;
}


// File: contracts/interfaces/IERC721A.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.16;

interface IERC721A {
    function totalSupply() external view returns (uint256);
    function tokenByIndex(uint256 index) external view returns (uint256);
    function tokenOfOwnerByIndex(address owner, uint256 index) external view returns (uint256);
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
    function balanceOf(address owner) external view returns (uint256);
    function ownerOf(uint256 tokenId) external view returns (address);
    function name() external view returns (string memory);
    function symbol() external view returns (string memory);
    function tokenURI(uint256 tokenId) external view returns (string memory);
    function approve(address to, uint256 tokenId) external;
    function getApproved(uint256 tokenId) external view returns (address);
    function setApprovalForAll(address operator, bool approved) external;
    function isApprovedForAll(address owner, address operator) external view returns (bool);
    function transferFrom(address from, address to, uint256 tokenId) external;
    function safeTransferFrom(address from, address to, uint256 tokenId) external;
    function safeTransferFrom(address from, address to, uint256 tokenId, bytes memory _data) external;
}

// File: contracts/TwoPaws/TwoPaws.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.16;

import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "../interfaces/IERC721A.sol";
import "@openzeppelin/contracts/token/ERC721/utils/ERC721Holder.sol";
import "@uniswap/v2-periphery/contracts/interfaces/IUniswapV2Router02.sol";
import "@uniswap/v2-core/contracts/interfaces/IUniswapV2Factory.sol";


// https://twitter.com/TwoPawsDefi
// https://arbitrum.twopaws.app/


// WT - Wrong Timestamp
// WR - Wrong Role
// NC - No Contracts
// WA - Wrong Amount
// WS - Wrong Status
// WLF - Wrong Lender Fee
// WTP - Wrong Tokens Pair
// TF - Transfer Filed
// LCB - The Lender cannot be the Borrower
// BCL - The Borrower cannot be the Lender
// CF - Cancel Filed
// BR - Only Borrower Can Repay
// LL - Only Lender Can Liquidate Order
// IB - Insufficient Tokens Balance
// EA - Empty Array
// ETF - Ether Transfer Filed
// LI - Locked NFTId
// 0A - Zero Address
// WD - Wrong Days
// WP - Wrong Price

library TransferHelper {
    function safeApprove(address token, address to, uint value) internal {
        // bytes4(keccak256(bytes('approve(address,uint256)')));
        (bool success, bytes memory data) = token.call(abi.encodeWithSelector(0x095ea7b3, to, value));
        require(success && (data.length == 0 || abi.decode(data, (bool))), 'TransferHelper: APPROVE_FAILED');
    }

    function safeTransfer(address token, address to, uint value) internal {
        // bytes4(keccak256(bytes('transfer(address,uint256)')));
        (bool success, bytes memory data) = token.call(abi.encodeWithSelector(0xa9059cbb, to, value));
        require(success && (data.length == 0 || abi.decode(data, (bool))), 'TransferHelper: TRANSFER_FAILED');
    }

    function safeTransferFrom(address token, address from, address to, uint value) internal {
        // bytes4(keccak256(bytes('transferFrom(address,address,uint256)')));
        (bool success, bytes memory data) = token.call(abi.encodeWithSelector(0x23b872dd, from, to, value));
        require(success && (data.length == 0 || abi.decode(data, (bool))), 'TransferHelper: TRANSFER_FROM_FAILED');
    }

    function safeTransferETH(address to, uint value) internal {
        (bool success,) = to.call{value : value}(new bytes(0));
        require(success, 'TransferHelper: ETH_TRANSFER_FAILED');
    }
}

interface IWETH is IERC20 {
    function deposit() external payable;

    function withdraw(uint) external;
}

interface IPawToken is IERC20 {
    function pair() external view returns (address);

    function devAddress() external view returns (address);
}

interface IPAWDAO is IERC721A {
    function mintByProtocol(address to) external;

    function getOwnerIds(uint256 idsCount, address owner) external view returns (uint256[] memory ids);
}

contract TwoPaws is Ownable, ERC721Holder {
    enum Status {
        OPEN,
        WORKING,
        CLOSED,
        CANCELED
    }
    enum Role {
        SUPPLY,
        BORROW
    }

    struct SupportedTokenSettings {
        address tokenAddress;
        bool isRewarded;
        uint256 denominator;
        uint256 rewardAmountPerDayForDenominator;
    }

    struct Order {
        uint256 id;
        address lender;
        address borrower;
        address loanToken;
        uint256 loanAmount;
        address collateralToken;
        uint256 collateralAmount;
        uint256 lenderFeeAmount;
        uint256[] timestamps;
        uint256 rewardAmount;
        Status status;
        Role role;
        uint256 openTimestamp;
    }

    IPAWDAO public protocolNFT;
    IPawToken public protocolToken;
    address public governanceContract;
    address public DAODistributor;
    IUniswapV2Router02 public immutable pancakeRouter = IUniswapV2Router02(0x10ED43C718714eb63d5aA57B78B54704E256024E);

    uint256 public constant protocolTokenDecimals = 18;
    mapping(address => uint256) public tradesCount;
    mapping(address => uint256) public necessaryBalanceForReward;
    // Orders
    uint256 public ordersCount;
    mapping(uint256 => Order) public orders;
    // DAO
    address[] public supportedTokens;
    mapping(address => SupportedTokenSettings) public supportedTokensSettings;
    uint256 public DAOFeePercent = 50; // 0,5%
    uint256 public minTradesPrice = 500;
    uint256 public ratioCoefficient = 10;
    uint256 public tradesCountMultiplier = 5;
    uint256 public maxRewardAmount = 40000 * 10 ** protocolTokenDecimals;
    uint256 public minBalanceForReward = 500000 * 10 ** protocolTokenDecimals;
    uint256 public protocolNFTBuyPrice = 1 ether;
    // AirDrop
    bool public stopAddingWallets;
    mapping(address => bool) public isDroppedAddresses;

    uint256 public constant MAX_ADDRESSES = 17470;
    uint256 public constant MAX_TOKENS = 15_000_000 * 10 ** protocolTokenDecimals;
    uint256 public constant INIT_CLAIM = 3000 * 10 ** protocolTokenDecimals;

    uint256 public claimedSupply = 0;
    uint256 public claimedCount = 0;


    event NewOrder(uint256 indexed orderId, Order order);
    event OrderStatusChange(uint256 indexed orderId, Status status);

    modifier noContracts() {
        require(msg.sender == tx.origin, "NC");
        _;
    }

    function init(
        address _protocolToken,
        address _protocolNFT,
        address _governanceContract,
        address _DAODistributor
    ) public onlyOwner {
        require(address(protocolToken) == address(0));
        protocolToken = IPawToken(_protocolToken);
        protocolNFT = IPAWDAO(_protocolNFT);
        governanceContract = _governanceContract;
        DAODistributor = _DAODistributor;
    }

    function newOrder(
        address _loanToken,
        uint256 _loanAmount,
        address _collateralToken,
        uint256 _collateralAmount,
        uint256 _lenderFeeAmount,
        uint256[] memory _timestamps,
        Role _role
    ) public noContracts returns (uint256 orderId) {
        require(msg.sender != address(0), "0A");
        require(
            _timestamps[0] > block.timestamp &&
            _timestamps[1] > block.timestamp &&
            _timestamps[1] > _timestamps[0] + 120,
            "WTS"
        );
        require(_role == Role.SUPPLY || _role == Role.BORROW, "WR");
        require(
            (_loanAmount > 0) &&
            (_collateralAmount > 0) &&
            (_loanAmount > _lenderFeeAmount), "WA");
        require(
            (_loanToken != address(0)) &&
            (_collateralToken != address(0)) &&
            (_loanToken != IPawToken(protocolToken).pair()) &&
            (_collateralToken != IPawToken(protocolToken).pair()) &&
            (_loanToken != address(protocolToken)) &&
            (_collateralToken != address(protocolToken)) &&
            (_loanToken != _collateralToken)
        , "WTP");


        uint256 amount;
        uint256 DAOFeeAmount;
        if (_role == Role.SUPPLY) {
            amount = _transferToProtocol(_loanToken, msg.sender, _loanAmount);
            DAOFeeAmount = amount * DAOFeePercent / 10000;
            require(_lenderFeeAmount + DAOFeeAmount < amount, "WA");
        } else if (_role == Role.BORROW) {
            amount = _transferToProtocol(_collateralToken, msg.sender, _collateralAmount);
            DAOFeeAmount = _loanAmount * DAOFeePercent / 10000;
            require(_lenderFeeAmount + DAOFeeAmount < _loanAmount, "WA");
        }
        orderId = ordersCount;
        Order memory order;
        order = Order(
            orderId,
            _role == Role.SUPPLY ? msg.sender : address(0),
            _role == Role.BORROW ? msg.sender : address(0),
            _loanToken,
            _role == Role.SUPPLY ? amount : _loanAmount,
            _collateralToken,
            _role == Role.BORROW ? amount : _collateralAmount,
            _lenderFeeAmount,
            _timestamps,
            0, // rewardAmount
            Status.OPEN,
            _role,
            block.timestamp
        );
        orders[orderId] = order;
        ordersCount++;

        emit NewOrder(orderId, order);
        return orderId;
    }

    function _transferToProtocol(address _token, address _sender, uint256 _amount) public returns (uint256 amount) {
        uint256 balanceBefore = IERC20(_token).balanceOf(address(this));
        TransferHelper.safeTransferFrom(_token, _sender, address(this), _amount);
        uint256 balanceAfter = IERC20(_token).balanceOf(address(this));
        require((balanceAfter - balanceBefore) > 0, "TF");
        return balanceAfter - balanceBefore;
    }

    function getOrder(uint256 _orderId) public noContracts {
        if (orders[_orderId].role == Role.SUPPLY) {
            _getSupplyOrder(_orderId);
        } else if (orders[_orderId].role == Role.BORROW) {
            _getBorrowOrder(_orderId);
        }

        if (orders[_orderId].lenderFeeAmount == 0) {
            if (
                (orders[_orderId].loanAmount >= supportedTokensSettings[orders[_orderId].loanToken].denominator) &&
                (supportedTokensSettings[orders[_orderId].loanToken].denominator != 0)
            ) {
                tradesCount[orders[_orderId].lender]++;
                tradesCount[orders[_orderId].borrower]++;
            }
        } else {
            uint256 partOfLoanAmount = orders[_orderId].loanAmount / orders[_orderId].lenderFeeAmount;
            if (
                (orders[_orderId].loanAmount >= supportedTokensSettings[orders[_orderId].loanToken].denominator) &&
                (supportedTokensSettings[orders[_orderId].loanToken].denominator != 0) &&
                (partOfLoanAmount > 10)
            ) {
                tradesCount[orders[_orderId].lender]++;
                tradesCount[orders[_orderId].borrower]++;
            }
        }

        emit OrderStatusChange(_orderId, orders[_orderId].status);
    }

    function calcRewardAmount(uint256 _orderId) public view returns (uint256 rewardAmount) {
        uint256 loanDenominator = supportedTokensSettings[orders[_orderId].loanToken].denominator;
        uint256 collateralDenominator = supportedTokensSettings[orders[_orderId].collateralToken].denominator;
        uint256 loanAmount = orders[_orderId].loanAmount;
        uint256 collateralAmount = orders[_orderId].collateralAmount;

        uint256 borrowerTokenBalance = protocolToken.balanceOf(msg.sender);
        if (
            (borrowerTokenBalance > 0) &&
            (loanDenominator != 0) &&
            (collateralDenominator != 0) &&
            (orders[_orderId].lenderFeeAmount == 0) &&
            supportedTokensSettings[orders[_orderId].loanToken].isRewarded &&
            supportedTokensSettings[orders[_orderId].collateralToken].isRewarded
        ) {
            uint256 timeDelta = block.timestamp - orders[_orderId].openTimestamp;
            uint256 denominatorCount = loanAmount * 1000 / loanDenominator;
            uint256 rewardAmountPerDay = denominatorCount * supportedTokensSettings[orders[_orderId].loanToken].rewardAmountPerDayForDenominator;
            uint256 rewardAmountPerSecond = rewardAmountPerDay / 1 days;
            rewardAmount = timeDelta * rewardAmountPerSecond;
            rewardAmount = rewardAmount <= maxRewardAmount * 1000 ? rewardAmount : maxRewardAmount * 1000;
            rewardAmount = rewardAmount / 1000;


            uint256 loanCoefficient = loanAmount / loanDenominator;
            uint256 collateralCoefficient = collateralAmount / collateralDenominator;

            if (
                (loanCoefficient + ratioCoefficient > collateralCoefficient) &&
                (borrowerTokenBalance >= rewardAmount)
            ) {
                return rewardAmount;
            } else {
                return 0;
            }

        } else {
            return 0;
        }
    }

    function _getSupplyOrder(uint256 _orderId) private {
        require(block.timestamp < orders[_orderId].timestamps[1], "WT");
        require(msg.sender != address(0), "0A");
        require(msg.sender != orders[_orderId].lender, "LCB");
        require(orders[_orderId].status == Status.OPEN, "WS");
        orders[_orderId].status = Status.WORKING;

        uint256 amount = _transferToProtocol(
            orders[_orderId].collateralToken,
            msg.sender,
            orders[_orderId].collateralAmount
        );
        orders[_orderId].collateralAmount = amount;
        uint256 DAOFeeAmount = orders[_orderId].loanAmount * DAOFeePercent / 10000;


        TransferHelper.safeTransfer(
            orders[_orderId].loanToken,
            msg.sender,
            orders[_orderId].loanAmount - (DAOFeeAmount + orders[_orderId].lenderFeeAmount)
        );
        TransferHelper.safeTransfer(
            orders[_orderId].loanToken,
            DAODistributor,
            DAOFeeAmount
        );
        if (orders[_orderId].lenderFeeAmount > 0) {
            TransferHelper.safeTransfer(
                orders[_orderId].loanToken,
                orders[_orderId].lender,
                orders[_orderId].lenderFeeAmount
            );
        }
        orders[_orderId].borrower = msg.sender;
        uint256 rewardAmount = calcRewardAmount(_orderId);
        necessaryBalanceForReward[msg.sender] += rewardAmount;
        orders[_orderId].rewardAmount = rewardAmount;
    }

    function _getBorrowOrder(uint256 _orderId) private {
        require(block.timestamp < orders[_orderId].timestamps[1], "WT");
        require(msg.sender != address(0), "0A");
        require(orders[_orderId].status == Status.OPEN, "WS");
        orders[_orderId].status = Status.WORKING;
        require(msg.sender != orders[_orderId].borrower, "BCL");
        uint256 amount = _transferToProtocol(
            orders[_orderId].loanToken,
            msg.sender,
            orders[_orderId].loanAmount
        );
        orders[_orderId].loanAmount = amount;
        uint256 DAOFeeAmount = amount * DAOFeePercent / 10000;
        require(orders[_orderId].lenderFeeAmount + DAOFeeAmount < amount, "WA");
        TransferHelper.safeTransfer(
            orders[_orderId].loanToken,
            orders[_orderId].borrower,
            amount - (orders[_orderId].lenderFeeAmount + DAOFeeAmount)
        );
        TransferHelper.safeTransfer(
            orders[_orderId].loanToken,
            DAODistributor,
            DAOFeeAmount
        );
        if (orders[_orderId].lenderFeeAmount > 0) {
            TransferHelper.safeTransfer(
                orders[_orderId].loanToken,
                msg.sender,
                orders[_orderId].lenderFeeAmount
            );
        }
        orders[_orderId].lender = msg.sender;
    }

    function cancelOrder(uint256 _orderId) public noContracts {
        require(orders[_orderId].status == Status.OPEN, "WS");
        orders[_orderId].status = Status.CANCELED;
        require(
            (orders[_orderId].role == Role.SUPPLY && orders[_orderId].lender == msg.sender) ||
            (orders[_orderId].role == Role.BORROW && orders[_orderId].borrower == msg.sender),
            "CF"
        );
        uint256 amount = orders[_orderId].role == Role.SUPPLY
        ? orders[_orderId].loanAmount
        : orders[_orderId].collateralAmount;

        orders[_orderId].role == Role.SUPPLY
        ? TransferHelper.safeTransfer(orders[_orderId].loanToken, msg.sender, amount)
        : TransferHelper.safeTransfer(orders[_orderId].collateralToken, msg.sender, amount);

        emit OrderStatusChange(_orderId, orders[_orderId].status);
    }

    function repayOrder(uint256 _orderId) public noContracts {
        require(orders[_orderId].status == Status.WORKING, "WS");
        orders[_orderId].status = Status.CLOSED;
        require(
            orders[_orderId].timestamps[0] < block.timestamp &&
            block.timestamp < orders[_orderId].timestamps[1],
            "WT"
        );
        require(orders[_orderId].borrower == msg.sender, "BR");
        uint256 amount = _transferToProtocol(
            orders[_orderId].loanToken,
            msg.sender,
            orders[_orderId].loanAmount
        );
        TransferHelper.safeTransfer(
            orders[_orderId].loanToken,
            orders[_orderId].lender,
            amount
        );
        TransferHelper.safeTransfer(
            orders[_orderId].collateralToken,
            orders[_orderId].borrower,
            orders[_orderId].collateralAmount
        );

        if (protocolToken.balanceOf(msg.sender) >= necessaryBalanceForReward[msg.sender]) {
            _reward(orders[_orderId].borrower, orders[_orderId].rewardAmount);
        }

        necessaryBalanceForReward[msg.sender] -= orders[_orderId].rewardAmount;
        emit OrderStatusChange(_orderId, orders[_orderId].status);
    }

    function liquidateOrder(uint256 _orderId) public noContracts {
        require(orders[_orderId].status == Status.WORKING, "WS");
        orders[_orderId].status = Status.CLOSED;
        require(block.timestamp > orders[_orderId].timestamps[1], "WT");
        require(orders[_orderId].lender == msg.sender, "LL");
        TransferHelper.safeTransfer(
            orders[_orderId].collateralToken,
            orders[_orderId].lender,
            orders[_orderId].collateralAmount
        );
        emit OrderStatusChange(_orderId, orders[_orderId].status);
    }

    function _reward(address _to, uint256 _amount) private {
        if (_amount > 0) {
            uint256 contractBalance = protocolToken.balanceOf(address(this));
            if (contractBalance > _amount + minBalanceForReward) {
                require(protocolToken.transfer(_to, _amount), "TF");
            }
        }
    }

    function buyNFTForETH(uint256[] memory _tokensIds) public payable {
        uint256 ETHAmount = _tokensIds.length * protocolNFTBuyPrice;
        require(msg.value == ETHAmount);
        uint256 NFTCount = _tokensIds.length;
        uint256 halfETHAmount = ETHAmount / 2;

        address[] memory path = new address[](2);
        path[0] = pancakeRouter.WETH();
        path[1] = address(protocolToken);
        pancakeRouter.swapExactETHForTokensSupportingFeeOnTransferTokens{value : ETHAmount - halfETHAmount}(
            0,
            path,
            address(this),
            (block.timestamp + 300)
        );

        (bool success,) = DAODistributor.call{value : halfETHAmount}("");


        if (success) {
            for (uint256 i = 0; i < NFTCount; i++) {
                protocolNFT.transferFrom(address(this), msg.sender, _tokensIds[i]);
            }
        }
    }

    function getNFTForTradesCount() public {
        require(tradesCount[msg.sender] >= minTradesPrice);
        protocolNFT.mintByProtocol(msg.sender);
        tradesCount[msg.sender] = tradesCount[msg.sender] - minTradesPrice;
    }

    function getAllOrders() public view returns (Order[] memory allOrders) {
        allOrders = new Order[](ordersCount);
        for (uint256 i = 0; i < ordersCount; i++) {
            Order storage order = orders[i];
            allOrders[i] = order;
        }
        return allOrders;
    }

    function getOrdersPart(uint256 _start, uint256 _end) public view returns (Order[] memory allOrders) {
        _end = _end == 0 || _end >= ordersCount ? ordersCount : _end;
        allOrders = new Order[](_end - _start);
        for (uint256 i = _start; i < _end; i++) {
            Order memory order = orders[i];
            allOrders[i - _start] = order;
        }
        return allOrders;
    }

    function getSupportedTokens() public view returns (address[] memory allTokens){
        allTokens = new address[](supportedTokens.length);
        for (uint256 i = 0; i < supportedTokens.length; i++) {
            address token = supportedTokens[i];
            allTokens[i] = token;
        }
        return allTokens;
    }

    function getSupportedTokensWithSettings() public view returns (SupportedTokenSettings[] memory supportedTokensWithSettings){
        supportedTokensWithSettings = new SupportedTokenSettings[](supportedTokens.length);
        for (uint256 i = 0; i < supportedTokens.length; i++) {
            SupportedTokenSettings storage tokenWithSettings = supportedTokensSettings[supportedTokens[i]];
            supportedTokensWithSettings[i] = tokenWithSettings;
        }
        return supportedTokensWithSettings;
    }
    //DAO
    function transferOwnershipToDAO() public {
        require(protocolNFT.balanceOf(address(this)) < 500);
        _transferOwnership(governanceContract);
    }

    function addSupportedToken(
        address _tokenAddress,
        uint256 _denominator,
        uint256 _rewardAmountPerDayForDenominator
    ) public onlyOwner {
        for (uint256 i = 0; i < supportedTokens.length; i++) {
            require(supportedTokens[i] != _tokenAddress);
        }
        supportedTokens.push(_tokenAddress);
        supportedTokensSettings[_tokenAddress].tokenAddress = _tokenAddress;
        supportedTokensSettings[_tokenAddress].isRewarded = true;
        supportedTokensSettings[_tokenAddress].denominator = _denominator;
        supportedTokensSettings[_tokenAddress].rewardAmountPerDayForDenominator = _rewardAmountPerDayForDenominator;
    }

    function setTokenSettings(
        address _tokenAddress,
        bool _flag,
        uint256 _denominator,
        uint256 _rewardAmountPerDayForDenominator
    ) public {
        require(msg.sender == DAODistributor);
        supportedTokensSettings[_tokenAddress].isRewarded = _flag;
        supportedTokensSettings[_tokenAddress].denominator = _denominator;
        supportedTokensSettings[_tokenAddress].rewardAmountPerDayForDenominator = _rewardAmountPerDayForDenominator;
    }

    function changeDAOFeePercent(uint256 _DAOFeePercent) public onlyOwner {
        require(_DAOFeePercent >= 10 && _DAOFeePercent <= 150);
        DAOFeePercent = _DAOFeePercent;
    }

    function changeMinBalanceForReward(uint256 _minBalanceForReward) public onlyOwner {
        require(
            (_minBalanceForReward >= 50000 * 10 ** protocolTokenDecimals) &&
            _minBalanceForReward <= 50000000 * 10 ** protocolTokenDecimals
        );
        minBalanceForReward = _minBalanceForReward;
    }

    function changeTradesCountMultiplier(uint256 _tradesCountMultiplier) public onlyOwner {
        require(
            (_tradesCountMultiplier >= 1) &&
            _tradesCountMultiplier <= 20
        );
        tradesCountMultiplier = _tradesCountMultiplier;
    }

    function changeMinTradesPrice(uint256 _minTradesPrice) public onlyOwner {
        require(
            (_minTradesPrice >= 1) &&
            (_minTradesPrice <= 10000)
        );
        minTradesPrice = _minTradesPrice;
    }

    function changeRatioCoefficient(uint256 _ratioCoefficient) public onlyOwner {
        require(
            (_ratioCoefficient >= 1) &&
            (_ratioCoefficient <= 100)
        );
        ratioCoefficient = _ratioCoefficient;
    }

    function changeMaxRewardAmount(uint256 _maxRewardAmount) public onlyOwner {
        require(
            (_maxRewardAmount >= 1 * 10 ** protocolTokenDecimals) &&
            _maxRewardAmount <= 100000 * 10 ** protocolTokenDecimals
        );
        maxRewardAmount = _maxRewardAmount;
    }

    function changeDAODistributor(address _DAODistributor) public onlyOwner {
        require(_DAODistributor != address(0));
        DAODistributor = _DAODistributor;
    }

    // AirDrop
    function addWallets(address[] memory _wallets, bool _stop) public onlyOwner {
        require(!stopAddingWallets);
        for (uint256 i = 0; i < _wallets.length; i++) {
            isDroppedAddresses[_wallets[i]] = true;
        }

        if (_stop) {
            stopAddingWallets = true;
        }
    }

    function canClaimAmount() public view returns (uint256) {

        if (
            claimedCount >= MAX_ADDRESSES &&
            !isDroppedAddresses[msg.sender]
        ) {
            return 0;
        }

        uint256 supplyPerAddress = INIT_CLAIM;
        uint256 curClaimedCount = claimedCount + 1;
        uint256 claimedPercent = (curClaimedCount * 100 * 10 ** protocolTokenDecimals) / MAX_ADDRESSES;
        uint256 curPercent = 5 * 10 ** protocolTokenDecimals;

        while (curPercent <= claimedPercent) {
            supplyPerAddress = (supplyPerAddress * 80) / 100;
            curPercent += 5 * 10 ** protocolTokenDecimals;
        }

        return supplyPerAddress;
    }

    function claim() public {
        require(isDroppedAddresses[msg.sender], "Wrong sender.");
        uint256 claimAmount = canClaimAmount();
        require(claimedSupply + claimAmount < MAX_TOKENS, "Airdrop has ended.");
        require(claimAmount >= 1 * 10 ** protocolTokenDecimals, "Airdrop has ended.");

        isDroppedAddresses[msg.sender] = false;
        claimedCount++;
        claimedSupply += claimAmount;
        require(protocolToken.transfer(msg.sender, claimAmount), "TF");

    }


    function withdraw() public onlyOwner returns (bytes memory){
        (, bytes memory resp) = owner().call{value : address(this).balance}("");
        return resp;
    }

    receive() external payable {
    }
}


