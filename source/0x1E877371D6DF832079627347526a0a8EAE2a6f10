
// File: @openzeppelin/contracts/security/ReentrancyGuard.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.8.0) (security/ReentrancyGuard.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        _nonReentrantBefore();
        _;
        _nonReentrantAfter();
    }

    function _nonReentrantBefore() private {
        // On the first call to nonReentrant, _status will be _NOT_ENTERED
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;
    }

    function _nonReentrantAfter() private {
        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}


// File: @openzeppelin/contracts/token/ERC20/IERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);
}


// File: contracts/Factory.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

import "./Staking.sol";
import "./interfaces/IStaking.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";

contract Factory {
    struct Pool {
        address poolAddress;
        address token;
        address owner;
        address factoryContract;
        uint256 rewards;
        uint256 decimals;
        uint256 rewardsDuration;
        uint256 totalTokenAmount;
    }

    uint256 public poolId;

    address owner;
    address token;
    address developer;

    uint256 platformFees;
    uint256 developerFees;

    mapping(uint256 => Pool) poolAddress;
    mapping(address => bool) poolStatus;
    mapping(address => uint256) poolID;
    mapping(address => uint256[]) userPoolAddress;
    mapping(address => mapping(uint256 => bool)) userStatus;

    constructor(
        address _owner,
        address _developer,
        address _token,
        uint256 _platformFees,
        uint256 _developerFees
    ) {
        owner = _owner;
        token = _token;
        developer = _developer;
        developerFees = _developerFees;
        platformFees = _platformFees;
    }

    function setToken(address _token) external {
        require(owner == msg.sender, "not owner");
        token = _token;
    }

    function setDeveloperFee(uint256 _developerFees) external {
        require(owner == msg.sender, "not owner");
        developerFees = _developerFees;
    }

    function setPlatformFee(uint256 _platformFees) external {
        require(owner == msg.sender, "not owner");
        platformFees = _platformFees;
    }

    function setOwner(address _owner) external {
        require(owner == msg.sender, "not owner");
        owner = _owner;
    }

    function setDeveloper(address _developer) external {
        require(owner == msg.sender, "not owner");
        developer = _developer;
    }

    function createPool(
        address _token,
        address _owner,
        address _factoryContract,
        uint256 _rewards,
        uint256 _decimals,
        uint256 _rewardsDuration,
        uint256 _totalTokenAmount
    ) external returns (Staking pool) {
        require(owner == msg.sender, "not owner");
        pool = new Staking(
            _token,
            _owner,
            _factoryContract,
            _rewards,
            _decimals,
            _rewardsDuration,
            _totalTokenAmount
        );
        poolId += 1;
        poolAddress[poolId] = Pool(
            address(pool),
            _token,
            _owner,
            _factoryContract,
            _rewards,
            _decimals,
            _rewardsDuration,
            _totalTokenAmount
        );

        poolStatus[address(pool)] = true;
        poolID[address(pool)] = poolId;
    }

    function withdrawRewardToken(uint256 _poolId) external {
        require(
            IStaking(poolAddress[_poolId].poolAddress).stakeBalanceOfUser(
                msg.sender
            ) != 0,
            "token not stake"
        );

        uint256 reward = IStaking(poolAddress[_poolId].poolAddress).getReward(
            msg.sender
        );

        uint256 fees = (reward * (developerFees + platformFees)) / 100;
        reward = reward - fees;

        IERC20(token).transfer(msg.sender, reward);
        IERC20(token).transfer(owner, (fees / 2));
        IERC20(token).transfer(developer, (fees / 2));
    }

    function userStake(address user) external {
        require(poolStatus[msg.sender], "not pool address");

        uint256 _poolId = poolID[msg.sender];
        if (!userStatus[user][_poolId]) {
            userStatus[user][_poolId] = true;
            userPoolAddress[user].push((_poolId));
        }
    }

    function poolDetails(uint256 _poolId) external view returns (Pool memory) {
        return poolAddress[_poolId];
    }

    function userPool() external view returns (uint256[] memory) {
        return userPoolAddress[msg.sender];
    }

    function emergencyWithdraw(uint256 _amount) external {
        require(owner == msg.sender, "not owner");
        IERC20(token).transfer(owner, _amount);
    }
}


// File: contracts/interfaces/IFactory.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

interface IFactory {
    function userStake(address user) external;
}


// File: contracts/interfaces/IStaking.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

interface IStaking {
    function getReward(address user) external returns (uint256);

    function stakeBalanceOfUser(address _user) external view returns (uint256);
}


// File: contracts/Staking.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "./interfaces/IFactory.sol";

contract Staking is ReentrancyGuard {
    IERC20 public token;

    struct UserInfo {
        uint256 amount;
        uint256 since;
        uint256 rewardDebt;
        uint256 poolEndTime;
        bool status;
    }

    address owner;
    address factoryContract;

    uint256 public rewards;
    uint256 public decimals;
    uint256 public _totalSupply;
    uint256 public rewardsDuration;
    uint256 public totalTokenAmount;
    uint256 public totalTokenStacked;

    bool pause;

    mapping(address => UserInfo) userInfo;
    mapping(address => uint256[]) userPoolAddress;

    event Staked(address user, uint256 amount);
    event Withdrawn(address user, uint256 amount);
    event RewardPaid(address user, uint256 reward);

    modifier updateReward(address account) {
        userInfo[msg.sender].rewardDebt = _earned(msg.sender);
        _;
    }

    modifier checkStackedTOken(uint256 _amount) {
        require(totalTokenStacked >= (_amount + _totalSupply), "Pool End");
        _;
    }

    modifier onlyOwner() {
        require(owner == msg.sender, "Caller is not a owner");
        _;
    }

    modifier paused() {
        require(!pause, "contract is paused");
        _;
    }

    constructor(
        address _token,
        address _owner,
        address _factoryContract,
        uint256 _rewards,
        uint256 _decimals,
        uint256 _rewardsDuration,
        uint256 _totalTokenAmount
    ) {
        owner = _owner;
        token = IERC20(_token);
        factoryContract = _factoryContract;
        rewards = _rewards;
        decimals = _decimals;
        rewardsDuration = _rewardsDuration;
        totalTokenAmount = _totalTokenAmount;
        totalTokenStacked = _totalTOkenStacked(
            _rewards,
            _decimals,
            _totalTokenAmount
        );
    }

    function stake(
        uint256 _amount
    ) external nonReentrant checkStackedTOken(_amount) paused {
        require(!userInfo[msg.sender].status, "already stacked");
        _totalSupply += _amount;

        userInfo[msg.sender].amount += _amount;
        userInfo[msg.sender].since = block.timestamp;
        userInfo[msg.sender].poolEndTime = block.timestamp + rewardsDuration;
        userInfo[msg.sender].status = true;

        token.transferFrom(msg.sender, address(this), _amount);
        IFactory(factoryContract).userStake(msg.sender);

        emit Staked(msg.sender, _amount);
    }

    function withdraw() external nonReentrant paused {
        require(
            block.timestamp >= userInfo[msg.sender].poolEndTime,
            "pool not end"
        );

        require(userInfo[msg.sender].amount > 0, "token not stacked");

        uint256 _amount = userInfo[msg.sender].amount;

        _totalSupply = _totalSupply - _amount;
        userInfo[msg.sender].amount = userInfo[msg.sender].amount - _amount;
        userInfo[msg.sender].since = block.timestamp;

        token.transfer(msg.sender, _amount);
        emit Withdrawn(msg.sender, _amount);
    }

    function getReward(
        address _user
    ) external nonReentrant paused returns (uint256) {
        require(
            factoryContract == msg.sender || msg.sender == address(this),
            "not factory contract"
        );

        uint256 reward = _earned(_user);
        userInfo[_user].rewardDebt = 0;
        userInfo[_user].since = block.timestamp;

        emit RewardPaid(_user, reward);
        return reward;
    }

    function withdrawRewardToken(
        uint256 _amount
    ) external nonReentrant onlyOwner {
        token.transfer(msg.sender, _amount);
    }

    function pausedContract(bool _status) external nonReentrant onlyOwner {
        pause = _status;
    }

    function earned(address _account) external view returns (uint256 reward) {
        return _earned(_account);
    }

    function _earned(address _account) internal view returns (uint256 reward) {
        if (block.timestamp > userInfo[_account].poolEndTime) {
            if (userInfo[_account].since > userInfo[_account].poolEndTime) {
                reward = 0;
            } else {
                reward =
                    userInfo[_account].rewardDebt +
                    (((userInfo[_account].poolEndTime -
                        userInfo[_account].since) *
                        userInfo[_account].amount *
                        rewards) / (rewardsDuration * decimals));
            }
        } else {
            reward =
                userInfo[_account].rewardDebt +
                (((block.timestamp - userInfo[_account].since) *
                    userInfo[_account].amount *
                    rewards) / (rewardsDuration * decimals));
        }
    }

    function _totalTOkenStacked(
        uint256 _rewards,
        uint256 _decimal,
        uint256 _totalTokenAmount
    ) internal pure returns (uint256 totalTOkenStacked) {
        totalTOkenStacked = (_decimal * _totalTokenAmount) / _rewards;
    }

    function stakeBalanceOfUser(address _user) external view returns (uint256) {
        return userInfo[_user].amount;
    }

    function currentTime() external view returns (uint256) {
        return block.timestamp;
    }

    function getPoolEndTime(
        address _user
    ) external view returns (UserInfo memory) {
        return userInfo[_user];
    }
}

