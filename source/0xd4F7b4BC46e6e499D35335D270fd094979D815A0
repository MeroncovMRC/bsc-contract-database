
// File: BondedFeeDistributor.sol
pragma solidity 0.8.12;

import "SafeERC20.sol";
import "Ownable.sol";
import "IERC20.sol";
import "IEpsProxy.sol";
import "IFeeDistributor.sol";


contract BondedFeeDistributor is Ownable {
    using SafeERC20 for IERC20;

    struct StreamData {
        uint256 start;
        uint256 amount;
        uint256 claimed;
    }
    struct Deposit {
        uint256 timestamp;
        uint256 amount;
    }
    struct DepositData {
        uint256 index;
        Deposit[] deposits;
    }

    // Fees are transferred into this contract as they are collected, and in the same tokens
    // that they are collected in. The total amount collected each week is recorded in
    // `weeklyFeeAmounts`. At the end of a week, the fee amounts are streamed out over
    // the following week based on each user's lock weight at the end of that week. Data
    // about the active stream for each token is tracked in `activeUserStream`

    // fee token -> week -> total amount received that week
    mapping(address => uint256[65535]) public weeklyFeeAmounts;
    // user -> fee token -> data about the active stream
    mapping(address => mapping(address => StreamData)) activeUserStream;


    // Weekly bonded balances are stored within a dynamic array, where the array index
    // represents the week. The final value is always the current balance. The length
    // will be less than that of `getWeek` if a user has not interacted in over a week.

    // user -> weekly bonded balance
    mapping(address => uint256[]) weeklyUserBalance;
    // weekly total bonded balance
    uint256[] totalBalance;

    // Deposited balances are "bonded" in order to receive fees. Each deposit cannot be
    // "unbonded" (withdrawn via a linear stream) for `MIN_BOND_DURATION` after bonding.

    // user -> deposit data
    mapping (address => DepositData) userDeposits;
    // users -> data on the active unbonding stream
    mapping (address => StreamData) exitStream;


    // array of all fee tokens that have been added
    address[] public feeTokens;
    // timestamp when a fee token was last claimed
    mapping(address => uint256) public lastClaim;
    // known balance of each token, used to calculate amounts when receiving new fees
    mapping(address => uint256) tokenBalance;

    // account earning rewards => receiver of rewards for this account
    // if receiver is set to address(0), rewards are paid to the earner
    // this is used to aid 3rd party contract integrations
    mapping (address => address) public claimReceiver;
    // when set to true, other accounts cannot call `claim` on behalf of an account
    mapping(address => bool) public blockThirdPartyActions;

    // Ellipsis contracts
    address public immutable EPX;
    IFeeDistributor public immutable epsFeeDistributor;

    // DotDot contracts
    IERC20 public dEPX;
    address public DDD;
    address public lpDepositor;
    IEllipsisProxy public proxy;

    // new weeks within this contract begin on Thursday 00:00:00 UTC
    uint256 public immutable startTime;

    uint256 constant WEEK = 86400 * 7;
    uint256 constant public MIN_BOND_DURATION = 86400 * 8;
    uint256 constant public UNBOND_STREAM_DURATION = 86400 * 15;

    event FeesClaimed(
        address caller,
        address indexed account,
        address indexed receiver,
        address indexed token,
        uint256 amount
    );

    constructor(address _EPX, IFeeDistributor _feeDistributor) {
        EPX = _EPX;
        epsFeeDistributor = _feeDistributor;
        startTime = _feeDistributor.startTime();
    }

    function setAddresses(
        IERC20 _dEPX,
        address _DDD,
        address _lpDepositor,
        IEllipsisProxy _proxy
    ) external onlyOwner {
        dEPX = _dEPX;
        DDD = _DDD;
        lpDepositor = _lpDepositor;
        proxy = _proxy;

        renounceOwnership();
    }

    function setClaimReceiver(address _receiver) external {
        claimReceiver[msg.sender] = _receiver;
    }

    function setBlockThirdPartyActions(bool _block) external {
        blockThirdPartyActions[msg.sender] = _block;
    }

    function getWeek() public view returns (uint256) {
        return (block.timestamp - startTime) / 604800;
    }

    function feeTokensLength() external view returns (uint) {
        return feeTokens.length;
    }

    /**
        @notice Get an array of claimable amounts of different tokens accrued from protocol fees
        @param _user Address to query claimable amounts for
        @param _tokens List of tokens to query claimable amounts of
     */
    function claimable(address _user, address[] calldata _tokens)
        external
        view
        returns (uint256[] memory amounts)
    {
        amounts = new uint256[](_tokens.length);
        for (uint256 i = 0; i < _tokens.length; i++) {
            (amounts[i], ) = _getClaimable(_user, _tokens[i]);
        }
        return amounts;
    }

    /**
        @notice The total amount of bonded tokens held in the contract
     */
    function bondedSupply() external view returns (uint256) {
        uint256 length = totalBalance.length;
        if (length == 0) return 0;
        return totalBalance[length-1];
    }

    /**
        @notice The total amount of bonded tokens held in the contract for `_user`
        @dev Does not include any balance in an active unbonding stream
     */
    function bondedBalance(address _user) external view returns (uint256) {
        uint256 length = weeklyUserBalance[_user].length;
        if (length == 0) return 0;
        return weeklyUserBalance[_user][length-1];
    }

    /**
        @notice The amount of bonded tokens for `_user` which have passed the
                minimum bond duration and so could begin unbonding immediately.
     */
    function unbondableBalance(address _user) public view returns (uint256) {
        uint balance;
        DepositData storage data = userDeposits[_user];
        for (uint256 i = data.index; i < data.deposits.length; i++) {
            Deposit storage dep = data.deposits[i];
            if (dep.timestamp + MIN_BOND_DURATION > block.timestamp) break;
            balance += dep.amount;
        }
        return balance;
    }

    /**
        @notice Get info on the current unbonding stream for a user
        @return claimable Streamable balance that can currently be withdrawn
        @return total Total balance left in the stream
     */
    function streamingBalances(address _user) public view returns (uint256 claimable, uint256 total)
    {
        StreamData storage stream = exitStream[_user];
        if (stream.start == 0) return (0, 0);
        uint256 remaining = stream.amount - stream.claimed;
        if (stream.start + UNBOND_STREAM_DURATION < block.timestamp) {
            return (remaining, remaining);
        } else {
            uint256 claimable = stream.amount * (block.timestamp - stream.start) / UNBOND_STREAM_DURATION;
            return (claimable - stream.claimed, remaining);
        }
    }

    /**
        @notice Claim accrued protocol fees according to a locked balance in `TokenLocker`.
        @dev Fees are claimable up to the end of the previous week. Claimable fees from more
             than one week ago are released immediately, fees from the previous week are streamed.
        @param _user Address to claim for. Any account can trigger a claim for any other account.
        @param _tokens Array of tokens to claim for.
        @return claimedAmounts Array of amounts claimed.
     */
    function claim(address _user, address[] calldata _tokens)
        external
        returns (uint256[] memory claimedAmounts)
    {
        if (msg.sender != _user) {
            require(!blockThirdPartyActions[_user], "Cannot claim on behalf of this account");
        }

        _extendBalanceArray(weeklyUserBalance[_user]);
        _extendBalanceArray(totalBalance);

        address receiver = claimReceiver[_user];
        if (receiver == address(0)) receiver = _user;

        claimedAmounts = new uint256[](_tokens.length);
        address[] memory tokensToFetch = new address[](_tokens.length);
        uint256 toFetchLength;

        for (uint256 i = 0; i < _tokens.length; i++) {
            address token = _tokens[i];

            uint256 lastClaimed = lastClaim[token];
            if (lastClaimed + 86400 <= block.timestamp) {
                // once a day, claim new fees from Ellipsis
                tokensToFetch[toFetchLength] = token;
                toFetchLength++;
            }

            StreamData memory stream;
            (claimedAmounts[i], stream) = _getClaimable(_user, token);
            activeUserStream[_user][token] = stream;
            if (claimedAmounts[i] > 0) {
                tokenBalance[token] -= claimedAmounts[i];
                IERC20(token).safeTransfer(receiver, claimedAmounts[i]);
            }
            emit FeesClaimed(msg.sender, _user, receiver, token, claimedAmounts[i]);
        }

        if (toFetchLength > 0) {
            assembly { mstore(tokensToFetch, toFetchLength) }
            fetchEllipsisFees(tokensToFetch);
        }

        return claimedAmounts;
    }

    /**
        @notice Bond dEPX within the contract in order to receive fees
        @dev Bonded tokens cannot be withdrawn for `MIN_BOND_DURATION`
        @param _user Address to deposit tokens for
        @param _amount Amount of dEPX to deposit
     */
    function deposit(address _user, uint256 _amount) external returns (bool) {
        if (msg.sender != address(dEPX)) {
            // if the caller is dEPX we trust the balance was updated
            // and skip the transfer
            if (msg.sender != _user) {
                require(!blockThirdPartyActions[_user], "Cannot deposit on behalf of this account");
            }
            dEPX.safeTransferFrom(msg.sender, address(this), _amount);
        }

        tokenBalance[address(dEPX)] += _amount;
        uint256 balance = _extendBalanceArray(weeklyUserBalance[_user]);
        uint256 total = _extendBalanceArray(totalBalance);

        uint256 week = getWeek();
        weeklyUserBalance[_user][week] = balance + _amount;
        totalBalance[week] = total + _amount;

        DepositData storage data = userDeposits[_user];
        uint256 timestamp = block.timestamp / 86400 * 86400;
        uint256 length = data.deposits.length;
        if (length == 0 || data.deposits[length-1].timestamp < timestamp) {
            data.deposits.push(Deposit({timestamp: timestamp, amount: _amount}));
        } else {
            data.deposits[length-1].amount += _amount;
        }
        return true;
    }

    /**
        @notice Initiate an unbonding stream, allowing withdrawal of bonded tokens over the
                unbonding duration.
        @dev If there is already an active unbonding stream, any unclaimed balance is added
             to the new stream.
     */
    function initiateUnbondingStream(uint256 _amount) external returns (bool) {
        uint256 balance = _extendBalanceArray(weeklyUserBalance[msg.sender]);
        require(balance >= _amount, "Insufficient balance");
        uint256 total = _extendBalanceArray(totalBalance);

        uint256 week = getWeek();
        weeklyUserBalance[msg.sender][week] = balance - _amount;
        totalBalance[week] = total - _amount;

        uint256 remaining = _amount;
        DepositData storage data = userDeposits[msg.sender];
        for (uint256 i = data.index; ; i++) {
            Deposit memory dep = data.deposits[i];
            require(dep.timestamp + MIN_BOND_DURATION <= block.timestamp, "Insufficient unbondable balance");
            if (remaining >= dep.amount) {
                remaining -= dep.amount;
                delete data.deposits[i];
            } else {
                dep.amount -= remaining;
                remaining = 0;
            }
            if (remaining == 0) {
                data.index = i;
                break;
            }
        }

        StreamData storage stream = exitStream[msg.sender];
        exitStream[msg.sender] = StreamData({
            start: block.timestamp,
            amount: stream.amount - stream.claimed + _amount,
            claimed: 0
        });

        return true;
    }

    /**
        @notice Withdraw tokens that have finished unbonding.
     */
    function withdrawUnbondedTokens(address _receiver) external returns (bool) {
        StreamData storage stream = exitStream[msg.sender];
        uint256 amount;
        if (stream.start > 0) {
            (amount,) = streamingBalances(msg.sender);
            if (stream.start + UNBOND_STREAM_DURATION < block.timestamp) {
                delete exitStream[msg.sender];
            } else {
                stream.claimed = stream.claimed + amount;
            }
            tokenBalance[address(dEPX)] -= amount;
            dEPX.safeTransfer(_receiver, amount);
        }
        return true;
    }

    /**
        @notice Fetch fees from the Ellipsis fee distributor
        @dev Fees received within a week are distributed in the following week.
             When a user claims a fee token from this contract, an onward claim
             to Ellipsis is made if the last claim was more than one day ago.
             In theory, this should mean each fee token must be claimed using
             `fetchEllipsisFees` once to create an initial claimable amount
             for users. Subsequent calls will happen via regular user interactions.
     */
    function fetchEllipsisFees(address[] memory _tokens) public {
        proxy.claimFees(_tokens);
        uint256 week = getWeek();
        for (uint i = 0; i < _tokens.length; i++) {
            address token = _tokens[i];
            uint256 balance = tokenBalance[token];
            uint256 received = IERC20(token).balanceOf(address(this)) - balance;
            if (received > 0) {
                if (balance == 0 && lastClaim[token] == 0) feeTokens.push(token);
                weeklyFeeAmounts[token][week] += received;
                tokenBalance[token] = balance + received;
                lastClaim[token] = block.timestamp;
            }
        }
    }

    /**
        @notice Notify contract of newly received DDD and EPX fees
        @dev Only callable by `LpDepositor`. We trust the caller to supply the correct amounts.
     */
    function notifyFeeAmounts(uint256 _epxAmount, uint256 _dddAmount) external returns (bool) {
        require(msg.sender == lpDepositor);
        uint256 week = getWeek();

        weeklyFeeAmounts[EPX][week] += _epxAmount;
        tokenBalance[EPX] += _epxAmount;

        weeklyFeeAmounts[DDD][week] += _dddAmount;
        tokenBalance[DDD] += _dddAmount;

        return true;
    }

    function _getClaimable(address _user, address _token)
        internal
        view
        returns (uint256, StreamData memory)
    {
        uint256 claimableWeek = getWeek();
        uint256 balanceLength = weeklyUserBalance[_user].length;

        if (claimableWeek == 0 || balanceLength == 0) {
            // the first full week hasn't completed yet or the user has never made a deposit
            return (0, StreamData({start: startTime, amount: 0, claimed: 0}));
        }

        // the previous week is the claimable one
        claimableWeek -= 1;
        StreamData memory stream = activeUserStream[_user][_token];
        uint256 lastClaimWeek;
        if (stream.start == 0) {
            lastClaimWeek = 0;
        } else {
            lastClaimWeek = (stream.start - startTime) / WEEK;
        }

        uint256 amount;
        if (claimableWeek == lastClaimWeek) {
            // special case: claim is happening in the same week as a previous claim
            uint256 previouslyClaimed = stream.claimed;
            stream = _buildStreamData(_user, _token, claimableWeek);
            amount = stream.claimed - previouslyClaimed;
            return (amount, stream);
        }

        if (stream.start > 0) {
            // if there is a partially claimed week, get the unclaimed amount and increment
            // `lastClaimWeeek` so we begin iteration on the following week
            amount = stream.amount - stream.claimed;
            lastClaimWeek += 1;
        }

        // iterate over weeks that have passed fully without any claims
        uint256 balance;
        uint256 total;
        uint256 totalLength = totalBalance.length;
        for (uint256 i = lastClaimWeek; i < claimableWeek; i++) {
            if (balanceLength > i) balance = weeklyUserBalance[_user][i];
            if (balance == 0) continue;
            if (totalLength > i) total = totalBalance[i];
            amount += weeklyFeeAmounts[_token][i] * balance / total;
        }

        // add a partial amount for the active week
        stream = _buildStreamData(_user, _token, claimableWeek);

        return (amount + stream.claimed, stream);
    }

    function _buildStreamData(
        address _user,
        address _token,
        uint256 _week
    ) internal view returns (StreamData memory) {
        uint256 start = startTime + _week * WEEK;
        uint256 length = weeklyUserBalance[_user].length;
        uint256 balance = length > _week ? weeklyUserBalance[_user][_week] : weeklyUserBalance[_user][length - 1];

        uint256 amount;
        uint256 claimed;
        if (balance > 0) {
            length = totalBalance.length;
            uint256 total = length > _week ? totalBalance[_week] : totalBalance[length - 1];
            amount = weeklyFeeAmounts[_token][_week] * balance / total;
            claimed = amount * (block.timestamp - 604800 - start) / WEEK;
        }
        return StreamData({start: start, amount: amount, claimed: claimed});
    }

    function _extendBalanceArray(uint256[] storage balances) internal returns (uint256) {
        uint256 week = getWeek();
        uint256 length = balances.length;
        uint256 value = 0;
        if (length > 0) {
            value = balances[length - 1];
        }
        while (length <= week) {
            balances.push(value);
            length++;
        }
        return value;
    }

}


// File: SafeERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/utils/SafeERC20.sol)

pragma solidity ^0.8.0;

import "IERC20.sol";
import "Address.sol";

/**
 * @title SafeERC20
 * @dev Wrappers around ERC20 operations that throw on failure (when the token
 * contract returns false). Tokens that return no value (and instead revert or
 * throw on failure) are also supported, non-reverting calls are assumed to be
 * successful.
 * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,
 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.
 */
library SafeERC20 {
    using Address for address;

    function safeTransfer(
        IERC20 token,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));
    }

    function safeTransferFrom(
        IERC20 token,
        address from,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));
    }

    /**
     * @dev Deprecated. This function has issues similar to the ones found in
     * {IERC20-approve}, and its usage is discouraged.
     *
     * Whenever possible, use {safeIncreaseAllowance} and
     * {safeDecreaseAllowance} instead.
     */
    function safeApprove(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        // safeApprove should only be called when setting an initial allowance,
        // or when resetting it to zero. To increase and decrease it, use
        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
        require(
            (value == 0) || (token.allowance(address(this), spender) == 0),
            "SafeERC20: approve from non-zero to non-zero allowance"
        );
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
    }

    function safeIncreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        uint256 newAllowance = token.allowance(address(this), spender) + value;
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
    }

    function safeDecreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            uint256 newAllowance = oldAllowance - value;
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
        }
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     */
    function _callOptionalReturn(IERC20 token, bytes memory data) private {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We use {Address.functionCall} to perform this call, which verifies that
        // the target address contains contract code and also asserts for success in the low-level call.

        bytes memory returndata = address(token).functionCall(data, "SafeERC20: low-level call failed");
        if (returndata.length > 0) {
            // Return data is optional
            require(abi.decode(returndata, (bool)), "SafeERC20: ERC20 operation did not succeed");
        }
    }
}


// File: IERC20.sol
// SPDX-License-Identifier: MIT

pragma solidity 0.8.12;

/**
 * Based on the OpenZeppelin IER20 interface:
 * https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC20/IERC20.sol
 *
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);

    function name() external view returns (string memory);
    function symbol() external view returns (string memory);
    function decimals() external view returns (uint8);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}


// File: Address.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}


// File: Ownable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (access/Ownable.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable {
    address public owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(msg.sender);
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner == msg.sender, "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = owner;
        owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}


// File: IEpsProxy.sol
pragma solidity 0.8.12;

interface IEllipsisProxy {
    function lock(uint256 _amount) external returns (bool);
    function extendLock(uint256 _amount, uint256 _weeks) external returns (bool);
    function deposit(address _token, uint256 _amount) external returns (uint256);
    function withdraw(address _receiver, address _token, uint256 _amount) external returns (uint256);
    function claimEmissions(address _token) external returns (uint256);
    function claimFees(address[] calldata _tokens) external returns (bool);
    function vote(address[] calldata _tokens, uint256[] calldata _votes) external returns (bool);
    function createTokenApprovalVote(address _token) external returns (uint256 _voteIndex);
    function voteForTokenApproval(uint256 _voteIndex, uint256 _yesVotes) external returns (bool);
    function getReward(address _lpToken, address[] calldata _rewards) external returns (bool);
}

// File: IFeeDistributor.sol
pragma solidity 0.8.12;


interface IFeeDistributor {

    function startTime() external view returns (uint256);

    /**
        @notice Set the claim receiver address for the caller
        @param _receiver Claim receiver address
     */
    function setClaimReceiver(address _receiver) external;

    /**
        @notice Allow or block third-party calls to deposit, withdraw
                or claim rewards on behalf of the caller
     */
    function setBlockThirdPartyActions(bool _block) external;

    /**
        @notice Claim accrued protocol fees
        @param _user Address to claim for
        @param _tokens Array of tokens to claim for
        @return claimedAmounts Array of amounts claimed
     */
    function claim(address _user, address[] calldata _tokens) external returns (uint256[] memory claimedAmounts);
}
