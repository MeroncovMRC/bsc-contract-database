
// File: FreezingStaking.sol
// SPDX-License-Identifier: MIT
// GoldQuality

pragma solidity ^0.8.19;

import "IERC20.sol";
import "IFreezingStaking.sol";
import "Ownable.sol";

contract FreezingStaking is IFreezingStaking, Ownable {
    IERC20 public token;
    bool public isRun;

    struct Stake {
        uint256 amount;
        uint256 timestamp;
        uint256 period;
    }

    mapping(address => Stake[]) public stakes;
    mapping(uint256 => uint256) public period2percent;

    uint256 constant private SECONDS_IN_DAY = 86400;

    event Staked(address indexed user, uint256 period, uint256 amount);
    event UnStaked(address indexed user, uint256 amount, uint256 rewardAmount);
    event Stop(address user);

    constructor(address _token)
    {
        token = IERC20(_token);
        isRun = true;

        period2percent[180] = 40;
        period2percent[270] = 43;
        period2percent[360] = 45;
    }

    function getStakeCount(address _user)
    external view
    returns (uint256)
    {
        return stakes[_user].length;
    }

    function getStakeInfo(address _user)
    public view
    returns (
        uint256 totalAmount,
        uint256 availableAmount,
        uint256 rewardAmount,
        bool[] memory available
    )
    {
        (totalAmount, availableAmount, rewardAmount, available) = (0, 0, 0, new bool[](stakes[_user].length));

        for (uint256 i = 0; i < stakes[_user].length; i++) {
            totalAmount += stakes[_user][i].amount;

            uint256 secondsPassed = block.timestamp - stakes[_user][i].timestamp;

            if (secondsPassed >= stakes[_user][i].period * SECONDS_IN_DAY) {
                available[i] = true;
                availableAmount += stakes[_user][i].amount;

                rewardAmount += stakes[_user][i].amount * period2percent[stakes[_user][i].period] * (secondsPassed / SECONDS_IN_DAY) / (100 * 365);
            }
        }
    }

    function stop()
    external onlyOwner
    {
        require(isRun, "Already stopped");
        isRun = false;

        emit Stop(_msgSender());
    }

    function stake(uint256 _amount, uint _period)
    external
    {
        require(isRun, "Staking is closed");
        require(_amount != 0, "Amount must not be zero");
        require(period2percent[_period] != 0, "Wrong period");
        require(token.balanceOf(_msgSender()) >= _amount, "Not enough tokens");
        require(token.allowance(_msgSender(), address(this)) >= _amount, "Token allowance is low");

        require(token.transferFrom(_msgSender(), address(this), _amount), "Token transfer failed");

        stakes[_msgSender()].push(Stake(_amount, block.timestamp, _period));

        emit Staked(_msgSender(), _period, _amount);
    }

    function unStake()
    external
    {
        require(stakes[_msgSender()].length != 0, "No stakes");

        (uint256 totalAmount, uint256 availableAmount, uint256 rewardAmount, bool[] memory available) = getStakeInfo(_msgSender());

        require(availableAmount != 0, "No stakes to withdraw");
        require(availableAmount + rewardAmount <= token.balanceOf(address(this)) , "Insufficient staking balance");

        for (uint256 i = 0; i < available.length; i++) {
            if (available[available.length - 1 - i]) {
                delete stakes[_msgSender()][available.length - 1 - i];
            }
        }

        token.transfer(_msgSender(), availableAmount + rewardAmount);

        emit UnStaked(_msgSender(), availableAmount, rewardAmount);
    }
}

// File: IERC20.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.19;

interface IERC20 {
    function balanceOf(address account) external view returns (uint256);
    function allowance(address owner, address spender) external view returns (uint256);
    function transfer(address to, uint256 amount) external returns (bool);
    function transferFrom(address from, address to, uint256 amount) external returns (bool);
    function burn(uint256 amount) external;
}

// File: IFreezingStaking.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.19;

interface IFreezingStaking {
    function stake(uint256 _amount, uint _period) external;
    function unStake() external;
}

// File: Ownable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (access/Ownable.sol)

pragma solidity ^0.8.0;

import "Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}


// File: Context.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

