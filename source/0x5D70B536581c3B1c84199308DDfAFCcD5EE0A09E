
// File: @openzeppelin/contracts/access/Ownable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby disabling any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}


// File: @openzeppelin/contracts/token/ERC20/IERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address from, address to, uint256 amount) external returns (bool);
}


// File: @openzeppelin/contracts/utils/Context.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}


// File: contracts/PropTokenPresale.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.19;

import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";

interface IPropNFT {
    function totalSupply() external view returns (uint256);

    function mint(address, uint256) external;

    function ownerOf(uint256) external view returns (address);
}

contract PropTokenPresale is Ownable {
    enum PROUND {
        INACTIVE,
        ROUND1,
        ROUND2,
        ROUND3,
        FINISHED
    }

    struct NFTInfo {
        uint256 tokenID;
        address community;
        uint256 amount;
        bool isClaimed;
    }

    address public propToken;
    address public propNFT;

    address public busd;

    uint256 public constant FIRST_PRICE = 0.0015 ether; // price of prop token of round1 in usdt
    uint256 public constant SECOND_PRICE = 0.0018 ether; // price of prop token of round1 in usdt
    uint256 public constant THIRD_PRICE = 0.002 ether; // price of prop token of round1 in usdt

    mapping(PROUND => uint256) public PRESALE_CAP_PERROUND; //token supply for round1, round2, round3 10^8 * 10 ^ decimal of prop(8)

    uint256 public immutable START_DAY; // start date of PRESALE
    uint256 public immutable FIRST_END_DAY; // end date of round1
    uint256 public immutable SECOND_END_DAY; // end date of round2
    uint256 public immutable THIRD_END_DAY; // end date of round3

    uint256 public minBuyAmount = 50 ether; //at least have to buy 50 usdc

    uint256 public totalRaisedAmount = 0; //busd amount

    uint8 public devFee = 3;
    uint8 public communityFee = 5;

    uint8 public unlockPercent;

    address constant devWallet = 0xCC272fb9D42f2c7668b4911Ed675757D3BB736eD;
    address constant treasuryWallet =
        0x40F3c98a3Bc57b21f4b8d2a36369a0A96F84cdDF;

    address public winCommunity;
    uint256 public prizeDeadline;

    mapping(PROUND => uint256) public raisedAmountPerRound; //prop
    mapping(address => uint256) public communityRaisedAmount; //busd
    mapping(address => uint256) public communityNumber;
    mapping(address => mapping(address => bool)) isCommunityMember;

    mapping(uint256 => NFTInfo) public nftDetails;

    mapping(address => bool) public isRegistered;

    mapping(address => uint256) public lockedAmount;
    mapping(address => uint256) public withdrawAmount;

    event BoughtTokens(
        address buyer,
        address refer,
        uint256 value,
        uint256 nftID
    );

    event ClaimedReward(uint256 nftID);

    constructor(uint256 _startDay, uint256 _cap, address _busd) {
        START_DAY = _startDay;
        FIRST_END_DAY = START_DAY + 15 days;
        SECOND_END_DAY = START_DAY + 30 days;
        THIRD_END_DAY = START_DAY + 45 days;
        PRESALE_CAP_PERROUND[PROUND.ROUND1] = _cap;
        PRESALE_CAP_PERROUND[PROUND.ROUND2] = _cap;
        PRESALE_CAP_PERROUND[PROUND.ROUND3] = _cap;

        busd = _busd;
    }

    function registerCommunity(address _leaderAddress) external onlyOwner {
        isRegistered[_leaderAddress] = true;
    }

    function setToken(address _tokenAddr) public onlyOwner {
        propToken = _tokenAddr;
    }

    function setNFT(address _tokenAddr) public onlyOwner {
        propNFT = _tokenAddr;
    }

    function setBuyToken(address _tokenAddr) public onlyOwner {
        busd = _tokenAddr;
    }

    function getICOPrice(PROUND _round) public pure returns (uint256 rate) {
        if (_round == PROUND.ROUND1) rate = FIRST_PRICE;
        else if (_round == PROUND.ROUND2) rate = SECOND_PRICE;
        else if (_round == PROUND.ROUND3) rate = THIRD_PRICE;
        else rate == 0;
    }

    function getCurrentRound() public view returns (PROUND round) {
        if (block.timestamp < START_DAY) round = PROUND.INACTIVE;
        else if (block.timestamp <= FIRST_END_DAY) round = PROUND.ROUND1;
        else if (block.timestamp <= SECOND_END_DAY) round = PROUND.ROUND2;
        else if (block.timestamp <= THIRD_END_DAY) round = PROUND.ROUND3;
        else round = PROUND.FINISHED;
    }

    function isActive(PROUND _round) public view returns (bool) {
        require(_round != PROUND.INACTIVE, "Presale is not active yet.");
        require(_round != PROUND.FINISHED, "Presale already finished.");
        if (raisedAmountPerRound[_round] < PRESALE_CAP_PERROUND[_round])
            return true;
        else return false;
    }

    function buyTokens(address _refer, uint256 _busdAmount) public {
        require(isRegistered[_refer], "Invalid referral code.");
        require(_busdAmount >= minBuyAmount, "Have to buy more tokens.");
        PROUND round = getCurrentRound();
        require(isActive(round), "Already sold out!");

        uint256 price = getICOPrice(round);
        IERC20(busd).transferFrom(
            msg.sender,
            treasuryWallet,
            (_busdAmount * (100 - devFee - communityFee)) / 100
        );
        IERC20(busd).transferFrom(
            msg.sender,
            devWallet,
            (_busdAmount * devFee) / 100
        );
        IERC20(busd).transferFrom(
            msg.sender,
            _refer,
            (_busdAmount * communityFee) / 100
        );

        uint256 propAmount = (_busdAmount / price) * 10 ** 8;

        totalRaisedAmount += _busdAmount; // Increment raised amount
        raisedAmountPerRound[round] += propAmount;

        lockedAmount[msg.sender] += propAmount;

        uint256 tokenID = IPropNFT(propNFT).totalSupply() + 1;

        NFTInfo memory nftInfo = NFTInfo(tokenID, _refer, propAmount, false);
        nftDetails[tokenID] = nftInfo;
        communityRaisedAmount[_refer] += _busdAmount;

        if (!isCommunityMember[msg.sender][_refer]) {
            isCommunityMember[msg.sender][_refer] = true;
            communityNumber[_refer]++;
        }

        IPropNFT(propNFT).mint(msg.sender, tokenID);

        emit BoughtTokens(msg.sender, _refer, propAmount, tokenID); // log event onto the blockchain
    }

    function claimReward(uint256 _tokenId) external {
        require(unlockPercent == 100, "Can't claim reward for now!");
        require(
            IPropNFT(propNFT).ownerOf(_tokenId) == msg.sender,
            "You are not the token owner!"
        );
        require(!nftDetails[_tokenId].isClaimed, "Already claimed!");
        require(
            nftDetails[_tokenId].community == winCommunity,
            "Not win community!"
        );

        require(
            block.timestamp <= prizeDeadline,
            "Can't claim reward any more."
        );

        IERC20(propToken).transfer(msg.sender, nftDetails[_tokenId].amount);

        nftDetails[_tokenId].isClaimed = true;

        emit ClaimedReward(_tokenId);
    }

    function setUnlockPercent(uint8 _unlock) external onlyOwner {
        require(_unlock <= 100, "Invalid percent!");
        unlockPercent = _unlock;
    }

    function updateRoundCap(PROUND _round, uint256 _cap) external onlyOwner {
        PRESALE_CAP_PERROUND[_round] = _cap;
    }

    function setReward(address _winner, uint256 _deadline) external onlyOwner {
        winCommunity = _winner;
        prizeDeadline = _deadline;
    }

    function claimPROPs(uint256 _amount) external {
        require(
            _amount <= getClaimableAmount(msg.sender),
            "Can't unlock that much amount!"
        );
        withdrawAmount[msg.sender] += _amount;
        IERC20(propToken).transfer(msg.sender, _amount);
    }

    function getClaimableAmount(address _holder) public view returns (uint256) {
        return
            (lockedAmount[_holder] * unlockPercent) /
            100 -
            withdrawAmount[_holder];
    }

    function withdrawPROPs() external onlyOwner {
        uint256 restBalance = IERC20(propToken).balanceOf(address(this));
        IERC20(propToken).transfer(msg.sender, restBalance);
    }
}

