// SPDX-License-Identifier: MIT
pragma solidity ^0.8.15;

interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(
        address owner,
        address spender
    ) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(
        address indexed owner,
        address indexed spender,
        uint256 value
    );
}

abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(
        address indexed previousOwner,
        address indexed newOwner
    );

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(
            newOwner != address(0),
            "Ownable: new owner is the zero address"
        );
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

library SafeMath {
    /**
     * @dev Returns the addition of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryAdd(
        uint256 a,
        uint256 b
    ) internal pure returns (bool, uint256) {
        unchecked {
            uint256 c = a + b;
            if (c < a) return (false, 0);
            return (true, c);
        }
    }

    /**
     * @dev Returns the substraction of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function trySub(
        uint256 a,
        uint256 b
    ) internal pure returns (bool, uint256) {
        unchecked {
            if (b > a) return (false, 0);
            return (true, a - b);
        }
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryMul(
        uint256 a,
        uint256 b
    ) internal pure returns (bool, uint256) {
        unchecked {
            // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
            // benefit is lost if 'b' is also tested.
            // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522
            if (a == 0) return (true, 0);
            uint256 c = a * b;
            if (c / a != b) return (false, 0);
            return (true, c);
        }
    }

    /**
     * @dev Returns the division of two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryDiv(
        uint256 a,
        uint256 b
    ) internal pure returns (bool, uint256) {
        unchecked {
            if (b == 0) return (false, 0);
            return (true, a / b);
        }
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryMod(
        uint256 a,
        uint256 b
    ) internal pure returns (bool, uint256) {
        unchecked {
            if (b == 0) return (false, 0);
            return (true, a % b);
        }
    }

    /**
     * @dev Returns the addition of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `+` operator.
     *
     * Requirements:
     *
     * - Addition cannot overflow.
     */
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        return a + b;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        return a - b;
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `*` operator.
     *
     * Requirements:
     *
     * - Multiplication cannot overflow.
     */
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        return a * b;
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator.
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        return a / b;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        return a % b;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting with custom message on
     * overflow (when the result is negative).
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {trySub}.
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b <= a, errorMessage);
            return a - b;
        }
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting with custom message on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b > 0, errorMessage);
            return a / b;
        }
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting with custom message when dividing by zero.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryMod}.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b > 0, errorMessage);
            return a % b;
        }
    }
}

contract CONGStaking is Ownable {
    using SafeMath for uint256;

    uint256 public constant YEAR = 365 days;
    uint256 public constant PERCENT_DIVIDER = 10000;

    address public congToken;
    uint256 public flexibleAPR = 900;

    uint256 public totalStakedUsersInLP;
    uint256 public totalStakedTokensInLP;
    uint256 public totalUnstakedTokensInLP;
    uint256 public totalRewardedTokensInLP;

    uint256 public totalStakedUsersInFP;
    uint256 public totalStakedTokensInFP;
    uint256 public totalUnstakedTokensInFP;
    uint256 public totalRewardedTokensInFP;

    uint256[5] public lockedAPR = [1400, 1900, 2200, 3900, 5900];
    uint256[5] public lockDurations = [
        30 days,
        60 days,
        90 days,
        180 days,
        365 days
    ];

    struct Stake {
        uint256 apr;
        uint256 planId;
        uint256 amount;
        uint256 reward;
        uint256 consumedReward;
        uint256 endTime;
        uint256 startTime;
        uint256 withdrawnAt;
        uint256 lastCompoundedAt;
    }

    struct User {
        uint256 totalStaked;
        uint256 totalUnstaked;
        uint256 totalRewarded;
        uint256 lockedStakeCount;
        uint256 flexibleStakeCount;
        mapping(uint256 => Stake) lockedStakes;
        mapping(uint256 => Stake) flexibleStakes;
    }

    mapping(address => User) public users;

    event STAKE(
        address _user,
        uint256 _amount,
        uint256 _stakeTime,
        bool isLocked
    );
    event UNSTAKE(
        address _user,
        uint256 _index,
        uint256 _reward,
        uint256 _unstakeTime,
        bool isLocked
    );

    constructor(address _token) {
        congToken = _token;
        _transferOwnership(0x613e4b06a44848D93EAADE56D53B360e49A3303d);
    }

    function lockedStake(uint256 _amount, uint256 _planID) external {
        require(_amount != 0, "Amount != 0");
        require(_planID < lockedAPR.length, "Invalid Plan");

        IERC20(congToken).transferFrom(msg.sender, address(this), _amount);

        User storage user = users[msg.sender];
        uint256 currentIndex = ++user.lockedStakeCount;

        Stake storage _currentStake = user.lockedStakes[currentIndex];
        _currentStake.planId = _planID;
        _currentStake.apr = lockedAPR[_planID];
        _currentStake.amount = _amount;
        _currentStake.reward = calculateLockedPlanReward(_planID, _amount);
        _currentStake.startTime = block.timestamp;
        _currentStake.lastCompoundedAt = block.timestamp;
        _currentStake.endTime = block.timestamp + lockDurations[_planID];

        // update total staked info
        if (currentIndex == 1) {
            totalStakedUsersInLP++;
        }
        user.totalStaked = user.totalStaked.add(_amount);
        totalStakedTokensInLP = totalStakedTokensInLP.add(_amount);

        emit STAKE(msg.sender, _amount, block.timestamp, true);
    }

    function lockedUnstake(uint256 _index) external {
        User storage user = users[msg.sender];
        require(_index > 0 && _index <= user.lockedStakeCount, "Invalid ID");

        Stake storage stakeInfo = user.lockedStakes[_index];
        require(block.timestamp > stakeInfo.endTime, "Time not finished");
        require(stakeInfo.withdrawnAt == 0, "Already withdrawn");
        stakeInfo.withdrawnAt = block.timestamp;

        uint256 remainingReward = stakeInfo.reward.sub(
            stakeInfo.consumedReward
        );
        if (remainingReward != 0) {
            IERC20(congToken).transfer(msg.sender, remainingReward); // reward
        }
        IERC20(congToken).transfer(msg.sender, stakeInfo.amount); // staked

        // update total staked info
        user.totalUnstaked = user.totalUnstaked.add(stakeInfo.amount);
        user.totalRewarded = user.totalRewarded.add(remainingReward);

        totalUnstakedTokensInLP = totalUnstakedTokensInLP.add(stakeInfo.amount);
        totalRewardedTokensInLP = totalRewardedTokensInLP.add(remainingReward);

        emit UNSTAKE(
            msg.sender,
            _index,
            remainingReward,
            block.timestamp,
            true
        );
    }

    function compoundLockedStake(uint256 _index) external {
        User storage user = users[msg.sender];
        require(_index > 0 && _index <= user.lockedStakeCount, "Invalid ID");

        Stake storage stakeInfo = user.lockedStakes[_index];
        require(stakeInfo.withdrawnAt == 0, "Already withdrawn");

        uint256 currentIndex = ++user.lockedStakeCount;
        uint256 currentReward = calculateCurrentLockPlanReward(
            msg.sender,
            _index
        );
        require(currentReward != 0, "Reward=0");

        stakeInfo.lastCompoundedAt = block.timestamp;
        stakeInfo.consumedReward = stakeInfo.consumedReward.add(currentReward);

        Stake storage _currentStake = user.lockedStakes[currentIndex];
        _currentStake.planId = stakeInfo.planId;
        _currentStake.apr = lockedAPR[stakeInfo.planId];
        _currentStake.amount = currentReward;
        _currentStake.reward = calculateLockedPlanReward(
            stakeInfo.planId,
            currentReward
        );
        _currentStake.lastCompoundedAt = block.timestamp;
        _currentStake.startTime = block.timestamp;
        _currentStake.endTime =
            block.timestamp +
            lockDurations[stakeInfo.planId];

        // update total staked info
        user.totalStaked = user.totalStaked.add(currentReward);
        totalStakedTokensInLP = totalStakedTokensInLP.add(currentReward);

        user.totalRewarded = user.totalRewarded.add(currentReward);
        totalRewardedTokensInLP = totalRewardedTokensInLP.add(currentReward);

        emit STAKE(msg.sender, currentReward, block.timestamp, true);
    }

    function flexibleStake(uint256 _amount) external {
        require(_amount > 0, "Amount != 0");

        IERC20(congToken).transferFrom(msg.sender, address(this), _amount);

        User storage user = users[msg.sender];
        uint256 currentIndex = ++user.flexibleStakeCount;

        Stake storage _currentStake = user.flexibleStakes[currentIndex];
        _currentStake.apr = flexibleAPR;
        _currentStake.amount = _amount;
        _currentStake.startTime = block.timestamp;
        _currentStake.lastCompoundedAt = block.timestamp;
        _currentStake.reward = _amount.mul(flexibleAPR).div(PERCENT_DIVIDER);

        // update total staked info
        if (currentIndex == 1) {
            totalStakedUsersInFP++;
        }
        user.totalStaked = user.totalStaked.add(_amount);
        totalStakedTokensInFP = totalStakedTokensInFP.add(_amount);

        emit STAKE(msg.sender, _amount, block.timestamp, false);
    }

    function compoundFlexibleStake(uint256 _index) external {
        User storage user = users[msg.sender];
        require(_index > 0 && _index <= user.flexibleStakeCount, "Invalid ID");

        Stake storage stakeInfo = user.flexibleStakes[_index];
        require(stakeInfo.withdrawnAt == 0, "Already withdrawn");

        uint256 elapsedDuration = block.timestamp - stakeInfo.lastCompoundedAt;
        uint256 currentReward = (stakeInfo.reward.div(YEAR)).mul(
            elapsedDuration
        );
        require(currentReward != 0, "Reward=0");

        stakeInfo.lastCompoundedAt = block.timestamp;
        stakeInfo.consumedReward = stakeInfo.consumedReward.add(currentReward);

        uint256 currentIndex = ++user.flexibleStakeCount;

        Stake storage _currentStake = user.flexibleStakes[currentIndex];
        _currentStake.apr = flexibleAPR;
        _currentStake.amount = currentReward;
        _currentStake.reward = currentReward.mul(flexibleAPR).div(
            PERCENT_DIVIDER
        );
        _currentStake.startTime = block.timestamp;
        _currentStake.lastCompoundedAt = block.timestamp;

        // update total staked info
        user.totalStaked = user.totalStaked.add(currentReward);
        totalStakedTokensInFP = totalStakedTokensInFP.add(currentReward);

        user.totalRewarded = user.totalRewarded.add(currentReward);
        totalRewardedTokensInFP = totalRewardedTokensInFP.add(currentReward);

        emit STAKE(msg.sender, currentReward, block.timestamp, false);
    }

    function flexibleUnstake(uint256 _index) external {
        User storage user = users[msg.sender];
        require(_index > 0 && _index <= user.flexibleStakeCount, "Invalid ID");

        Stake storage stakeInfo = user.flexibleStakes[_index];
        require(stakeInfo.withdrawnAt == 0, "Already withdrawn");
        stakeInfo.withdrawnAt = block.timestamp;

        IERC20(congToken).transfer(msg.sender, stakeInfo.amount); // staked

        uint256 elapsedDuration = block.timestamp - stakeInfo.lastCompoundedAt;
        uint256 currentReward = (stakeInfo.reward.div(YEAR)).mul(
            elapsedDuration
        );
        if (currentReward != 0) {
            IERC20(congToken).transfer(msg.sender, currentReward); // reward
        }

        // update total staked info
        user.totalUnstaked = user.totalUnstaked.add(stakeInfo.amount);
        user.totalRewarded = user.totalRewarded.add(currentReward);

        totalUnstakedTokensInFP = totalUnstakedTokensInFP.add(stakeInfo.amount);
        totalRewardedTokensInFP = totalRewardedTokensInFP.add(currentReward);

        emit UNSTAKE(msg.sender, _index, currentReward, block.timestamp, false);
    }

    function calculateLockedPlanReward(
        uint256 _planID,
        uint256 _amount
    ) public view returns (uint256 estimatedReward) {
        require(_planID < lockedAPR.length, "Invalid Plan");
        estimatedReward = (_amount.mul(lockedAPR[_planID]).div(PERCENT_DIVIDER))
            .div(YEAR.div(lockDurations[_planID]));
    }

    function calculateCurrentLockPlanReward(
        address _user,
        uint256 _index
    ) public view returns (uint256 estimatedReward) {
        User storage user = users[_user];
        require(_index > 0 && _index <= user.lockedStakeCount, "Invalid ID");

        Stake storage stakeInfo = user.lockedStakes[_index];
        if (stakeInfo.withdrawnAt != 0) {
            return 0;
        }

        uint256 elapsedDur = block.timestamp - stakeInfo.lastCompoundedAt;
        uint256 perSecondReward = stakeInfo.reward.div(
            lockDurations[stakeInfo.planId]
        );

        estimatedReward = perSecondReward.mul(elapsedDur);
        if (estimatedReward + stakeInfo.consumedReward > stakeInfo.reward) {
            estimatedReward = stakeInfo.reward - stakeInfo.consumedReward;
        }
    }

    function calculateCurrentFlexiblePlanReward(
        address _user,
        uint256 _index
    ) public view returns (uint256 estimatedReward) {
        User storage user = users[_user];
        require(_index > 0 && _index <= user.flexibleStakeCount, "Invalid ID");

        Stake storage stakeInfo = user.flexibleStakes[_index];
        if (stakeInfo.withdrawnAt != 0) {
            return 0;
        }

        uint256 elapsedDur = block.timestamp - stakeInfo.lastCompoundedAt;
        uint256 perSecondReward = stakeInfo.reward.div(YEAR);

        estimatedReward = perSecondReward.mul(elapsedDur);
        // if (estimatedReward + stakeInfo.consumedReward > stakeInfo.reward) {
        //     estimatedReward = stakeInfo.reward - stakeInfo.consumedReward;
        // }
    }

    function getLockedStakeInfo(
        address _usr,
        uint256 _index
    )
        public
        view
        returns (
            uint256 planId,
            uint256 apr,
            uint256 amount,
            uint256 reward,
            uint256 startTime,
            uint256 endTime,
            bool withdrawn
        )
    {
        User storage _user = users[_usr];
        require(_index > 0 && _index <= _user.lockedStakeCount, "Invalid ID");
        Stake memory stakeInfo = _user.lockedStakes[_index];

        return (
            stakeInfo.planId,
            stakeInfo.apr,
            stakeInfo.amount,
            stakeInfo.reward,
            stakeInfo.startTime,
            stakeInfo.endTime,
            stakeInfo.withdrawnAt == 0 ? false : true
        );
    }

    function getFlexibleStakeInfo(
        address _usr,
        uint256 _index
    )
        public
        view
        returns (
            uint256 planId,
            uint256 apr,
            uint256 amount,
            uint256 reward,
            uint256 startTime,
            bool withdrawn
        )
    {
        User storage _user = users[_usr];
        require(_index > 0 && _index <= _user.flexibleStakeCount, "Invalid ID");
        Stake memory stakeInfo = _user.flexibleStakes[_index];

        uint256 elapsedDuration = block.timestamp - stakeInfo.startTime;
        uint256 currentReward = (stakeInfo.reward.div(YEAR)).mul(
            elapsedDuration
        );

        return (
            stakeInfo.planId,
            stakeInfo.apr,
            stakeInfo.amount,
            currentReward,
            stakeInfo.startTime,
            stakeInfo.withdrawnAt == 0 ? false : true
        );
    }

    function setToken(address _addr) external onlyOwner {
        congToken = _addr;
    }

    function setFlexibleStakeAPR(uint256 _apr) external onlyOwner {
        flexibleAPR = _apr;
    }

    function setLockedStakeAPR(uint256[5] memory _aprs) external onlyOwner {
        lockedAPR = _aprs;
    }

    function setLockedDurations(
        uint256[5] memory _durations
    ) external onlyOwner {
        lockDurations = _durations;
    }

    function removeUnstuckTokens(
        address _receipent,
        IERC20 _token,
        uint256 _amnt
    ) external onlyOwner {
        _token.transfer(_receipent, _amnt);
    }
}