
// File: BaseStaking.sol
// SPDX-License-Identifier: MIT
// GoldQuality

pragma solidity ^0.8.19;

import "IERC20.sol";
import "IBaseStaking.sol";
import "Ownable.sol";

contract BaseStaking is IBaseStaking, Ownable {
    IERC20 public token;
    uint256 public startDate;
    bool public isRun;

    struct Stake {
        uint256 amount;
        uint256 timestamp;
    }

    mapping(address => Stake[]) public stakes;
    uint256[] public firstDatePercents;
    uint256[] public firstDateYears;
    uint256[] public bonusPercents;
    uint256[] public bonusDays;

    uint256 constant private SECONDS_IN_DAY = 86400;
    uint256 constant private SECONDS_IN_YEAR = 31536000;

    event Staked(address indexed user, uint256 amount);
    event UnStaked(address indexed user, uint256 amount, uint256 rewardAmount);
    event Stop(address user);

    constructor(address _token, uint256 _startDate)
    {
        require(_startDate <= block.timestamp, "Amount must not be zero");

        token = IERC20(_token);
        startDate = _startDate;
        isRun = true;

        firstDateYears = [0, 1, 2];
        firstDatePercents = [37, 27, 17];

        bonusDays = [0, 90, 120, 150, 180, 240, 300, 360, 450, 540, 630, 720, 900];
        bonusPercents = [0, 1, 2, 3, 5, 7, 8, 10, 12, 13, 14, 15, 16];
    }

    function getStakeCount(address _user)
    external view
    returns (uint256)
    {
        return stakes[_user].length;
    }

    function getStakeInfo(address _user)
    public view
    returns (
        uint256 totalAmount,
        uint256 rewardAmount
    )
    {
        (totalAmount, rewardAmount) = (0, 0);

        for (uint256 i = 0; i < stakes[_user].length; i++) {
            totalAmount += stakes[_user][i].amount;

            uint256 yearPassed = (block.timestamp - startDate) / SECONDS_IN_YEAR;
            uint256 daysPassed = (block.timestamp - stakes[_user][i].timestamp) / SECONDS_IN_DAY;

            rewardAmount += stakes[_user][i].amount * (
                firstDatePercents[_getRightIndex(firstDateYears, yearPassed)] +
                bonusPercents[_getRightIndex(bonusDays, daysPassed)]
            ) * daysPassed / (100 * 365);
        }
    }

    function stop()
    external onlyOwner
    {
        require(isRun, "Already stopped");
        isRun = false;

        emit Stop(_msgSender());
    }

    function stake(uint256 _amount)
    external
    {
        require(isRun, "Staking is closed");
        require(_amount != 0, "Amount must not be zero");
        require(token.balanceOf(_msgSender()) >= _amount, "Not enough tokens");
        require(token.allowance(_msgSender(), address(this)) >= _amount, "Token allowance is low");

        require(token.transferFrom(_msgSender(), address(this), _amount), "Token transfer failed");

        stakes[_msgSender()].push(Stake(_amount, block.timestamp));

        emit Staked(_msgSender(), _amount);
    }

    function unStake()
    external
    {
        require(stakes[_msgSender()].length != 0, "No stakes");

        (uint256 totalAmount, uint256 rewardAmount) = getStakeInfo(_msgSender());

        require(totalAmount + rewardAmount <= token.balanceOf(address(this)) , "Insufficient staking balance");

        delete stakes[_msgSender()];
        token.transfer(_msgSender(), totalAmount + rewardAmount);

        emit UnStaked(_msgSender(), totalAmount, rewardAmount);
    }

    function _getRightIndex(
        uint256[] memory array,
        uint256 value
    )
    internal view
    returns (uint256)
    {
        uint256 index = 1;

        while (index < array.length) {
            if (value < array[index]) {
                break;
            } else {
                index++;
            }
        }

        return index - 1;
    }
}

// File: IERC20.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.19;

interface IERC20 {
    function balanceOf(address account) external view returns (uint256);
    function allowance(address owner, address spender) external view returns (uint256);
    function transfer(address to, uint256 amount) external returns (bool);
    function transferFrom(address from, address to, uint256 amount) external returns (bool);
    function burn(uint256 amount) external;
}

// File: IBaseStaking.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.19;

interface IBaseStaking {
    function stake(uint256 _amount) external;
    function unStake() external;
}

// File: Ownable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (access/Ownable.sol)

pragma solidity ^0.8.0;

import "Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}


// File: Context.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

