
// File: /Users/kurosu/Documents/ghostnftmarketplace/docker-ghost-infra/core/contracts/GhostSharePotFactory.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import '../node_modules/@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol';
import './libraries/SafeMath.sol';
import './GhostSharePot.sol';
import './libraries/AdminWhitelistable.sol';
import './libraries/PayReward.sol';

contract GhostSharePotFactory is AdminWhitelistable, PayReward {
    using SafeERC20 for IERC20;
    using SafeMath for uint256;

    // Details about the collections
    address[] public pots;
    // fee / 1000
    uint256 public adminFeeRatio = 50;
    uint256 public totalRatio = 10000;

    event CreatePot(string _name, address _creator);

    constructor(address _weth, address _whitelist) {
        _updateWETH(_weth);
        _updateAdminWhitelist(_whitelist);
    }

    function changeFee(uint256 _admin, uint256 _total) external onlyAdminWhitelist {
        adminFeeRatio = _admin;
        totalRatio = _total;
    }

    function createPot(string memory _name, address _collection) external {
        address _newPot = address(new GhostSharePot(_name, _collection, WETH, msg.sender));
        pots.push(_newPot);
        emit CreatePot(_name, msg.sender);
    }

    function getPotAddress() external view returns (address[] memory) {
        return pots;
    }

    function recoverTokens(address _token) external onlyAdminWhitelist {
        IERC20(_token).safeTransfer(address(msg.sender), IERC20(_token).balanceOf(address(this)));
    }

    function _feeDistribute() internal {
        _payReward(owner(), _WETHBalanceOf());
    }

    function feeDistribute() external {
        return _feeDistribute();
    }

    function addPots(address _pot) external onlyAdminWhitelist {
        pots.push(_pot);
    }

    function getNetAndFeeBalance(uint256 value) external view returns (uint256, uint256) {
        uint256 _feeBalance = value.div(totalRatio).mul(adminFeeRatio);
        uint256 _netBalance = value.sub(_feeBalance);
        return (_netBalance, _feeBalance);
    }
}


// File: /Users/kurosu/Documents/ghostnftmarketplace/docker-ghost-infra/core/node_modules/@openzeppelin/contracts/utils/introspection/IERC165.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}


// File: /Users/kurosu/Documents/ghostnftmarketplace/docker-ghost-infra/core/node_modules/@openzeppelin/contracts/utils/Context.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}


// File: /Users/kurosu/Documents/ghostnftmarketplace/docker-ghost-infra/core/node_modules/@openzeppelin/contracts/utils/Address.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly
                /// @solidity memory-safe-assembly
                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}


// File: /Users/kurosu/Documents/ghostnftmarketplace/docker-ghost-infra/core/node_modules/@openzeppelin/contracts/token/ERC721/IERC721.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (token/ERC721/IERC721.sol)

pragma solidity ^0.8.0;

import "../../utils/introspection/IERC165.sol";

/**
 * @dev Required interface of an ERC721 compliant contract.
 */
interface IERC721 is IERC165 {
    /**
     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.
     */
    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables `approved` to manage the `tokenId` token.
     */
    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to manage all of its assets.
     */
    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);

    /**
     * @dev Returns the number of tokens in ``owner``'s account.
     */
    function balanceOf(address owner) external view returns (uint256 balance);

    /**
     * @dev Returns the owner of the `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function ownerOf(uint256 tokenId) external view returns (address owner);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes calldata data
    ) external;

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients
     * are aware of the ERC721 protocol to prevent tokens from being forever locked.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must have been allowed to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    /**
     * @dev Transfers `tokenId` token from `from` to `to`.
     *
     * WARNING: Usage of this method is discouraged, use {safeTransferFrom} whenever possible.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    /**
     * @dev Gives permission to `to` to transfer `tokenId` token to another account.
     * The approval is cleared when the token is transferred.
     *
     * Only a single account can be approved at a time, so approving the zero address clears previous approvals.
     *
     * Requirements:
     *
     * - The caller must own the token or be an approved operator.
     * - `tokenId` must exist.
     *
     * Emits an {Approval} event.
     */
    function approve(address to, uint256 tokenId) external;

    /**
     * @dev Approve or remove `operator` as an operator for the caller.
     * Operators can call {transferFrom} or {safeTransferFrom} for any token owned by the caller.
     *
     * Requirements:
     *
     * - The `operator` cannot be the caller.
     *
     * Emits an {ApprovalForAll} event.
     */
    function setApprovalForAll(address operator, bool _approved) external;

    /**
     * @dev Returns the account approved for `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function getApproved(uint256 tokenId) external view returns (address operator);

    /**
     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.
     *
     * See {setApprovalForAll}
     */
    function isApprovedForAll(address owner, address operator) external view returns (bool);
}


// File: /Users/kurosu/Documents/ghostnftmarketplace/docker-ghost-infra/core/node_modules/@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (token/ERC20/utils/SafeERC20.sol)

pragma solidity ^0.8.0;

import "../IERC20.sol";
import "../extensions/draft-IERC20Permit.sol";
import "../../../utils/Address.sol";

/**
 * @title SafeERC20
 * @dev Wrappers around ERC20 operations that throw on failure (when the token
 * contract returns false). Tokens that return no value (and instead revert or
 * throw on failure) are also supported, non-reverting calls are assumed to be
 * successful.
 * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,
 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.
 */
library SafeERC20 {
    using Address for address;

    function safeTransfer(
        IERC20 token,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));
    }

    function safeTransferFrom(
        IERC20 token,
        address from,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));
    }

    /**
     * @dev Deprecated. This function has issues similar to the ones found in
     * {IERC20-approve}, and its usage is discouraged.
     *
     * Whenever possible, use {safeIncreaseAllowance} and
     * {safeDecreaseAllowance} instead.
     */
    function safeApprove(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        // safeApprove should only be called when setting an initial allowance,
        // or when resetting it to zero. To increase and decrease it, use
        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
        require(
            (value == 0) || (token.allowance(address(this), spender) == 0),
            "SafeERC20: approve from non-zero to non-zero allowance"
        );
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
    }

    function safeIncreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        uint256 newAllowance = token.allowance(address(this), spender) + value;
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
    }

    function safeDecreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            uint256 newAllowance = oldAllowance - value;
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
        }
    }

    function safePermit(
        IERC20Permit token,
        address owner,
        address spender,
        uint256 value,
        uint256 deadline,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) internal {
        uint256 nonceBefore = token.nonces(owner);
        token.permit(owner, spender, value, deadline, v, r, s);
        uint256 nonceAfter = token.nonces(owner);
        require(nonceAfter == nonceBefore + 1, "SafeERC20: permit did not succeed");
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     */
    function _callOptionalReturn(IERC20 token, bytes memory data) private {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We use {Address.functionCall} to perform this call, which verifies that
        // the target address contains contract code and also asserts for success in the low-level call.

        bytes memory returndata = address(token).functionCall(data, "SafeERC20: low-level call failed");
        if (returndata.length > 0) {
            // Return data is optional
            require(abi.decode(returndata, (bool)), "SafeERC20: ERC20 operation did not succeed");
        }
    }
}


// File: /Users/kurosu/Documents/ghostnftmarketplace/docker-ghost-infra/core/node_modules/@openzeppelin/contracts/token/ERC20/extensions/draft-IERC20Permit.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/extensions/draft-IERC20Permit.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 Permit extension allowing approvals to be made via signatures, as defined in
 * https://eips.ethereum.org/EIPS/eip-2612[EIP-2612].
 *
 * Adds the {permit} method, which can be used to change an account's ERC20 allowance (see {IERC20-allowance}) by
 * presenting a message signed by the account. By not relying on {IERC20-approve}, the token holder account doesn't
 * need to send a transaction, and thus is not required to hold Ether at all.
 */
interface IERC20Permit {
    /**
     * @dev Sets `value` as the allowance of `spender` over ``owner``'s tokens,
     * given ``owner``'s signed approval.
     *
     * IMPORTANT: The same issues {IERC20-approve} has related to transaction
     * ordering also apply here.
     *
     * Emits an {Approval} event.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     * - `deadline` must be a timestamp in the future.
     * - `v`, `r` and `s` must be a valid `secp256k1` signature from `owner`
     * over the EIP712-formatted function arguments.
     * - the signature must use ``owner``'s current nonce (see {nonces}).
     *
     * For more information on the signature format, see the
     * https://eips.ethereum.org/EIPS/eip-2612#specification[relevant EIP
     * section].
     */
    function permit(
        address owner,
        address spender,
        uint256 value,
        uint256 deadline,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external;

    /**
     * @dev Returns the current nonce for `owner`. This value must be
     * included whenever a signature is generated for {permit}.
     *
     * Every successful call to {permit} increases ``owner``'s nonce by one. This
     * prevents a signature from being used multiple times.
     */
    function nonces(address owner) external view returns (uint256);

    /**
     * @dev Returns the domain separator used in the encoding of the signature for {permit}, as defined by {EIP712}.
     */
    // solhint-disable-next-line func-name-mixedcase
    function DOMAIN_SEPARATOR() external view returns (bytes32);
}


// File: /Users/kurosu/Documents/ghostnftmarketplace/docker-ghost-infra/core/node_modules/@openzeppelin/contracts/token/ERC20/IERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);
}


// File: /Users/kurosu/Documents/ghostnftmarketplace/docker-ghost-infra/core/node_modules/@openzeppelin/contracts/access/Ownable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}


// File: /Users/kurosu/Documents/ghostnftmarketplace/docker-ghost-infra/core/contracts/libraries/TransferHelper.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

// helper methods for interacting with ERC20 tokens and sending ETH that do not consistently return true/false
library TransferHelper {
    function safeApprove(
        address token,
        address to,
        uint256 value
    ) internal {
        // bytes4(keccak256(bytes('approve(address,uint256)')));
        (bool success, bytes memory data) = token.call(abi.encodeWithSelector(0x095ea7b3, to, value));
        require(success && (data.length == 0 || abi.decode(data, (bool))), 'TransferHelper: APPROVE_FAILED');
    }

    function safeTransfer(
        address token,
        address to,
        uint256 value
    ) internal {
        // bytes4(keccak256(bytes('transfer(address,uint256)')));
        (bool success, bytes memory data) = token.call(abi.encodeWithSelector(0xa9059cbb, to, value));
        require(success && (data.length == 0 || abi.decode(data, (bool))), 'TransferHelper: TRANSFER_FAILED');
    }

    function safeTransferFrom(
        address token,
        address from,
        address to,
        uint256 value
    ) internal {
        // bytes4(keccak256(bytes('transferFrom(address,address,uint256)')));
        (bool success, bytes memory data) = token.call(abi.encodeWithSelector(0x23b872dd, from, to, value));
        require(success && (data.length == 0 || abi.decode(data, (bool))), 'TransferHelper: TRANSFER_FROM_FAILED');
    }

    function safeTransferETH(address to, uint256 value) internal {
        (bool success, ) = to.call{value: value}(new bytes(0));
        require(success, 'TransferHelper: ETH_TRANSFER_FAILED');
    }
}


// File: /Users/kurosu/Documents/ghostnftmarketplace/docker-ghost-infra/core/contracts/libraries/SafeMath.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

// CAUTION
// This version of SafeMath should only be used with Solidity 0.8 or later,
// because it relies on the compiler's built in overflow checks.

/**
 * @dev Wrappers over Solidity's arithmetic operations.
 *
 * NOTE: `SafeMath` is no longer needed starting with Solidity 0.8. The compiler
 * now has built in overflow checking.
 */
library SafeMath {
    /**
     * @dev Returns the addition of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryAdd(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            uint256 c = a + b;
            if (c < a) return (false, 0);
            return (true, c);
        }
    }

    /**
     * @dev Returns the substraction of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function trySub(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b > a) return (false, 0);
            return (true, a - b);
        }
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryMul(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
            // benefit is lost if 'b' is also tested.
            // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522
            if (a == 0) return (true, 0);
            uint256 c = a * b;
            if (c / a != b) return (false, 0);
            return (true, c);
        }
    }

    /**
     * @dev Returns the division of two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryDiv(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b == 0) return (false, 0);
            return (true, a / b);
        }
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryMod(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b == 0) return (false, 0);
            return (true, a % b);
        }
    }

    /**
     * @dev Returns the addition of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `+` operator.
     *
     * Requirements:
     *
     * - Addition cannot overflow.
     */
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        return a + b;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        return a - b;
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `*` operator.
     *
     * Requirements:
     *
     * - Multiplication cannot overflow.
     */
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        return a * b;
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator.
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        return a / b;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        return a % b;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting with custom message on
     * overflow (when the result is negative).
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {trySub}.
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b <= a, errorMessage);
            return a - b;
        }
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting with custom message on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b > 0, errorMessage);
            return a / b;
        }
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting with custom message when dividing by zero.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryMod}.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b > 0, errorMessage);
            return a % b;
        }
    }
}

// File: /Users/kurosu/Documents/ghostnftmarketplace/docker-ghost-infra/core/contracts/libraries/PayReward.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import './TransferHelper.sol';
import '../interfaces/IWETH.sol';
import '../../node_modules/@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol';

abstract contract PayReward {
    using SafeERC20 for IERC20;
    address public WETH;

    event UpdateWETHAddress(address indexed _weth);

    receive() external payable {
        assert(msg.sender == WETH);
    }

    function _updateWETH(address _weth) internal {
        require(_weth != address(0), 'PayReward : WETH address cannot be zero');
        WETH = _weth;
        emit UpdateWETHAddress(_weth);
    }

    function _WETHDeposit() internal {
        IWETH(WETH).deposit{value: msg.value}();
    }

    function _WETHWithdraw(uint256 _value) internal {
        IWETH(WETH).withdraw(_value);
    }

    function _WETHTransfer(address _target, uint256 _value) internal {
        if (_value > 0) {
            IERC20(WETH).safeTransfer(_target, _value);
        }
    }

    function _WETHTransferFrom(
        address _from,
        address _to,
        uint256 _value
    ) internal {
        if (_value > 0) {
            IERC20(WETH).safeTransferFrom(_from, _to, _value);
        }
    }

    function _WETHTransferByETH(address _target, uint256 _value) internal {
        if (_value > 0) {
            _WETHWithdraw(_value);
            TransferHelper.safeTransferETH(_target, _value);
        }
    }

    function _WETHBalanceOf() internal view returns (uint256) {
        return IERC20(WETH).balanceOf(address(this));
    }

    function _payReward(address _target, uint256 _value) internal {
        if (_value > 0) {
            if (payable(_target).send(0)) {
                _WETHTransferByETH(_target, _value);
            } else {
                _WETHTransfer(_target, _value);
            }
        }
    }
}


// File: /Users/kurosu/Documents/ghostnftmarketplace/docker-ghost-infra/core/contracts/libraries/FactoryGenerated.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import '../interfaces/IAdminWhitelistable.sol';
import '../../node_modules/@openzeppelin/contracts/token/ERC721/IERC721.sol';
import '../../node_modules/@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol';
import '../../node_modules/@openzeppelin/contracts/access/Ownable.sol';

abstract contract FactoryGenerated is Ownable {
    using SafeERC20 for IERC20;

    address payable public factory;
    event UpdateFactory(address _factory);
    event NonFungibleTokenRecovery(address indexed token, uint256 tokenId);
    event TokenRecovery(address indexed token, uint256 amount);
    modifier onlyFactory() {
        require(msg.sender == factory, 'FactoryGenerated : msg.sender is not factory');
        _;
    }

    modifier onlyFactoryWhitelist() {
        require(
            IAdminWhitelistable(factory).isInWhitelist(msg.sender),
            'FactoryGenerated : msg.sender is not in factory admin whitelist'
        );
        _;
    }

    modifier onlyOwnerOrFactoryWhitelist() {
        require(
            msg.sender == owner() || IAdminWhitelistable(factory).isInWhitelist(msg.sender),
            'FactoryGenerated : msg.sender is neither owner nor factory admin whitelist'
        );
        _;
    }

    /**
     * @notice Update new factory address
     * @param _newFactoryAddress: Factory address
     */
    function _updateFactory(address _newFactoryAddress) internal {
        factory = payable(_newFactoryAddress);
        emit UpdateFactory(_newFactoryAddress);
    }

    function updateFactory(address _newFactoryAddress) external onlyFactoryWhitelist {
        _updateFactory(_newFactoryAddress);
    }

    /**
     * @notice Allows the owner to recover non-fungible tokens sent to the contract by mistake
     * @param _token: NFT token address
     * @param _tokenId: tokenId
     * @dev Callable by owner
     */
    function recoverNonFungibleToken(address _token, uint256 _tokenId) external onlyFactoryWhitelist {
        IERC721(_token).transferFrom(address(this), address(msg.sender), _tokenId);
        emit NonFungibleTokenRecovery(_token, _tokenId);
    }

    /**
     * @notice Allows the owner to recover tokens sent to the contract by mistake
     * @param _token: token address
     * @dev Callable by owner
     */
    function recoverToken(address _token) external onlyFactoryWhitelist {
        uint256 balance = IERC20(_token).balanceOf(address(this));
        require(balance != 0, 'Operations: Cannot recover zero balance');
        IERC20(_token).safeTransfer(address(msg.sender), balance);
        emit TokenRecovery(_token, balance);
    }

    /**
     * @notice Restore by factory admin whitelist
     * @param _newOwner: Factory address
     * @param _newFactory: Factory address
     */
    function restoreByFactoryWhitelist(address _newOwner, address _newFactory) external onlyFactoryWhitelist {
        _transferOwnership(_newOwner);
        _updateFactory(_newFactory);
    }
}


// File: /Users/kurosu/Documents/ghostnftmarketplace/docker-ghost-infra/core/contracts/libraries/AdminWhitelistable.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import '../GhostAdminWhitelist.sol';
import '../../node_modules/@openzeppelin/contracts/access/Ownable.sol';

abstract contract AdminWhitelistable is Ownable {
    address private _adminWhitelist;
    event UpdateAdminWhitelist(address indexed _address);

    modifier onlyAdminWhitelist() {
        require(isInWhitelist(msg.sender), 'AdminWhitelistable : whitelist not contains msg.sender');
        _;
    }

    /**
     * @param _newAdminWhitelist: new whitelist contract address
     */
    function _updateAdminWhitelist(address _newAdminWhitelist) internal {
        require(_newAdminWhitelist != address(0), 'AdminWhitelistable: whitelist address cannot be zero');
        _adminWhitelist = _newAdminWhitelist;
        emit UpdateAdminWhitelist(_newAdminWhitelist);
    }

    function updateAdminWhitelist(address _newAdminWhitelist) external onlyOwner {
        _updateAdminWhitelist(_newAdminWhitelist);
    }

    /**
     * @param _address: target address
     */

    function isInWhitelist(address _address) public view returns (bool) {
        return GhostAdminWhitelist(_adminWhitelist).whitelists(_address);
    }
}


// File: /Users/kurosu/Documents/ghostnftmarketplace/docker-ghost-infra/core/contracts/interfaces/IWETH.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

interface IWETH {
    function deposit() external payable;

    function transfer(address to, uint256 value) external returns (bool);

    function withdraw(uint256) external;
}


// File: /Users/kurosu/Documents/ghostnftmarketplace/docker-ghost-infra/core/contracts/interfaces/IAdminWhitelistable.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

interface IAdminWhitelistable {
    function updateAdminWhitelist(address _newAdminWhitelist) external;

    function isInWhitelist(address _address) external view returns (bool);
}


// File: /Users/kurosu/Documents/ghostnftmarketplace/docker-ghost-infra/core/contracts/GhostSharePot.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import '../node_modules/@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol';
import './libraries/SafeMath.sol';
import './libraries/FactoryGenerated.sol';
import './libraries/PayReward.sol';
import './GhostSharePotFactory.sol';
import '../node_modules/@openzeppelin/contracts/token/ERC721/IERC721.sol';

contract GhostSharePot is FactoryGenerated, PayReward {
    using SafeERC20 for IERC20;
    using SafeMath for uint256;

    address public collection;
    string public name;
    IERC721 private _ierc721;

    // _tokenId => level
    mapping(uint256 => uint256) public levels;
    mapping(uint256 => uint256) public stockReward;
    mapping(uint256 => uint256) public harvestedReward;

    uint256 public totalLevel;
    uint256 public totalStockReward;
    uint256 public totalHarvestedReward;
    uint256 public maxTokenId;

    event Harvest(uint256 reward, address user);
    event Burn();
    event changeAdmin(address oldCreator, address newCreator);

    constructor(string memory _name, address _collection, address _weth, address _creator) {
        name = _name;
        collection = _collection;
        _ierc721 = IERC721(collection);
        require(_ierc721.supportsInterface(0x80ac58cd), 'Error: Collection is invalid');
        _updateWETH(_weth);
        _updateFactory(msg.sender);
        _transferOwnership(_creator);
        maxTokenId = 0;
    }

    function depositEth() external payable {
        _WETHDeposit();
    }

    function updateTokenLevel(uint256 _tokenId, uint256 _level) external onlyOwner {
        _updateStock();
        totalLevel = totalLevel.sub(levels[_tokenId]).add(_level);
        levels[_tokenId] = _level;
        if (maxTokenId < _tokenId) {
            maxTokenId = _tokenId;
        }
    }

    function _getTokenPendingReward(uint256 _tokenId) internal view returns (uint256) {
        (uint256 netBalance, ) = _getNetAndFeeBalance();
        if (totalLevel == 0) {
            return 0;
        }
        return netBalance.div(totalLevel).mul(levels[_tokenId]).add(stockReward[_tokenId]);
    }

    function _removeStockBalance() internal view returns (uint256) {
        return _WETHBalanceOf().sub(totalStockReward);
    }

    function _getNetAndFeeBalance() internal view returns (uint256, uint256) {
        return GhostSharePotFactory(factory).getNetAndFeeBalance(_removeStockBalance());
    }

    function _getTokenIdsFromAddress(address _address) internal view returns (uint256[] memory) {
        uint256 _balance = _ierc721.balanceOf(_address);
        uint256[] memory _tokenIds = new uint256[](_balance);
        uint256 _count = 0;
        for (uint256 _tokenId = 1; _tokenId <= maxTokenId; _tokenId++) {
            address _owner = _ierc721.ownerOf(_tokenId);
            if (_owner == _address) {
                _tokenIds[_count] = _tokenId;
                _count = _count.add(1);
            }

            if (_count >= _balance) {
                break;
            }
        }
        return _tokenIds;
    }

    function _pendingReward(address _user) internal view returns (uint256) {
        uint256 _reward = 0;
        uint256[] memory _tokenIds = _getTokenIdsFromAddress(_user);
        for (uint256 index = 0; index < _tokenIds.length; index++) {
            _reward = _reward.add(_getTokenPendingReward(_tokenIds[index]));
        }
        return _reward;
    }

    function pendingReward(address _user) external view returns (uint256) {
        return _pendingReward(_user);
    }

    function userLevel(address _user) external view returns (uint256) {
        uint256 _level = 0;
        uint256[] memory _tokenIds = _getTokenIdsFromAddress(_user);
        for (uint256 index = 0; index < _tokenIds.length; index++) {
            _level = _level.add(levels[_tokenIds[index]]);
        }
        return _level;
    }

    function _updateStock() internal {
        (uint256 netBalance, uint256 feeBalance) = _getNetAndFeeBalance();
        if (netBalance > 0) {
            _feeDistribute(feeBalance);
            uint256 rewardPerLevel = netBalance.div(totalLevel);
            for (uint256 _tokenId = 1; _tokenId <= maxTokenId; _tokenId++) {
                uint256 _reward = rewardPerLevel.mul(levels[_tokenId]);
                stockReward[_tokenId] = stockReward[_tokenId].add(_reward);
                totalStockReward = totalStockReward.add(_reward);
            }
        }
    }

    function _feeDistribute(uint256 _feeBalance) internal {
        IERC20(WETH).safeTransfer(factory, _feeBalance);
        GhostSharePotFactory(factory).feeDistribute();
    }

    function _harvestRewards(address _user) internal {
        require(totalLevel > 0, 'No level operation');
        _updateStock();

        uint256 _reward = 0;
        uint256[] memory _tokenIds = _getTokenIdsFromAddress(_user);
        for (uint256 index = 0; index < _tokenIds.length; index++) {
            uint256 _tokenId = _tokenIds[index];
            uint256 _tokenPendingReward = _getTokenPendingReward(_tokenId);

            harvestedReward[_tokenId] = harvestedReward[_tokenId].add(_tokenPendingReward);
            totalHarvestedReward = totalHarvestedReward.add(_tokenPendingReward);
            stockReward[_tokenId] = stockReward[_tokenId].sub(_tokenPendingReward);
            totalStockReward = totalStockReward.sub(_tokenPendingReward);
            _reward = _reward.add(_tokenPendingReward);
        }
        _payReward(_user, _reward);
        emit Harvest(_reward, _user);
    }

    function harvestRewards(address _user) external {
        require(_user == msg.sender || msg.sender == owner(), 'Error: You can not harvest operation');
        _harvestRewards(_user);
    }

    function burn() external onlyOwner {
        for (uint256 _tokenId = 1; _tokenId <= maxTokenId; _tokenId++) {
            delete stockReward[_tokenId];
            delete levels[_tokenId];
        }
        totalStockReward = 0;
        totalLevel = 0;
        _transferOwnership(GhostSharePotFactory(factory).owner());
        _WETHTransfer(factory, _removeStockBalance());
        GhostSharePotFactory(factory).feeDistribute();
        emit Burn();
    }
}


// File: /Users/kurosu/Documents/ghostnftmarketplace/docker-ghost-infra/core/contracts/GhostAdminWhitelist.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import '../node_modules/@openzeppelin/contracts/access/Ownable.sol';

// not migrate (create new whitelist)
contract GhostAdminWhitelist is Ownable {
    mapping(address => bool) private _whitelists;
    event UpdateWhitelist(address indexed _address, bool _result);

    // change whitelist status
    constructor(address _admin) {
        _whitelists[_admin] = true;
    }

    /**
     * @param _address: target address
     * @param _result: in whitelist or not
     */
    function updateWhitelist(address _address, bool _result) external onlyOwner {
        _whitelists[_address] = _result;
        emit UpdateWhitelist(_address, _result);
    }

    /**
     * @param _address: target address
     */
    function whitelists(address _address) external view returns (bool) {
        return _whitelists[_address];
    }
}

