pragma solidity ^0.8.11;
// SPDX-License-Identifier: MIT
// @title BEP20
// @created_by  AvalonBC

abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

library Math {
  function add(uint256 a, uint256 b) internal pure returns (uint256) {
      uint256 c = a + b;
      require(c >= a, "Addition overflow");
      return c;
    }

    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
      require(b <= a, "Subtraction overflow");
      uint256 c = a - b;
      return c;
    }

    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        if (a == 0) {
            return 0;
        }
        uint256 c = a * b;
        require(c / a == b, "Multiplication overflow");
        return c;
    }
}

abstract contract Ownable is Context {
    address private _owner;
    address private _newOwner;

    event OwnerShipTransferred(address indexed oldOwner, address indexed newOwner);

    constructor() {
        _owner = _msgSender();
        _newOwner = _msgSender();
        emit OwnerShipTransferred(address(0), _msgSender());
    }

    function owner() public view virtual returns (address) {
        return _owner;
    }

    function newOwner() public view virtual returns (address) {
        return _newOwner;
    }

    modifier onlyOwner() {
        require(owner() == _msgSender(), "You are not the owner");
        _;
    }

    function transferOwnership(address newOwner_) public virtual onlyOwner{
        require(newOwner_ != address(0), "Invalid address");
        _newOwner = newOwner_;
    }

    function acceptOwnership() public virtual{
        require(newOwner() == _msgSender(), "You are not the new owner");
        _transferOwnership(newOwner());
    }

     function _transferOwnership(address newOwner_) internal virtual{
        emit OwnerShipTransferred(owner(), newOwner_);
        _owner = newOwner_;
    }

}

interface IBEPC20 {
    function name() external view returns (string memory);
    function symbol() external view returns (string memory);
    function decimals() external view returns (uint8);
    function totalSupply() external view returns (uint256);
    
    function getOwner() external view returns (address);
    function balanceOf(address account) external view returns (uint256);
    function transfer(address recipient, uint256 amount) external returns (bool);
    function allowance(address owner, address spender) external view returns (uint256);
    function approve(address spender, uint256 amount) external returns (bool);
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);

    event Transfer(address indexed from, address indexed to, uint256 value);
    event Approval(address indexed owner, address indexed spender, uint256 value);

}

contract BEP20Token is Context, IBEPC20, Ownable{
    using Math for uint256;

    mapping(address => uint256) private _balances;
    mapping(address => mapping(address => uint256)) private _allowances;

    string private _name;
    string private _symbol;
    uint8 private _decimals;
    uint256 private _totalSupply;

    constructor (string memory name_, string memory symbol_, uint256 totalSupply_, uint8 decimals_)  {
        _name = name_;
        _symbol = symbol_;
        _totalSupply = totalSupply_.mul(10 ** decimals_);
        _decimals = decimals_;
        _balances[_msgSender()] = _balances[_msgSender()].add(_totalSupply);
        emit Transfer(address(0), _msgSender(), _totalSupply);
    }

    function getOwner() external override view returns (address) {
        return owner();
    }
  
    function name() public view virtual override returns (string memory) {
        return _name;
    }
    
    function symbol() public view virtual override returns (string memory) {
        return _symbol;
    }
    
    function decimals() public view virtual override returns (uint8) {
        return _decimals;
    }
    
    function totalSupply() public view virtual override returns (uint256) {
        return _totalSupply;
    }
    
    function balanceOf(address account) public view virtual override returns (uint256) {
        return _balances[account];
    }

    function transfer(address to, uint256 value) public virtual override returns (bool) {
        _transfer(_msgSender(), to, value);
        return true;
    }

    function allowance(address owner, address spender) public view virtual override returns (uint256) {
        return _allowances[owner][spender];
    }

    function approve(address spender, uint256 value) public virtual override returns (bool) {
        _approve(_msgSender(), spender, value);
        return true;
    }
    
    function transferFrom(address sender, address recipient, uint256 value) public virtual override returns (bool) {
        _transfer(sender, recipient, value);
        _approve(sender, _msgSender(), _allowances[sender][_msgSender()].sub(value));
        return true;
    }

    function _transfer(address from_, address to_, uint256 amount_) internal virtual{
        require(from_ != address(0), "Sender Invalid address");
        require(to_ != address(0), "Recipient Invalid Address");
        _balances[from_] = _balances[from_].sub(amount_);
        _balances[to_] = _balances[to_].add(amount_);
        emit Transfer(from_, to_, amount_);
    }

    function _approve(address owner_, address spender_, uint256 amount_) internal virtual{
        require(owner_ != address(0), "Approve from the zero address");
        require(spender_ != address(0), "Approve to the zero address");
        _allowances[owner_][spender_] = amount_;
        emit Approval(owner_, spender_, amount_);
    }

    function decreaseAllowance(address spender, uint256 subtractedValue) public virtual returns (bool) {
        _approve(_msgSender(), spender, _allowances[_msgSender()][spender].sub(subtractedValue));
        return true;
    }

    function increaseAllowance(address spender, uint256 addedValue) public virtual returns (bool) {
        _approve(_msgSender(), spender, _allowances[_msgSender()][spender].add(addedValue));
        return true;
    }

    function burn(uint256 amount_) public virtual onlyOwner{
        _balances[ _msgSender()] = _balances[ _msgSender()].sub(amount_);
        _totalSupply = _totalSupply.sub(amount_);
        emit Transfer(_msgSender(), address(0), amount_);
    }

}