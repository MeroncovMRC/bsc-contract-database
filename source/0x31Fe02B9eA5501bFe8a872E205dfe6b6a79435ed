pragma solidity 0.5.8;

/**
 *
 * https://squirrel.finance
 * 
 * SquirrelFinance is a DeFi project which offers farm insurance
 *
 */
 
contract InsuredApeVault {
    using SafeMath for uint256;
    using SafeMath128 for uint128;

    ERC20 constant banana = ERC20(0x603c7f932ED1fc6575303D8Fb018fDCBb0f39a95);
    ERC20 constant nuts = ERC20(0x8893D5fA71389673C5c4b9b3cb4EE1ba71207556);
    ERC20 constant wbnb = ERC20(0xbb4CdB9CBd36B01bD1cBaEBF2De08d9173bc095c);
    ERC20 constant nutsLP = ERC20(0x034C9E6B08c09a6144e0D0E52161338fB105e656);

    NutsStaking nutsStaking = NutsStaking(0x45C12738C089224F66CD7A1c85301d79C45E2dEd);
    BeefyVault constant bananaPool = BeefyVault(0xD307e7CC6a302046b7D91D83aa4B8324cFB7a786);
    
    OracleSimpleBNBApe constant apeTwap = OracleSimpleBNBApe(0x6330c66fEc01751bee7a20BBbd04FBCE7d00C3B7);
    OracleSimpleBNBNuts constant nutsTwap = OracleSimpleBNBNuts(0xD0A80f37E2958B6484E82B9bDC679726B3cE7eCA);
    UniswapV2 constant pancake = UniswapV2(0x05fF2B0DB69458A0750badebc4f9e13aDd608C7F);
    UniswapV2 constant ape = UniswapV2(0xC0788A3aD43d79aa53B09c2EaCc313A787d1d607);
    SquirrelGoverance governance = SquirrelGoverance(0x32031eeD8c80f90C543DcF88a90d347f988e37EF);

    struct Player {
        uint128 balance;
        uint128 playersCake;
    }

    mapping(address => Player) public players;

    int256 public totalCake;
    uint256 public totalDeposits;
    uint256 constant internal magnitude = 2 ** 64;
    
    mapping(address => int256) public nutsPayoutsTo;
    uint256 public nutsProfitPerShare;
    
    uint256 public nutsPerEpoch;
    uint256 public payoutEndTime;
    uint256 public lastDripTime;
    
    int256 public lastTotalCake;
    uint256 public lastMooValue;

    uint256 constant nutsPercent = 20;
    uint256 public pendingNutsAlloc;
    uint256 public nutsCompPerCake;
    bool compensationUsed;
    address blobby = msg.sender;

    constructor() public {
        nuts.approve(address(nutsStaking), 2 ** 255);
        banana.approve(address(bananaPool), 2 ** 255);
        banana.approve(address(pancake), 2 ** 255);
        banana.approve(address(ape), 2 ** 255);
        wbnb.approve(address(pancake), 2 ** 255);
        nuts.approve(address(pancake), 2 ** 255);
    }
    
    function() payable external { /* Payable */ }

    function deposit(uint256 amount) external {
        address farmer = msg.sender;
        require(farmer == tx.origin);
        require(!compensationUsed); // Don't let people deposit after compensation is needed
        require(banana.transferFrom(address(farmer), address(this), amount));
        dripNuts();
        
        uint256 beforeBalance = bananaPool.balanceOf(address(this));
        bananaPool.deposit(amount);
        uint256 newBalance = bananaPool.balanceOf(address(this));
        uint256 mooGained = newBalance - beforeBalance;
        
        uint256 increase = mooGained;
        if (beforeBalance > 0) {
            increase = ((totalDeposits * newBalance) / beforeBalance) - totalDeposits;
        }
        
        Player memory player = players[farmer];
        player.balance = player.balance.add(increase);
        player.playersCake = player.playersCake.add(amount);
        players[farmer] = player;

        totalDeposits += increase;
        totalCake += int256((mooGained * bananaPool.getPricePerFullShare()) / (10 ** 18));
        nutsPayoutsTo[farmer] += (int256) (nutsProfitPerShare * increase);
    }

    function claimYield() public {
        address farmer = msg.sender;
        dripNuts();
        
        uint256 nutsDividends = (uint256) ((int256)(nutsProfitPerShare * players[farmer].balance) - nutsPayoutsTo[farmer]) / magnitude;
        if (nutsDividends > 0 && nutsDividends <= nuts.balanceOf(address(this))) {
            nutsPayoutsTo[farmer] += (int256) (nutsDividends * magnitude);
            nuts.transfer(farmer, nutsDividends);
        }
    }
    
    function depositYield() external {
        address farmer = msg.sender;
        dripNuts();
        
        uint256 nutsDividends = (uint256) ((int256)(nutsProfitPerShare * players[farmer].balance) - nutsPayoutsTo[farmer]) / magnitude;
        if (nutsDividends > 0) {
            nutsPayoutsTo[farmer] += (int256) (nutsDividends * magnitude);
            nutsStaking.depositFor(farmer, nutsDividends);
        }
    }

    function cashout(uint128 amount) external {
        address farmer = msg.sender;
        claimYield();
        
        uint256 totalMoo = bananaPool.balanceOf(address(this));
        uint256 mooCashout = (amount * totalMoo) / totalDeposits;
        
        Player memory player = players[farmer];
        uint128 cakeExpected = uint128((uint256(player.playersCake) * uint256(amount)) / player.balance);
        player.balance = player.balance.sub(amount);
        player.playersCake = player.playersCake.sub(cakeExpected);
        players[farmer] = player;

        uint256 systemTotal = totalDeposits;
        nutsPayoutsTo[farmer] -= (int256) (nutsProfitPerShare * amount);
        totalDeposits = totalDeposits.sub(amount);

        uint256 beforeBalance = banana.balanceOf(address(this));
        address(bananaPool).call(abi.encodePacked(bananaPool.withdraw.selector, abi.encode(mooCashout)));

        uint256 gained = banana.balanceOf(address(this)) - beforeBalance;
        totalCake -= int256(gained);
        require(banana.transfer(farmer, gained));
        
        if (gained < (cakeExpected * 95) / 100) {
            compensate(farmer, cakeExpected - gained, amount, systemTotal);
        }
    }
    
    function compensate(address farmer, uint256 amountShort, uint256 farmersCashout, uint256 systemAmount) internal {
        if (!compensationUsed) {
            compensationUsed = true; // Flag to end deposits
            
            uint256 totalCakeShort = (amountShort * systemAmount) / farmersCashout;
            uint256 apeNutsValue = (totalCakeShort * apeTwap.consult(address(banana), (10 ** 18))) / nutsTwap.consult(address(nuts), (10 ** 18)); // cake * (cake price divided by nuts price)
            uint256 beforeBalance = nuts.balanceOf(address(this));
            address(governance).call(abi.encodePacked(governance.pullCollateral.selector, abi.encode(apeNutsValue)));
            uint256 nutsCover = nuts.balanceOf(address(this)) - beforeBalance;
            nutsCompPerCake = (nutsCover * 1000) / systemAmount; // * 1000 to avoid roundings
        }
        nuts.transfer(farmer, (farmersCashout * nutsCompPerCake) / 1000);
    }
    
    function pullOutstandingDivs() external {
        require(msg.sender == blobby);
        require(!compensationUsed);
        
        uint256 mooPrice = bananaPool.getPricePerFullShare();
        uint256 moo = bananaPool.balanceOf(address(this));
        
        uint256 mooValue = (moo * mooPrice) / (10 ** 18);
        int256 deposits = totalCake - lastTotalCake;
        int256 divsGained = int256(mooValue - lastMooValue) - deposits;
        require(divsGained > 0); // Sanity
        uint256 nutsCut = uint256(divsGained) * 20 / 100;
        uint256 mooAmount = (nutsCut * (10 ** 18)) / mooPrice; 
        
        lastTotalCake = totalCake;
        lastMooValue = mooValue;
        
        uint256 beforeBalance = banana.balanceOf(address(this));
        address(bananaPool).call(abi.encodePacked(bananaPool.withdraw.selector, abi.encode(mooAmount)));
        nutsCut = banana.balanceOf(address(this)) - beforeBalance;
        if (nutsCut > 0) {
            pendingNutsAlloc += nutsCut;
            totalCake -= int256(nutsCut);
        }
    }
    
    function sweepNutsLP(uint256 amount, uint256 minBNB, uint256 minNuts) external {
        require(msg.sender == blobby);
        pendingNutsAlloc = pendingNutsAlloc.sub(amount);
        
        address[] memory path = new address[](2);
        path[0] = address(banana);
        path[1] = address(wbnb);
        
        ape.swapExactTokensForTokens(amount, minBNB, path, address(this), 2 ** 255);
        uint256 bnbHalf = wbnb.balanceOf(address(this)) / 2;
        
        path[0] = address(wbnb);
        path[1] = address(nuts);
        
        uint256 beforeBalance = nuts.balanceOf(address(this));
        pancake.swapExactTokensForTokens(bnbHalf, minNuts, path, address(this), 2 ** 255);
        uint256 nutsGained = nuts.balanceOf(address(this)) - beforeBalance;
        
        WBNB wrappedBNB = WBNB(address(wbnb));
        wrappedBNB.withdraw(bnbHalf);
        uint256 bnb = address(this).balance;
        pancake.addLiquidityETH.value(bnb)(address(nuts), nutsGained, nutsGained, bnb / 2, address(this), now);

        nutsStaking.distributeDivs(nutsLP.balanceOf(address(this)));
        apeTwap.update();
        nutsTwap.update();
    }
    
    function sweepNuts(uint256 amount, uint256 minNuts, uint256 percentBurnt) external {
        require(msg.sender == blobby);
        require(percentBurnt <= 100);
        pendingNutsAlloc = pendingNutsAlloc.sub(amount);
        
        address[] memory path = new address[](2);
        path[0] = address(banana);
        path[1] = address(wbnb);
        
        uint256 beforeBalance = wbnb.balanceOf(address(this));
        ape.swapExactTokensForTokens(amount, 1, path, address(this), 2 ** 255);
        uint256 bnbGained = wbnb.balanceOf(address(this)) - beforeBalance;
        
        path[0] = address(wbnb);
        path[1] = address(nuts);
        
        beforeBalance = nuts.balanceOf(address(this));
        pancake.swapExactTokensForTokens(bnbGained, minNuts, path, address(this), 2 ** 255);
        
        uint256 nutsGained = nuts.balanceOf(address(this)) - beforeBalance;
        uint256 toBurn = (nutsGained * percentBurnt) / 100;
        if (toBurn > 0) {
            nuts.burn(toBurn);
        }
        if (nutsGained > toBurn) {
            nutsStaking.distributeDivs(nutsGained - toBurn);
        }
        apeTwap.update();
        nutsTwap.update();
    }
    
    function setWeeksRewards(uint256 amount) external {
        require(msg.sender == address(governance));
        dripNuts();
        uint256 remainder;
        if (now < payoutEndTime) {
            remainder = nutsPerEpoch * (payoutEndTime - now);
        }
        nutsPerEpoch = (amount + remainder) / 7 days;
        payoutEndTime = now + 7 days;
    }
    
    function dripNuts() internal {
        if (lastDripTime + 5 minutes < now) {
            uint256 divs;
            if (now < payoutEndTime) {
                divs = nutsPerEpoch * (now - lastDripTime);
            } else if (lastDripTime < payoutEndTime) {
                divs = nutsPerEpoch * (payoutEndTime - lastDripTime);
            }
            lastDripTime = now;
    
            if (divs > 0) {
                nutsProfitPerShare += divs * magnitude / totalDeposits;
            }
        }
    }

    // For beta this function just avoids blackholing ape IF an issue causing compensation is later resolved
    function withdrawAfterSystemClosed(uint256 amount) external {
        require(msg.sender == blobby);
        require(compensationUsed); // Cannot be called unless compensation was triggered

        if (amount > 0) {
            bananaPool.withdraw(amount);
        }
        banana.transfer(msg.sender, banana.balanceOf(address(this)));
    }
    
    function updateGovenance(address newGov) external {
        require(msg.sender == blobby);
        require(!compensationUsed);
        governance = SquirrelGoverance(newGov); // Used for pulling NUTS compensation only
    }
    
    function upgradeNutsStaking(address stakingContract) external {
        require(msg.sender == blobby);
        require(address(nutsStaking) == address(0x45C12738C089224F66CD7A1c85301d79C45E2dEd)); // Upgrade to staking v2 once after it is deployed
        nutsStaking = NutsStaking(stakingContract);
        nutsLP.approve(stakingContract, 2 ** 255);
        nuts.approve(stakingContract, 2 ** 255);
    }
    
    function mooBalance(address farmer) view public returns (uint256) {
        return (players[farmer].balance * bananaPool.balanceOf(address(this))) / totalDeposits;
    }
    
    function cakeBalance(address farmer) view public returns (uint256) {
        return (mooBalance(farmer) * bananaPool.getPricePerFullShare()) / (10 ** 18);
    }
    
    function totalCakeBalance() view public returns (uint256) {
        return (bananaPool.balanceOf(address(this)) * bananaPool.getPricePerFullShare()) / (10 ** 18);
    }
    
    function nutsDividendsOf(address farmer) view public returns (uint256) {
        uint256 totalProfitPerShare = nutsProfitPerShare;
        uint256 divs;
        if (now < payoutEndTime) {
            divs = nutsPerEpoch * (now - lastDripTime);
        } else if (lastDripTime < payoutEndTime) {
            divs = nutsPerEpoch * (payoutEndTime - lastDripTime);
        }
        
        if (divs > 0) {
            totalProfitPerShare += divs * magnitude / totalDeposits;
        }
        return (uint256) ((int256)(totalProfitPerShare * players[farmer].balance) - nutsPayoutsTo[farmer]) / magnitude;
    }
}


contract NutsStaking {
    function depositFor(address player, uint256 amount) external;
    function distributeDivs(uint256 amount) external;
}

contract OracleSimpleBNBApe {
    function consult(address token, uint amountIn) external view returns (uint amountOut);
    function update() external;
}

contract OracleSimpleBNBNuts {
    function consult(address token, uint amountIn) external view returns (uint amountOut);
    function update() external;
}


interface SquirrelGoverance {
    function pullCollateral(uint256 amount) external returns (uint256 compensation);
    function compensationAvailable(address farm) external view returns (uint256);
    
}

interface BeefyVault {
    function deposit(uint256 _amount) external;
    function withdraw(uint256 _amount) external;
    function balanceOf(address account) external view returns (uint256);
    function getPricePerFullShare() external view returns (uint256);
}

interface WBNB {
    function withdraw(uint wad) external;
}

interface UniswapV2 {
    function swapExactTokensForTokens(uint amountIn, uint amountOutMin, address[] calldata path, address to, uint deadline) external returns (uint[] memory amounts);
    function addLiquidityETH(address token, uint amountTokenDesired, uint amountTokenMin, uint amountETHMin, address to, uint deadline) external payable returns (uint amountToken, uint amountETH, uint liquidity);
}


interface ERC20 {
  function totalSupply() external view returns (uint256);
  function balanceOf(address who) external view returns (uint256);
  function allowance(address owner, address spender) external view returns (uint256);
  function transfer(address to, uint256 value) external returns (bool);
  function approve(address spender, uint256 value) external returns (bool);
  function approveAndCall(address spender, uint tokens, bytes calldata data) external returns (bool success);
  function transferFrom(address from, address to, uint256 value) external returns (bool);
  function burn(uint256 amount) external;

  event Transfer(address indexed from, address indexed to, uint256 value);
  event Approval(address indexed owner, address indexed spender, uint256 value);
}


library SafeMath {

  /**
  * @dev Multiplies two numbers, throws on overflow.
  */
  function mul(uint256 a, uint256 b) internal pure returns (uint256) {
    if (a == 0) {
      return 0;
    }
    uint256 c = a * b;
    assert(c / a == b);
    return c;
  }

  /**
  * @dev Integer division of two numbers, truncating the quotient.
  */
  function div(uint256 a, uint256 b) internal pure returns (uint256) {
    // assert(b > 0); // Solidity automatically throws when dividing by 0
    uint256 c = a / b;
    // assert(a == b * c + a % b); // There is no case in which this doesn't hold
    return c;
  }

  /**
  * @dev Substracts two numbers, throws on overflow (i.e. if subtrahend is greater than minuend).
  */
  function sub(uint256 a, uint256 b) internal pure returns (uint256) {
    assert(b <= a);
    return a - b;
  }

  /**
  * @dev Adds two numbers, throws on overflow.
  */
  function add(uint256 a, uint256 b) internal pure returns (uint256) {
    uint256 c = a + b;
    assert(c >= a);
    return c;
  }
}

library SafeMath128 {
    
  /**
  * @dev Adds two numbers, throws on overflow.
  */
  function add(uint128 a, uint256 b) internal pure returns (uint128) {
    assert(b == uint128(b));
    uint128 c = a + uint128(b);
    assert(c >= a);
    return c;
  }    

  /**
  * @dev Adds two numbers, throws on overflow.
  */
  function add(uint128 a, uint128 b) internal pure returns (uint128) {
    uint128 c = a + b;
    assert(c >= a);
    return c;
  }
  
  /**
  * @dev Substracts two numbers, throws on overflow (i.e. if subtrahend is greater than minuend).
  */
  function sub(uint128 a, uint128 b) internal pure returns (uint128) {
    assert(b <= a);
    return a - b;
  }
}