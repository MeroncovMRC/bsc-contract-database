
// File: contracts/RDB.sol

// SPDX-License-Identifier: Unlicensed
pragma solidity 0.8.19;
import "@uniswap/v2-periphery/contracts/interfaces/IUniswapV2Router02.sol";
import "@uniswap/v2-core/contracts/interfaces/IUniswapV2Factory.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/utils/Address.sol";
import "@openzeppelin/contracts/utils/math/SafeMath.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";

// file: pinkBotInterface.sol
interface IPinkAntiBot {
    function setTokenOwner(address owner) external;

    function onPreTransferCheck(
        address from,
        address to,
        uint256 amount
    ) external;
}

// file: RDB.sol
/**
 * @title RDB Token
 * @dev Implementation of the RDB Token
 */
contract RDB is Context, IERC20, Ownable {
    using SafeMath for uint256;
    using Address for address;

    mapping(address => uint256) private _rOwned;
    mapping(address => uint256) private _tOwned;
    mapping(address => mapping(address => uint256)) private _allowances;


    mapping(address => bool) private _isExcludedFromFee;
    mapping(address => bool) private _isExcluded;
    bool public isTradingEnabled;
    bool public isPinkBotEnabled;

    address[] private _excluded;

    uint256 private constant MAX = ~uint256(0);
    uint256 private constant _tTotal = 100000000 * 1e9; // 100 million supply
    uint256 private _rTotal = (MAX - (MAX % _tTotal));
    uint256 private _tFeeTotal;
    uint256 public maxWallet = 1000000 * 1e9; // 1 million (1% of the supply)



    string private constant _name = "RDB Token";
    string private constant _symbol = "RDB";
    uint8 private constant _decimals = 9;

    struct BuyFee {
        uint16 marketingFee;
        uint16 reflectionFee;
        uint16 developmentFee;
        uint16 founderFee;
    }

    struct SellFee {
        uint16 marketingFee;
        uint16 reflectionFee;
        uint16 developmentFee;
        uint16 founderFee;
    }

    BuyFee public buyFee;
    SellFee public sellFee;

    uint16 private _reflectionFee;
    uint16 private _marketingFee;
    uint16 private _developmentFee;
    uint16 private _founderFee;

    IUniswapV2Router02 public uniswapV2Router;
    address public uniswapV2Pair;
    address public founderWallet;
    address public developmentWallet;
    address public marketingWallet;
    address public presaleAddress;

    IPinkAntiBot public pinkAntiBot;


    bool internal inSwapAndLiquify;
    bool public swapAndLiquifyEnabled = true;

    uint256 private swapThreshold = 1000 * 1e9;


    event MinTokensBeforeSwapUpdated(uint256 minTokensBeforeSwap);
    event SwapAndLiquifyEnabledUpdated(bool enabled);
    event SwapAndLiquify(
        uint256 tokensSwapped,
        uint256 ethReceived
    );
    event MaxWalletUpdated(uint256 amount);
    event BuyFeesUpdated (uint256 marketingFee, uint256 ReflectionFee, uint256 developmentFee, uint256 FounderFee);
    event SellFeesUpdated (uint256 marketingFee, uint256 ReflectionFee, uint256 developmentFee, uint256 FounderFee);
    event TokensClaimed (address indexed token, uint256 amount);
    event BNBClaimed (uint256 amount);

    event RouterUpdated(address indexed oldRouter, address indexed newRouter);

    modifier lockTheSwap() {
        inSwapAndLiquify = true;
        _;
        inSwapAndLiquify = false;
    }

    /**
     * @dev Contract constructor function. Initializes contract state variables and sets the owner of the contract.
     * The constructor sets the initial supply, fee rates, and creates a Uniswap pair for the token.
     * It also sets the addresses for the marketing, development, and founder wallets.
     * Finally, it excludes the owner and the contract itself from fees, and emits a `Transfer` event.
     */
    constructor() {
        _rOwned[_msgSender()] = _rTotal;

        // Set fee rates
        buyFee.reflectionFee = 1;
        buyFee.marketingFee = 1;
        buyFee.developmentFee =1;
        buyFee.founderFee = 1;

        sellFee.reflectionFee = 2;
        sellFee.marketingFee = 2;
        sellFee.developmentFee = 2;
        sellFee.founderFee = 2;

        // Creating an instance of the PinkAntiBot variable from the given address
        //https://github.com/pinkmoonfinance/pink-antibot-guide
        pinkAntiBot = IPinkAntiBot(0x8EFDb3b642eb2a20607ffe0A56CFefF6a95Df002);
        pinkAntiBot.setTokenOwner(msg.sender);

        IUniswapV2Router02 _uniswapV2Router = IUniswapV2Router02(
            0x10ED43C718714eb63d5aA57B78B54704E256024E // pancakeswap Router
        );
        // Create a uniswap pair for this new token
        uniswapV2Pair = IUniswapV2Factory(_uniswapV2Router.factory())
            .createPair(address(this), _uniswapV2Router.WETH());

        // set the rest of the contract variables
        uniswapV2Router = _uniswapV2Router;

        // set your wallets below

        founderWallet = address(0xc7f72fEf9B09503cEf0c26188D714403dce71E7E);
        developmentWallet = address(0x269F5B5A349DDD46126a6AFfD1936b776F55162F);
        marketingWallet = address(0xE0747a60E81B616990cEbF791602e41571755D6c);
        //exclude owner and this contract from fee
        _isExcludedFromFee[owner()] = true;
        _isExcludedFromFee[address(this)] = true;


        emit Transfer(address(0), _msgSender(), _tTotal );

    }

    /**
     * @dev Returns the token name.
     */
    function name() public pure returns (string memory) {
        return _name;
    }

    /**
     * @dev Returns the token symbol.
     */
    function symbol() public pure returns (string memory) {
        return _symbol;
    }

    /**
     * @dev Returns the token decimals.
     */
    function decimals() public pure returns (uint8) {
        return _decimals;
    }

    /**
     * @dev Returns the total token supply.
     */
    function totalSupply() public pure override returns (uint256) {
        return _tTotal;
    }

    /**
     * @dev Returns the token balance of the given `account`.
     */
    function balanceOf(address account) public view override returns (uint256) {
        if (_isExcluded[account]) return _tOwned[account];
        return tokenFromReflection(_rOwned[account]);
    }

    /**
     * @dev Returns the token balance of the given `account`.
     */
    function transfer(address recipient, uint256 amount)
    public
    override
    returns (bool)
    {
        _transfer(_msgSender(), recipient, amount);
        return true;
    }

    /**
     * @dev Returns the token allowance for the given `owner` and `spender`.
     */
    function allowance(address tokenOwner, address spender)
    public
    view
    override
    returns (uint256)
    {
        return _allowances[tokenOwner][spender];
    }

    /**
     * @dev Sets the allowance to `amount` for the `spender` address.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function approve(address spender, uint256 amount)
    public
    override
    returns (bool)
    {
        _approve(_msgSender(), spender, amount);
        return true;
    }

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * This function emits a {Transfer} event and an {Approval} event indicating
     * the updated allowance.
     *
     * Requirements:
     * - `sender` and `recipient` cannot be the zero address.
     * - `sender` must have a balance of at least `amount`.
     * - the caller must have allowance for `sender`'s tokens of at least `amount`.
     *
     * @param sender the address of the sender.
     * @param recipient the address of the recipient.
     * @param amount the amount of tokens to be transferred.
     * @return a boolean value indicating whether the operation succeeded.
     */
    function transferFrom(address sender, address recipient, uint256 amount)
    public
    override
    returns (bool)
    {
        _transfer(sender, recipient, amount);
        _approve(
            sender,
            _msgSender(),
            _allowances[sender][_msgSender()].sub(
                amount,
                "ERC20: transfer amount exceeds allowance"
            )
        );
        return true;
    }

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function increaseAllowance(address spender, uint256 addedValue)
    public
    virtual
    returns (bool)
    {
        _approve(
            _msgSender(),
            spender,
            _allowances[_msgSender()][spender].add(addedValue)
        );
        return true;
    }

    /**
    * @dev Decreases the allowance granted by the caller to `spender`.
    * This is an alternative to {approve} that can be used as a mitigation for
    * problems described in {IERC20-approve}.
    * Emits an {Approval} event indicating the updated allowance.
    * Requirements:
    * - `spender` cannot be the zero address.
    * - `spender` must have allowance for the caller of at least
    * `subtractedValue`.
    * @param spender The address which will spend the funds.
    * @param subtractedValue The amount of allowance to decrease.
    * @return A boolean value indicating whether the operation succeeded.
    */
    function decreaseAllowance(address spender, uint256 subtractedValue)
    public
    virtual
    returns (bool)
    {
        _approve(
            _msgSender(),
            spender,
            _allowances[_msgSender()][spender].sub(
                subtractedValue,
                "ERC20: decreased allowance below zero"
            )
        );
        return true;
    }

    /**
     * @dev Transfer tokens to the contract itself, which will effectively remove the tokens from circulation.
     * This will increase the rate at which all holders will earn rewards.
     * @param tAmount The amount of tokens to be delivered (removed from circulation).
     */
    function deliver(uint256 tAmount) public {
        address sender = _msgSender();
        require(
            !_isExcluded[sender],
            "Excluded addresses cannot call this function"
        );
        (uint256 rAmount, , , , , ) = _getValues(tAmount);
        _rOwned[sender] = _rOwned[sender].sub(rAmount);
        _rTotal = _rTotal.sub(rAmount);
        _tFeeTotal = _tFeeTotal.add(tAmount);
    }

    /**
     * @dev Returns the reflection amount of given token amount
     *
     * This function is used to get the reflection amount from a given token amount with the option to deduct the transfer fee.
     * It's a view function meaning it will not modify the state of the blockchain. It will only read the values.
     *
     * @param tAmount - the token amount to get the reflection from
     * @param deductTransferFee - a boolean value indicating whether the transfer fee should be deducted
     *
     * @return - the calculated reflection amount
     */
    function reflectionFromToken(uint256 tAmount, bool deductTransferFee)
    public
    view
    returns (uint256)
    {
        require(tAmount <= _tTotal, "Amount must be less than supply");
        if (!deductTransferFee) {
            (uint256 rAmount, , , , , ) = _getValues(tAmount);
            return rAmount;
        } else {
            (, uint256 rTransferAmount, , , , ) = _getValues(tAmount);
            return rTransferAmount;
        }
    }

    /**
    * @dev Converts reflection amount to token amount
    * @param rAmount amount of reflection
    * @return amount of tokens
    */
    function tokenFromReflection(uint256 rAmount)
    public
    view
    returns (uint256)
    {
        require(
            rAmount <= _rTotal,
            "Amount must be less than total reflections"
        );
        uint256 currentRate = _getRate();
        return rAmount.div(currentRate);
    }

    ///@dev update the router
    ///@param newAddress: owner can update the router address
    function updateRouter(address newAddress) external onlyOwner {
        require(newAddress != address(uniswapV2Router), "The router already has that address");
        require(newAddress != address(0), "zero address not allowed");
        address oldRouter = address(uniswapV2Router);
        uniswapV2Router = IUniswapV2Router02(newAddress);
        address get_pair =
                                IUniswapV2Factory(uniswapV2Router.factory()).getPair(address(this),
                uniswapV2Router.WETH());
        if (get_pair == address(0)) {
            uniswapV2Pair =
                                    IUniswapV2Factory(uniswapV2Router.factory()).createPair(address(this),
                    uniswapV2Router.WETH());
        } else {
            uniswapV2Pair = get_pair;
        }

        emit RouterUpdated(oldRouter, newAddress);
    }

    ///@dev enable or disbale pinkbot
    ///@param value: boolean value, true means enabled, false means disabled
    function managePinkBot (bool value) external onlyOwner {
        isPinkBotEnabled = value;
    }

    ///@dev set presale address (useful incase using pinksale or similiar platform for presale)
    ///@param _presaleAddress: presale address
    function setPresaleAddress(address _presaleAddress) external onlyOwner {
        require (_presaleAddress != address(0), "zero address not allowed");
        presaleAddress = _presaleAddress;
        _isExcludedFromFee[_presaleAddress] = true;
    }

    ///@dev update max wallet amount
    ///@param amount: new maxWallet amount, must be greator than equal to 1 percent of the supply
    function updateMaxWalletAmount (uint256 amount) external onlyOwner {
        require (amount >= totalSupply() /100,"amount must be 1% or more of the total supply");
        maxWallet = amount;
        emit MaxWalletUpdated (amount);
    }

    ///@notice Returns if an address is excluded or not from reflection
    function isExcludedFromReward(address account) public view returns (bool) {
        return _isExcluded[account];
    }

    ///@notice Returns total reflection distributed so far
    function totalFees() public view returns (uint256) {
        return _tFeeTotal;
    }

    ///@dev exclude a particular address from reward
    ///@param account: address to be excluded from reward
    function excludeFromReward(address account) public onlyOwner {
        require(!_isExcluded[account], "Account is already excluded");
        if (_rOwned[account] > 0) {
            _tOwned[account] = tokenFromReflection(_rOwned[account]);
        }
        _isExcluded[account] = true;
        _excluded.push(account);
    }

    ///@dev include a address in reward
    ///@param account: address to be added in reward mapping again
    function includeInReward (address account) external onlyOwner {
        require(_isExcluded [account], "Account is already excluded");
        for (uint256 i = 0; i < _excluded.length; i++) {

            if (_excluded[i] == account){
                //updating _rOwned to make sure the balances stay the same
                if (_tOwned [account] > 0) {
                    uint256 newrOwned = _tOwned [account].mul(_getRate());
                    _rTotal = _rTotal.sub(_rOwned [account]-newrOwned);
                    _rOwned[account] = newrOwned;
                }
                else{
                    _rOwned [account] = 0;
                }
                _tOwned[account]= 0;
                _excluded [i] = _excluded [_excluded.length-1];
                _isExcluded [account] = false;
                _excluded.pop();
                break;
            }
        }
    }

    ///@dev manage exclude and include fee
    ///@param account: account to be excluded or included
    ///@param excluded: boolean value, true means excluded, false means included
    function excludeFromFee(address account, bool excluded) external onlyOwner {
        require(account != presaleAddress, "Presale address cannot be included in fees again");
        if (excluded) {
            require(!_isExcludedFromFee[account], "already excluded");
        }
        _isExcludedFromFee[account] = excluded;
    }


    ///@dev set buy fees
    ///@param market: new marketing fee on buy
    ///@param reflection: new reflection fee on buy
    ///@param development: new development fee on buy
    ///@param founder: new founder fee on buy
    function setBuyFee(
        uint16 market,
        uint16 reflection,
        uint16 development,
        uint16 founder
    ) external onlyOwner {
        buyFee.marketingFee = market;
        buyFee.reflectionFee = reflection;
        buyFee.developmentFee = development;
        buyFee.founderFee = founder;
        uint256 totalBuyFee = market  + reflection + development + founder;
        require(totalBuyFee <= 10, "max buy fee limit is 10%");

        emit BuyFeesUpdated (market, reflection, development, founder);
    }

    ///@dev set sell fees
    ///@param market: new marketing fee on sell
    ///@param reflection: new reflection fee on sell
    ///@param development: new development fee on sell
    ///@param founder: new founder fee on sell
    function setSellFee(
        uint16 market,
        uint16 reflection,
        uint16 development,
        uint16 founder
    ) external onlyOwner {
        sellFee.marketingFee = market;
        sellFee.reflectionFee = reflection;
        sellFee.developmentFee = development;
        sellFee.founderFee = founder;
        uint256 totalSellFee = market  + reflection + development + founder;
        require(totalSellFee <= 10, "max sell fee limit is 10%");
        emit SellFeesUpdated (market, reflection, development, founder);
    }

    ///@dev set swap amount after which collected tax should be swappped for ether
    ///@param numTokens: new token amount
    function setSwapTokensAtAmount(uint256 numTokens)  external onlyOwner {
        swapThreshold = numTokens * 1e9;
        emit MinTokensBeforeSwapUpdated(numTokens);
    }


    ///@dev claim stucked tokens from contract
    ///@param _token: token address to be rescued
    function claimStuckTokens(address _token) external onlyOwner {
        require(_token != address(0), "Invalid token address");
        require(_token != address(this), "Can't claim native token");
        IERC20 erc20token = IERC20(_token);
        uint256 balance = erc20token.balanceOf(address(this));
        erc20token.transfer(owner(), balance);
        emit TokensClaimed(_token, balance);
    }

    ///@dev owner can claim any stucked BNB from contract
    function claimBNB() external onlyOwner {
        (bool sent,) = owner().call{value: address(this).balance}("");
        require (sent, "bnb transfer failed");
        emit BNBClaimed (address(this).balance);
    }

    //to recieve ETH from uniswapV2Router when swaping
    receive() external payable {
        this;
    }

    /**
     * @dev Internal function that handles how the fees are stored and reflected.
     * This method is called after fees are calculated and is used to reduce the total reflected amount and fee total.
     *
     * @param rFee The reflection fee amount in RType.
     * @param tFee The fee amount in TokenType.
     */
    function _reflectFee(uint256 rFee, uint256 tFee) private {
        _rTotal = _rTotal.sub(rFee);
        _tFeeTotal = _tFeeTotal.add(tFee);
    }

    /**
     * @dev Internal function to calculate reflection values.
     * @param tAmount The total amount of tokens.
     * @return rAmount The calculated reflection amount.
     * @return rTransferAmount The calculated transfer reflection amount.
     * @return rFee The calculated reflection fee.
     */
    function _getValues(uint256 tAmount)
    private
    view
    returns (
        uint256,
        uint256,
        uint256,
        uint256,
        uint256,
        uint256
    )
    {
        (
            uint256 tTransferAmount,
            uint256 tFee,
            uint256 tMarketing,
            uint256 tDevelopment,
            uint256 tFounder
        ) = _getTValues(tAmount);
        (uint256 rAmount, uint256 rTransferAmount, uint256 rFee) = _getRValues(
            tAmount,
            tFee,
            tMarketing,
            tDevelopment,
            tFounder,
            _getRate()
        );
        return (
            rAmount,
            rTransferAmount,
            rFee,
            tTransferAmount,
            tFee,
            tMarketing
        );
    }

    function _getTValues(uint256 tAmount)
    private
    view
    returns (
        uint256,
        uint256,
        uint256,
        uint256,
        uint256
    )
    {
        uint256 tFee = calculateReflectionFee(tAmount);
        uint256 tMarketing = calculateMarketingFee(tAmount);
        uint256 tDevelopment = calculateDevelopmentFee(tAmount);
        uint256 tFounder = calculateFounderFee(tAmount);
        uint256 tTransferAmount = tAmount.sub(tFee).sub(tMarketing);
        tTransferAmount = tTransferAmount.sub(tDevelopment).sub(tFounder);
        return (tTransferAmount, tFee, tMarketing, tDevelopment, tFounder);
    }

    function _getRValues(
        uint256 tAmount,
        uint256 tFee,
        uint256 tMarketing,
        uint256 tDevelopment,
        uint256 tFounder,
        uint256 currentRate
    )
    private
    pure
    returns (
        uint256,
        uint256,
        uint256
    )
    {
        uint256 rAmount = tAmount.mul(currentRate);
        uint256 rFee = tFee.mul(currentRate);
        uint256 rMarketing = tMarketing.mul(currentRate);
        uint256 rDevelopment = tDevelopment.mul(currentRate);
        uint256 rFounder = tFounder.mul(currentRate);
        uint256 rTransferAmount = rAmount.sub(rFee).sub(rMarketing).sub(
            rDevelopment).sub(rFounder);
        return (rAmount, rTransferAmount, rFee);
    }

    /**
     * @dev Internal function that handles the transfer of fees to the contract address.
     * This method is called before fees are distributed and is used to store the fees in this contract.
     */
    function _getRate() private view returns (uint256) {
        (uint256 rSupply, uint256 tSupply) = _getCurrentSupply();
        return rSupply.div(tSupply);
    }

    /**
     * @dev Internal function that handles the transfer of fees to the contract address.
     * This method is called before fees are distributed and is used to store the fees in this contract.
     */
    function _getCurrentSupply() private view returns (uint256, uint256) {
        uint256 rSupply = _rTotal;
        uint256 tSupply = _tTotal;
        for (uint256 i = 0; i < _excluded.length; i++) {
            if (
                _rOwned[_excluded[i]] > rSupply ||
                _tOwned[_excluded[i]] > tSupply
            ) return (_rTotal, _tTotal);
            rSupply = rSupply.sub(_rOwned[_excluded[i]]);
            tSupply = tSupply.sub(_tOwned[_excluded[i]]);
        }
        if (rSupply < _rTotal.div(_tTotal)) return (_rTotal, _tTotal);
        return (rSupply, tSupply);
    }

    /**
     * @dev Internal function that handles how the marketing fee is taken from the transaction.
     * This is necessary to ensure the marketing wallet receives the correct number of tokens.
     * @param tMarketing The number of tokens to be taken as marketing fee from the transaction.
     */
    function _takeMarketing(uint256 tMarketing) private {
        uint256 currentRate = _getRate();
        uint256 rMarketing = tMarketing.mul(currentRate);
        _rOwned[address(this)] = _rOwned[address(this)].add(rMarketing);
        if (_isExcluded[address(this)])
            _tOwned[address(this)] = _tOwned[address(this)].add(tMarketing);

    }

    /**
     * @dev Internal function to handle the collection of development fees during transactions.
     * The collected development fees are added to the contract's balance.
     * @param tDevelopment The amount of tokens to be collected as development fees.
     */
    function _takeDevelopment(uint256 tDevelopment) private {
        uint256 currentRate = _getRate();
        uint256 rDevelopment = tDevelopment.mul(currentRate);

        _rOwned[address(this)] = _rOwned[address(this)].add(rDevelopment);
        if (_isExcluded[address(this)]) {
            _tOwned[address(this)] = _tOwned[address(this)].add(tDevelopment);
        }
    }

    function _takeFounder(uint256 tFounder) private {
        uint256 currentRate = _getRate();
        uint256 rFounder = tFounder.mul(currentRate);

        _rOwned[address(this)] = _rOwned[address(this)].add(rFounder);
        if (_isExcluded[address(this)]) {
            _tOwned[address(this)] = _tOwned[address(this)].add(tFounder);
        }
    }

    ///@notice enabled trading globally,
    ///can be called once and can never be turned off.
    function enableTrading () external onlyOwner {
        require (!isTradingEnabled, "trading is already live");
        isTradingEnabled = true;

    }

    ///@dev Calculates the reflection fee for a given amount.
    /// @param _amount The amount to calculate the reflection fee for.
    /// @return The calculated reflection fee.
    function calculateReflectionFee(uint256 _amount) private view returns (uint256) {
        return _amount.mul(_reflectionFee).div(10**2);
    }

    ///@dev Calculates the Marketing fee for a given amount.
    /// @param _amount The amount to calculate the marketing fee for.
    /// @return The calculated marketing fee.
    function calculateMarketingFee(uint256 _amount)
    private
    view
    returns (uint256)
    {
        return _amount.mul(_marketingFee).div(10**2);
    }

    ///@dev Calculates the Development fee for a given amount.
    /// @param _amount The amount to calculate the development fee for.
    /// @return The calculated development fee.
    function calculateDevelopmentFee(uint256 _amount)
    private
    view
    returns (uint256)
    {
        return _amount.mul(_developmentFee).div(10**2);
    }


    ///@dev Calculates the founder fee for a given amount.
    /// @param _amount The amount to calculate the founder fee for.
    /// @return The calculated founder fee.
    function calculateFounderFee(uint256 _amount) private view returns (uint256){
        return _amount.mul(_founderFee).div(10**2);
    }

    /**
     * @dev Internal function to reflect the transaction fee. This is done by reducing the total reflected supply (_rTotal) and increasing
     * the total fee (_tFeeTotal) by the amount of the reflected fee.
     */
    function removeAllFee() private {
        _reflectionFee = 0;
        _marketingFee = 0;
        _developmentFee = 0;
        _founderFee = 0;
    }

    /**
     * @dev Sets the fees for buy transactions.
     * The fees are set according to the `buyFee` struct values.
     * This function is called in the `_tokenTransfer()` function when a buy transaction is detected.
     */
    function setBuy() private {
        _reflectionFee = buyFee.reflectionFee;
        _marketingFee = buyFee.marketingFee;
        _developmentFee = buyFee.developmentFee;
        _founderFee = buyFee.founderFee;
    }

    /**
     * @dev This function sets the fees for selling transactions.
     * It is called whenever a sell is happening. The fees are set according to the sellFee structure.
     * The fees include reflectionFee, marketingFee, developmentFee and founderFee.
     * These fees are used to distribute rewards, cover operational costs, etc.
     */
    function setSell() private {
        _reflectionFee = sellFee.reflectionFee;
        _marketingFee = sellFee.marketingFee;
        _developmentFee = sellFee.developmentFee;
        _founderFee = sellFee.founderFee;
    }

    /**
     * @dev Function to check if an address is excluded from fee
     * @param account Address of the account to check
     * @return bool indicating whether the account is excluded from fee
     */
    function isExcludedFromFee(address account) public view returns (bool) {
        return _isExcludedFromFee[account];
    }

    /**
     * @dev Toggles the state of the swap and liquify feature of the contract.
     * When `swapAndLiquifyEnabled` is true, the contract will automatically swap and liquify tokens.
     * This is beneficial as it automatically adds liquidity and stabilizes the token price.
     * When `swapAndLiquifyEnabled` is false, the contract will not swap and liquify tokens.
     * This function can only be called by the owner of the contract.
     */
    function toggleSwapAndLiquify() external onlyOwner {
        swapAndLiquifyEnabled = !swapAndLiquifyEnabled;
    }

    /**
     * @dev Internal function that sets the approval amount for a given spender. `approve` and `increaseAllowance`
     * call this function to update the spender's allowance. It emits an `Approval` event.
     * @param tokenOwner The address of the token owner.
     * @param spender The address of the spender.
     * @param amount The amount of allowance.
     */
    function _approve(
        address tokenOwner,
        address spender,
        uint256 amount
    ) private {
        require(tokenOwner != address(0), "ERC20: approve from the zero address");
        require(spender != address(0), "ERC20: approve to the zero address");

        _allowances[tokenOwner][spender] = amount;
        emit Approval(tokenOwner, spender, amount);
    }

    /**
     * @dev A private function to handle token transfers. It contains conditions to check whether the transfer is to or from an excluded account,
     * whether it's a buy or sell operation and whether the max wallet limit has been exceeded.
     * @param from The account to transfer tokens from.
     * @param to The account to transfer tokens to.
     * @param amount The amount of tokens to transfer.
     */
    function _transfer(
        address from,
        address to,
        uint256 amount
    ) private {
        require(from != address(0), "ERC20: transfer from the zero address");
        require(to != address(0), "ERC20: transfer to the zero address");
        require(amount > 0, "Transfer amount must be greater than zero");

        if(from != owner() && to != owner()){
            require(isTradingEnabled, "Trading is not enabled yet");
        }

        if(from != owner() || from != presaleAddress){
            require (isTradingEnabled, "trading is not live");
        }

        // is the token balance of this contract address over the min number of
        // tokens that we need to initiate a swap + liquidity lock?
        // also, don't get caught in a circular liquidity event.
        // also, don't swap & liquify if sender is uniswap pair.
        uint256 contractTokenBalance = balanceOf(address(this));

        bool overMinTokenBalance = contractTokenBalance >=
                    swapThreshold;
        if (
            overMinTokenBalance &&
            !inSwapAndLiquify &&
            from != uniswapV2Pair &&
            swapAndLiquifyEnabled
        ) {

            swapAndLiquify(contractTokenBalance);
        }

        //indicates if fee should be deducted from transfer
        bool takeFee = true;

        //if any account belongs to _isExcludedFromFee account then remove the fee
        if (_isExcludedFromFee[from] || _isExcludedFromFee[to]) {
            takeFee = false;
        }

        //transfer amount, it will take reflection, liquidity fee
        _tokenTransfer(from, to, amount, takeFee);
    }

    /**
     * @dev Swaps and liquifies tokens whenever it reaches the threshold.
     * This function is used to automatically add liquidity and balance the price impact of large sales.
     * It swaps half of the tokens for BNB, then pairs it with the remaining half to add as a liquidity pair on PancakeSwap.
     * @param tokens The number of tokens to be swapped and liquified.
     */
    function swapAndLiquify(uint256 tokens) private lockTheSwap {
        uint256 oldBalance = address(this).balance;
        // swap tokens for ETH
        swapTokensForEth(tokens);
        // how much ETH did we just swap into?
        uint256 newBalance = address(this).balance.sub(oldBalance);
        uint256 divisor = buyFee.marketingFee + buyFee.developmentFee + buyFee.founderFee
            + sellFee.marketingFee + sellFee.developmentFee + sellFee.founderFee;

        uint256 marketingShare = newBalance.mul(buyFee.marketingFee + sellFee.marketingFee).div(divisor);
        uint256 developmentShare = newBalance.mul(buyFee.developmentFee + sellFee.developmentFee).div(divisor);
        (bool sent,) = marketingWallet.call{value: marketingShare}("");
        require(sent,"bnb transfer to marketing wallet failed");
        (bool sent1,) = developmentWallet.call{value: developmentShare}("");
        require(sent1,"bnb transfer to development wallet failed");
        (bool sent2,) = founderWallet.call{value: address(this).balance}("");
        require(sent2,"bnb transfer to founder wallet failed");

        emit SwapAndLiquify(tokens, newBalance);
    }

    /**
     * @dev Swaps tokens for BNB. This function is used for the swapAndLiquify function.
     * @param tokenAmount The number of tokens to be swapped.
     */
    function swapTokensForEth(uint256 tokenAmount) private {
        // generate the uniswap pair path of token -> weth
        address[] memory path = new address[](2);
        path[0] = address(this);
        path[1] = uniswapV2Router.WETH();

        _approve(address(this), address(uniswapV2Router), tokenAmount);

        // make the swap
        uniswapV2Router.swapExactTokensForETHSupportingFeeOnTransferTokens(
            tokenAmount,
            0, // accept any amount of BNB
            path,
            address(this),
            block.timestamp
        );
    }

    /**
     * @dev Swaps BNB for tokens which are then sent to a burn address to reduce supply.
     * This function is only callable by the contract owner.
     * @param bnbAmount The amount of BNB to swap for tokens.
     */
    function swapBNBforTokens (uint256 bnbAmount) private {
        address[] memory path = new address[](2);
        path[0] = uniswapV2Router.WETH();
        path[1] = address(this);

        // make the swap
        uniswapV2Router.swapExactETHForTokensSupportingFeeOnTransferTokens{value: bnbAmount}(
            0, // accept any amount of tokens
            path,
            address(0xdead),
            block.timestamp
        );
    }

    /**
     * @dev Updates the addresses of the fee wallets
     * @param marketing The new address for the marketing wallet
     * @param founder The new address for the founder wallet
     * @param development The new address for the development wallet
     *
     * Emits a `Transfer` event.
     *
     * Requirements:
     * - `marketing` must not be the zero address.
     * - `founder` must not be the zero address.
     * - `development` must not be the zero address.
     */
    function updateWallets (address marketing, address founder, address development) external onlyOwner {
        //zero address is not allowed
        require (marketing != address(0) && founder != address(0) && development != address(0), "error zero address");
        marketingWallet = marketing;
        founderWallet = founder;
        developmentWallet = development;

    }

    /**
     * @dev Allows the owner to buy tokens using BNB and send them to the 0xdead address (burn address).
     * This function is used to decrease the supply of tokens in circulation, effectively increasing the value of remaining tokens.
     * The amount of BNB used for the buyback is the amount of BNB sent to the contract by the owner.
     */
    function buybackAndBurn () public payable onlyOwner {
        swapBNBforTokens(msg.value);
    }

    /**
     * @dev Internal function that transfers the token from a sender to a recipient.
     * It can also conditionally apply fees to the transfer.
     * @param sender address: the sender address.
     * @param recipient address: the recipient address.
     * @param amount uint256: the amount of tokens to transfer.
     * @param takeFee bool: whether to apply fees to this transfer.
     */
    function _tokenTransfer(
        address sender,
        address recipient,
        uint256 amount,
        bool takeFee
    ) private {
        removeAllFee();

        if (takeFee) {
            if(isPinkBotEnabled){
                pinkAntiBot.onPreTransferCheck(sender, recipient, amount);
            }

            if (sender == uniswapV2Pair) {
                setBuy();

            }
            if (recipient == uniswapV2Pair) {
                setSell();
            }

            if( recipient != uniswapV2Pair){
                require (balanceOf(recipient) + amount <= maxWallet, "max wallet limit exceed");

            }
        }

        if (_isExcluded[sender] && !_isExcluded[recipient]) {
            _transferFromExcluded(sender, recipient, amount);
        } else if (!_isExcluded[sender] && _isExcluded[recipient]) {
            _transferToExcluded(sender, recipient, amount);
        } else if (!_isExcluded[sender] && !_isExcluded[recipient]) {
            _transferStandard(sender, recipient, amount);
        } else if (_isExcluded[sender] && _isExcluded[recipient]) {
            _transferBothExcluded(sender, recipient, amount);
        } else {
            _transferStandard(sender, recipient, amount);
        }
    }

    /**
     * @dev Standard transfer function called during a transaction.
     * Takes care of calculating values, updating balances and reflecting fees.
     *
     * @param sender - address initiating the transaction
     * @param recipient - address receiving the tokens
     * @param tAmount - amount of tokens being transferred
     */
    function _transferStandard(
        address sender,
        address recipient,
        uint256 tAmount
    ) private {
        (
            uint256 rAmount,
            uint256 rTransferAmount,
            uint256 rFee,
            uint256 tTransferAmount,
            uint256 tFee,
            uint256 tMarketing
        ) = _getValues(tAmount);
        _rOwned[sender] = _rOwned[sender].sub(rAmount);
        _rOwned[recipient] = _rOwned[recipient].add(rTransferAmount);
        _takeMarketing(tMarketing);
        _takeDevelopment(calculateDevelopmentFee(tAmount));
        _takeFounder(calculateFounderFee(tAmount));
        _reflectFee(rFee, tFee);
        emit Transfer(sender, recipient, tTransferAmount);
    }

    /**
     * @dev Transfer tokens from a non-excluded account to an excluded account, taking into account the reflection mechanism.
     * The sender's balance is reduced by the reflection amount of the tokens being sent.
     * The recipient's excluded balance is increased by the actual token amount.
     * The recipient's reflected balance is increased by the reflection amount of the tokens being sent.
     * Marketing, development and founder fees are taken from the transaction.
     * The total fees and reflection fees are reflected.
     * A Transfer event is emitted with the details of the transfer.
     *
     * @param sender The address of the account sending the tokens.
     * @param recipient The address of the account receiving the tokens.
     * @param tAmount The amount of tokens being sent.
     */
    function _transferToExcluded(
        address sender,
        address recipient,
        uint256 tAmount
    ) private {
        (
            uint256 rAmount,
            uint256 rTransferAmount,
            uint256 rFee,
            uint256 tTransferAmount,
            uint256 tFee,
            uint256 tMarketing
        ) = _getValues(tAmount);
        _rOwned[sender] = _rOwned[sender].sub(rAmount);
        _tOwned[recipient] = _tOwned[recipient].add(tTransferAmount);
        _rOwned[recipient] = _rOwned[recipient].add(rTransferAmount);
        _takeMarketing(tMarketing);
        _takeDevelopment(calculateDevelopmentFee(tAmount));
        _takeFounder(calculateFounderFee(tAmount));

        _reflectFee(rFee, tFee);
        emit Transfer(sender, recipient, tTransferAmount);
    }

    /**
     * @dev Transfers tokens from an excluded address to another address.
     * The function calculates the necessary values using the _getValues function,
     * reduces the sender's balance, updates the recipient's balance, takes the necessary fees,
     * reflects the fee, and emits a Transfer event.
     *
     * Requirements:
     * - `sender` must be an excluded address.
     * - `recipient` must not be the zero address.
     * - `tAmount` must be less than or equal to the `sender`'s token balance.
     *
     * @param sender the address to transfer from.
     * @param recipient the address to transfer to.
     * @param tAmount the amount of tokens to be transferred.
     */
    function _transferFromExcluded(
        address sender,
        address recipient,
        uint256 tAmount
    ) private {
        (
            uint256 rAmount,
            uint256 rTransferAmount,
            uint256 rFee,
            uint256 tTransferAmount,
            uint256 tFee,
            uint256 tMarketing
        ) = _getValues(tAmount);
        _tOwned[sender] = _tOwned[sender].sub(tAmount);
        _rOwned[sender] = _rOwned[sender].sub(rAmount);
        _rOwned[recipient] = _rOwned[recipient].add(rTransferAmount);
        _takeMarketing(tMarketing);
        _takeDevelopment(calculateDevelopmentFee(tAmount));
        _takeFounder(calculateFounderFee(tAmount));

        _reflectFee(rFee, tFee);
        emit Transfer(sender, recipient, tTransferAmount);
    }

    /**
     * @notice This function handles transfers between two addresses that are both excluded from fees.
     * @param sender The address initiating the transfer.
     * @param recipient The address receiving the transfer.
     * @param tAmount The amount of tokens to be transferred.
     */
    function _transferBothExcluded(
        address sender,
        address recipient,
        uint256 tAmount
    ) private {
        (
            uint256 rAmount,
            uint256 rTransferAmount,
            uint256 rFee,
            uint256 tTransferAmount,
            uint256 tFee,
            uint256 tMarketing
        ) = _getValues(tAmount);
        _tOwned[sender] = _tOwned[sender].sub(tAmount);
        _rOwned[sender] = _rOwned[sender].sub(rAmount);
        _tOwned[recipient] = _tOwned[recipient].add(tTransferAmount);
        _rOwned[recipient] = _rOwned[recipient].add(rTransferAmount);
        _takeMarketing(tMarketing);
        _takeDevelopment(calculateDevelopmentFee(tAmount));
        _takeFounder(calculateFounderFee(tAmount));

        _reflectFee(rFee, tFee);
        emit Transfer(sender, recipient, tTransferAmount);
    }
}


// File: @openzeppelin/contracts/token/ERC20/IERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address from, address to, uint256 amount) external returns (bool);
}


// File: @openzeppelin/contracts/utils/math/SafeMath.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (utils/math/SafeMath.sol)

pragma solidity ^0.8.0;

// CAUTION
// This version of SafeMath should only be used with Solidity 0.8 or later,
// because it relies on the compiler's built in overflow checks.

/**
 * @dev Wrappers over Solidity's arithmetic operations.
 *
 * NOTE: `SafeMath` is generally not needed starting with Solidity 0.8, since the compiler
 * now has built in overflow checking.
 */
library SafeMath {
    /**
     * @dev Returns the addition of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryAdd(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            uint256 c = a + b;
            if (c < a) return (false, 0);
            return (true, c);
        }
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function trySub(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b > a) return (false, 0);
            return (true, a - b);
        }
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryMul(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
            // benefit is lost if 'b' is also tested.
            // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522
            if (a == 0) return (true, 0);
            uint256 c = a * b;
            if (c / a != b) return (false, 0);
            return (true, c);
        }
    }

    /**
     * @dev Returns the division of two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryDiv(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b == 0) return (false, 0);
            return (true, a / b);
        }
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryMod(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b == 0) return (false, 0);
            return (true, a % b);
        }
    }

    /**
     * @dev Returns the addition of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `+` operator.
     *
     * Requirements:
     *
     * - Addition cannot overflow.
     */
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        return a + b;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        return a - b;
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `*` operator.
     *
     * Requirements:
     *
     * - Multiplication cannot overflow.
     */
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        return a * b;
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator.
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        return a / b;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        return a % b;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting with custom message on
     * overflow (when the result is negative).
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {trySub}.
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        unchecked {
            require(b <= a, errorMessage);
            return a - b;
        }
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting with custom message on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        unchecked {
            require(b > 0, errorMessage);
            return a / b;
        }
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting with custom message when dividing by zero.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryMod}.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        unchecked {
            require(b > 0, errorMessage);
            return a % b;
        }
    }
}


// File: @openzeppelin/contracts/utils/Address.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     *
     * Furthermore, `isContract` will also return true if the target contract within
     * the same transaction is already scheduled for destruction by `SELFDESTRUCT`,
     * which only has an effect at the end of a transaction.
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://consensys.net/diligence/blog/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.8.0/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(address target, bytes memory data, uint256 value) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verify that a low level call to smart-contract was successful, and revert (either by bubbling
     * the revert reason or using the provided one) in case of unsuccessful call or if target was not a contract.
     *
     * _Available since v4.8._
     */
    function verifyCallResultFromTarget(
        address target,
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        if (success) {
            if (returndata.length == 0) {
                // only check isContract if the call was successful and the return data is empty
                // otherwise we already know that it was a contract
                require(isContract(target), "Address: call to non-contract");
            }
            return returndata;
        } else {
            _revert(returndata, errorMessage);
        }
    }

    /**
     * @dev Tool to verify that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason or using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            _revert(returndata, errorMessage);
        }
    }

    function _revert(bytes memory returndata, string memory errorMessage) private pure {
        // Look for revert reason and bubble it up if present
        if (returndata.length > 0) {
            // The easiest way to bubble the revert reason is using memory via assembly
            /// @solidity memory-safe-assembly
            assembly {
                let returndata_size := mload(returndata)
                revert(add(32, returndata), returndata_size)
            }
        } else {
            revert(errorMessage);
        }
    }
}


// File: @openzeppelin/contracts/access/Ownable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby disabling any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}


// File: @uniswap/v2-core/contracts/interfaces/IUniswapV2Factory.sol
pragma solidity >=0.5.0;

interface IUniswapV2Factory {
    event PairCreated(address indexed token0, address indexed token1, address pair, uint);

    function feeTo() external view returns (address);
    function feeToSetter() external view returns (address);

    function getPair(address tokenA, address tokenB) external view returns (address pair);
    function allPairs(uint) external view returns (address pair);
    function allPairsLength() external view returns (uint);

    function createPair(address tokenA, address tokenB) external returns (address pair);

    function setFeeTo(address) external;
    function setFeeToSetter(address) external;
}


// File: @uniswap/v2-periphery/contracts/interfaces/IUniswapV2Router02.sol
pragma solidity >=0.6.2;

import './IUniswapV2Router01.sol';

interface IUniswapV2Router02 is IUniswapV2Router01 {
    function removeLiquidityETHSupportingFeeOnTransferTokens(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline
    ) external returns (uint amountETH);
    function removeLiquidityETHWithPermitSupportingFeeOnTransferTokens(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline,
        bool approveMax, uint8 v, bytes32 r, bytes32 s
    ) external returns (uint amountETH);

    function swapExactTokensForTokensSupportingFeeOnTransferTokens(
        uint amountIn,
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external;
    function swapExactETHForTokensSupportingFeeOnTransferTokens(
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external payable;
    function swapExactTokensForETHSupportingFeeOnTransferTokens(
        uint amountIn,
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external;
}


// File: @uniswap/v2-periphery/contracts/interfaces/IUniswapV2Router01.sol
pragma solidity >=0.6.2;

interface IUniswapV2Router01 {
    function factory() external pure returns (address);
    function WETH() external pure returns (address);

    function addLiquidity(
        address tokenA,
        address tokenB,
        uint amountADesired,
        uint amountBDesired,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline
    ) external returns (uint amountA, uint amountB, uint liquidity);
    function addLiquidityETH(
        address token,
        uint amountTokenDesired,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline
    ) external payable returns (uint amountToken, uint amountETH, uint liquidity);
    function removeLiquidity(
        address tokenA,
        address tokenB,
        uint liquidity,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline
    ) external returns (uint amountA, uint amountB);
    function removeLiquidityETH(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline
    ) external returns (uint amountToken, uint amountETH);
    function removeLiquidityWithPermit(
        address tokenA,
        address tokenB,
        uint liquidity,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline,
        bool approveMax, uint8 v, bytes32 r, bytes32 s
    ) external returns (uint amountA, uint amountB);
    function removeLiquidityETHWithPermit(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline,
        bool approveMax, uint8 v, bytes32 r, bytes32 s
    ) external returns (uint amountToken, uint amountETH);
    function swapExactTokensForTokens(
        uint amountIn,
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external returns (uint[] memory amounts);
    function swapTokensForExactTokens(
        uint amountOut,
        uint amountInMax,
        address[] calldata path,
        address to,
        uint deadline
    ) external returns (uint[] memory amounts);
    function swapExactETHForTokens(uint amountOutMin, address[] calldata path, address to, uint deadline)
        external
        payable
        returns (uint[] memory amounts);
    function swapTokensForExactETH(uint amountOut, uint amountInMax, address[] calldata path, address to, uint deadline)
        external
        returns (uint[] memory amounts);
    function swapExactTokensForETH(uint amountIn, uint amountOutMin, address[] calldata path, address to, uint deadline)
        external
        returns (uint[] memory amounts);
    function swapETHForExactTokens(uint amountOut, address[] calldata path, address to, uint deadline)
        external
        payable
        returns (uint[] memory amounts);

    function quote(uint amountA, uint reserveA, uint reserveB) external pure returns (uint amountB);
    function getAmountOut(uint amountIn, uint reserveIn, uint reserveOut) external pure returns (uint amountOut);
    function getAmountIn(uint amountOut, uint reserveIn, uint reserveOut) external pure returns (uint amountIn);
    function getAmountsOut(uint amountIn, address[] calldata path) external view returns (uint[] memory amounts);
    function getAmountsIn(uint amountOut, address[] calldata path) external view returns (uint[] memory amounts);
}


// File: @openzeppelin/contracts/utils/Context.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

