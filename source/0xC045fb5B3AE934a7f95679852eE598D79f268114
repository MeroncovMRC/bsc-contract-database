pragma solidity ^0.6.0;
// SPDX-License-Identifier: UNLICENSED

/**
 * @dev Wrappers over Solidity's arithmetic operations with added overflow
 * checks.
 *
 * Arithmetic operations in Solidity wrap on overflow. This can easily result
 * in bugs, because programmers usually assume that an overflow raises an
 * error, which is the standard behavior in high level programming languages.
 * `SafeMath` restores this intuition by reverting the transaction when an
 * operation overflows.
 *
 * Using this library instead of the unchecked operations eliminates an entire
 * class of bugs, so it's recommended to use it always.
 */
library SafeMath {
    /**
     * @dev Returns the addition of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `+` operator.
     *
     * Requirements:
     *
     * - Addition cannot overflow.
     */
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        uint256 c = a + b;
        require(c >= a, "SafeMath: addition overflow");

        return c;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        return sub(a, b, "SafeMath: subtraction overflow");
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting with custom message on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b <= a, errorMessage);
        uint256 c = a - b;

        return c;
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `*` operator.
     *
     * Requirements:
     *
     * - Multiplication cannot overflow.
     */
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
        // benefit is lost if 'b' is also tested.
        // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522
        if (a == 0) {
            return 0;
        }

        uint256 c = a * b;
        require(c / a == b, "SafeMath: multiplication overflow");

        return c;
    }

    /**
     * @dev Returns the integer division of two unsigned integers. Reverts on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        return div(a, b, "SafeMath: division by zero");
    }

    /**
     * @dev Returns the integer division of two unsigned integers. Reverts with custom message on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b > 0, errorMessage);
        uint256 c = a / b;
        // assert(a == b * c + a % b); // There is no case in which this doesn't hold

        return c;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * Reverts when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        return mod(a, b, "SafeMath: modulo by zero");
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * Reverts with custom message when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b != 0, errorMessage);
        return a % b;
    }
    
    function ceil(uint256 a, uint256 m) internal pure returns (uint256 r) {
        require(m != 0, "SafeMath: to ceil number shall not be zero");
        return (a + m - 1) / m * m;
    }
}


// ----------------------------------------------------------------------------
// BEP Token Standard #20 Interface
// https://github.com/ethereum/EIPs/blob/master/EIPS/eip-20-token-standard.md
// ----------------------------------------------------------------------------
/**
 * @dev Interface of the BEP20 standard as defined in the EIP.
 */
interface IBEP20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

// ----------------------------------------------------------------------------
// 'BSCCROP ' BEP 20 token

// Symbol      : BSCCROP
// Name        : BSCCROP
// Total supply: 21 M
// Decimals    : 18


// ----------------------------------------------------------------------------
// BEP20 Token, with the addition of symbol, name and decimals and assisted
// token transfers
// ----------------------------------------------------------------------------
contract BSCCROP is IBEP20 {
    using SafeMath for uint256;
   
    string public symbol = "BSCCROP";
    string public  name = "BSCCROP";
    uint256 public decimals = 18;
    
    uint256 _totalSupply = 21000000 * 10 ** (decimals);
    
    address Advisors = 0x5c9b7B895262D711cC2656aEe7d190B930ba8126;
    address Team = 0x3B7446Abb094281f9E641113D9f421F4fd950821;
    address Marketing = 0x469292f0c13577Ba84323a391658a4a5a748ac4b;
    address PublicSale = 0xC16DcfCfeb013be45BdCEDC956bbeBf3cF715c5c;
    address Treasury = 0xD60F16ca4a50e19b192d0e6f2d56184f8799789e;
    address StakeFarming = 0x305A3a0F217fc2c67cdA0E9F567fb7672dDb7706;
    address Liquidity = 0x37E6cc43fAA4725C3AA7d52339098F8deded682b;
    
    uint256 transactionCost = 1; // 1% will be burned on every transaction
    uint256 unLockPercentage = 25; 
    uint256 unLockPeriod =  90 days; 
    uint256 minSupply = 14000000 * 10 ** (decimals);
    uint256 public deployTime;
    
    mapping(address => uint256) balances;
    mapping(address => mapping(address => uint256)) allowed;
    mapping(address => locking) locked;
    struct locking{
        uint256 initial;
        uint256 locked;
    }
   
    // ------------------------------------------------------------------------
    // Constructor
    // ------------------------------------------------------------------------
    constructor() public {
        // 10% unlocked, remaining vested quaterly
        balances[Advisors] =  balances[Advisors].add(1050000 * 10 ** (18)); // 5%
        emit Transfer(address(0), Advisors, 1050000 * 10 ** (18));
        locked[Advisors].initial = locked[Advisors].initial.add(945000 * 10 ** (decimals)); // 90% locked initially
        locked[Advisors].locked = locked[Advisors].locked.add(945000 * 10 ** (decimals));
        
        // 20% unlocked, remaining vested quaterly
        balances[Team] =  balances[Team].add(4200000  * 10 ** (18)); // 20%
        emit Transfer(address(0), Team, 4200000  * 10 ** (18));
        locked[Team].initial = locked[Team].initial.add(3360000 * 10 ** (decimals)); // 80% locked initially
        locked[Team].locked = locked[Team].locked.add(3360000 * 10 ** (decimals));
        
        // 10% unlocked, remaining vested quaterly
        balances[Marketing] =  balances[Marketing].add(1050000 * 10 ** (18)); // 5%
        emit Transfer(address(0), Marketing, 1050000 * 10 ** (18));
        locked[Marketing].initial = locked[Marketing].initial.add(945000 * 10 ** (decimals)); // 90% locked initially
        locked[Marketing].locked = locked[Marketing].locked.add(945000 * 10 ** (decimals));
        
        // no vesting - all unlocked
        balances[PublicSale] =  balances[PublicSale].add(2100000  * 10 ** (18)); // 10%
        emit Transfer(address(0), PublicSale, 2100000  * 10 ** (18));
        
        // no vesting
        balances[Treasury] =  balances[Treasury].add(5250000  * 10 ** (18)); // 25%
        emit Transfer(address(0), Treasury, 5250000  * 10 ** (18));
        
        // no vesting
        balances[StakeFarming] =  balances[StakeFarming].add(5250000 * 10 ** (18)); // 25%
        emit Transfer(address(0), StakeFarming, 5250000 * 10 ** (18));
        
        // no vesting
        balances[Liquidity] =  balances[Liquidity].add(2100000  * 10 ** (18)); // 10%
        emit Transfer(address(0), Liquidity, 2100000  * 10 ** (18));
        
        deployTime = block.timestamp;
        
    }

   
    /** BEP20Interface function's implementation **/
   
    function totalSupply() external override view returns (uint256){
       return _totalSupply;
    }
   
    // ------------------------------------------------------------------------
    // Get the token balance for account `tokenOwner`
    // ------------------------------------------------------------------------
    function balanceOf(address tokenOwner) external override view returns (uint256 balance) {
        return balances[tokenOwner];
    }
    
    // ------------------------------------------------------------------------
    // Token owner can approve for `spender` to transferFrom(...) `tokens`
    // from the token owner's account
    // ------------------------------------------------------------------------
    function approve(address spender, uint256 tokens) external override returns (bool success){
        allowed[msg.sender][spender] = tokens;
        emit Approval(msg.sender,spender,tokens);
        return true;
    }
    
    // ------------------------------------------------------------------------
    // Returns the amount of tokens approved by the owner that can be
    // transferred to the spender's account
    // ------------------------------------------------------------------------
    function allowance(address tokenOwner, address spender) external override view returns (uint256 remaining) {
        return allowed[tokenOwner][spender];
    }

    // ------------------------------------------------------------------------
    // Transfer the balance from token owner's account to `to` account
    // - Owner's account must have sufficient balance to transfer
    // - 0 value transfers are allowed
    // ------------------------------------------------------------------------
    function transfer(address to, uint256 tokens) public override returns (bool success) {
        
        require(address(to) != address(0), "Invalid receiver address");
        require(balances[msg.sender] >= tokens, "Insufficient senders balance");

        if (locked[msg.sender].locked > 0){
            require(_checkTransfer(tokens, msg.sender));
        }
        
        // APPLY BURNING ON EACH TRANSACTION - UNLESS WHITELISTED
        balances[msg.sender] = balances[msg.sender].sub(tokens);
        
        uint256 deduction = deductionsToApply(tokens, to);
        burnTokens(deduction);
        
        balances[to] = balances[to].add(tokens.sub(deduction));
        emit Transfer(msg.sender, to, tokens.sub(deduction));
        return true;
    }
    
    // ------------------------------------------------------------------------
    // Transfer `tokens` from the `from` account to the `to` account
    //
    // The calling account must already have sufficient tokens approve(...)-d
    // for spending from the `from` account and
    // - From account must have sufficient balance to transfer
    // - Spender must have sufficient allowance to transfer
    // - 0 value transfers are allowed
    // ------------------------------------------------------------------------
    function transferFrom(address from, address to, uint256 tokens) external override returns (bool success){
        require(tokens <= allowed[from][msg.sender], "Insufficient allowance"); //check allowance
        require(balances[from] >= tokens, "Insufficient senders balance");

        if (locked[from].locked != 0){
            require(_checkTransfer(tokens, from), "tokens are locked");
        }
        
        balances[from] = balances[from].sub(tokens);
        allowed[from][msg.sender] = allowed[from][msg.sender].sub(tokens);
      
        uint256 deduction = deductionsToApply(tokens, to);
        burnTokens(deduction);
       
        balances[to] = balances[to].add(tokens.sub(deduction));
        emit Transfer(from, to, tokens.sub(tokens));
        return true;
    }
    
    function deductionsToApply(uint256 tokens, address to) private view returns(uint256){
        uint256 deduction = 0;
        
        if(_totalSupply > minSupply && msg.sender != Team && msg.sender != Liquidity && msg.sender != Treasury && to != 0xcc3885FcFF76B622DEeBC9E53b475c129418D392 && to != 0xA854979DD9E65331a4f9AFF4E1421Ea3988D4beD){
        
            deduction = onePercent(tokens).mul(transactionCost);
        
            if(_totalSupply.sub(deduction) < minSupply)
                deduction = _totalSupply.sub(minSupply);
        }
        
        return deduction;
    }
    
    // ------------------------------------------------------------------------
    // Burn the `value` amount of tokens
    // ------------------------------------------------------------------------
    function burnTokens(uint256 value) internal{
        require(_totalSupply >= value); // burn the deducted tokens
        _totalSupply = _totalSupply.sub(value);
        emit Transfer(msg.sender, address(0), value);
    }
    
    // ------------------------------------------------------------------------
    // Calculates onePercent of the uint256 amount sent
    // ------------------------------------------------------------------------
    function _checkTransfer(uint256 _tokens, address _user) internal returns(bool){
        uint256 unLockedTokens = ((block.timestamp.sub(deployTime)).div(unLockPeriod)).mul(unLockPercentage);
        if (unLockedTokens > 100){
            unLockedTokens = 100;
        }
        locked[_user].locked = locked[_user].initial.sub(onePercent(locked[_user].initial).mul(unLockedTokens));
        require(( balances[_user].sub(locked[_user].locked)) >= _tokens, "tokens are locked");
        return true;
    }
    
    // ------------------------------------------------------------------------
    // Calculates onePercent of the uint256 amount sent
    // ------------------------------------------------------------------------
    function onePercent(uint256 _tokens) internal pure returns (uint256){
        uint256 roundValue = _tokens.ceil(100);
        uint onePercentofTokens = roundValue.mul(100).div(100 * 10**uint(2));
        return onePercentofTokens;
    }
    
}