// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

/**
 * @title SafeMath
 * @dev Math operations with safety checks that revert on error
 */
library SafeMath {
    /**
     * @dev Returns the largest of two numbers.
     */
    function max(uint256 a, uint256 b) internal pure returns (uint256) {
        return a >= b ? a : b;
    }

    /**
     * @dev Returns the smallest of two numbers.
     */
    function min(uint256 a, uint256 b) internal pure returns (uint256) {
        return a < b ? a : b;
    }

    /**
     * @dev Calculates the average of two numbers. Since these are integers,
     * averages of an even and odd number cannot be represented, and will be
     * rounded down.
     */
    function average(uint256 a, uint256 b) internal pure returns (uint256) {
        // (a + b) / 2 can overflow, so we distribute
        return (a / 2) + (b / 2) + (((a % 2) + (b % 2)) / 2);
    }

    /**
     * @dev Multiplies two numbers, reverts on overflow.
     */
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
        // benefit is lost if 'b' is also tested.
        // See: https://github.com/OpenZeppelin/openzeppelin-solidity/pull/522
        if (a == 0) {
            return 0;
        }

        uint256 c = a * b;
        require(c / a == b);

        return c;
    }

    /**
     * @dev Integer division of two numbers truncating the quotient, reverts on division by zero.
     */
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        require(b > 0); // Solidity only automatically asserts when dividing by 0
        uint256 c = a / b;
        // assert(a == b * c + a % b); // There is no case in which this doesn't hold

        return c;
    }

    /**
     * @dev Subtracts two numbers, reverts on overflow (i.e. if subtrahend is greater than minuend).
     */
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        require(b <= a);
        uint256 c = a - b;

        return c;
    }

    /**
     * @dev Adds two numbers, reverts on overflow.
     */
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        uint256 c = a + b;
        require(c >= a);

        return c;
    }

    /**
     * @dev Divides two numbers and returns the remainder (unsigned integer modulo),
     * reverts when dividing by zero.
     */
    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        require(b != 0);
        return a % b;
    }
}

contract Ownable {
    address public _owner;

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(_owner == msg.sender, "Ownable: caller is not the owner");
        _;
    }

    function changeOwner(address newOwner) public onlyOwner {
        _owner = newOwner;
    }
}

/**
 * @title ERC20 interface
 * @dev see https://github.com/ethereum/EIPs/issues/20
 */
interface IERC20 {
    function totalSupply() external view returns (uint256);

    function balanceOf(address who) external view returns (uint256);

    function allowance(address owner, address spender)
        external
        view
        returns (uint256);

    function transfer(address to, uint256 value) external returns (bool);

    function approve(address spender, uint256 value) external returns (bool);

    function transferFrom(
        address from,
        address to,
        uint256 value
    ) external returns (bool);

    event Transfer(address indexed from, address indexed to, uint256 value);

    event Approval(
        address indexed owner,
        address indexed spender,
        uint256 value
    );
}

/**
 * @title SafeERC20
 * @dev Wrappers around ERC20 operations that throw on failure.
 * To use this library you can add a `using SafeERC20 for ERC20;` statement to your contract,
 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.
 */
library SafeERC20 {
    using SafeMath for uint256;

    function safeTransfer(
        IERC20 token,
        address to,
        uint256 value
    ) internal {
        require(token.transfer(to, value));
    }

    function safeTransferFrom(
        IERC20 token,
        address from,
        address to,
        uint256 value
    ) internal {
        require(token.transferFrom(from, to, value));
    }

    function safeApprove(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        // safeApprove should only be called when setting an initial allowance,
        // or when resetting it to zero. To increase and decrease it, use
        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
        require((value == 0) || (token.allowance(msg.sender, spender) == 0));
        require(token.approve(spender, value));
    }

    function safeIncreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        uint256 newAllowance = token.allowance(address(this), spender).add(
            value
        );
        require(token.approve(spender, newAllowance));
    }

    function safeDecreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        uint256 newAllowance = token.allowance(address(this), spender).sub(
            value
        );
        require(token.approve(spender, newAllowance));
    }
}

interface OldMiner {
    function userEarned(address user) external view returns (uint256, uint256, uint256, uint256, uint256);

}

contract NZMiner is Ownable {
    using SafeMath for uint256;
    using SafeERC20 for IERC20;

    uint public rate = 1000;
    uint256 public claims;

    OldMiner public oldMiner = OldMiner(0x447d31c4F1e718578588FCeB5d9A816167Da5d52);
    address public nzAddress = address(0x5854972A4de57DF158aC429E7Ef4F5083cD354E4);


    mapping(uint256 => Claim) public claimInfo;
    mapping(address => uint256[]) private userClaims;
    mapping(address => UserReward) public userRewardInfo;

    struct Claim {
        address user;
        uint256 amount;
        uint256 rewardTime;
        uint256 typeNumber;
    }

    struct UserReward {
        uint256 totalRewards;
        uint256 accTotalReward;
        uint256 peerTotalReward;
    }

   
    event RewardPaid(address indexed user, uint256 reward);

    constructor() {
        _owner = msg.sender;
    }

    //to recieve ETH from uniswapV2Router when swaping
    receive() external payable {}

    function changeRate(uint newRate) public onlyOwner {
        rate = newRate;
    }

    function claim() external {
        (, uint i, uint p, , uint pending) = oldMiner.userEarned(msg.sender);

        UserReward storage ur = userRewardInfo[msg.sender];

        pending = pending.sub(ur.totalRewards);
        i = i.sub(ur.accTotalReward);
        p = p.sub(ur.peerTotalReward);

        ur.totalRewards += pending;
        ur.accTotalReward += i;
        ur.peerTotalReward += p;

        uint totalRewards = pending.add(i).add(p).mul(rate).div(10000);

        claims = claims.add(1);
        Claim storage c0 = claimInfo[claims];
        c0.user = msg.sender;
        c0.amount = pending.mul(rate).div(10000);
        c0.rewardTime = block.timestamp;
        c0.typeNumber = 0;
        userClaims[msg.sender].push(claims);

        claims = claims.add(1);
        Claim storage c1 = claimInfo[claims];
        c1.user = msg.sender;
        c1.amount = uint(i).add(p).mul(rate).div(10000);
        c1.rewardTime = block.timestamp;
        c1.typeNumber = 1;
        userClaims[msg.sender].push(claims);

        IERC20(nzAddress).safeTransfer(msg.sender, totalRewards);
        emit RewardPaid(msg.sender, totalRewards);

    }

    function clearPot(address to, uint256 amount) external onlyOwner {
        if (amount > IERC20(nzAddress).balanceOf(address(this))) {
            amount = IERC20(nzAddress).balanceOf(address(this));
        }
        IERC20(nzAddress).safeTransfer(to, amount);
    }
}