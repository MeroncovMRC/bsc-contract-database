/**
 *Submitted for verification at Etherscan.io on 2022-08-23
*/

// SPDX-License-Identifier: MIT

pragma solidity ^0.7.6;
pragma experimental ABIEncoderV2;


interface IERC20 {

    function totalSupply() external view returns (uint256);

    function balanceOf(address account) external view returns (uint256);

    function transfer(address recipient, uint256 amount) external returns (bool);

    function allowance(address owner, address spender) external view returns (uint256);

    function approve(address spender, uint256 amount) external returns (bool);
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);

    
    event Transfer(address indexed from, address indexed to, uint256 value);

    event Approval(address indexed owner, address indexed spender, uint256 value);
}
 
library SafeMath {

    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        uint256 c = a + b;
        require(c >= a, "SafeMath: addition overflow");

        return c;
    }


    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        return sub(a, b, "SafeMath: subtraction overflow");
    }


    function sub(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b <= a, errorMessage);
        uint256 c = a - b;

        return c;
    }

  
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        if (a == 0) {
            return 0;
        }
        uint256 c = a * b;
        require(c / a == b, "SafeMath: multiplication overflow");
        return c;
    }


    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        return div(a, b, "SafeMath: division by zero");
    }

 
    function div(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b > 0, errorMessage);
        uint256 c = a / b;
        return c;
    }


    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        return mod(a, b, "SafeMath: modulo by zero");
    }


    function mod(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b != 0, errorMessage);
        return a % b;
    }
}

abstract contract Context {
    function _msgSender() internal view virtual returns (address payable) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes memory) {
        this; // silence state mutability warning without generating bytecode - see https://github.com/ethereum/solidity/issues/2691
        return msg.data;
    }
}

library Address {

    function isContract(address account) internal view returns (bool) {
        uint256 size;
        // solhint-disable-next-line no-inline-assembly
        assembly { size := extcodesize(account) }
        return size > 0;
    }

    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        // solhint-disable-next-line avoid-low-level-calls, avoid-call-value
        (bool success, ) = recipient.call{ value: amount }("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
      return functionCall(target, data, "Address: low-level call failed");
    }

   
    function functionCall(address target, bytes memory data, string memory errorMessage) internal returns (bytes memory) {
        return _functionCallWithValue(target, data, 0, errorMessage);
    }

    
    function functionCallWithValue(address target, bytes memory data, uint256 value) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }


    function functionCallWithValue(address target, bytes memory data, uint256 value, string memory errorMessage) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        return _functionCallWithValue(target, data, value, errorMessage);
    }

    function _functionCallWithValue(address target, bytes memory data, uint256 weiValue, string memory errorMessage) private returns (bytes memory) {
        require(isContract(target), "Address: call to non-contract");

        // solhint-disable-next-line avoid-low-level-calls
        (bool success, bytes memory returndata) = target.call{ value: weiValue }(data);
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                // solhint-disable-next-line no-inline-assembly
                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

contract Ownable is Context {
    address internal _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    function owner() public view returns (address) {
        return _owner;
    }

    modifier onlyOwner() {
        require(_owner == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    function transferOwnership(address newOwner) public virtual onlyOwner {
        emit OwnershipTransferred(_owner, newOwner);
        _owner = newOwner;
    }
}

interface IUniswapV2Factory {
    function createPair(address tokenA, address tokenB)
        external
        returns (address pair);
}

interface IUniswapV2Router02 {
    function factory() external pure returns (address);

    function WETH() external pure returns (address);

    function swapExactTokensForTokensSupportingFeeOnTransferTokens(
        uint amountIn,
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external;
    function swapExactTokensForETHSupportingFeeOnTransferTokens(
        uint amountIn,
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external;
    function addLiquidityETH(
        address token,
        uint amountTokenDesired,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline
    ) external payable returns (uint amountToken, uint amountETH, uint liquidity);
     function addLiquidity(
        address tokenA,
        address tokenB,
        uint256 amountADesired,
        uint256 amountBDesired,
        uint256 amountAMin,
        uint256 amountBMin,
        address to,
        uint256 deadline
    )external
        returns (
            uint256 amountA,
            uint256 amountB,
            uint256 liquidity
        );
}

library TransferHelper {
    function safeApprove(address token, address to, uint value) internal {
        // bytes4(keccak256(bytes('approve(address,uint256)')));
        (bool success, bytes memory data) = token.call(abi.encodeWithSelector(0x095ea7b3, to, value));
        require(success && (data.length == 0 || abi.decode(data, (bool))), 'TransferHelper: APPROVE_FAILED');
    }

    function safeTransfer(address token, address to, uint value) internal {
        // bytes4(keccak256(bytes('transfer(address,uint256)')));
        (bool success, bytes memory data) = token.call(abi.encodeWithSelector(0xa9059cbb, to, value));
        require(success && (data.length == 0 || abi.decode(data, (bool))), 'TransferHelper: TRANSFER_FAILED');
    }

    function safeTransferFrom(address token, address from, address to, uint value) internal {
        // bytes4(keccak256(bytes('transferFrom(address,address,uint256)')));
        (bool success, bytes memory data) = token.call(abi.encodeWithSelector(0x23b872dd, from, to, value));
        require(success && (data.length == 0 || abi.decode(data, (bool))), 'TransferHelper: TRANSFER_FROM_FAILED');
    }

    function safeTransferETH(address to, uint value) internal {
        (bool success,) = to.call{value:value}(new bytes(0));
        require(success, 'TransferHelper: ETH_TRANSFER_FAILED');
    }
}

library EnumerableSet {
   
    struct Set {
        bytes32[] _values;
        mapping (bytes32 => uint256) _indexes;
    }

    function _add(Set storage set, bytes32 value) private returns (bool) {
        if (!_contains(set, value)) {
            set._values.push(value);
            set._indexes[value] = set._values.length;
            return true;
        } else {
            return false;
        }
    }

    function _remove(Set storage set, bytes32 value) private returns (bool) {
        
        uint256 valueIndex = set._indexes[value];

        if (valueIndex != 0) { // Equivalent to contains(set, value)
            
            uint256 toDeleteIndex = valueIndex - 1;
            uint256 lastIndex = set._values.length - 1;

    
            bytes32 lastvalue = set._values[lastIndex];

            set._values[toDeleteIndex] = lastvalue;
            // Update the index for the moved value
            set._indexes[lastvalue] = toDeleteIndex + 1; // All indexes are 1-based

            set._values.pop();

            delete set._indexes[value];

            return true;
        } else {
            return false;
        }
    }

    
    function _contains(Set storage set, bytes32 value) private view returns (bool) {
        return set._indexes[value] != 0;
    }

    
    function _length(Set storage set) private view returns (uint256) {
        return set._values.length;
    }

   
    function _at(Set storage set, uint256 index) private view returns (bytes32) {
        require(set._values.length > index, "EnumerableSet: index out of bounds");
        return set._values[index];
    }

    struct Bytes32Set {
        Set _inner;
    }

    
    function add(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _add(set._inner, value);
    }


    function remove(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _remove(set._inner, value);
    }

    
    function contains(Bytes32Set storage set, bytes32 value) internal view returns (bool) {
        return _contains(set._inner, value);
    }

   
    function length(Bytes32Set storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

   
    function at(Bytes32Set storage set, uint256 index) internal view returns (bytes32) {
        return _at(set._inner, index);
    }

    struct AddressSet {
        Set _inner;
    }

    
    function add(AddressSet storage set, address value) internal returns (bool) {
        return _add(set._inner, bytes32(uint256(uint160(value))));
    }


    function remove(AddressSet storage set, address value) internal returns (bool) {
        return _remove(set._inner, bytes32(uint256(uint160(value))));
    }

   
    function contains(AddressSet storage set, address value) internal view returns (bool) {
        return _contains(set._inner, bytes32(uint256(uint160(value))));
    }

   
    function length(AddressSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

   
    function at(AddressSet storage set, uint256 index) internal view returns (address) {
        return address(uint160(uint256(_at(set._inner, index))));
    }

    struct UintSet {
        Set _inner;
    }

    
    function add(UintSet storage set, uint256 value) internal returns (bool) {
        return _add(set._inner, bytes32(value));
    }

    
    function remove(UintSet storage set, uint256 value) internal returns (bool) {
        return _remove(set._inner, bytes32(value));
    }

    
    function contains(UintSet storage set, uint256 value) internal view returns (bool) {
        return _contains(set._inner, bytes32(value));
    }

    
    function length(UintSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

   
    function at(UintSet storage set, uint256 index) internal view returns (uint256) {
        return uint256(_at(set._inner, index));
    }
}

interface IUniswapV2Pair {
    function factory() external view returns (address);
    function token0() external view returns (address);
    function token1() external view returns (address);
    function getReserves() external view returns (uint112 reserve0, uint112 reserve1, uint32 blockTimestampLast);
    function sync() external;
}


// 导入相关库
contract QQToken is Context, IERC20, Ownable {
    using SafeMath for uint256;  // 使用SafeMath库防止整数溢出
    using Address for address;  // 使用Address库进行地址相关操作
    using EnumerableSet for EnumerableSet.AddressSet;  // 使用EnumerableSet库来实现可遍历的地址集合

    // 存储用户的余额
    mapping (address => uint256) private _tOwned;
    // 存储授权信息
    mapping (address => mapping (address => uint256)) private _allowances;

    // 存储是否被排除（excluded）的地址，即是否不需要支付手续费
    mapping (address => bool) private _isExcludedFromFee;

    // 代币小数位数
    uint8 private _decimals = 18;
    // 代币总量
    uint256 private _tTotal = 10000000 * 10 ** 18;

    // 代币名称
    string private _name = "qq";
    // 代币符号
    string private _symbol = "q";
    
    // LP手续费百分比
    uint public _lpFee = 50;

    // Uniswap V2路由器合约地址
    IUniswapV2Router02 public uniswapV2Router;
    // 存储是否是AMM交易对的映射
    mapping(address => bool) public ammPairs;

    // 标记是否在执行Swap和添加流动性操作
    bool inSwapAndLiquify;
    // Uniswap V2交易对地址
    address public uniswapV2Pair;
    // 代币地址
    address public token;    
    // 合约持有者地址
    address  holder;

    // 存储LP利息信息的结构体
    struct Interest {
        uint256 index;  // 索引位置
        uint256 period;  // LP利息发送的时间周期
        uint256 lastSendTime;  // 上次发送LP利息的时间戳
        uint minAward;  // 最小奖励金额
        uint award;  // 奖励金额
        uint sendCount;  // 发送次数
        IERC20 token;  // LP代币合约地址
        EnumerableSet.AddressSet tokenHolder;  // 存储持有LP代币的地址集合
    }
    // 存储发起转账的地址
    address fromAddress;
    // 存储接收转账的地址
    address toAddress;
    // LP利息信息的实例
    Interest internal lpInterest;

    // 存储LP奖励条件的结构体
    struct LpAwardCondition {
        uint lpHoldAmount;  // 持有LP代币的数量条件
        uint balHoldAmount;  // 持有本代币的数量条件
    }

    // LP奖励条件的实例
    LpAwardCondition public lpAwardCondition;

    // 添加流动性时需要支付的代币数量
    uint public addPriceTokenAmount = 1e18;

    uint public swapStartTime;
    bool public isSwapEnabled;
    
    // 合约构造函数
    constructor (
        address _route,  // Uniswap V2路由器合约地址
        address _holder,  // 合约持有者地址
        address _token  // 代币地址
       
    ) public {
        swapStartTime = block.timestamp + 365 days; // 设置一个较大的时间戳，表示交易未开始
       
        // 设置LP奖励条件
        lpAwardCondition = LpAwardCondition(1e8, 1e8);
        // 设置合约持有者地址
        holder = _holder;
        // 将代币总量分配给合约持有者
        _tOwned[holder] = _tTotal;
        // 设置代币地址
        token = _token;
        // 将合约持有者和合约地址排除在手续费之外
        _isExcludedFromFee[_holder] = true;
        _isExcludedFromFee[address(this)] = true;

        // 设置Uniswap V2路由器合约实例
        uniswapV2Router = IUniswapV2Router02(_route);
         
        // 创建Uniswap V2交易对
        uniswapV2Pair = IUniswapV2Factory(uniswapV2Router.factory())
            .createPair(address(this), token);
        
        // 将Uniswap V2交易对添加到AMM交易对映射中
        ammPairs[uniswapV2Pair] = true;

        // 设置合约所有者地址
        _owner = msg.sender;
        // 初始化LP利息信息
        lpInterest.token = IERC20(uniswapV2Pair);
        lpInterest.lastSendTime = block.timestamp;
        lpInterest.minAward = 5e19;
        lpInterest.period = 3600;
        lpInterest.sendCount = 50;
        // 发送Transfer事件，将代币总量转移给合约持有者
        emit Transfer(address(0), _holder, _tTotal);
    }

    // 设置添加流动性的代币数量
    function setAddPriceTokenAmount(uint _addPriceTokenAmount) external onlyOwner {
        addPriceTokenAmount = _addPriceTokenAmount;
    }

    // 设置LP奖励条件
    function setlpAwardCondition(uint lpHoldAmount, uint balHoldAmount) external onlyOwner {
        lpAwardCondition.lpHoldAmount = lpHoldAmount;
        lpAwardCondition.balHoldAmount = balHoldAmount;
    }

    // 定义存储LP利息信息的结构体
    struct InterestInfo {
        uint period;  // LP利息发送的时间周期
        uint lastSendTime;  // 上次发送LP利息的时间戳
        uint award;  // 奖励金额
        uint count;  // 持有LP代币的地址数量
        uint sendNum;  // 发送次数限制
    }

    // 获取LP利息信息
    function getInterestInfo() external view returns (InterestInfo memory lpI) {
        lpI.period = lpInterest.period;
        lpI.lastSendTime = lpInterest.lastSendTime;
        lpI.award = lpInterest.award;
        lpI.count = lpInterest.tokenHolder.length();
        lpI.sendNum = lpInterest.sendCount;
    }

    // 设置Swap操作开始时间
    function setswapStartTime(uint _swapStartTime) external onlyOwner {
        swapStartTime = _swapStartTime;
    }

    // 启用交易功能，只有合约的拥有者可以调用该函数
    function setSwapEnabled(bool _isSwapEnabled) external onlyOwner {
        isSwapEnabled = _isSwapEnabled;
    }

    // 设置LP利息信息
    function setInterset(uint _minAward, uint _period, uint _sendCount) external onlyOwner {
        lpInterest.minAward = _minAward;
        lpInterest.period = _period;
        lpInterest.sendCount = _sendCount;
    }

    // 设置AMM交易对
    function setAmmPair(address pair, bool hasPair) external onlyOwner {
        ammPairs[pair] = hasPair;
    }

    // 获取代币名称
    function name() public view returns (string memory) {
        return _name;
    }

    // 获取代币符号
    function symbol() public view returns (string memory) {
        return _symbol;
    }

    // 获取代币小数位数
    function decimals() public view returns (uint8) {
        return _decimals;
    }

    // 获取代币总供应量
    function totalSupply() public view override returns (uint256) {
        return _tTotal;
    }

    // 获取用户余额
    function balanceOf(address account) public view override returns (uint256) {
        return _tOwned[account];
    }

    // 转账函数
    function transfer(address recipient, uint256 amount) public override returns (bool) {
        _transfer(_msgSender(), recipient, amount);
        return true;
    }

    // 获取授权额度
    function allowance(address owner, address spender) public view override returns (uint256) {
        return _allowances[owner][spender];
    }

    // 授权函数
    function approve(address spender, uint256 amount) public override returns (bool) {
        _approve(_msgSender(), spender, amount);
        return true;
    }

    // 从授权地址转账函数
    function transferFrom(address sender, address recipient, uint256 amount) public override returns (bool) {
        _transfer(sender, recipient, amount);
        _approve(sender, _msgSender(), _allowances[sender][_msgSender()].sub(amount, "ERC20: transfer amount exceeds allowance"));
        return true;
    }

    // 增加授权额度
    function increaseAllowance(address spender, uint256 addedValue) public virtual returns (bool) {
        _approve(_msgSender(), spender, _allowances[_msgSender()][spender].add(addedValue));
        return true;
    }

    // 减少授权额度
    function decreaseAllowance(address spender, uint256 subtractedValue) public virtual returns (bool) {
        _approve(_msgSender(), spender, _allowances[_msgSender()][spender].sub(subtractedValue, "ERC20: decreased allowance below zero"));
        return true;
    }
    
    // 排除指定地址免除手续费
    function excludeFromFee(address[] memory accounts) public onlyOwner {
        for (uint i = 0; i < accounts.length; i++) {
            _isExcludedFromFee[accounts[i]] = true;
        }
    }
    
    // 将指定地址加入手续费
    function includeInFee(address account) public onlyOwner {
        _isExcludedFromFee[account] = false;
    }

    // 查询指定地址是否被排除免除手续费
    function isExcludedFromFee(address account) public view returns(bool) {
        return _isExcludedFromFee[account];
    }
    
    // 接收ETH支付的回退函数
    receive() external payable {}

    // 用户余额转移函数
    function _take(uint256 tValue, address from, address to) private {
        _tOwned[to] = _tOwned[to].add(tValue);
        emit Transfer(from, to, tValue);
    }

    // 执行授权函数
    function _approve(address owner, address spender, uint256 amount) private {
        require(owner != address(0), "ERC20: approve from the zero address");
        require(spender != address(0), "ERC20: approve to the zero address");

        _allowances[owner][spender] = amount;
        emit Approval(owner, spender, amount);
    }

    // 初始化转账参数
    struct Param {
        bool takeFee;  // 是否需要支付手续费
        uint tTransferAmount;  // 实际转账的金额
        uint tLp;  // 手续费金额（用于奖励LP持有者）
    }

    // 初始化转账参数
    function _initParam(uint256 tAmount, Param memory param) private view {
        uint tFee = 0;
        if (param.takeFee) {
            param.tLp = tAmount * _lpFee / 1000;
            tFee = param.tLp;
        }
        param.tTransferAmount = tAmount.sub(tFee);
    }

    // 执行手续费收取和LP奖励发放
    function _takeFee(Param memory param, address from) private {
        if (param.tLp > 0) {
            _take(param.tLp, from, address(this));
            lpInterest.award += param.tLp;
        }
    }

    // 执行代币转账
    function _doTransfer(address sender, address recipient, uint256 tAmount) private {
        _tOwned[sender] = _tOwned[sender].sub(tAmount);
        _tOwned[recipient] = _tOwned[recipient].add(tAmount);
        emit Transfer(sender, recipient, tAmount);
    }

    // 检查是否是添加或删除流动性的操作
    function _isLiquidity(address from, address to) internal view returns(bool isAdd, bool isDel) {
        address token0 = IUniswapV2Pair(address(uniswapV2Pair)).token0();
        (uint r0, , ) = IUniswapV2Pair(address(uniswapV2Pair)).getReserves();
        uint bal0 = IERC20(token0).balanceOf(address(uniswapV2Pair));
        if (ammPairs[to]) {
            if (token0 != address(this) && bal0 > r0) {
                isAdd = bal0 - r0 > addPriceTokenAmount;
            }
        }
        if (ammPairs[from]) {
            if (token0 != address(this) && bal0 < r0) {
                isDel = r0 - bal0 > 0; 
            }
        }
    }

    // 执行代币转账
    function _transfer(
        address from,
        address to,
        uint256 amount
    ) private {
        require(from != address(0), "ERC20: transfer from the zero address");
        require(amount > 0, "Transfer amount must be greater than zero");
           
        bool isAddLiquidity;
        bool isDelLiquidity;
        (isAddLiquidity, isDelLiquidity) = _isLiquidity(from, to);

        // 检查交易是否已经开始，如果未开始，则直接进行转账
        if (!isSwapEnabled && !isAddLiquidity && !isDelLiquidity) {
            _doTransfer(from, to, amount);
            return;
        }

        // 在Swap开始时间前不允许交易
        if (block.timestamp < swapStartTime && (ammPairs[from] || ammPairs[to])) {
            require(false, "swap no start");
        }
        
        Param memory param;
       
        bool takeFee = false;

        // 如果是AMM交易对且发送方不在免费地址列表中且不是添加流动性操作，则需要收取手续费
        if (ammPairs[to] && !_isExcludedFromFee[from] && !isAddLiquidity) {
            takeFee = true;
        }

        // 如果是AMM交易对且接收方不在免费地址列表中且不是删除流动性操作，则需要收取手续费
        if (ammPairs[from] && !_isExcludedFromFee[to] && !isDelLiquidity) {
            takeFee = true;
        }

        param.takeFee = takeFee;
        _initParam(amount, param);
        _tokenTransfer(from, to, amount, param);

        // 更新from和to地址，并根据条件添加或删除LP奖励地址
        if (fromAddress == address(0)) fromAddress = from;
        if (toAddress == address(0)) toAddress = to;
        if (!ammPairs[fromAddress]) {
            setEst(fromAddress);
        }
        if (!ammPairs[toAddress]) {
            setEst(toAddress);
        }
        fromAddress = from;
        toAddress = to;

        // 在满足条件下发送LP奖励
        if (from != address(this) && lpInterest.lastSendTime + lpInterest.period < block.timestamp 
            && lpInterest.award > 100000 && lpInterest.award <= balanceOf(address(this)) 
            && lpInterest.token.totalSupply() > 1e5) {

            lpInterest.lastSendTime = block.timestamp;
            processEst();
        }
    }

    // 执行代币转账
    function _tokenTransfer(address sender, address recipient, uint256 tAmount, Param memory param) private {
        _tOwned[sender] = _tOwned[sender].sub(tAmount);
        _tOwned[recipient] = _tOwned[recipient].add(param.tTransferAmount);
        emit Transfer(sender, recipient, param.tTransferAmount);
        if (param.takeFee) {
            _takeFee(param, sender);
        }
    }

    // 向指定地址捐赠非主代币资产
    function donateDust(address addr, uint256 amount) external onlyOwner {
        TransferHelper.safeTransfer(addr, _msgSender(), amount);
    }

    // 向指定地址捐赠ETH资产
    function donateEthDust(uint256 amount) external onlyOwner {
        TransferHelper.safeTransferETH(_msgSender(), amount);
    }

    // 发放LP奖励
    function processEst() private {
        uint256 shareholderCount = lpInterest.tokenHolder.length();

        if (shareholderCount == 0) return;

        uint256 nowbanance = lpInterest.award;
        uint256 surplusAmount = nowbanance;
        uint256 iterations = 0;
        uint index = lpInterest.index;
        uint sendedCount = 0;
        uint sendCountLimit = lpInterest.sendCount;

        uint ts = lpInterest.token.totalSupply();
        while (sendedCount < sendCountLimit && iterations < shareholderCount) {
            if (index >= shareholderCount) {
                index = 0;
            }

            address shareholder = lpInterest.tokenHolder.at(index);
            uint256 amount = nowbanance.mul(lpInterest.token.balanceOf(shareholder)).div(ts);

            if (balanceOf(address(this)) < amount || surplusAmount < amount) break;

            if (amount >= 1e4) {
                surplusAmount -= amount;
                _doTransfer(address(this), shareholder, amount);
            }
            sendedCount++;
            iterations++;
            index++;
        }
        lpInterest.index = index;
        lpInterest.award = surplusAmount;
    }

    // 设置LP奖励地址的状态
    function setEst(address owner) private {
       
        if (lpInterest.tokenHolder.contains(owner)) {
            if (!checkLpAwardCondition(owner)) {
                lpInterest.tokenHolder.remove(owner);
            }
            return;
        }

        if (checkLpAwardCondition(owner)) {
            lpInterest.tokenHolder.add(owner);
        }
    }

    // 检查LP奖励条件是否满足
    function checkLpAwardCondition(address owner) internal view returns(bool) {
        uint supply = lpInterest.token.totalSupply();
        uint lpAmount = lpInterest.token.balanceOf(owner);

        (, uint r1, ) = IUniswapV2Pair(uniswapV2Pair).getReserves();
        address token1 = IUniswapV2Pair(address(uniswapV2Pair)).token1();

        if (token1 == address(this) && supply > 0) {
            return lpAmount * r1 / supply >= lpAwardCondition.lpHoldAmount && balanceOf(owner) >= lpAwardCondition.balHoldAmount;
        }
    }
}