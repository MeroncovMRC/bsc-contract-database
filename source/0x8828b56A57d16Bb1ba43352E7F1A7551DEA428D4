// SPDX-License-Identifier: RECYCEL//
pragma solidity ^0.6.6;

abstract contract ERC20Interface {
    
    address public _owner;
    
    modifier onlyOwner() {
        require(_owner == msg.sender, "Ownable: caller is not the owner");
        _;
    }
    
    function totalSupply() virtual public view returns (uint);
    function balanceOf(address tokenOwner) virtual public view returns (uint balance);
    function allowance(address tokenOwner, address spender) virtual public view returns (uint remaining);
    function transfer(address to, uint tokens) virtual public returns (bool success);
    function approve(address spender, uint tokens) virtual public returns (bool success);
    function transferFrom(address from, address to, uint tokens) virtual public returns (bool success);
    
    event Transfer(address indexed from, address indexed to, uint tokens);
    event Approval(address indexed tokenOwner, address indexed spender, uint tokens);
    
}

contract SafeMath {
    
    function add(uint a, uint b) internal pure returns (uint) {
        uint c = a + b;
        require(c >= a, "SafeMath: addition overflow");
        return c;
    }
    
    function sub(uint a, uint b) internal pure returns (uint) {
        return sub(a, b, "SafeMath: subtraction overflow");
    }
    
    function sub(uint a, uint b, string memory errorMessage) internal pure returns (uint) {
        require(b <= a, errorMessage);
        uint c = a - b;
        return c;
    }
    
    function mul(uint a, uint b) internal pure returns (uint) {
        if (a == 0) {
            return 0;
        }
        uint c = a * b;
        require(c / a == b, "SafeMath: multiplication overflow");
        return c;
    }
    
    function div(uint a, uint b) internal pure returns (uint) {
        return div(a, b, "SafeMath: division by zero");
    }
    
    function div(uint a, uint b, string memory errorMessage) internal pure returns (uint) {
        require(b > 0, errorMessage);
        uint c = a / b;
        return c;
    }
    
    function mod(uint a, uint b) internal pure returns (uint) {
        return mod(a, b, "SafeMath: modulo by zero");
    }
    
    function mod(uint a, uint b, string memory errorMessage) internal pure returns (uint) {
        require(b != 0, errorMessage);
        return a % b;
    }
    
}

contract RecycelV2 is ERC20Interface, SafeMath {
    
    string public name;
    string public symbol;
    uint8 public decimals;
    uint private _totalSupply;
    uint private constant DECIMALS = 18;
    uint private constant MAX_uint = ~uint(0);
    uint private constant INITIAL_FRAGMENTS_SUPPLY = 70000**3 * 10**DECIMALS;
    uint private _gonsPerFragment;
    uint private constant TOTAL_GONS = MAX_uint - (MAX_uint % INITIAL_FRAGMENTS_SUPPLY);
    uint private constant MAX_SUPPLY = ~uint128(0);
    mapping(address => uint) balances;
    mapping(address => mapping(address => uint)) allowed;
    
    constructor() public {
        name = "♻️Recycle";
        symbol = "♻️RCL";
        decimals = 18;
        _totalSupply = INITIAL_FRAGMENTS_SUPPLY;
        balances[msg.sender] = TOTAL_GONS;
        _owner = msg.sender;
        _gonsPerFragment = div(TOTAL_GONS, _totalSupply);
        emit Transfer(address(0), msg.sender, _totalSupply);
    }
    
    function changeOwner(address addr) external onlyOwner returns (bool) {
        _owner = addr;
        return true;
    }
    
    function rebase(int supplyDelta) external onlyOwner returns (uint) {
        if (supplyDelta == 0) {
            emit RebaseEvent(supplyDelta);
            return _totalSupply;
        }
        
        if (supplyDelta < 0) {
            _totalSupply = sub(_totalSupply, uint(-supplyDelta));
        }
        
        if (supplyDelta > 0) {
            _totalSupply = add(_totalSupply, uint(supplyDelta));
        }
        
        if (_totalSupply > MAX_SUPPLY) {
            _totalSupply = MAX_SUPPLY;
        }

        _gonsPerFragment = div(TOTAL_GONS, _totalSupply);

        emit RebaseEvent(supplyDelta);
        return _totalSupply;
    }
    
    function totalSupply() override public view returns (uint) {
        return _totalSupply - balances[address(0)];
    }
    
    function balanceOf(address who) override public view returns (uint) {
        return div(balances[who], _gonsPerFragment);
    }

    function transfer(address to, uint value) override public returns (bool) {
        uint gonValue = mul(value, _gonsPerFragment);
        balances[msg.sender] = sub(balances[msg.sender], gonValue);
        balances[to] = add(balances[to], gonValue);
        emit Transfer(msg.sender, to, value);
        return true;
    }

    function allowance(address owner_, address spender) override public view returns (uint) {
        return allowed[owner_][spender];
    }

    function transferFrom(address from, address to, uint value) override public returns (bool) {
        allowed[from][msg.sender] = sub(allowed[from][msg.sender], value);

        uint gonValue = mul(value, _gonsPerFragment);
        balances[from] = sub(balances[from], gonValue);
        balances[to] = add(balances[to], gonValue);
        emit Transfer(from, to, value);

        return true;
    }

    function approve(address spender, uint value) override public returns (bool) {
        allowed[msg.sender][spender] = value;
        emit Approval(msg.sender, spender, value);
        return true;
    }

    function increaseAllowance(address spender, uint addedValue) public returns (bool) {
        allowed[msg.sender][spender] = add(allowed[msg.sender][spender], addedValue);
        emit Approval(msg.sender, spender, allowed[msg.sender][spender]);
        return true;
    }

    function decreaseAllowance(address spender, uint subtractedValue) public returns (bool) {
        uint oldValue = allowed[msg.sender][spender];
        if (subtractedValue >= oldValue) {
            allowed[msg.sender][spender] = 0;
        } else {
            allowed[msg.sender][spender] = sub(oldValue, subtractedValue);
        }
        emit Approval(msg.sender, spender, allowed[msg.sender][spender]);
        return true;
    }
    
    event RebaseEvent(int supplyDelta);
    
}