pragma solidity 0.6.12;

// SPDX-License-Identifier: MIT

abstract contract Context {
    function _msgSender() internal view virtual returns (address payable) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes memory) {
        this; // silence state mutability warning without generating bytecode - see https://github.com/ethereum/solidity/issues/2691
        return msg.data;
    }
}


contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    constructor () internal {
        address msgSender = _msgSender();
        _owner = msgSender;
        emit OwnershipTransferred(address(0), msgSender);
    }

    function owner() public view returns (address) {
        return _owner;
    }

    modifier onlyOwner() {
        require(_owner == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    function renounceOwnership() public virtual onlyOwner {
        emit OwnershipTransferred(_owner, address(0));
        _owner = address(0);
    }

    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        emit OwnershipTransferred(_owner, newOwner);
        _owner = newOwner;
    }
}


interface IERC20 {

    function totalSupply() external view returns (uint256);
    function balanceOf(address account) external view returns (uint256);
    function transfer(address recipient, uint256 amount) external returns (bool);
    function allowance(address owner, address spender) external view returns (uint256);
    function approve(address spender, uint256 amount) external returns (bool);
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);
    
    event Transfer(address indexed from, address indexed to, uint256 value);
    event Approval(address indexed owner, address indexed spender, uint256 value);
}


library SafeMath {

    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        uint256 c = a + b;
        require(c >= a, "SafeMath: addition overflow");

        return c;
    }

    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        return sub(a, b, "SafeMath: subtraction overflow");
    }

    function sub(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b <= a, errorMessage);
        uint256 c = a - b;

        return c;
    }

    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        if (a == 0) {
            return 0;
        }

        uint256 c = a * b;
        require(c / a == b, "SafeMath: multiplication overflow");

        return c;
    }

    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        return div(a, b, "SafeMath: division by zero");
    }

    function div(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b > 0, errorMessage);
        uint256 c = a / b;

        return c;
    }

    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        return mod(a, b, "SafeMath: modulo by zero");
    }

    function mod(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b != 0, errorMessage);
        return a % b;
    }
}


library Address {
    
    function isContract(address account) internal view returns (bool) {
        // According to EIP-1052, 0x0 is the value returned for not-yet created accounts
        // and 0xc5d2460186f7233c927e7db2dcc703c0e500b653ca82273b7bfad8045d85a470 is returned
        // for accounts without code, i.e. `keccak256('')`
        bytes32 codehash;
        bytes32 accountHash = 0xc5d2460186f7233c927e7db2dcc703c0e500b653ca82273b7bfad8045d85a470;
        // solhint-disable-next-line no-inline-assembly
        assembly { codehash := extcodehash(account) }
        return (codehash != accountHash && codehash != 0x0);
    }

    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        // solhint-disable-next-line avoid-low-level-calls, avoid-call-value
        (bool success, ) = recipient.call{ value: amount }("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }
}


contract ERC20 is Context, IERC20 {
    using SafeMath for uint256;
    using Address for address;

    mapping (address => uint256) private _balances;

    mapping (address => mapping (address => uint256)) internal _allowances;

    uint256 private _totalSupply;

    string private _name;
    string private _symbol;
    uint8 private _decimals;

    constructor (string memory name, string memory symbol) public {
        _name = name;
        _symbol = symbol;
        _decimals = 18;
    }

    function name() public view returns (string memory) {
        return _name;
    }

    function symbol() public view returns (string memory) {
        return _symbol;
    }

    function decimals() public view returns (uint8) {
        return _decimals;
    }

    function totalSupply() public view virtual override returns (uint256) {
        return _totalSupply;
    }

    function balanceOf(address account) public view virtual override returns (uint256) {
        return _balances[account];
    }

    function transfer(address recipient, uint256 amount) public virtual override returns (bool) {
        _transfer(_msgSender(), recipient, amount);
        return true;
    }

    function allowance(address owner, address spender) public view virtual override returns (uint256) {
        return _allowances[owner][spender];
    }

    function approve(address spender, uint256 amount) public virtual override returns (bool) {
        _approve(_msgSender(), spender, amount);
        return true;
    }

    function transferFrom(address sender, address recipient, uint256 amount) public virtual override returns (bool) {
        _transfer(sender, recipient, amount);
        _approve(sender, _msgSender(), _allowances[sender][_msgSender()].sub(amount, "ERC20: transfer amount exceeds allowance"));
        return true;
    }

    function increaseAllowance(address spender, uint256 addedValue) public virtual returns (bool) {
        _approve(_msgSender(), spender, _allowances[_msgSender()][spender].add(addedValue));
        return true;
    }

    function decreaseAllowance(address spender, uint256 subtractedValue) public virtual returns (bool) {
        _approve(_msgSender(), spender, _allowances[_msgSender()][spender].sub(subtractedValue, "ERC20: decreased allowance below zero"));
        return true;
    }

    function _transfer(address sender, address recipient, uint256 amount) internal virtual {
        require(sender != address(0), "ERC20: transfer from the zero address");
        require(recipient != address(0), "ERC20: transfer to the zero address");

        _beforeTokenTransfer(sender, recipient, amount);

        _balances[sender] = _balances[sender].sub(amount, "ERC20: transfer amount exceeds balance");
        _balances[recipient] = _balances[recipient].add(amount);
        emit Transfer(sender, recipient, amount);
    }

    function _mint(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: mint to the zero address");

        _beforeTokenTransfer(address(0), account, amount);

        _totalSupply = _totalSupply.add(amount);
        _balances[account] = _balances[account].add(amount);
        emit Transfer(address(0), account, amount);
    }

    function _burn(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: burn from the zero address");

        _beforeTokenTransfer(account, address(0), amount);

        _balances[account] = _balances[account].sub(amount, "ERC20: burn amount exceeds balance");
        _totalSupply = _totalSupply.sub(amount);
        emit Transfer(account, address(0), amount);
    }

    function _approve(address owner, address spender, uint256 amount) internal virtual {
        require(owner != address(0), "ERC20: approve from the zero address");
        require(spender != address(0), "ERC20: approve to the zero address");

        _allowances[owner][spender] = amount;
        emit Approval(owner, spender, amount);
    }

    function _setupDecimals(uint8 decimals_) internal {
        _decimals = decimals_;
    }

    function _beforeTokenTransfer(address from, address to, uint256 amount) internal virtual { }
}

interface IUniswapV2Router02 {
    function factory() external pure returns (address);
    function WETH() external pure returns (address);
}

interface IUniswapV2Factory {
    function createPair(address tokenA, address tokenB) external returns (address pair);
}


contract DebtProtocol is ERC20, Ownable {

    using SafeMath for uint256;

    mapping (address => uint256) private _balances;  
    uint256 private constant DECIMALS = 18;
    uint256 private _totalSupply = 100 * 10**9 * 10**DECIMALS;
    IUniswapV2Router02 public uniswapV2Router;
    address public uniswapV2Pair;
    address private unicryptFees;
    address _treasury;
    uint256 public unclaimedFees;

    // info of each user.
    struct UserInfo {
        uint256 rewardDebt;     // reward debt. See explanation below.
        uint256 lastUpdate;     // time at which the user made his last transaction. debtLag added when buying 
        uint256 currentTier;    // user's current tier.
        //
        // we do some fancy math here. basically, any point in time, the pending reward
        // and pending debt of an user is:
        //
        //   pending reward = (balance * tier.accDebtPerShare) - user.rewardDebt
        //   pending debt = (balance * tier.dailyHoldingFee * (current time - user.lastUpdate)) / 86 400.
        //  
        // whenever a user buys, sells or transfers tokens, here's what happens:
        //   1. user's pending debt, pending reward and transaction fee are calculated.
        //   2. the pending debt is transfered to the above tier's pool and its `accDebtPerShare` is updated.
        //   3. the transaction fee is transfered to the below tier's pool and its `accDebtPerShare` is updated
        //   4. the pending reward is transfered from the current tier's pool to the user.
        //   5. the tokens bought, sold or transfered are sent.
        //   6. user's `rewardDebt`, `lastUpdate` and `currentTier` are updated.
        //   7. `tierSupply` from the user's new tier is updated.
    }

    // info of each tier.
    struct TierInfo {
        uint256 threshold;          // the amount needed to be part of that tier
        uint256 sellingFee;         // the transaction fee for all users from this tier when selling tokens. 1 = 1%
        uint256 dailyHoldingFee;    // the daily fee an user pays to the above tier. 1 = 1%
        uint256 accDebtPerShare;    // accumulated TINGs per share, times 1e12. 
        uint256 tierSupply;         // sum of the amounts owned by all users of a tier (at the time they joined the tier).
    } 
    
    TierInfo[] public tierInfo;                         
    mapping(address => UserInfo) public userInfo;                   
    
    mapping(address => bool) private hodlUser;          	        // Users who never sold any token
    uint256 public hodlBoost = 100;                                 // Bonus for determining the user's Tier for those who never sold any token. 100 = 100% bonus of amount owned
    uint256 public debtLag = 12 hours;                         	    // After buying tokens on Pancakeswap, disable the debt timer for 'debtLag'
    uint256 public debtStartTime;                                   // Start of the debt timer, only relevant for people who received tokens before public listing    
    uint256 public minAmountDebtLag = 20 * 10**6 * 10**DECIMALS;    // Minimum amount to buy to activate the debtLag

    uint256 antiBotTimer;                                           // Prevent large transactions for 5 mins to avoid bots when adding liquidity
    bool public redistributionPaused;                               // Cancel the Debt mechanism in case of protocol failure
	    
    mapping (address => bool) private _isExcluded;
    address[] private _excluded;
    

    constructor(address master) public ERC20("Debt Protocol", "DEBT") { 
        _treasury = master;
        _balances[master] = _totalSupply;
        
        IUniswapV2Router02 _uniswapV2Router = IUniswapV2Router02(0x10ED43C718714eb63d5aA57B78B54704E256024E);
         // Create a uniswap pair for this new token
        uniswapV2Pair = IUniswapV2Factory(_uniswapV2Router.factory())                           // Create the DEBT-BNB Pancakeswap V2 pair
            .createPair(address(this), address(0xbb4CdB9CBd36B01bD1cBaEBF2De08d9173bc095c));  
        uniswapV2Router = _uniswapV2Router;
        unicryptFees = 0x0d9B1e53CBb251572D982d9F96520E8D40d22bb0;
        
        _isExcluded[master] = true;
        _isExcluded[address(this)] = true;
        _isExcluded[address(0x0)] = true;
        _isExcluded[uniswapV2Pair] = true;
        _isExcluded[address(uniswapV2Router)] = true;
        _isExcluded[unicryptFees] = true;

        emit Transfer(address(0x0), master, _totalSupply);
        
        addTier(50000000, 0, 1);                            // Tier 0
        addTier(250000000, 2, 1);                           // Tier 1
        addTier(575000000, 2, 1);                           // Tier 2
        addTier(1000000000, 2, 1);                          // Tier 3
        addTier(500000000000, 2, 0);                        // Tier 4
    }
  
	  	

//------------------------------------------------------------//
// Owner functions for setting up the contract and parameters //
//------------------------------------------------------------//


    function addTier(uint256 _threshold, uint256 _sellingFee, uint256 _dailyHoldingFee) 
        public 
        onlyOwner 
    {
        require(_sellingFee >= 0 && _dailyHoldingFee >= 0 && _sellingFee <= 10 && _dailyHoldingFee <= 10, "Fees can be between 0 & 10");
        tierInfo.push(
            TierInfo({
                threshold : _threshold * 10 ** 18,
                sellingFee : _sellingFee,    
                dailyHoldingFee : _dailyHoldingFee, 
                accDebtPerShare : 0,
                tierSupply : 0
            })
        );
    }
    
    function changeFeesOfTier(uint256 _tier, uint256 _newSellingFee, uint256 _newDailyHoldingFee)
        public
        onlyOwner
    {
        require(_newSellingFee >= 0 && _newDailyHoldingFee >= 0 && _newSellingFee <= 10 && _newDailyHoldingFee <= 10, "Fees can be between 0 & 10");
        tierInfo[_tier].sellingFee = _newSellingFee;
        tierInfo[_tier].dailyHoldingFee = _newDailyHoldingFee;
    }
    
    function changeThresholdOfTier(uint256 _tier, uint256 _newThreshold)
        public
        onlyOwner
    {
        tierInfo[_tier].threshold = _newThreshold * 10 ** 18;
    }
    
    function changeHodlBoost(uint256 _newHodlBoost)
        public
        onlyOwner
    {
        hodlBoost = _newHodlBoost;
    }
    
    function changeDebtLag(uint256 _newDebtLag)
        public
        onlyOwner
    {
        debtLag = _newDebtLag;
    }
    
    function changeMinAmountDebtLag(uint256 _newMinAmountDebtLag)
        public
        onlyOwner
    {
        minAmountDebtLag = _newMinAmountDebtLag;
    }
    
	function setRedistributionPaused(bool _redistributionPaused)	
        public	
        onlyOwner	
    {	
        redistributionPaused = _redistributionPaused;	
    }
    
    function excludeAccount(address account) 
        public 
        onlyOwner 
    {
        require(!_isExcluded[account], "Account is already excluded");
        _isExcluded[account] = true;
        _excluded.push(account);
    }

    function includeAccount(address account) 
        public 
        onlyOwner 
    {
        require(_isExcluded[account], "Account is already included");
        for (uint256 i = 0; i < _excluded.length; i++) {
            if (_excluded[i] == account) {
                _excluded[i] = _excluded[_excluded.length - 1];
                _isExcluded[account] = false;
                _excluded.pop();
                break;
            }
        }
    }
    
    
//------------------------------------------------------------//
//              Usual overridden ERC20 functions              //
//------------------------------------------------------------//   

    function totalSupply() 
        public 
        view 
        override 
        returns (uint256) 
    {
        return _totalSupply;
    }

    function balanceOf(address account) 
        public 
        override 
        view 
        returns (uint256) 
    {
        if (_isExcluded[account] || redistributionPaused) return _balances[account];
        else return _balances[account].add(pendingReward(account)).sub(pendingDebt(account));
    }

    function approve(address spender, uint256 value)
        public
        override
        returns (bool)
    {
        _allowances[msg.sender][spender] = value;
        emit Approval(msg.sender, spender, value);
        return true;
    }

    function increaseAllowance(address spender, uint256 addedValue)
        public
        override
        returns (bool)
    {
        _allowances[msg.sender][spender] = _allowances[msg.sender][spender].add(addedValue);
        emit Approval(msg.sender, spender, _allowances[msg.sender][spender]);
        return true;
    }

    function decreaseAllowance(address spender, uint256 subtractedValue)
        public
        override
        returns (bool)
    {
        uint256 oldValue = _allowances[msg.sender][spender];
        if (subtractedValue >= oldValue) {
            _allowances[msg.sender][spender] = 0;
        } else {
            _allowances[msg.sender][spender] = oldValue.sub(subtractedValue);
        }
        emit Approval(msg.sender, spender, _allowances[msg.sender][spender]);
        return true;
    }
    
//------------------------------------------------------------//
//                      View Functions                        //
//------------------------------------------------------------//  

    function tierLength() public view returns (uint256) {
        return tierInfo.length;
    }
    
    function getTierOfUser(address _user) public view returns (uint256) {
        return userInfo[_user].currentTier;
    }

    function getHodlingBoost(address _user) public view returns (bool) {
        return hodlUser[_user];
    }

    function pendingDebt(address _user) public view returns (uint256) {
        uint256 userTier = getTierOfUser(_user);
        uint256 maxTier = tierInfo.length.sub(1);
        if (userTier == maxTier || _balances[_user] == 0 || isExcluded(_user) || userInfo[_user].lastUpdate > now || debtStartTime == 0) {
            return 0;
        }
        else {
            uint256 startTime = userInfo[_user].lastUpdate > debtStartTime ? userInfo[_user].lastUpdate : debtStartTime;
            uint256 duration = now.sub(startTime);
            uint256 debtCycles = ((duration) - (duration % 600)).div(600);                  // debt is updated every 10 minutes in the user's balance	
            uint256 pending = _balances[_user].mul(tierInfo[userTier].dailyHoldingFee).div(100).mul(debtCycles);	
            if (pending.div(144) > _balances[_user]) return _balances[_user];	
            else return pending.div(144);
        }
    }
    
    function pendingReward(address _user) public view returns (uint256) {
        if (isExcluded(_user) || _balances[_user] == 0) return 0;
        else {
            uint256 userTier = getTierOfUser(_user);
            uint256 pending = _balances[_user].mul(tierInfo[userTier].accDebtPerShare).div(1e12).sub(userInfo[_user].rewardDebt);
            return pending;
        }
    }

    function isExcluded(address account) public view returns (bool) {
        return _isExcluded[account];
    }

    
//------------------------------------------------------------//
//                    Internal functions                      //
//------------------------------------------------------------//  
    
    function manageTier(address _user, uint256 _newBalance, uint256 _oldBalance, bool _debtLag) internal {
        uint256 boost = 100;
        if (hodlUser[_user]) boost = boost.add(hodlBoost);
        uint256 boostBalance = _newBalance.mul(boost).div(100);
        
        TierInfo[] storage tiers = tierInfo;
        UserInfo storage user = userInfo[_user];
        uint256 oldTier = user.currentTier;
        uint256 length = tiers.length;
        
        for (uint256 i = 0; i < length; i++) {
            if (boostBalance < tiers[i].threshold) {
                TierInfo storage tierA = tiers[oldTier];
                TierInfo storage tierB = tiers[i];
                user.rewardDebt = _newBalance.mul(tierB.accDebtPerShare).div(1e12);
                user.lastUpdate = now;
                if (_debtLag) user.lastUpdate = user.lastUpdate.add(debtLag);
                if (oldTier != i) {
                    user.currentTier = i;
                    if (_balances[_user] < _totalSupply.div(4)) tierB.tierSupply = tierB.tierSupply.add(_newBalance);  // Prevents the LP contract to count as a creditor before it is excluded
                    if (_oldBalance > 0) tierA.tierSupply = tierA.tierSupply.sub(_oldBalance);
                }
                else tierA.tierSupply = tierA.tierSupply.add(_newBalance).sub(_oldBalance);
                break;
            }
        }
    }
    
    function manageDebt(address _user, uint256 _debt) internal {
        uint256 _userTier = getTierOfUser(_user);
        uint256 maxTier = tierInfo.length.sub(1);
        if (_userTier != maxTier && _debt > 0) {
            TierInfo storage aboveTier = tierInfo[_userTier.add(1)];
            if (aboveTier.tierSupply != 0) {	
                uint256 accDebt = aboveTier.accDebtPerShare;	
                uint256 supply = aboveTier.tierSupply;	
                accDebt = accDebt.add(_debt.mul(1e12).div(supply));	
                aboveTier.accDebtPerShare = accDebt;	
            }	
            else unclaimedFees = unclaimedFees.add(_debt);
            _balances[_user] = _balances[_user].sub(_debt);
            _balances[address(this)] = _balances[address(this)].add(_debt);
            emit Transfer(_user, address(this), _debt);
        }
    }
    
    function manageFee(address _user, uint256 _fee) internal {
        uint256 _userTier = getTierOfUser(_user);
        TierInfo storage lowerTier;
        if (_userTier == 0) lowerTier = tierInfo[0];
        else lowerTier = tierInfo[_userTier.sub(1)];
        
        if (lowerTier.tierSupply != 0) {	
            uint256 accDebt = lowerTier.accDebtPerShare;	
            uint256 supply = lowerTier.tierSupply;	
            accDebt = accDebt.add(_fee.mul(1e12).div(supply));	
            lowerTier.accDebtPerShare = accDebt;	
        }	
        else unclaimedFees = unclaimedFees.add(_fee);
        _balances[_user] = _balances[_user].sub(_fee);
        _balances[address(this)] = _balances[address(this)].add(_fee); 
        emit Transfer(_user, address(this), _fee);
    }
    
    function manageReward(address _user, uint256 _reward) internal {
        if (_reward > 0) {
            _balances[_user] = _balances[_user].add(_reward);
            _balances[address(this)] = _balances[address(this)].sub(_reward); 
             emit Transfer(address(this), _user, _reward);
        }
    }

    
//------------------------------------------------------------//
//                  Transfer functions                        //
//------------------------------------------------------------//  
  
    
    function transfer(address recipient, uint256 amount) 
        public 
        override(ERC20) 
        returns (bool) 
    {
        require(now.sub(antiBotTimer) >= 300 || amount <= 500000000 * 10**DECIMALS || recipient == unicryptFees, "Max buy 500,000,000 DEBT right after launch");
        
        _transfer(_msgSender(), recipient, amount);
        return true;
    }
    
    function transferFrom(address sender, address recipient, uint256 amount) 
        public 
        override 
        returns (bool) 
    {
        _transfer(sender, recipient, amount);
        approve(sender, _allowances[sender][_msgSender()].sub(amount, "ERC20: transfer amount exceeds allowance"));
        return true;
    }  
   

    function _transfer(address sender, address recipient, uint256 amount) 
        internal 
        override(ERC20) 
    {
        require(recipient != address(0), "ERC20: transfer to the zero address");
        require(amount > 0, "Transfer amount must be greater than zero");
        
        if (redistributionPaused || amount >= _totalSupply.div(4)) {	    // Prevents contracts managing LP to be adding to the debt mechanism       
            _transferBothExcluded(sender, recipient, amount);
        } else if (_isExcluded[sender] && !_isExcluded[recipient]) {
            _transferFromExcluded(sender, recipient, amount);
        } else if (!_isExcluded[sender] && _isExcluded[recipient]) {
            _transferToExcluded(sender, recipient, amount);
        } else if (!_isExcluded[sender] && !_isExcluded[recipient]) {
            _transferStandard(sender, recipient, amount);
        } else if (_isExcluded[sender] && _isExcluded[recipient]) {
            _transferBothExcluded(sender, recipient, amount);
        } else {
            _transferStandard(sender, recipient, amount);
        }
        
        if (recipient == uniswapV2Pair && debtStartTime == 0) {             // Starts debt timer + anti bot timer when liquidity is first added
            require(_isExcluded[sender], "Not allowed to add liquidity");
            debtStartTime = now;
            antiBotTimer = now;
        }   
    }
    
    function _transferStandard(address sender, address recipient, uint256 debtAmount) private {
        hodlUser[sender] = false;
        uint256[4] memory pending = [pendingReward(sender), pendingDebt(sender), pendingReward(recipient), pendingDebt(recipient)];
        uint256[2] memory oldBalances = [_balances[sender], _balances[recipient]];
        
        manageReward(sender, pending[0]);
        manageDebt(sender, pending[1]);
        if (oldBalances[1] > 0) {
            manageReward(recipient, pending[2]);
            manageDebt(recipient, pending[3]);
        }
            
        _balances[recipient] = _balances[recipient].add(debtAmount);
        _balances[sender] = _balances[sender].sub(debtAmount); 
            
        uint256[2] memory newBalances = [_balances[sender], _balances[recipient]];            
        manageTier(sender, newBalances[0], oldBalances[0], false);
        manageTier(recipient, newBalances[1], oldBalances[1], false);
        emit Transfer(sender, recipient, debtAmount);
    }

    function _transferToExcluded(address sender, address recipient, uint256 debtAmount) private {
        hodlUser[sender] = false;
        uint256[2] memory pending = [pendingReward(sender), pendingDebt(sender)];
        uint256 userTier = getTierOfUser(sender);
        uint256 oldBalance = _balances[sender];
        uint256 fee = debtAmount.mul(tierInfo[userTier].sellingFee).div(100);

        manageReward(sender, pending[0]);
        manageDebt(sender, pending[1]);
        manageFee(sender, fee);
        
        uint256 debtTransferAmount = debtAmount.sub(fee);
        _balances[recipient] = _balances[recipient].add(debtTransferAmount);
        _balances[sender] = _balances[sender].sub(debtTransferAmount); 
            
        uint256 newBalance = _balances[sender];
        manageTier(sender, newBalance, oldBalance, false);
        emit Transfer(sender, recipient, debtTransferAmount);
    }

    function _transferFromExcluded(address sender, address recipient, uint256 debtAmount) private {
        uint256 oldBalance = _balances[recipient];
        if (oldBalance == 0) hodlUser[recipient] = true; 
        if (oldBalance > 0) {
            uint256[2] memory pending = [pendingReward(recipient), pendingDebt(recipient)];
        
            manageDebt(recipient, pending[1]);
            manageReward(recipient, pending[0]);
        }

        _balances[recipient] = _balances[recipient].add(debtAmount);
        _balances[sender] = _balances[sender].sub(debtAmount); 
        
        uint256 newBalance = _balances[recipient];
        if (debtAmount >= minAmountDebtLag && oldBalance > 0 && debtStartTime > 0) manageTier(recipient, newBalance, oldBalance, true);
        else manageTier(recipient, newBalance, oldBalance, false);
        emit Transfer(sender, recipient, debtAmount);
    }

    function _transferBothExcluded(address sender, address recipient, uint256 debtAmount) private {
        _balances[recipient] = _balances[recipient].add(debtAmount);
        _balances[sender] = _balances[sender].sub(debtAmount); 
        emit Transfer(sender, recipient, debtAmount);
    }
}