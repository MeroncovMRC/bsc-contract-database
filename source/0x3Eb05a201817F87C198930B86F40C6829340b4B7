
// File: contracts/Governance/DMT.sol
/**
 *Submitted for verification at BscScan.com on 2020-09-27
*/

pragma solidity ^0.5.16;

import "../Utils/SafeMath.sol";
import "../Manager/ManagerProxy.sol";

contract DMT is ManagerProxy {
    using SafeMath for uint;

    /// @notice BEP-20 token name for this token
    string public name;

    /// @notice BEP-20 token symbol for this token
    string public symbol;

    /// @notice BEP-20 token decimals for this token
    uint8 public decimals;

    /// @notice Total number of tokens in circulation
    uint public totalSupply;

    /// @notice Allowance amounts on behalf of others
    mapping (address => mapping (address => uint96)) internal allowances;

    /// @notice Official record of token balances for each account
    mapping (address => uint96) internal balances;

    /// @notice A record of each accounts delegate
    mapping (address => address) public delegates;

    /// @notice A checkpoint for marking number of votes from a given block
    struct Checkpoint {
        uint32 fromBlock;
        uint96 votes;
    }

    /// @notice A record of votes checkpoints for each account, by index
    mapping (address => mapping (uint32 => Checkpoint)) public checkpoints;

    /// @notice The number of checkpoints for each account
    mapping (address => uint32) public numCheckpoints;

    /// @notice The EIP-712 typehash for the contract's domain
    bytes32 public constant DOMAIN_TYPEHASH = keccak256("EIP712Domain(string name,uint256 chainId,address verifyingContract)");

    /// @notice The EIP-712 typehash for the delegation struct used by the contract
    bytes32 public constant DELEGATION_TYPEHASH = keccak256("Delegation(address delegatee,uint256 nonce,uint256 expiry)");

    /// @notice A record of states for signing / validating signatures
    mapping (address => uint) public nonces;

    /// @notice An event thats emitted when an account changes its delegate
    event DelegateChanged(address indexed delegator, address indexed fromDelegate, address indexed toDelegate);

    /// @notice An event thats emitted when a delegate account's vote balance changes
    event DelegateVotesChanged(address indexed delegate, uint previousBalance, uint newBalance);

    /// @notice The standard BEP-20 transfer event
    event Transfer(address indexed from, address indexed to, uint256 amount);

    /// @notice The standard BEP-20 approval event
    event Approval(address indexed owner, address indexed spender, uint256 amount);

    /**
     * @notice Construct a new DMT token
     * @param _account The initial account to grant all the tokens
     */
    constructor(string memory _name, string memory _symbol, uint8 _decimals, address _account) public {
        totalSupply = 500000000e18;
        name = _name;
        symbol = _symbol;
        decimals = _decimals;
        balances[_account] = uint96(totalSupply);
        emit Transfer(address(0), _account, totalSupply);
    }

    function mint(address _account, uint _amount) external ValidDMTState CheckPermit("DMTAdmin") OutBlacklist2(msg.sender, _account) {
        uint96 amount96 = safe96(_amount, "amount exceeds 96 bits");
        uint96 newAmount = add96(balances[_account], amount96, "amount overflows");
        balances[_account] = newAmount;
        totalSupply = totalSupply.add(_amount);
        emit Transfer(address(0), _account, _amount);
    }

    function burn(address _account, uint _amount) external ValidDMTState OutBlacklist2(msg.sender, _account) {
        uint96 amount96 = safe96(_amount, "amount exceeds 96 bits");
        require(balances[_account] >= amount96, "DMT/insufficient-balance");
        if (_account != msg.sender && allowances[_account][msg.sender] != uint96(-1)) {
            require(allowances[_account][msg.sender] >= amount96, "DMT/insufficient-allowance");
            uint96 newAllowance = sub96(allowances[_account][msg.sender], amount96, "amount exceeds spender allowance");
            allowances[_account][msg.sender] = newAllowance;
        }

        uint96 newBalance = sub96(balances[_account], amount96, "amount exceeds spender allowance");
        balances[_account] = newBalance;
        totalSupply = totalSupply.sub(_amount);
        emit Transfer(_account, address(0), _amount);
    }

    /// IERC20

    /**
     * @notice Get the number of tokens `spender` is approved to spend on behalf of `account`
     * @param account The address of the account holding the funds
     * @param spender The address of the account spending the funds
     * @return The number of tokens approved
     */
    function allowance(address account, address spender) external view returns (uint) {
        return allowances[account][spender];
    }

    /**
     * @notice Approve `spender` to transfer up to `amount` from `src`
     * @dev This will overwrite the approval amount for `spender`
     * @param spender The address of the account which may transfer tokens
     * @param rawAmount The number of tokens that are approved (2^256-1 means infinite)
     * @return Whether or not the approval succeeded
     */
    function approve(address spender, uint rawAmount) external ValidDMTState OutBlacklist2(msg.sender, spender) returns (bool) {
        uint96 amount;
        if (rawAmount == uint(-1)) {
            amount = uint96(-1);
        } else {
            amount = safe96(rawAmount, "DMT::approve: amount exceeds 96 bits");
        }

        allowances[msg.sender][spender] = amount;

        emit Approval(msg.sender, spender, amount);
        return true;
    }

    /**
     * @notice Get the number of tokens held by the `account`
     * @param account The address of the account to get the balance of
     * @return The number of tokens held
     */
    function balanceOf(address account) external view returns (uint) {
        return balances[account];
    }

    /**
     * @notice Transfer `amount` tokens from `msg.sender` to `dst`
     * @param dst The address of the destination account
     * @param rawAmount The number of tokens to transfer
     * @return Whether or not the transfer succeeded
     */
    function transfer(address dst, uint rawAmount) external ValidDMTState OutBlacklist2(msg.sender, dst) returns (bool) {
        uint96 amount = safe96(rawAmount, "DMT::transfer: amount exceeds 96 bits");
        _transferTokens(msg.sender, dst, amount);
        return true;
    }

    /**
     * @notice Transfer `amount` tokens from `src` to `dst`
     * @param src The address of the source account
     * @param dst The address of the destination account
     * @param rawAmount The number of tokens to transfer
     * @return Whether or not the transfer succeeded
     */
    function transferFrom(address src, address dst, uint rawAmount) external ValidDMTState OutBlacklist3(msg.sender, src, dst) returns (bool) {
        address spender = msg.sender;
        uint96 spenderAllowance = allowances[src][spender];
        uint96 amount = safe96(rawAmount, "DMT::approve: amount exceeds 96 bits");

        if (spender != src && spenderAllowance != uint96(-1)) {
            uint96 newAllowance = sub96(spenderAllowance, amount, "DMT::transferFrom: transfer amount exceeds spender allowance");
            allowances[src][spender] = newAllowance;

            emit Approval(src, spender, newAllowance);
        }

        _transferTokens(src, dst, amount);
        return true;
    }

    /**
     * @notice Delegate votes from `msg.sender` to `delegatee`
     * @param delegatee The address to delegate votes to
     */
    function delegate(address delegatee) public ValidDMTState OutBlacklist2(msg.sender, delegatee) {
        return _delegate(msg.sender, delegatee);
    }

    /**
     * @notice Delegates votes from signatory to `delegatee`
     * @param delegatee The address to delegate votes to
     * @param nonce The contract state required to match the signature
     * @param expiry The time at which to expire the signature
     * @param v The recovery byte of the signature
     * @param r Half of the ECDSA signature pair
     * @param s Half of the ECDSA signature pair
     */
    function delegateBySig(address delegatee, uint nonce, uint expiry, uint8 v, bytes32 r, bytes32 s) public ValidDMTState OutBlacklist2(msg.sender, delegatee) {
        bytes32 domainSeparator = keccak256(abi.encode(DOMAIN_TYPEHASH, keccak256(bytes(name)), getChainId(), address(this)));
        bytes32 structHash = keccak256(abi.encode(DELEGATION_TYPEHASH, delegatee, nonce, expiry));
        bytes32 digest = keccak256(abi.encodePacked("\x19\x01", domainSeparator, structHash));
        address signatory = ecrecover(digest, v, r, s);
        require(signatory != address(0), "DMT::delegateBySig: invalid signature");
        require(nonce == nonces[signatory]++, "DMT::delegateBySig: invalid nonce");
        require(now <= expiry, "DMT::delegateBySig: signature expired");
        return _delegate(signatory, delegatee);
    }

    /**
     * @notice Gets the current votes balance for `account`
     * @param account The address to get votes balance
     * @return The number of current votes for `account`
     */
    function getCurrentVotes(address account) external view returns (uint96) {
        uint32 nCheckpoints = numCheckpoints[account];
        return nCheckpoints > 0 ? checkpoints[account][nCheckpoints - 1].votes : 0;
    }

    /**
     * @notice Determine the prior number of votes for an account as of a block number
     * @dev Block number must be a finalized block or else this function will revert to prevent misinformation.
     * @param account The address of the account to check
     * @param blockNumber The block number to get the vote balance at
     * @return The number of votes the account had as of the given block
     */
    function getPriorVotes(address account, uint blockNumber) public view returns (uint96) {
        require(blockNumber < block.number, "DMT::getPriorVotes: not yet determined");

        uint32 nCheckpoints = numCheckpoints[account];
        if (nCheckpoints == 0) {
            return 0;
        }

        // First check most recent balance
        if (checkpoints[account][nCheckpoints - 1].fromBlock <= blockNumber) {
            return checkpoints[account][nCheckpoints - 1].votes;
        }

        // Next check implicit zero balance
        if (checkpoints[account][0].fromBlock > blockNumber) {
            return 0;
        }

        uint32 lower = 0;
        uint32 upper = nCheckpoints - 1;
        while (upper > lower) {
            uint32 center = upper - (upper - lower) / 2; // ceil, avoiding overflow
            Checkpoint memory cp = checkpoints[account][center];
            if (cp.fromBlock == blockNumber) {
                return cp.votes;
            } else if (cp.fromBlock < blockNumber) {
                lower = center;
            } else {
                upper = center - 1;
            }
        }
        return checkpoints[account][lower].votes;
    }

    function _delegate(address delegator, address delegatee) internal {
        address currentDelegate = delegates[delegator];
        uint96 delegatorBalance = balances[delegator];
        delegates[delegator] = delegatee;

        emit DelegateChanged(delegator, currentDelegate, delegatee);

        _moveDelegates(currentDelegate, delegatee, delegatorBalance);
    }

    function _transferTokens(address src, address dst, uint96 amount) internal {
        require(src != address(0), "DMT::_transferTokens: cannot transfer from the zero address");
        require(dst != address(0), "DMT::_transferTokens: cannot transfer to the zero address");

        balances[src] = sub96(balances[src], amount, "DMT::_transferTokens: transfer amount exceeds balance");
        balances[dst] = add96(balances[dst], amount, "DMT::_transferTokens: transfer amount overflows");
        emit Transfer(src, dst, amount);

        _moveDelegates(delegates[src], delegates[dst], amount);
    }

    function _moveDelegates(address srcRep, address dstRep, uint96 amount) internal {
        if (srcRep != dstRep && amount > 0) {
            if (srcRep != address(0)) {
                uint32 srcRepNum = numCheckpoints[srcRep];
                uint96 srcRepOld = srcRepNum > 0 ? checkpoints[srcRep][srcRepNum - 1].votes : 0;
                uint96 srcRepNew = sub96(srcRepOld, amount, "DMT::_moveVotes: vote amount underflows");
                _writeCheckpoint(srcRep, srcRepNum, srcRepOld, srcRepNew);
            }

            if (dstRep != address(0)) {
                uint32 dstRepNum = numCheckpoints[dstRep];
                uint96 dstRepOld = dstRepNum > 0 ? checkpoints[dstRep][dstRepNum - 1].votes : 0;
                uint96 dstRepNew = add96(dstRepOld, amount, "DMT::_moveVotes: vote amount overflows");
                _writeCheckpoint(dstRep, dstRepNum, dstRepOld, dstRepNew);
            }
        }
    }

    function _writeCheckpoint(address delegatee, uint32 nCheckpoints, uint96 oldVotes, uint96 newVotes) internal {
        uint32 blockNumber = safe32(block.number, "DMT::_writeCheckpoint: block number exceeds 32 bits");

        if (nCheckpoints > 0 && checkpoints[delegatee][nCheckpoints - 1].fromBlock == blockNumber) {
            checkpoints[delegatee][nCheckpoints - 1].votes = newVotes;
        } else {
            checkpoints[delegatee][nCheckpoints] = Checkpoint(blockNumber, newVotes);
            numCheckpoints[delegatee] = nCheckpoints + 1;
        }

        emit DelegateVotesChanged(delegatee, oldVotes, newVotes);
    }

    function safe32(uint n, string memory errorMessage) internal pure returns (uint32) {
        require(n < 2**32, errorMessage);
        return uint32(n);
    }

    function safe96(uint n, string memory errorMessage) internal pure returns (uint96) {
        require(n < 2**96, errorMessage);
        return uint96(n);
    }

    function add96(uint96 a, uint96 b, string memory errorMessage) internal pure returns (uint96) {
        uint96 c = a + b;
        require(c >= a, errorMessage);
        return c;
    }

    function sub96(uint96 a, uint96 b, string memory errorMessage) internal pure returns (uint96) {
        require(b <= a, errorMessage);
        return a - b;
    }

    function getChainId() internal pure returns (uint) {
        uint256 chainId;
        assembly { chainId := chainid() }
        return chainId;
    }
}

// File: contracts/Utils/SafeMath.sol
pragma solidity ^0.5.16;

/**
 * @dev Wrappers over Solidity's arithmetic operations with added overflow
 * checks.
 *
 * Arithmetic operations in Solidity wrap on overflow. This can easily result
 * in bugs, because programmers usually assume that an overflow raises an
 * error, which is the standard behavior in high level programming languages.
 * `SafeMath` restores this intuition by reverting the transaction when an
 * operation overflows.
 *
 * Using this library instead of the unchecked operations eliminates an entire
 * class of bugs, so it's recommended to use it always.
 */
library SafeMath {
  /**
   * @dev Returns the addition of two unsigned integers, reverting on
   * overflow.
   *
   * Counterpart to Solidity's `+` operator.
   *
   * Requirements:
   * - Addition cannot overflow.
   */
  function add(uint256 a, uint256 b) internal pure returns (uint256) {
    return add(a, b, "SafeMath: addition overflow");
  }

  /**
   * @dev Returns the subtraction of two unsigned integers, reverting with custom message on
   * overflow (when the result is negative).
   *
   * Counterpart to Solidity's `-` operator.
   *
   * Requirements:
   * - Subtraction cannot overflow.
   */
  function add(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
    uint256 c = a + b;
    require(c >= a, errorMessage);

    return c;
  }

  /**
   * @dev Returns the subtraction of two unsigned integers, reverting on
   * overflow (when the result is negative).
   *
   * Counterpart to Solidity's `-` operator.
   *
   * Requirements:
   * - Subtraction cannot overflow.
   */
  function sub(uint256 a, uint256 b) internal pure returns (uint256) {
    return sub(a, b, "SafeMath: subtraction overflow");
  }

  /**
   * @dev Returns the subtraction of two unsigned integers, reverting with custom message on
   * overflow (when the result is negative).
   *
   * Counterpart to Solidity's `-` operator.
   *
   * Requirements:
   * - Subtraction cannot overflow.
   */
  function sub(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
    require(b <= a, errorMessage);
    uint256 c = a - b;

    return c;
  }

  /**
   * @dev Returns the multiplication of two unsigned integers, reverting on
   * overflow.
   *
   * Counterpart to Solidity's `*` operator.
   *
   * Requirements:
   * - Multiplication cannot overflow.
   */
  function mul(uint256 a, uint256 b) internal pure returns (uint256) {
    // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
    // benefit is lost if 'b' is also tested.
    // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522
    if (a == 0) {
      return 0;
    }

    uint256 c = a * b;
    require(c / a == b, "SafeMath: multiplication overflow");

    return c;
  }

  /**
   * @dev Returns the integer division of two unsigned integers. Reverts on
   * division by zero. The result is rounded towards zero.
   *
   * Counterpart to Solidity's `/` operator. Note: this function uses a
   * `revert` opcode (which leaves remaining gas untouched) while Solidity
   * uses an invalid opcode to revert (consuming all remaining gas).
   *
   * Requirements:
   * - The divisor cannot be zero.
   */
  function div(uint256 a, uint256 b) internal pure returns (uint256) {
    return div(a, b, "SafeMath: division by zero");
  }

  /**
   * @dev Returns the integer division of two unsigned integers. Reverts with custom message on
   * division by zero. The result is rounded towards zero.
   *
   * Counterpart to Solidity's `/` operator. Note: this function uses a
   * `revert` opcode (which leaves remaining gas untouched) while Solidity
   * uses an invalid opcode to revert (consuming all remaining gas).
   *
   * Requirements:
   * - The divisor cannot be zero.
   */
  function div(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
    // Solidity only automatically asserts when dividing by 0
    require(b > 0, errorMessage);
    uint256 c = a / b;
    // assert(a == b * c + a % b); // There is no case in which this doesn't hold

    return c;
  }

  /**
   * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
   * Reverts when dividing by zero.
   *
   * Counterpart to Solidity's `%` operator. This function uses a `revert`
   * opcode (which leaves remaining gas untouched) while Solidity uses an
   * invalid opcode to revert (consuming all remaining gas).
   *
   * Requirements:
   * - The divisor cannot be zero.
   */
  function mod(uint256 a, uint256 b) internal pure returns (uint256) {
    return mod(a, b, "SafeMath: modulo by zero");
  }

  /**
   * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
   * Reverts with custom message when dividing by zero.
   *
   * Counterpart to Solidity's `%` operator. This function uses a `revert`
   * opcode (which leaves remaining gas untouched) while Solidity uses an
   * invalid opcode to revert (consuming all remaining gas).
   *
   * Requirements:
   * - The divisor cannot be zero.
   */
  function mod(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
    require(b != 0, errorMessage);
    return a % b;
  }
}


// File: contracts/Manager/ManagerProxy.sol
pragma solidity ^0.5.16;

//import "./Manager.sol";
import "../Utils/Ownable.sol";
import "../interfaces/IManager.sol";

contract ManagerProxy is Ownable {
    IManager public manager;

    event NewManager(address indexed oldManager, address indexed newManager);

    modifier CheckPermit(string memory permit) {
        require(manager.userPermits(msg.sender, permit), "no permit");
        _;
    }

    modifier CheckPermitsOr(string[] memory permits) {
        bool granted = false;
        for (uint i = 0; i < permits.length; i++) {
            if (manager.userPermits(msg.sender, permits[i])) {
                granted = true;
                break;
            }
        }

        require(granted, "no permit");
        _;
    }

    modifier CheckUserPermit(address user, string memory permit) {
        require(manager.userPermits(user, permit), "no permit");
        _;
    }

    modifier CheckPermitOrGuardian(string memory permit) {
        require(manager.guardian() == msg.sender || manager.userPermits(msg.sender, permit), "no permit");
        _;
    }

    modifier OutBlacklist(address account) {
        address[] memory addArr = new address[](1);
        addArr[0] = account;
        require(!manager.inBlacklist(addArr), "account is in blacklist");
        _;
    }

    modifier OutBlacklist2(address account0, address account1) {
        address[] memory addArr = new address[](2);
        addArr[0] = account0;
        addArr[1] = account1;
        require(!manager.inBlacklist(addArr), "account is in blacklist");
        _;
    }

    modifier OutBlacklist3(address account0, address account1, address account2) {
        address[] memory addArr = new address[](3);
        addArr[0] = account0;
        addArr[1] = account1;
        addArr[2] = account2;
        require(!manager.inBlacklist(addArr), "account is in blacklist");
        _;
    }

    modifier ValidUDSDState() {
        require(!manager.dusdPaused(), "DUSD is paused");
        _;
    }

    modifier ValidDMTState() {
        require(!manager.dmtPaused(), "DMT is paused");
        _;
    }

    function setManager(address _manager) external onlyOwner {
        return _setManagerInternal(_manager);
    }

    function _getComptroller() internal view returns(address) {
        return manager.members("comptroller");
    }

    function _getIncome() internal view returns(address) {
        return manager.members("income");
    }

    function _getIncomeCache() internal view returns(address) {
        return manager.members("incomeCache");
    }

    function _getLiquidateFeeSwap() internal view returns(address) {
        return manager.members("liquidateSwap");
    }

    function _getReservedAssetSwap() internal view returns(address) {
        return manager.members("reservedSwap");
    }

    function _getDMT() internal view returns(address) {
        return manager.members("DMT");
    }

    function _getDUSD() internal view returns(address) {
        return manager.members("DUSD");
    }

    function _getDAOPool() internal view returns(address) {
        return manager.members("DAOPool");
    }

    function _getTreasury() internal view returns(address) {
        return manager.members("treasury");
    }

    // with decimal 18
    function _getRedeemTreasuryPercent() internal view returns(uint) {
        return manager.values("redeemTreasuryPercent");
    }

    // with decimal 18
    function _getReserveDAOPercent() internal view returns(uint) {
        return manager.values("reserveDAOPercent");
    }

    // with decimal 18
    function _getDUSDInterestDAOPercent() internal view returns(uint) {
        return manager.values("dusdInterestDAOPercent");
    }

    // with decimal 18
    function _getDUSDInflationDAOPercent() internal view returns(uint) {
        return manager.values("dusdInflationDAOPercent");
    }

    // with decimal 18
    function _getDUSDMintRate() internal view returns(uint) {
        return manager.values("dusdMintRate");
    }

    // with decimal 18
    function _getLiquidateFeePercent() internal view returns(uint) {
        return manager.values("liquidateFeePercent");
    }

    // with decimal 18
    function _getLiquidateFeeDAOPercent() internal view returns(uint) {
        return manager.values("liquidateFeeDAOPercent");
    }

    function _setManagerInternal(address _manager) internal {
        address oldManager = address(manager);
        manager = IManager(_manager);
        emit NewManager(oldManager, _manager);
    }

}


// File: contracts/Utils/Ownable.sol
pragma solidity ^0.5.16;

import "./Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
contract Ownable is Context {
  address public owner;
  address public pendingOwner;

  event NewOwner(address indexed oldOwner, address indexed owner);
  event NewPendingOwner(address indexed pendingOwner);

  modifier onlyOwner() {
    require(msg.sender == owner, "Ownable: only owner");
    _;
  }

  modifier onlyPendingOwner() {
    require(msg.sender == pendingOwner, "Ownable: only pending owner");
    _;
  }

  /**
   * @dev Initializes the contract setting the deployer as the initial owner.
   */
  constructor () internal {
    owner = _msgSender();
    emit NewOwner(address(0), owner);
  }

  function setPendingOwner(address _pendingOwner) external onlyOwner {
    require(_pendingOwner != address(0), "Ownable: pending owner is 0");
    pendingOwner = _pendingOwner;

    emit NewPendingOwner(pendingOwner);
  }

  function acceptOwner() external onlyPendingOwner {
    address old = owner;
    owner = msg.sender;
    pendingOwner = address(0);

    emit NewOwner(old, owner);
  }
}


// File: contracts/interfaces/IManager.sol
pragma solidity ^0.5.16;

interface IManager {
    function userPermits(address user, string calldata permit) external view returns(bool);
    function guardian() external view returns(address);
    function inBlacklist(address[] calldata accounts) external view returns(bool);
    function protocolPaused() external view returns(bool);
    function redeemPaused() external view returns(bool);
    function repayPaused() external view returns(bool);
    function liquidatePaused() external view returns(bool);
    function transferDmTokenPaused() external view returns(bool);

    function marketMintPaused(address market) external view returns(bool);
    function marketBorrowPaused(address market) external view returns(bool);

    function mintVAIPaused() external view returns(bool);
    function repayVAIPaused() external view returns(bool);

    function dusdPaused() external view returns(bool);
    function dmtPaused() external view returns(bool);

    function members(string calldata mem) external view returns(address);
    function values(string calldata key) external view returns(uint256);
}

// File: contracts/Utils/Context.sol
pragma solidity 0.5.16;

/*
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with GSN meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
contract Context {
  // Empty internal constructor, to prevent people from mistakenly deploying
  // an instance of this contract, which should be used via inheritance.
  constructor () internal { }

  function _msgSender() internal view returns (address payable) {
    return msg.sender;
  }

  function _msgData() internal view returns (bytes memory) {
    this; // silence state mutability warning without generating bytecode - see https://github.com/ethereum/solidity/issues/2691
    return msg.data;
  }
}
